!***********************************************************************
!  PAY037B:  Update FICA Status on Paylines Created with PYLOAD        *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  30772369                                            !
!                                                                      *
!***********************************************************************
!

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !Printer and page-size initialization

begin-report
  do Init-Report
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'PAY037B' to $ReportID

  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  
  do Get-Report-Language   ! Procedure that decides the report language
                           ! and then calls 'Report-Translation' procedure
                           ! that reads the language dep. text.

  move $PAY037B_ReportTitle to $ReportTitle

  do Array-Initialization

  do Payroll-Report-Initialization

end-procedure

! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation

! Read from PAY037B program id
  do Init_Report_Translation($ReportID,$curr_language_cd)
  do Append_Report_Translation('HR')

  if $Program_Read = 'Y'
    do Get_Field_Information ('PAY037B', 'REPORT_TITLE',  $PAY037B_ReportTitle,  #dummy_width)
    do Get_Field_Information ('PAY037B', 'ON_CYCLE',      $PAY037B_OnCycle,      #dummy_width)
    do Get_Field_Information ('PAY037B', 'OFF_CYCLE',     $PAY037B_OffCycle,     #dummy_width)
    do Get_Field_Information ('PAY037B', 'RUN_ID',        $PAY037B_Run_ID,       #dummy_width)
    do Get_Field_Information ('PAY037B', 'EMPLID',        $PAY037B_EmpId,        #dummy_width)
    do Get_Field_Information ('PAY037B', 'EMPL_RCD',      $PAY037B_Empl_Rcd,     #dummy_width)
    do Get_Field_Information ('PAY037B', 'NAME',          $PAY037B_Name,         #dummy_width)
    do Get_Field_Information ('PAY037B', 'PAGE_NUM',      $PAY037B_Page_Num,     #dummy_width)
    do Get_Field_Information ('PAY037B', 'LINE_NUM',      $PAY037B_Line_Num,     #dummy_width)
    do Get_Field_Information ('PAY037B', 'SEPCHK',        $PAY037B_SepChk,       #dummy_width)
    do Get_Field_Information ('PAY037B', 'OLD',           $PAY037B_Old,          #dummy_width)
    do Get_Field_Information ('PAY037B', 'NEW',           $PAY037B_New,          #dummy_width)
    do Get_Field_Information ('PAY037B', 'ADDL_NBR',      $PAY037B_Addl_Nbr,     #dummy_width)
    do Get_Field_Information ('PAY037B', 'FICA_STATUS',   $PAY037B_FICA_Status,  #dummy_width)
    do Get_Field_Information ('PAY037B', 'ERNCD',         $PAY037B_Erncd,        #dummy_width)
    do Get_Field_Information ('PAY037B', 'DESCRSHORT',    $PAY037B_DescrShort,   #dummy_width)
    do Get_Field_Information ('PAY037B', 'NO_PAYLINE',    $PAY037B_NO_PAYLINE,   #dummy_width)
    do Get_Field_Information ('PAY037B', 'PU_SOURCE',     $PAY037B_PU_SOURCE,    #dummy_width)
    do Get_Field_Information ('PAY037B', 'PAYSHEET',      $PAY037B_PAYSHEET,     #dummy_width)
    do Get_Field_Information ('PAY037B', 'SOURCE',        $PAY037B_SOURCE,       #dummy_width)
  end-if

end-procedure



begin-procedure Array-Initialization

  create-array name=EarnArray           -
               size=200                 -
               field=PageNum:number     -
               field=LineNum:number     -
               field=AddlNbr:number     -
               field=SepChk:number      -
               field=FICAStatus:char    -
               field=Erncd:char         -
               field=DescrShort:char    -
               field=NewAddlNbr:number  -
               field=NewFICAStatus:char -
               field=PUSource:char

end-procedure


begin-heading 11 name = 'Report'

  if rtrim($RC_PAYINIT.RUN_ID, ' ') = ''
     let $CycleDesc = $PAY037B_OffCycle
  else
    if rtrim(&RC_PAYINIT.CYCLE_SELECT, ' ') = 'F'
     let $CycleDesc = $PAY037B_OffCycle
    else
     let $CycleDesc = $PAY037B_OnCycle
    end-if
  end-if

  print $CycleDesc         (4)   center

  if rtrim($RC_PAYINIT.RUN_ID, ' ') = ''
    move 'Selected Page ' to $PageRange
    move #SlctPageFrom to $DispPageFrom b9999
    move #SlctPageThru to $DispPageThru b9999
    concat $DispPageFrom with $PageRange
    concat ' to ' with $PageRange
    concat $DispPageThru with $PageRange
  end-if

  print $PageRange         (5)   center

  if rtrim($RC_PAYINIT.RUN_ID, ' ') <> ''
    print $PAY037B_RUN_ID          (+1,1)
    print $RC_PAYINIT.RUN_ID      (0,13)
  end-if

  position (1)

  #Include 'stdhtr06.sqc'


  move 2000  to #Msg_SetNbr
  move 418   to #Msg_Nbr
  do Get-Message-Catalog

  let $Prt_Msg = substr($Msg_Desc,1,159)
  print $Prt_Msg                  (+3,1)
  let $Prt_Msg = substr($Msg_Desc,161,153)
  print $Prt_Msg                  (+1,1)
  let $Prt_Msg = substr($Msg_Desc,316,94)
  print $Prt_Msg                  (+1,1)

  print $PAY037B_EmpId            (+2,1)

  print $PAY037B_Empl_Rcd         (0,15)
  print $PAY037B_Name             (0,26,30)

  print $PAY037B_Page_Num         (0,58)
  print $PAY037B_Line_Num         (0,66)
  print $PAY037B_SepChk           (0,74)
  print $PAY037B_Addl_Nbr         (0,85)

  print $PAY037B_FICA_Status      (0,102)
  print $PAY037B_Erncd            (0,117)
  print $PAY037B_DescrShort       (0,130)
  print $PAY037B_PU_Source        (0,145)

end-heading


begin-heading 13 name = 'Update'

  if rtrim($RC_PAYINIT.RUN_ID, ' ') = ''
     let $CycleDesc = $PAY037B_OffCycle
  else
    if rtrim(&RC_PAYINIT.CYCLE_SELECT, ' ') = 'F'
     let $CycleDesc = $PAY037B_OffCycle
    else
     let $CycleDesc = $PAY037B_OnCycle
    end-if
  end-if

  print $CycleDesc         (4)   center

  if rtrim($RC_PAYINIT.RUN_ID, ' ') = ''
    move 'Selected Page ' to $PageRange
    move #SlctPageFrom to $DispPageFrom b9999
    move #SlctPageThru to $DispPageThru b9999
    concat $DispPageFrom with $PageRange
    concat ' to ' with $PageRange
    concat $DispPageThru with $PageRange
  end-if

  print $PageRange         (5)   center

  if rtrim($RC_PAYINIT.RUN_ID, ' ') <> ''
    print $PAY037B_RUN_ID          (+1,1)
    print $RC_PAYINIT.RUN_ID      (0,13)
  end-if

  position (1)

  #Include 'stdhtr06.sqc'


  move 2000  to #Msg_SetNbr
  move 419   to #Msg_Nbr
  do Get-Message-Catalog
  
  let $Prt_Msg = substr($Msg_Desc,1,162)
  print $Prt_Msg                  (+3,1)
  let $Prt_Msg = substr($Msg_Desc,164,175)
  print $Prt_Msg                  (+1,1)
  let $Prt_Msg = substr($Msg_Desc,340,174)
  print $Prt_Msg                  (+1,1)

  print $PAY037B_OLD             (+2,90)
  print $PAY037B_NEW             (0,107)
  print $PAY037B_OLD             (0,122)
  print $PAY037B_NEW             (0,137)
  print $PAY037B_Paysheet        (0,173)

  print $PAY037B_EmpId           (+1,1)
  print $PAY037B_Empl_Rcd        (0,15)
  print $PAY037B_Name            (0,26,30)

  print $PAY037B_Page_Num        (0,58)
  print $PAY037B_Line_Num        (0,66)
  print $PAY037B_SepChk          (0,74)
  print $PAY037B_Addl_Nbr        (0,85)

  print $PAY037B_Addl_Nbr        (0,102)
  print $PAY037B_FICA_Status     (0,118)
  print $PAY037B_FICA_Status     (0,133)
  print $PAY037B_Erncd           (0,148)
  print $PAY037B_DescrShort      (0,160)
  print $PAY037B_Source          (0,174)
  
end-heading


begin-procedure Report

  if rtrim(&RC_PAYINIT.PY_ACTION, ' ') = 'U'
    Alter-Report
      HEADING = 'Update'
  else
    Alter-Report
      HEADING = 'Report'
  end-if

  let $CycleInd = ' '

  if rtrim(&RC_PAYINIT.RUN_ID,' ') = ''
      move 'Y' to $SlctOffCycleA
      move 'Y' to $SlctOffCycleB
  else
    if rtrim(&RC_PAYINIT.CYCLE_SELECT, ' ') = 'F'
      move 'Y' to $SlctOffCycleA
      move 'Y' to $SlctOffCycleB
    else
      move 'N' to $SlctOffCycleA
      move 'N' to $SlctOffCycleB
    end-if
  end-if

  let #Payline_Count = 0
  move 1 to #changeLevel

begin-SELECT
A.COMPANY    () on-break print=never  level=1 procedure=New-Header
A.PAY_END_DT () on-break print=never  level=2 procedure=New-Header
A.PAYGROUP   () on-break print=never  level=3 procedure=New-Header
B.OFF_CYCLE
B.PAGE_NUM
B.LINE_NUM
B.ADDL_NBR
B.SEPCHK
B.EMPLID
B.EMPL_RCD
C.NAME
B.REG_PAY_HRS
B.OT_HRS
B.REG_PAY
B.FICA_STATUS_EE

  move &A.Company     to $Company
  move &A.PayGroup    to $PayGroup
  move &A.Pay_End_Dt  to $PayEndDate
  move &B.OFF_CYCLE   to $Off-Cycle

  do Initialize-Earn-Array

  let $RegOvt          = 'N'
  let $Split_AddlNbr   = 'N'
  let $Upd_FICA_Status = 'N'

  if &B.REG_PAY_HRS <> 0
      or &B.OT_HRS <> 0
      or &B.REG_PAY <> 0
    let $RegOvt = 'Y'
  end-if

  do Read-Oth-Earns

  if $Split_AddlNbr = 'Y'
        or $Upd_FICA_Status = 'Y'

    add 1 to #Payline_Count

    if $RC_PAYINIT.PY_ACTION ='R'
      do Print-Oth-Earns
    else
      if $RC_PAYINIT.PY_ACTION = 'U'
        if #COVIDCount = 1
          if $Split_AddlNbr = 'Y'
            do Insert-Earnings
          else
            if $Upd_FICA_Status = 'Y'
              do Update-FICA-Status
            end-if
          end-if
        else
          do Insert-Earnings
        end-if

        do Print-Oth-Earns

      end-if
    end-if
  end-if

FROM  PS_PAY_CALENDAR A,
      PS_PAY_EARNINGS  B,
      PS_PAY_LINE C
WHERE [$SlctCalendar]
  AND B.OFF_CYCLE IN ($SlctOffCycleA, $SlctOffCycleB)
  AND B.PAGE_NUM BETWEEN #SlctPageFrom AND #SlctPageThru
  AND B.COMPANY      = A.COMPANY
  AND B.PAYGROUP     = A.PAYGROUP
  AND B.PAY_END_DT   = A.PAY_END_DT
  AND C.COMPANY      = B.COMPANY
  AND C.PAYGROUP     = B.PAYGROUP
  AND C.PAY_END_DT   = B.PAY_END_DT
  AND C.OFF_CYCLE    = B.OFF_CYCLE
  AND C.PAGE_NUM     = B.PAGE_NUM
  AND C.LINE_NUM     = B.LINE_NUM
  AND B.OK_TO_PAY    = 'Y'
  AND B.PAY_LINE_STATUS IN ('C', 'E', 'I', 'U')
  AND B.PAY_SHEET_SRC = 'P'
  AND C.SINGLE_CHECK_USE IN ('P', 'N')
ORDER BY A.COMPANY,
         A.PAY_END_DT,
         A.PAYGROUP,
         B.PAGE_NUM,
         B.LINE_NUM,
         B.ADDL_NBR
end-SELECT

   if #Payline_Count = 0
     display ' '
     display $PAY037B_NO_PAYLINE
     display ' '
   end-if

   do Reset

end-procedure


begin-procedure Initialize-Earn-Array

  move 0 to #i

  while #i <= #EarnMax

    put                    0             -
                           0             -
                           0             -
                           0             -
                           ' '           -
                           ' '           -
                           ' '           -
                           0             -
                           ' '           -
                           ' '           -
        into EarnArray(#i)               -
                           PageNum       -
                           LineNum       -
                           AddlNbr       -
                           SepChk        -
                           FICAStatus    -
                           Erncd         -
                           DescrShort    -
                           NewAddlNbr    -
                           NewFICAStatus -
                           PUSource
    add 1 to #i
  end-while

  move -1 to #EarnMax

end-procedure


begin-procedure Read-Oth-Earns

begin-SELECT
COUNT(*) &OthCount

FROM  PS_PAY_OTH_EARNS D
WHERE D.COMPANY    = $Company
  AND D.PAYGROUP   = $PayGroup
  AND D.PAY_END_DT = $PayEndDate
  AND D.OFF_CYCLE  = $Off-Cycle
  AND D.PAGE_NUM   = &B.PAGE_NUM
  AND D.LINE_NUM   = &B.LINE_NUM
  AND D.ADDL_NBR   = &B.ADDL_NBR
  AND D.PU_SOURCE <> ' '

end-SELECT

  move &OthCount to #OthCount

  if #OthCount > 0
    do Read-COVID-Earns
  end-if

end-procedure



begin-procedure Read-COVID-Earns

  move -1 to #i

  let #COVIDCount = 0
  let $Upd_FICA_Status = 'N'

begin-SELECT
G.ERNCD
G.PU_SOURCE
I.DESCRSHORT
H.FICA_STATUS_EE

  if &B.FICA_STATUS_EE = 'N'
        and rtrim(&H.FICA_STATUS_EE, ' ') <> ''
    move &H.FICA_STATUS_EE to $New_FICAStatus
    let $Upd_FICA_Status = 'Y'
  else
    move &B.FICA_STATUS_EE to $New_FICAStatus
  end-if

  add 1 to #i

  put                 &B.PAGE_NUM       -
                      &B.LINE_NUM       -
                      &B.ADDL_NBR       -
                      &B.SEPCHK         -
                      &B.FICA_STATUS_EE -
                      &G.ERNCD          -
                      &I.DESCRSHORT     -
                      &B.ADDL_NBR       -
                      $New_FICAStatus   -
                      &G.PU_SOURCE      -
  into EarnArray(#i)  PageNum           -
                      LineNum           -
                      AddlNbr           -
                      SepChk            -
                      FICAStatus        -
                      Erncd             -
                      DescrShort        -
                      NewAddlNbr        -
                      NewFICAStatus     -
                      PUSource

  add 1  to  #COVIDCount


FROM  PS_PAY_OTH_EARNS G,
      PS_PY_REDUCE_ERN H,
      PS_EARNINGS_TBL  I
WHERE G.COMPANY    = $Company
  AND G.PAYGROUP   = $PayGroup
  AND G.PAY_END_DT = $PayEndDate
  AND G.OFF_CYCLE  = $Off-Cycle
  AND G.PAGE_NUM   = &B.PAGE_NUM
  AND G.LINE_NUM   = &B.LINE_NUM
  AND G.ADDL_NBR   = &B.ADDL_NBR
  AND G.PU_SOURCE <> ' '
  AND G.ERNCD      = H.ERNCD
  AND G.ERNCD      = I.ERNCD
  AND I.EFFDT      =
      (SELECT MAX(I1.EFFDT)
         FROM PS_EARNINGS_TBL I1
        WHERE I1.ERNCD = I.ERNCD
          AND I1.EFFDT <= G.PAY_END_DT)
ORDER BY G.ERNCD

end-SELECT

  if #i > #EarnMax
    move #i to #EarnMax
  end-if

  if $RegOvt = 'Y'
        and #COVIDCount > 0
    let $Split_AddlNbr = 'Y'
    let #i = 0
    do Put-Temp-AddlNbr
  end-if

  if &OthCount > #COVIDCount
        and #COVIDCount > 0
    let $Split_AddlNbr = 'Y'
    let #i = 0
    do Put-Temp-AddlNbr
  end-if

  if &OthCount = #COVIDCount
        and #COVIDCount > 1
    let $Split_AddlNbr = 'Y'
    let #i = 1
    do Put-Temp-AddlNbr
  end-if

  if $Split_AddlNbr = 'Y'
     or $Upd_FICA_Status = 'Y'
     let $PrintHeader = 'Y'
  end-if

end-procedure


begin-procedure Put-Temp-AddlNbr


  while #i <= #EarnMax

      put                  999             -
      into EarnArray(#i)                   -
                           NewAddlNbr


    add 1  to  #i

  end-while

end-procedure


begin-procedure Print-Oth-Earns

  move 0 to #i

  while #i <= #EarnMax
    get                  #PageNum       -
                         #LineNum       -
                         #AddlNbr       -
                         #SepChk        -
                         $FICAStatus    -
                         $Erncd         -
                         $DescrShort    -
                         #NewAddlNbr    -
                         $NewFICAStatus -
                         $PUSource      -
      from EarnArray(#i) PageNum        -
                         LineNum        -
                         AddlNbr        -
                         SepChk         -
                         FICAStatus     -
                         Erncd          -
                         DescrShort     -
                         NewAddlNbr     -
                         NewFICAStatus  -
                         PUSource

    if $RC_PAYINIT.PY_ACTION ='R'

      if not (#AddlNbr = #NewAddlNbr)
           or not (rtrim($FICAStatus, ' ')
                         = rtrim($NewFICAStatus, ' '))

        print &B.EMPLID               (+1,1)
        print &B.EMPL_RCD             (0,19)

        let $Name = RTRIM(&C.Name, ' ')
        print $Name                   (0,26,30)

        print #PageNum                (0,57)  edit b9999
        print #LineNum                (0,67)  edit b99
        print #SepChkm                (0,74)  edit b9
        print #AddlNbr                (0,90) edit b99

        let $FieldName  = 'FICA_STATUS_EE'
        let $FieldValue = $FICAStatus
        do Read-Translate-Table
        move $XLatShortName to $FICAStatus_Print
        print $FICAStatus_Print       (0,102)
        print $Erncd                  (0,120)
        print $DescrShort             (0,130)
        print $PUSource               (0,152)
      end-if

    else
      if $RC_PAYINIT.PY_ACTION ='U'

        if #AddlNbr <> #NewAddlNbr
             or rtrim($FICAStatus, ' ')
                        <> rtrim($NewFICAStatus, ' ')

          print &B.EMPLID               (+1,1)
          print &B.EMPL_RCD             (0,19)

          let $Name = RTRIM(&C.Name, ' ')
          print $Name                   (0,26)

          print #PageNum                (0,57)  edit b9999
          print #LineNum                (0,67)  edit b99
          print #SepChkm                (0,74)  edit b9
          print #AddlNbr                (0,90)  edit b99
          print #NewAddlNbr             (0,108) edit b99

          let $FieldName  = 'FICA_STATUS_EE'
          let $FieldValue = $FICAStatus
          do Read-Translate-Table
          move $XLatShortName to $FICAStatus_Print
          print $FICAStatus_Print       (0,118)
          let $FieldName  = 'FICA_STATUS_EE'
          let $FieldValue = $NewFICAStatus
          do Read-Translate-Table
          move $XLatShortName to $FICAStatus_Print
          print $FICAStatus_Print       (0,133)

          print $Erncd                  (0,151)
          print $DescrShort             (0,160)
          print $PUSource               (0,176)

        end-if

      end-if
    end-if

    add 1  to #i

  end-while

end-procedure


begin-procedure Update-FICA-Status

  let $err-statement = 'Update-FICA-Status'

begin-SQL on-error=Sql-Statement-Error
UPDATE
PS_PAY_EARNINGS
SET FICA_STATUS_EE = 'H'
WHERE COMPANY    = $Company
  AND PAYGROUP   = $PayGroup
  AND PAY_END_DT = $PayEndDate
  AND OFF_CYCLE  = $Off-Cycle
  AND PAGE_NUM   = &B.PAGE_NUM
  AND LINE_NUM   = &B.LINE_NUM
  AND ADDL_NBR   = &B.ADDL_NBR
end-SQL

end-procedure


begin-procedure Insert-Earnings

begin-SELECT
MAX(H.ADDL_NBR) &Last_AddlNbr

FROM PS_PAY_EARNINGS H
WHERE COMPANY   = $Company
 AND PAYGROUP   = $Paygroup
 AND PAY_END_DT = $PayEndDate
 AND OFF_CYCLE  = $Off-Cycle
 AND PAGE_NUM   = &B.PAGE_NUM
 AND LINE_NUM   = &B.LINE_NUM
end-SELECT

  move &Last_AddlNbr to #Last_AddlNbr

  add 1  to  #Last_AddlNbr

  do Insert-Other-Earns

end-procedure


begin-procedure Insert-Other-Earns

  let #i = 0

  while #i <= #EarnMax
    get                  #PageNum       -
                         #LineNum       -
                         #AddlNbr       -
                         #SepChk        -
                         $FICAStatus    -
                         $Erncd         -
                         $DescrShort    -
                         #NewAddlNbr    -
                         $NewFICAStatus -
      from EarnArray(#i) PageNum        -
                         LineNum        -
                         AddlNbr        -
                         SepChk         -
                         FICAStatus     -
                         Erncd          -
                         DescrShort     -
                         NewAddlNbr     -
                         NewFICAStatus

    if #COVIDCount = #OthCount
                and #i = 0

      if $FICAStatus <> $NewFICAStatus
        do Update-FICA-Status
      end-if
    else

      do Insert-New-Pay-Earnings
      do Insert-New-Oth-Earns
      do Delete-Old-Oth-Earns
      do Update-PSHUP-Entry

      put                  #Last_AddlNbr   -
      into EarnArray(#i)                   -
                           NewAddlNbr


      add 1  to  #Last_AddlNbr
    end-if

    add 1  to  #i

  end-while

end-procedure


begin-procedure Insert-New-Pay-Earnings

  let $err-statement = 'Insert-New-Pay-Earnings'

begin-SQL on-error=Sql-Statement-Error

INSERT
INTO PS_PAY_EARNINGS
(COMPANY
,PAYGROUP
,PAY_END_DT
,OFF_CYCLE
,PAGE_NUM
,LINE_NUM
,ADDL_NBR
,SEPCHK
,EMPLID
,EMPL_RCD
,BENEFIT_RCD_NBR
,EARNS_END_DT
,EARNS_BEGIN_DT
,ADDLPAY_REASON
,PAY_LINE_STATUS
,OK_TO_PAY
,SINGLE_CHECK_USE
,JOB_PAY
,DISABLE_DIR_DEP
,GROSSUP
,BUSINESS_UNIT
,DEPTID
,JOBCODE
,POSITION_NBR
,GL_PAY_TYPE
,ACCT_CD
,SHIFT
,SHIFT_RT
,HOURLY_RT
,ERNCD_REG_HRS
,ERNCD_OT_HRS
,ERNCD_REG_EARNS
,REG_PAY_HRS
,REG_HRS
,OT_HRS
,REG_HRLY_EARNS
,OT_HRLY_EARNS
,REG_PAY
,REG_EARNS
,REG_EARN_HRS
,DED_TAKEN
,DED_SUBSET_ID
,DED_TAKEN_GENL
,DED_SUBSET_GENL
,STATE
,LOCALITY
,PAY_FREQUENCY
,TAX_PERIODS
,TAX_METHOD
,ADDL_TAXES
,OVERRIDE_HOURLY_RT
,TL_SOURCE
,FLSA_RT
,RATE_USED
,FLSA_REQUIRED
,PAY_SHEET_SRC
,EI_PRIOR_PD_CORR
,COMP_RATECD_REG
,COMP_RATECD_OT
,COMPRATE_USED_REG
,COMPRATE_USED_OT
,FICA_STATUS_EE
,PAID_PRDS_PER_YEAR
,ORIG_PAYGROUP
,FLSA_STATUS
,XREF_NUM
,FLSA_END_DT
,UNION_CD
,BEN_DED_STATUS
,GENL_DED_STATUS
,HP_CONTRACT_NUM
,HP_CONTRACT_SEQ
,PY_JUR_CODE
)
SELECT
 COMPANY
,PAYGROUP
,PAY_END_DT
,OFF_CYCLE
,PAGE_NUM
,LINE_NUM
,#Last_AddlNbr
,SEPCHK
,EMPLID
,EMPL_RCD
,BENEFIT_RCD_NBR
,EARNS_END_DT
,EARNS_BEGIN_DT
,ADDLPAY_REASON
,'U'
,OK_TO_PAY
,SINGLE_CHECK_USE
,JOB_PAY
,DISABLE_DIR_DEP
,GROSSUP
,BUSINESS_UNIT
,DEPTID
,JOBCODE
,POSITION_NBR
,GL_PAY_TYPE
,ACCT_CD
,SHIFT
,SHIFT_RT
,HOURLY_RT
,ERNCD_REG_HRS
,ERNCD_OT_HRS
,ERNCD_REG_EARNS
,REG_PAY_HRS
,REG_HRS
,OT_HRS
,REG_HRLY_EARNS
,OT_HRLY_EARNS
,REG_PAY
,REG_EARNS
,REG_EARN_HRS
,DED_TAKEN
,DED_SUBSET_ID
,DED_TAKEN_GENL
,DED_SUBSET_GENL
,STATE
,LOCALITY
,PAY_FREQUENCY
,TAX_PERIODS
,TAX_METHOD
,ADDL_TAXES
,OVERRIDE_HOURLY_RT
,TL_SOURCE
,FLSA_RT
,RATE_USED
,FLSA_REQUIRED
,PAY_SHEET_SRC
,EI_PRIOR_PD_CORR
,COMP_RATECD_REG
,COMP_RATECD_OT
,COMPRATE_USED_REG
,COMPRATE_USED_OT
,$New_FICAStatus
,PAID_PRDS_PER_YEAR
,ORIG_PAYGROUP
,FLSA_STATUS
,XREF_NUM
,FLSA_END_DT
,UNION_CD
,BEN_DED_STATUS
,GENL_DED_STATUS
,HP_CONTRACT_NUM
,HP_CONTRACT_SEQ
,PY_JUR_CODE
FROM PS_PAY_EARNINGS
WHERE COMPANY   = $Company
 AND PAYGROUP   = $Paygroup
 AND PAY_END_DT = $PayEndDate
 AND OFF_CYCLE  = $Off-Cycle
 AND PAGE_NUM   = &B.PAGE_NUM
 AND LINE_NUM   = &B.LINE_NUM
 AND ADDL_NBR   = &B.ADDL_NBR
end-SQL

end-procedure


begin-procedure Insert-New-Oth-Earns

  let $err-statement = 'Insert-New-Oth-Earns'

begin-SQL on-error=Sql-Statement-Error

INSERT
INTO PS_PAY_OTH_EARNS
(COMPANY
,PAYGROUP
,PAY_END_DT
,OFF_CYCLE
,PAGE_NUM
,LINE_NUM
,ADDL_NBR
,SEPCHK
,ERNCD
,JOB_PAY
,OTH_HRS
,OTH_PAY
,OTH_EARNS
,ADD_GROSS
,TAX_METHOD
,ADDL_SEQ
,TL_SOURCE
,RATE_USED
,BAS_CREDIT_SW
,COMP_RATECD
,COMPRATE
,COMPRATE_USED
,XREF_NUM
,HRS_DIST_SW
,EX_DOC_ID
,EX_DOC_TYPE
,EX_LINE_NBR
,CURRENCY_CD
,VC_PLAN_ID
,VC_PAYOUT_PRD_ID
,GB_GROUP_ID
,APPLID
,AWARD_DATE
,NOTIFY_STATUS
,EIM_KEY
,PU_SOURCE
,RETROPAY_SEQ_NO
)
SELECT
 COMPANY
,PAYGROUP
,PAY_END_DT
,OFF_CYCLE
,PAGE_NUM
,LINE_NUM
,#Last_AddlNbr
,SEPCHK
,ERNCD
,JOB_PAY
,OTH_HRS
,OTH_PAY
,OTH_EARNS
,ADD_GROSS
,TAX_METHOD
,ADDL_SEQ
,TL_SOURCE
,RATE_USED
,BAS_CREDIT_SW
,COMP_RATECD
,COMPRATE
,COMPRATE_USED
,XREF_NUM
,HRS_DIST_SW
,EX_DOC_ID
,EX_DOC_TYPE
,EX_LINE_NBR
,CURRENCY_CD
,VC_PLAN_ID
,VC_PAYOUT_PRD_ID
,GB_GROUP_ID
,APPLID
,AWARD_DATE
,NOTIFY_STATUS
,EIM_KEY
,PU_SOURCE
,RETROPAY_SEQ_NO
FROM PS_PAY_OTH_EARNS
WHERE COMPANY   = $Company
 AND PAYGROUP   = $Paygroup
 AND PAY_END_DT = $PayEndDate
 AND OFF_CYCLE  = $Off-Cycle
 AND PAGE_NUM   = #PageNum
 AND LINE_NUM   = #LineNum
 AND ADDL_NBR   = #AddlNbr
 AND ERNCD      = $Erncd
end-SQL
end-procedure


begin-procedure Delete-Old-Oth-Earns
begin-SQL
DELETE
FROM PS_PAY_OTH_EARNS
WHERE COMPANY   = $Company
 AND PAYGROUP   = $Paygroup
 AND PAY_END_DT = $PayEndDate
 AND OFF_CYCLE  = $Off-Cycle
 AND PAGE_NUM   = #PageNum
 AND LINE_NUM   = #LineNum
 AND ADDL_NBR   = #AddlNbr
 AND ERNCD      = $Erncd
end-SQL
end-procedure


begin-procedure Update-PSHUP-Entry
begin-SQL
UPDATE
PS_PSHUP_TXN
SET ADDL_NBR = #Last_AddlNbr
WHERE COMPANY      = $Company
 AND PAYGROUP      = $Paygroup
 AND PAY_END_DT    = $PayEndDate
 AND OFF_CYCLE     = $Off-Cycle
 AND PAGE_NUM      = #PageNum
 AND LINE_NUM      = #LineNum
 AND ADDL_NBR      = #AddlNbr
 AND PU_TXN_TYPE   = 'E'
 AND ERNCD         = $Erncd
 AND PU_TXN_STATUS = 'I'
end-SQL
end-procedure

begin-procedure New-Header

  if $PrintHeader = 'Y'
    new-page
    let $PrintHeader = 'N'
  end-if

end-procedure


!******************************************************
!  RESET: Reset printer, print 'End of Report'        *
!         Note: Uncomment ENCODE statement to force   *
!               a printer reset at end of report      *
!******************************************************

begin-procedure Reset
  move 'Y' to $End_of_Report

  if #MsgFlagW
     print $MsgTextW (+2,1)
  end-if

end-procedure

begin-footing 2

  if $End_of_Report = 'Y'
    !*******************************************************************
    !  Use a globalized string if one was gotten from stdhdgtr         *
    !*******************************************************************
    if $stdhdg_end_rep = ''
      print 'End of Report' (+1,1) center
    else
      print $stdhdg_end_rep (+1,1) center
    end-if
 !  encode '<27>E' into $Reset
    print $Reset (+0,+1)
  end-if

end-footing


begin-procedure Sql-Statement-Error
  show $err-statement
  show ' Error : ' $sql-error

  display $Company
  display $Paygroup
  display $PayEndDate
  display $Off-Cycle
  display &B.PAGE_NUM
  display &B.LINE_NUM
  display &B.ADDL_NBR
  display #Last_AddlNbr
  display &B.FICA_STATUS_EE

end-procedure


begin-procedure Ignore
! dummy procedure to avoid SQL error message on delete of empty table
end-procedure


#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'getpgdta.sqc'  !Get-PayGroup-Data procedure
#Include 'payinit.sqc'   !Report Initialization and Timing
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'payrnctl.sqc'  !Get-Run-Control procedure
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'getrplng.sqc'  !Gets the report language
#Include 'getmsgct.sqc'  !Gets Message Catalog
#include 'readxlat.sqc'  !Read-Translate-Table procedure

