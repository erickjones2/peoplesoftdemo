!***********************************************************************
!  GPGBP11C.SQR - PART 3 DEDUCTIONS WORKING SHEET P11                  *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!----------------------------------------------------------------------
!                                                                      
!          $Date:  2013/01/18:04:04:59                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!----------------------------------------------------------------------
!
!                                                                      *
!***********************************************************************

!******************************************************************
!  Date         Modification
!  ----         ------------
!  03/2001      Design 2001/2002
!               + misc. enhancements
!  08/2005      Statutory modifications for 2004/2005 (Tax Credits and 
!               Reference fields deleted); Modified to print NI as taxcode 
!               if earnings b/w LEL and ET 
!
!*=================================================================

!******************************************************************
!*** Setting up the environment
!******************************************************************

#include 'setenv.sqc'   ! Set environment
#include 'setup32.sqc'  ! Printer and page-size initialization

!******************************************************************
!*** Constants definitions
!******************************************************************

!Define Column Location Variables
#define PageLength              177
#define PosNumPage              160

#define PosFreq/Period           3
#define PosFreq/TaxYear          5
#define PosStart                10
#define Pos1A                   22
#define Pos1B                   36
#define Pos1C                   48
#define Pos1D                   62
#define Pos1E                   84
#define Pos1F                   98
#define Pos1G                   114
#define Pos1G1                  120
#define Pos1G2                  123
#define Pos1H                   130
#define Pos1I                   154


!******************************************************************
!*** Heading section - Title
!******************************************************************

begin-heading  9 !This number must be changed to match the number
                 !of lines in the header (including column headings)

        print $ReportTitle              ()   bold center
        print 'Report ID:  '            (+1,1)
        print $ReportID                 (,{Pos1A})
        page-number                     ( ,{PosNumPage}) 'Page: '
        last-page                       () ' / '

        print 'Date:'                   (+1,1)
        print $Time_Begin               (,{Pos1A})
        print 'Selected Tax Year begins in'    (,{Pos1E})   Bold
        print $PNL_TAX_YEAR             (,{Pos1G})          Bold


        print 'Employee Name:'          (+2,1)
        print $SURNAME                  (,{Pos1A})
        print 'Tax Code / Basis:'       (,{Pos1E})
        print $TAXCODE                  (,{Pos1G})
        IF $TAXCODE='NI'
        ELSE
        print ' / '                     (,{Pos1G1})
        print #TAX_BASIS                (,{Pos1G2})         edit 9
        END-IF
        print 'Employee ID:'            (,{Pos1H})
        print $EMPLID                   (,{Pos1I})

        print 'Forename(s):'            (+1,1)
        print $FORENAMES                (,{Pos1A})
        print 'Date of Birth:'          (,{Pos1E})
        print $DOB                      (,{Pos1G})
        print 'Date of Starting:'       (,{Pos1H})
        print $HIRE_DT                  (,{Pos1I})

        print 'Pay Entity:'             (+1,1)
        print $PYENT_NAME               (,{Pos1A})
        print 'National Insurance No:'  (,{Pos1E})
        print $NI                       (,{Pos1G})
        print 'Date of Leaving:'        (,{Pos1H})
        print $TERM_DT                  (,{Pos1I})

!******************************************************************
!*** Heading section - Headers
!******************************************************************

        print 'Period'                  (+2,1)             Bold
        print 'Start Date'              (,{PosStart})      Bold
        print 'Daily Rate'              (,{Pos1A})         Bold
        print 'Stop Date'               (,{Pos1B})         Bold

end-heading
!******************************************************************
!***    Begin Report Section
!******************************************************************
begin-report

        SHOW 'P11C'

        !----------------------------------------------------------
        !***    ! SHOW timedate of the beginning
        !----------------------------------------------------------
        let $Time_Begin = edit(datenow(), 'DD/MM/YYYY HH24:MI:SS')
        show 'Time Program Began = ' $Time_Begin

        !----------------------------------------------------------
        !***    Call the main procedures
        !----------------------------------------------------------
        do INIT-REPORT
        do MAIN-REPORT

        !----------------------------------------------------------
        !***    SHOW informations
        !----------------------------------------------------------

        move #CountError to $CountError 99999
        SHOW 'CountError  ==> ' noline
        SHOW $CountError
        SHOW ' '

        !----------------------------------------------------------
        !***    SHOW timedate of the end
        !----------------------------------------------------------
        !let $Time_Ended = edit(datenow(), 'DD/MM/YYYY HH24:MI:SS')
        !show 'Time Program Ended = ' $Time_Ended

end-report


!******************************************************************
! I N I T I A L I Z E   A N D / O R   G E T    V A R I A B L E S
!******************************************************************


!*=================================================================
! Procedure     :INIT-REPORT
! Comment       :This procedure performs the application
!                initializations formats variables for use, and
!                calls the procedures to get input parameters.
!*=================================================================
begin-procedure INIT-REPORT

        let $ProcedureName      = 'INIT-REPORT'
        SHOW 'PROCEDURE->' $ProcedureName

        do INIT-DATETIME
        do INIT-NUMBER
        do INIT-VARIABLES
        do DEFINE-PRCS-VARS
        do GET-RUN-CONTROL-PARMS
        do GET-CURRENT-DATETIME

        if isnull($prcs_process_instance)
                SHOW 'Running P11 SQR'
                do ASK-VALUES !SQR not run through process scheduler
        else
                SHOW 'Running p11.SQR in the Process Scheduler'
                do GET-VALUES !SQR run through process scheduler
        end-if

        let $ReportID    = 'P11 - PART 3'
        let $ReportTitle = 'Deductions Working Sheet'
        !let $ReportSubTitle = ' '
        SHOW  ' '
        show $ReportID  ' '      $ReportTitle
        SHOW ' '
        show    'Operator ID: ' $Prcs_OprID  ' ' $Prcs_Run_Cntl_ID
        SHOW ' '
        let $WSQR-PROGRAM = 'GPGBP1C2'
        show 'init rep wsqr > ' $WSQR-PROGRAM

end-procedure INIT-REPORT

!*=================================================================
! Procedure    :INIT-VARIABLES
! Comment      :Initializations of all varaibles used by the report
!*=================================================================
begin-procedure INIT-VARIABLES

        let $ProcedureName      = 'INIT-VARIABLES'

        LET #TRUE               = 1
        LET #FALSE              = 0
        LET #CountError         = 0
        LET #counter            = 0
        LET #TotConPay          = 0
        LET #Tot1A              = 0
        LET #Tot1B              = 0
        LET #Tot1C              = 0
        LET #Tot1D              = 0
        LET #Tot1E              = 0
        LET #Tot1F              = 0
        LET #Tot1G              = 0
        LET #Tot1H              = 0

        LET $PRINTC             = 'N'
        LET $PRINTC_Y           = 'N'
        LET $PRINTC_T           = 'N'

end-procedure INIT-VARIABLES


!*=================================================================
! Procedure   :ASK-VALUES
! Comment     :This procedure prompts the user for those variable
!              values that will be used throughout the report.
!              ASK-VALUES is called when the user is not using
!              the process scheduler. This procedure calls whatever
!              procedures are needed. (eg. do ASK-FROM-THRU-DATE).
!*=================================================================
begin-procedure ASK-VALUES

        let $ProcedureName      = 'ASK-VALUES'

        !*---------------------------------------------------------
        !***    Enter the Calender Run ID
        !*---------------------------------------------------------
        !input $PNL_CAL_RUN_ID 'Enter Calender Run ID'

        !*---------------------------------------------------------
        !***    Enter the Pay Entity
        !*---------------------------------------------------------
        !input $PNL_PAY_ENTITY 'Enter Pay Entity'

        !*---------------------------------------------------------
        !***    Enter the Deptid
        !*---------------------------------------------------------
        !input $PNL_DEPTID 'Enter Department ID'

        !*---------------------------------------------------------
        !***    Enter the Pay Group
        !*---------------------------------------------------------
        !input $PNL_PAYGROUP 'Enter Pay Group '

        !*---------------------------------------------------------
        !***    Enter the Employee ID
        !*---------------------------------------------------------
        !input $PNL_EMPLID 'Enter Employee ID '

        !*---------------------------------------------------------
        !***    Enter the Tax Period
        !*---------------------------------------------------------
        !input $PNL_TAX_PERIOD 'Enter Tax Period'

        !*---------------------------------------------------------
        !***    Enter the Frequency ID
        !*---------------------------------------------------------
        !input $PNL_FREQUENCY_ID 'Enter Frequency ID'

        !*---------------------------------------------------------
        !***    Enter the Tax Year
        !*---------------------------------------------------------
        !input $PNL_TAX_YEAR 'Enter The Year the Tax Year Begins In'


end-procedure ASK-VALUES


!*=================================================================
! Procedure     :GET-VALUES
! Comment       :This procedure gets the variable name (beginning
!                with a $) that will be used throughout the report
!                using the run control parameters called in
!                GET-RUN-CONTROL-PARMS.
!*=================================================================
begin-procedure GET-VALUES

        let $ProcedureName      = 'GET-VALUES'

begin-SELECT on-error=ERROR-SQL   !SELECTS THE DATA FROM THE PANEL
RUNDB.CAL_RUN_ID
RUNDB.DEPTID
RUNDB.PAY_ENTITY
RUNDB.GP_PAYGROUP
RUNDB.EMPLID
RUNDB.FREQUENCY_ID
RUNDB.GPGB_TAX_PERIOD
RUNDB.GPGB_TAX_BGN_YR

        LET $PNL_CAL_RUN_ID         = RTRIM(&RUNDB.CAL_RUN_ID,' ')
        LET $PNL_DEPTID             = RTRIM(&RUNDB.DEPTID,' ')
        LET $PNL_PAY_ENTITY         = RTRIM(&RUNDB.PAY_ENTITY,' ')
        LET $PNL_PAYGROUP           = RTRIM(&RUNDB.GP_PAYGROUP,' ')
        LET $PNL_EMPLID             = RTRIM(&RUNDB.EMPLID,' ')
        LET #PNL_TAX_PERIOD         = &RUNDB.GPGB_TAX_PERIOD
        LET $PNL_FREQUENCY_ID       = RTRIM(&RUNDB.FREQUENCY_ID,' ')
        LET $PNL_TAX_YEAR           = RTRIM(EDIT(&RUNDB.GPGB_TAX_BGN_YR, '9999'),' ')
        LET #PNL_TAX_YEAR           = &RUNDB.GPGB_TAX_BGN_YR

        LET $PNL_TAX_PERIOD         = EDIT(#PNL_TAX_PERIOD,'09')

        FROM  PS_GPGB_RUNCTL_SQR RUNDB
        WHERE RUNDB.OPRID         = $Prcs_OprID
          AND RUNDB.RUN_CNTL_ID   = $Prcs_Run_Cntl_ID

end-SELECT

end-procedure GET-VALUES

!******************************************************************
!           S T A R T   O F   T H E   R E P O R T
!******************************************************************

!*=================================================================
! Procedure     : MAIN REPORT                                     !
! Descr         : This procedure is the main procedure of the     !
!                 report and calls other procedures               !
!                                                                 !
!*=================================================================

begin-procedure MAIN-REPORT

        let $ProcedureName      = 'MAIN-REPORT'
        SHOW 'PROCEDURE->' $ProcedureName

        DO SELECT-EMPLOYEES ($PNL_CAL_RUN_ID, $PNL_DEPTID, $PNL_PAY_ENTITY,
        $PNL_PAYGROUP, $PNL_EMPLID, $PNL_TAX_PERIOD, $PNL_FREQUENCY_ID,
        $PNL_TAX_YEAR, $SELECTED_EMPLOYEE, #SELECTED_TAX_PERIOD, $SELECTED_FREQUENCY)

        DO UPDATE-PRCS-RUN-STATUS

end-procedure MAIN-REPORT

!*=================================================================
! Procedure:  START-NEW-PAGE                                      !
! Descr:      New page each time the Emplid changes               !
!                                                                 !
!*=================================================================
begin-procedure START-NEW-PAGE($SELECTED_EMPLOYEE)

        let $ProcedureName      = 'START-NEW-PAGE'
        SHOW 'PROCEDURE->' $ProcedureName

        IF #_CURRENT-LINE >= 11

                DO PRINT-TOTALS
                CLEAR-ARRAY Name=TAX_Credit_ARR
                NEW-PAGE

        END-IF

end-procedure START-NEW-PAGE

!*=================================================================
! Procedure: PRINT-REPORT                                         !
! Descr:     Prints Employee information                          !
! NOTA !!!   This PRINT-REPORT is to be precessed when called
!            from Gpgbrttc.SQC and not from Gpgbrtee.SQC
!*=================================================================
begin-procedure PRINT-REPORT ($PIN_CODE, $PERIOD, $NI_CATEGORY, $EMPLID,
        $PAY_ENTITY)

        LET $ProcedureName = 'PRINT-REPORT'
        SHOW 'PROCEDURE->' $ProcedureName

        SHOW 'PR->' $_PRINTC '<-'
        IF $_PRINTC = 'Y'
                OR $_PRINTC_Y = 'Y'
                IF $_VALTC <> 'NIL'
                        print $PERIOD           (+1,{PosFreq/Period})
                        PRINT $_START_DATE      (,{PosStart})
                        PRINT #_RATE            (,{Pos1A}) Edit 999,999.99
                        PRINT $_STOP_DATE       (,{Pos1B})
                END-IF
        LET $_PRINTC = 'N'
        END-IF

end-procedure PRINT-REPORT

!*=================================================================
! Procedure: PRINT TOTALS                                         !
! Descr:     Print Employeee's Totals                             !
!                                                                 !
!*=================================================================
begin-procedure PRINT-TOTALS

        LET $ProcedureName = 'PRINT-TOTALS'
        SHOW 'PROCEDURE->' $ProcedureName

        graphic                 (+1,1,50) horz-line

   LET #i = 0
   WHILE #i<55
   Get $W_TC_DATE From Tax_Credit_ARR(#i) TC_Date

   if  $W_TC_DATE <> ''

        Get #W_RATE      From Tax_Credit_ARR(#i) TC_Rate
        Get $W_STOP_DATE From Tax_Credit_ARR(#i) TC_End
        Get #W_VALTCY    From Tax_Credit_ARR(#i) TC_Total
        Get $W_REF       From Tax_Credit_ARR(#i) TC_Ref

        print 'YtD'              (+2,1)
        print $PNL_TAX_YEAR     (,{PosFreq/TaxYear})
        PRINT $W_TC_DATE         (,{PosStart})
        PRINT #W_RATE            (,{Pos1A}) Edit 999,999.99
        PRINT $W_STOP_DATE       (,{Pos1B})
       
   else
        Break
   end-if

   let #i = #i + 1
   END-WHILE

        LET $_PRINTC_Y = 'N'
        LET $_PRINTC_T = 'N'

 end-procedure PRINT-TOTALS

!******************************************************************
!                  G E N E R A L   F U N C T I O N S
!******************************************************************

!******************************************************************
!***    ERROR - SQL error procedure
!******************************************************************
begin-procedure ERROR-SQL

        add 1 to #CountError

        show 'SQL Error -> ' $ProcedureName
        show 'SQL Error -> ' $ErrorMessage
        show 'SQL Error -> ' $SQL-ERROR

        stop

end-procedure ERROR-SQL

!******************************************************************
!***    Common SQC Modules Copied In For Program Usage
!******************************************************************
#include 'reset1.sqc'    ! Reset printer procedure
#include 'curdttim.sqc' ! Get-Current-DateTime procedure
#include 'datetime.sqc' ! Routines for date and time formatting
#include 'number.sqc'   ! Routines to format numbers
#include 'prcsapi.sqc'  ! Update Process Request API
#include 'prcsdef.sqc'  !Update Process Request variable declaration
#include 'readxlat.sqc' ! Read Translate Table
#include 'gpgbslee.sqc' ! Select EE From GP GBR Results table
#include 'gpgbrtee.sqc' ! Retrieve EE Accumulators Results
#include 'gpgbrttc.sqc' ! Retrieve EE Acum Tax Credit Results...
#include 'gpgbrttd.sqc' ! Retrieve EE Other PINs Results
#include 'gpgbrted.sqc' ! Retrieve EE Earning & Deduction Results
!******************************* End Of Program *******************
