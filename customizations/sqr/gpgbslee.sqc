!***********************************************************************
!  GPGBSLEE.SQC - SELECT EMPLOYEES                                     *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!----------------------------------------------------------------------
!
!          $Date:  2013/01/18:04:06:22                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!----------------------------------------------------------------------
!
!                                                                      *
!                                                                      *
!***********************************************************************
!  Date         Modification
!  ----         ------------
!  03/2001      Misc. Enhancements
!  10/2002      New procedure for EE Address
!
!*==================================================================
! Procedure     : SELECT EMPLOYEES                                 !
! Descr         : Retrieves employees for selected criteria chosen !
!               : from the run control panel                       !
!*==================================================================

begin-procedure SELECT-EMPLOYEES($PNL_CAL_RUN_ID, $PNL_DEPTID,
                $PNL_PAY_ENTITY, $PNL_PAYGROUP,$PNL_EMPLID,
                $PNL_TAX_PERIOD, $PNL_FREQUENCY_ID, $PNL_TAX_YEAR,
                :$SELECTED_EMPLOYEE, :#SELECTED_TAX_PERIOD, :$SELECTED_FREQUENCY)

        let $ProcedureName      = 'SELECT-EMPLOYEES'
        SHOW 'PROCEDURE->' $ProcedureName

        LET $Q                  = ''''

        LET $WHERE_STATEMENT    = 'WHERE GER.GPGB_TAX_BGN_YR = '
                                  || $PNL_TAX_YEAR
        LET $FREQ_WHERE         = ' AND GER.FREQUENCY_ID = '''
                                  || $PNL_FREQUENCY_ID || $Q
        LET $PERIOD_WHERE       = ' AND GER.GPGB_TAX_PERIOD = '
                                  || $PNL_TAX_PERIOD
        LET $CALENDER_WHERE     = ' AND GER.CAL_RUN_ID = '''
                                  || $PNL_CAL_RUN_ID || $Q
        LET $DEPTID_WHERE       = ' AND GER.DEPTID = '''
                                  || $PNL_DEPTID || $Q
        LET $PAY_ENTITY_WHERE   = ' AND GER.PAY_ENTITY = '''
                                  || $PNL_PAY_ENTITY || $Q
        LET $EMPLID_WHERE       = ' AND GER.EMPLID = '''
                                  || $PNL_EMPLID || $Q
        LET $PAYGROUP_WHERE     = ' AND GER.GP_PAYGROUP = '''
                                  || $PNL_PAYGROUP || $Q

        MOVE $PNL_TAX_PERIOD    TO   #TAX_PERIOD

        IF  #TAX_PERIOD <> 00
                LET $WHERE_STATEMENT= $WHERE_STATEMENT
                                      || $PERIOD_WHERE
        END-IF

        EVALUATE ''
        WHEN <> $PNL_FREQUENCY_ID
                LET $WHERE_STATEMENT = $WHERE_STATEMENT
                                       || $FREQ_WHERE
        WHEN <> $PNL_CAL_RUN_ID
                LET $WHERE_STATEMENT = $WHERE_STATEMENT
                                       || $CALENDER_WHERE
        WHEN <> $PNL_DEPTID
                LET $WHERE_STATEMENT = $WHERE_STATEMENT
                                       || $DEPTID_WHERE
                BREAK
        WHEN <> $PNL_PAY_ENTITY
                LET $WHERE_STATEMENT = $WHERE_STATEMENT
                                       || $PAY_ENTITY_WHERE

        WHEN <> $PNL_EMPLID
                LET $WHERE_STATEMENT = $WHERE_STATEMENT
                                       || $EMPLID_WHERE
                BREAK
        WHEN <> $PNL_PAYGROUP
                LET $WHERE_STATEMENT = $WHERE_STATEMENT
                                       || $PAYGROUP_WHERE
                BREAK
        END-EVALUATE

SHOW 'FINAL WHERE-> '  $Where_statement

        LET $W_Pay_entity = ' '
        LET $W_Sel_EmplID = ' '

!-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
begin-SELECT

GER.EMPLID              &EMPLID       () on-break print=never level=1 !NEW PAGE IS ONLY CALLED FROM LEVEL 2 AS LEVEL 1 CALLS LEVEL 2 ON EACH BREAK ANYWAY.
GER.PAY_ENTITY          &PAY_ENTITY   () on-break print=never after=START-NEW-PAGE($SELECTED_EMPLOYEE) level=2
GER.GPGB_TAX_PERIOD     &TAX_PERIOD
GER.CAL_RUN_ID
GER.DEPTID
GER.GP_PAYGROUP
GER.FREQUENCY_ID        &FREQUENCY
GER.GPGB_TAX_BGN_YR
GER.HIRE_DT             &LAST_HIRE_DT  () on-break print=never level=1
GER.LEAVE_DT            &LEAVE_DT

                LET $SELECTED_EMPLOYEE   = RTRIM(&EMPLID, ' ')
                LET #SELECTED_TAX_PERIOD = &TAX_PERIOD
                LET $SELECTED_FREQUENCY  = &FREQUENCY
                LET $_MAIN_SEL_PAY_ENTITY = RTRIM(&PAY_ENTITY, ' ')

        IF $W_Pay_entity <> $_MAIN_SEL_PAY_ENTITY
            DO GET-PAY-ENTITY
            LET $W_Pay_entity = $_MAIN_SEL_PAY_ENTITY
        END-IF
        IF $W_Sel_EmplID <> $SELECTED_EMPLOYEE
            ! Get current EE Address - Default is 'HOME' address      - 10/2002
            LET $W_Address_Type = 'HOME'
            LET $W_Sel_EmplID   = $SELECTED_EMPLOYEE
            DO GET-EE-ADDRESS($W_Sel_EmplID,$W_Address_Type,$_EE_ADDRESS1,$_EE_ADDRESS2,$_EE_ADDRESS3,
                                $_EE_CITY, $_EE_POSTAL, $_EE_COUNTY, $_EE_COUNTRY)
        END-IF

        !-------------------------------------------------------------------------

        DO RETRIEVE-TAX-CREDITS ($SELECTED_EMPLOYEE,
        #SELECTED_TAX_PERIOD, $SELECTED_FREQUENCY)

        DO RETRIEVE-TAX-DATA ($SELECTED_EMPLOYEE,
        #SELECTED_TAX_PERIOD, $SELECTED_FREQUENCY)

        DO RETRIEVE-EARNING-AND-DEDUCTIONS ($SELECTED_EMPLOYEE,
        #SELECTED_TAX_PERIOD, $SELECTED_FREQUENCY)

        DO RETRIEVE-EMPLOYEE-DATA ($SELECTED_EMPLOYEE,
        #SELECTED_TAX_PERIOD, $SELECTED_FREQUENCY, $PERIOD, $NI_CATEGORY, $EMPLID,
        $PAY_ENTITY)

       !-------------------------------------------------------------------------

        LET $_HIRE_DT      = DATETOSTR(&LAST_HIRE_DT, 'DD/MM/YYYY')           ! 03/2001
        LET $_TERM_DT      = DATETOSTR(&LEAVE_DT, 'DD/MM/YYYY')               ! 03/2001

        LET $_HIRE_DT_SP   = DATETOSTR(&LAST_HIRE_DT, 'DD MM YYYY')           ! 10/2002
        LET $_TERM_DT_SP   = DATETOSTR(&LEAVE_DT, 'DD MM YYYY')               ! 10/2002

        Do Convert-To-DTU-Date(&LAST_HIRE_DT,$_W_HIRE_DT)                     ! 10/2002
        Do Convert-To-DTU-Date(&LEAVE_DT,$_W_TERM_DT)                         ! 10/2002


FROM PS_GPGB_EE_RSLT GER, PS_GP_PYE_PRC_STAT GPPS
[$Where_statement]
AND GER.EMPLID = GPPS.EMPLID
AND GER.CAL_RUN_ID = GPPS.CAL_RUN_ID
AND GER.EMPL_RCD = GPPS.EMPL_RCD
AND GER.CAL_ID = GPPS.CAL_ID
AND GER.GP_PAYGROUP = GPPS.GP_PAYGROUP
AND GPPS.CALC_TYPE = 'P'
AND GPPS.RSLT_VER_NUM = (SELECT MAX (X.RSLT_VER_NUM) FROM PS_GP_PYE_PRC_STAT X
                                WHERE GPPS.EMPLID       = X.EMPLID
                         !      AND GPPS.CAL_RUN_ID     = X.CAL_RUN_ID         --> Non selected to get the last calc.
                                AND GPPS.EMPL_RCD       = X.EMPL_RCD
                                AND GPPS.CAL_ID         = X.CAL_ID
                                AND GPPS.GP_PAYGROUP    = X.GP_PAYGROUP
                                AND GPPS.CALC_TYPE      = X.CALC_TYPE)
AND GPPS.RSLT_REV_NUM = (SELECT MAX (X.RSLT_REV_NUM) FROM PS_GP_PYE_PRC_STAT X
                                WHERE GPPS.EMPLID       = X.EMPLID
                         !      AND GPPS.CAL_RUN_ID     = X.CAL_RUN_ID         --> Non selected to get the last calc.
                                AND GPPS.EMPL_RCD       = X.EMPL_RCD
                                AND GPPS.CAL_ID         = X.CAL_ID
                                AND GPPS.GP_PAYGROUP    = X.GP_PAYGROUP
                                AND GPPS.CALC_TYPE      = X.CALC_TYPE)

ORDER BY GER.GPGB_TAX_BGN_YR, GER.EMPLID, GER.HIRE_DT, GER.FREQUENCY_ID, GER.GPGB_TAX_PERIOD,
GER.PAY_ENTITY

END-SELECT
!-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*

end-procedure SELECT-EMPLOYEES

