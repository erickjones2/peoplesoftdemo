!***********************************************************************
!  PI706:    Contents of Interface Instance Table                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:13:24                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'  !Set environment
#include 'setup31.sqc' !Printer and page-size initialization


begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-procedure Init-Report

  move 'PI706' to $ReportID
  move 'Interface Instance Table' to $ReportTitle

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language

  move $Pi706_ReportTitle to $ReportTitle

  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation

! Read from PI706 program ids
do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PI706', 'REPORT_TITLE', $Pi706_ReportTitle, #dummy_width)
!!  do Get_Field_Information ('PI706', 'SYSTEMID', $Pi706_SID, #dummy_width)
  do Get_Field_Information ('PI706', 'INSTANCEID1', $Pi706_InstanceId1, #dummy_width)
  do Get_Field_Information ('PI706', 'INSTANCEID2', $Pi706_InstanceId2, #dummy_width)
  do Get_Field_Information ('PI706', 'EFFDT', $Pi706_EffDt, #dummy_width)
  do Get_Field_Information ('PI706', 'INSTIND1', $Pi706_InstInd1, #dummy_width)
  do Get_Field_Information ('PI706', 'INSTIND2', $Pi706_InstInd2, #dummy_width)
  do Get_Field_Information ('PI706', 'ROW', $Pi706_Row, #dummy_width)
  do Get_Field_Information ('PI706', 'RECORDNAME', $Pi706_RecordName, #dummy_width)
  do Get_Field_Information ('PI706', 'I_EIND1', $Pi706_IEInd1, #dummy_width)
  do Get_Field_Information ('PI706', 'I_EIND2', $Pi706_IEInd2, #dummy_width)
  do Get_Field_Information ('PI706', 'FIELD1NAME', $Pi706_Field1Name, #dummy_width)
  do Get_Field_Information ('PI706', 'INSTSEQ1', $Pi706_InstSeq1, #dummy_width)
  do Get_Field_Information ('PI706', 'INSTSEQ2', $Pi706_InstSeq2, #dummy_width)
  do Get_Field_Information ('PI706', 'FIELD1VALUE', $Pi706_Field1Value, #dummy_width)

end-procedure



begin-heading 7
  #Include 'stdhdgtr.sqc'

  print  $Pi706_InstanceId1  (5,6,10)
  print  $Pi706_InstInd1     (,30,7)
  print  $Pi706_IEInd1       (,63,7)
  print  $Pi706_InstSeq1     (,91,7)

  print  'SId'               (6,1,5)
  print  $Pi706_InstanceId2  (,6,10)
  print  $Pi706_EffDt        (,18,11)
  print  $Pi706_InstInd2     (,30,7)
  print  $Pi706_Row          (,38,7)
  print  $Pi706_RecordName   (,46,15)
  print  $Pi706_IEInd2       (,63,7)
  print  $Pi706_Field1Name   (,71,18)
  print  $Pi706_InstSeq2     (,91,7)
  print  $Pi706_Field1Value  (,99,10)

end-heading


begin-procedure Process-Main
begin-SELECT
A.PI_SYSTEM_ID        (+1,1,5) ON-BREAK LEVEL=1 SKIPLINES=0
A.PI_INSTANCE_ID      (,6,10)  on-break level=1
A.EFFDT
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print   $out         (,18,11)  on-break level=2
A.PI_INSTANCE_IND      (,30,7)  on-break level=1
A.ROW_NUM              (,38,7)  on-break level=1
A.PS_RECORD_NM         (,46,15)  on-break level=1
A.INCLD_EXCLD_IND      (,63,7)  on-break level=1
A.PS_FIELD1_NM         (,71,18)  on-break level=1
  do Get-Instance-Values
FROM  PS_PI_INSTANCE_TBL  A
ORDER BY A.PI_SYSTEM_ID, A.PI_INSTANCE_ID, A.EFFDT DESC
end-SELECT
end-procedure

begin-procedure Get-Instance-Values

let #row = 1

begin-SELECT

B.PI_INSTANCE_SEQ

  if #row = 1
      print &B.PI_INSTANCE_SEQ  (,91,7)
  else
      print &B.PI_INSTANCE_SEQ  (+1,91,7)
  end-if

B.PS_FIELD1_VALUE  (,99,10)

  let #row = #row + 1

FROM  PS_PI_INST_VALUE B
WHERE B.PI_INSTANCE_ID = &A.PI_INSTANCE_ID AND B.EFFDT = &A.EFFDT
ORDER BY B.PI_INSTANCE_SEQ
end-SELECT

end-procedure Get-Instance-Values


#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'getrplng.sqc'  !Gets the report language

