!***********************************************************************
!  GPMXAG01:  Auxiliar de Aguinaldo  - (Payroll Register)              *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:03:50:12                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#Include 'setup32.sqc'  !Printer and page-size initialization

begin-report
  move '1' to $ReportDateType
  move '1' to $ReportYear4
  do Init-Report
  do Stdapi-Term
end-report

Begin-procedure Select-Params
Let $SlctWhere1 = ''
Let $SlctWhere  = 'WHERE '
let $CompanyRC  = ''
 
BEGIN-SELECT
RC.CAL_RUN_ID  
RC.COMPANY
RC.GP_PAYGROUP
RC.DEPTID

   Let $CalRunID = rtrim(&RC.CAL_RUN_ID, ' ')
   Let $CompanyRC = rtrim(&RC.COMPANY, ' ')
   Let $DeptidRC = rtrim(&RC.DEPTID, ' ')
   Let $GPPygrpRC = rtrim(&RC.GP_PAYGROUP, ' ') 
 
FROM PS_GPMX_RC_AG001 RC
WHERE RC.OPRID = $prcs_oprid
AND RC.RUN_CNTL_ID = $prcs_run_cntl_id
END-SELECT

 If $CalRunID <> ''
      Concat 'WA.CAL_RUN_ID =' With $SlctWhere1
      Concat '''' With $SlctWhere1
      Concat $CalRunID With $SlctWhere1
      Concat '''' With $SlctWhere1
 End-if
 
  If $CompanyRC <> ''
     If rtrim($SlctWhere1, ' ') <> ''
        Concat ' AND WA.COMPANY =' With $SlctWhere1
        Concat '''' With $SlctWhere1
        Concat $CompanyRC With $SlctWhere1
        Concat '''' With $SlctWhere1
      else
        Concat ' WA.COMPANY =' With $SlctWhere1
        Concat '''' With $SlctWhere1
        Concat $CompanyRC With $SlctWhere1
        Concat '''' With $SlctWhere1
      end-if
  End-if

  If rtrim($GPPygrpRC, ' ') <> ''
    If rtrim($SlctWhere1, ' ') <> ''
       concat ' AND WA.GP_PAYGROUP = ' With $SlctWhere1
       Concat '''' With $SlctWhere1
       Concat $GPPygrpRC With $SlctWhere1
       Concat '''' With $SlctWhere1
    Else
      concat ' WA.GP_PAYGROUP = ' With $SlctWhere1
      Concat '''' With $SlctWhere1
      Concat $GPPygrpRC With $SlctWhere1
      Concat '''' With $SlctWhere1
    End-if
  End-if

  If rtrim($DeptidRC, ' ') <> ''
    If rtrim($SlctWhere1, ' ') <> ''
      concat ' AND WA.DEPTID = ' With $SlctWhere1
      Concat '''' With $SlctWhere1
      Concat $DeptIdRC With $SlctWhere1
      Concat '''' With $SlctWhere1
    Else
      concat ' WA.DEPTID = ' With $SlctWhere1
      Concat '''' With $SlctWhere1
      Concat $DeptIdRC With $SlctWhere1
      Concat '''' With $SlctWhere1
    End-If
  End-if
 
 If $SlctWhere1 <> '' 
    Let $SlctWhere1 =  $SlctWhere || $SlctWhere1 || ' AND WA.AMOUNT <> 0'  
 else
    Let $SlctWhere1 =  $SlctWhere || ' WA.AMOUNT <> 0'  
 End-if
 
 !show 'Condition: ' $SlctWhere1

End-procedure Select-Params

begin-procedure Init-Report
  move 'GPMXAG01'          to $ReportID
  move 'Auxiliar de Aguinaldo ' to $ReportTitle

  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  Do Get-Current-DateTime
  do Select-Params
  do Report
end-procedure Init-Report


begin-heading 9
  if $Company_RFC = ''
     Do Get-Company-Data
  end-if
  print 'PeopleSoft'            (1,)          center
  print 'Reporte:  '            (+1,1)
  print $ReportID               ()
  uppercase $ReportTitle
  print $ReportTitle            ()          center
  let #RptCol = {ColR} - 2
  page-number                   (0,#RptCol)  'No. Página. '
  print 'Fecha: '               (+1,#RptCol)
  print $ReportDate             ()
  print 'Hora:  '               (+1,#RptCol)
  print $ReportTime             ()

  if $CompanyRC <> ''
     print 'Compañía:'              (3,1,11)
     print &WA.COMPANY             (0,13,3)
     print $CompanyName            (0,20)
  end-if
  print 'Fecha de Inicio del Periodo:   '    (+1,001)
  print $BeginDtF               ()
  print 'Fecha de Fin del Periodo:   '      (0,050)
  move  &WA.PERIOD_END_DATE to $PayEndDate 
  do    Format-DateTime($PayEndDate, $out, {DEFDMY},'','')
  print $out                    ()
  if $CompanyRC <> ''
     print 'RFC:'                  (+1,001)   ! Company RFC
     print $Company_RFC            (0,006)
  end-if
  print '-'                 (+1,001,176) fill
  print 'Empleado'              (+1,001)
  print 'Nombre del Empleado'   ( 0,015)
  print 'Fecha Ingreso'         ( 0,044)
  print 'Días'                  ( 0,067)
  print 'Sal.Diario/Prom'       ( 0,079)
  print 'Faltas '               ( 0,101)
  print 'Días'                  ( 0,115)
  print 'Departamento'          (+1,001)
  print 'Puesto'                ( 0,044)
  print 'Trabajados'            ( 0,065)
  print 'Aguinaldo'             ( 0,113)
  print 'Importe'               ( 0,132)
  print 'I.S.R.'                ( 0,150)
  print 'Neto a Pagar'          ( 0,161)
  print '-'                     (+1,001,176) fill
end-heading

!**************************************
begin-procedure Get-Company-Data
!**************************************

begin-SELECT
C.RFC_ER_ID_MEX
   Let $Company_RFC = &C.RFC_ER_ID_MEX

FROM  PS_COMPANY_TBL_MEX C
WHERE C.COMPANY = $Company
AND   C.EFFDT   = (SELECT MAX(EFFDT) FROM   PS_COMPANY_TBL_MEX
                    WHERE  COMPANY    = C.COMPANY
                    AND    EFFDT     <= $PerEndDt)
                    
end-SELECT
end-procedure Get-Company-Data

!**************************************
begin-procedure Report
!**************************************
Let $Company_ant = ''

begin-SELECT
WA.EMPLID
WA.COMPANY
WA.DEPTID       () on-break print=never  procedure=Department-Change
  Let $DeptId = &WA.DEPTID
WA.GP_PAYGROUP  () on-break print=never  procedure=PayGroup-Change
  Let $Gp_Paygroup = Rtrim(&WA.GP_PAYGROUP,' ')

WA.CAL_RUN_ID
    Let $Cal_RunID = &WA.CAL_RUN_ID
WA.HIRE_DT
  Let $HireDt = &WA.HIRE_DT
WA.DAYS
  Let #Days = &WA.DAYS
WA.DAILY_RT
  Let #DailyRt = &WA.DAILY_RT
WA.GPMX_ABSENCES
  Let #Absence = &WA.GPMX_ABSENCES
WA.GPMX_XMAS_DAYS
  Let #XmasDays = &WA.GPMX_XMAS_DAYS
WA.AMOUNT
  Let #Amount = &WA.AMOUNT
WA.TAX_AMT
  Let #TaxAmt = &WA.TAX_AMT
WA.NET_PAY
  Let #NetPay = &WA.NET_PAY
WA.PERIOD_END_DATE
  Let $PerEndDt = &WA.PERIOD_END_DATE
WA.JOBCODE
  Let $Jobcode = rtrim(&WA.JOBCODE, ' ')
  !show '$Jobcode: '  $Jobcode
WA.CAL_ID  

  Let $Company = &WA.COMPANY
   
!  show '$Company:     '  $Company ' $Company_ant: ' $Company_ant 
!  show '$Gp_Paygroup: '  $Gp_Paygroup 
   
  If ($Company_ant <> $Company)
      if $Company_ant <> ''
      
         Do Company-Change
         Let $Company_ant = $Company
      else
         Let $Company_ant = $Company
      end-if
      
  end-if
   
  Let $Emplid = &WA.EMPLID
  Do Job
  Do Find-PayeeName
  
  Let $Cal_ID = &WA.CAL_ID  
  print $Emplid              (+2,001)
  print $PayeeName            (0,015,28)
  do Format-DateTime($HireDt, $out, {DEFDMY},'','')
    print  $out                 (0,044)
    print  #days                (0,067)    edit 999.99
    print  #DailyRt             (0,083)    edit 99999.99
    print  #Absence             (0,102)
    print  #XmasDays            (0,114)    edit 999.99
    print  #Amount              (0,129)    edit 9999999.99
    print  #TaxAmt              (0,146)    edit 9999999.99
    print  #NetPay              (0,164)    edit 9999999.99
    print $Deptname            (+1,001,30)
    print $Jobname              (0,044,30)
    Let #TotAmt_Com=#TotAmt_Com+#Amount
    Let #TotAmt_Pay=#TotAmt_Pay+#Amount
    Let #TotAmt_Dep=#TotAmt_Dep+#Amount
    Let #TaxAmt_Com=#TaxAmt_Com+#TaxAmt
    Let #TaxAmt_Pay=#TaxAmt_Pay+#TaxAmt
    Let #TaxAmt_Dep=#TaxAmt_Dep+#TaxAmt

    Let #AbsDay_Com=#AbsDay_Com+#Absence
    Let #AbsDay_Pay=#AbsDay_Pay+#Absence
    Let #AbsDay_Dep=#AbsDay_Dep+#Absence

    Let #NetPay_Com=#NetPay_Com+#NetPay
    Let #NetPay_Pay=#NetPay_Pay+#NetPay
    Let #NetPay_Dep=#NetPay_Dep+#NetPay

    do Begin-Date

FROM  PS_GPMX_XMAS_WA     WA
[$SlctWhere1]
AND WA.RSLT_SEG_NUM = (SELECT MAX(WA2.RSLT_SEG_NUM) FROM PS_GPMX_XMAS_WA WA2
WHERE WA.EMPLID = WA2.EMPLID
AND WA.EMPL_RCD = WA2.EMPL_RCD
AND WA.GP_PAYGROUP = WA2.GP_PAYGROUP
AND WA.COMPANY = WA2.COMPANY)
ORDER BY WA.COMPANY, WA.GP_PAYGROUP, WA.DEPTID
end-SELECT

  do Get-Company-Data-Mex
  do PayGroup-Desc

  print '' (+1,1)
  do Department-Change
  do Paygroup-Change
  do Company-Change
  print '' (+1,1)
  print '-'                         (+1,001,176) fill
end-procedure Report

!**************************************
Begin-Procedure Job
!show '* Job *'
!**************************************
Begin-Select
J.JOBCODE
J.SETID_JOBCODE
J.EMPL_RCD
J.SETID_DEPT

  !show 'J.JOBCODE: '  &J.JOBCODE
  
  If $Jobcode = ''
     let $Jobcode = &J.JOBCODE
  end-if 
  let $SetIDJobcode = &J.SETID_JOBCODE
  let $SetIDDept = &J.SETID_DEPT
  Do Find-Jobname
  do find-Deptname
  
FROM PS_JOB J
WHERE J.EMPLID = $Emplid
AND J.EFFDT = (SELECT MAX(EFFDT) 
           FROM PS_JOB 
           WHERE EMPLID = J.EMPLID
           AND EMPL_RCD = J.EMPL_RCD
           AND EFFDT <= $PerEndDt)
AND J.EFFSEQ = (SELECT MAX(J2.EFFSEQ) 
           FROM PS_JOB J2
           WHERE J2.EMPLID = J.EMPLID
           AND J2.EMPL_RCD = J.EMPL_RCD
           AND J2.EFFDT <= J.EFFDT)           
End-SELECT
End-Procedure


!**************************************
Begin-Procedure PayGroup-Desc
!show '* PayGroup-Desc *'
!**************************************
BEGIN-SELECT
PY.GP_PAYGROUP
  Let $Paygroup= &PY.GP_PAYGROUP
PY.DESCR
  Let $PayDesc=  &PY.DESCR

FROM PS_GP_PYGRP PY
WHERE PY.GP_PAYGROUP = $Gp_Paygroup
END-SELECT
End-Procedure PayGroup-Desc

!**************************************
begin-procedure Company-Change
!show '** Company-Change **'
!**************************************
  Let $Company_ant = rtrim($Company_ant, ' ')
  Do Get-Company-Data-Mex-ant

  Move 'Totales por Compañía' to $TotalType
  Move $CompanyName to $Title 
  Concat ' - ' With $Company_ant
  Concat $Title With $Company_ant
  
  if $CompanyRC = ''
     Do Get-Company-Data
     Concat '   RFC: ' with $Company_ant
     Concat $Company_RFC with $Company_ant
  end-if
  
  Move $Company_ant to $Title
  Move #TotAmt_Com to #Amtnum
  Move #TaxAmt_Com to #Taxnum
  Move #AbsDay_Com to #Absnum
  Move #NetPay_Com to #Netnum
  do Print-Totales
  Let #TotAmt_Com=0
  Let #TaxAmt_Com=0
  Let #AbsDay_Com=0
  Let #NetPay_Com=0
  Let #Amtnum=0
  Let #Taxnum=0
  Let #Absnum=0
  Let #Netnum=0
  
end-procedure Company-Change

!**************************************
begin-procedure PayGroup-Change
!show '** PayGroup-Change **'
!**************************************
  Let $PrintPayGrp = 'Y'  
  Do PayGroup-Desc
  Move 'Totales por Grupo de Pago' to $TotalType
  Move $PayDesc to $Title
  Let $Paygroup = rtrim($Paygroup, ' ')
  Concat ' - ' With $Paygroup
  Concat $Title With $Paygroup
  Move $Paygroup to $Title
  Move #TotAmt_Pay to #Amtnum
  Move #TaxAmt_Pay to #Taxnum
  Move #AbsDay_Pay to #Absnum
  Move #NetPay_Pay to #Netnum
  !show '#TotAmt_Pay: ' #TotAmt_Pay '#TaxAmt_Pay: ' #TaxAmt_Pay '#AbsDay_Pay: ' #AbsDay_Pay '#NetPay_Pay: ' #NetPay_Pay
  
  do Print-Totales
  Let #TotAmt_Pay=0
  Let #TaxAmt_Pay=0
  Let #AbsDay_Pay=0
  Let #NetPay_Pay=0
  Let #Amtnum=0
  Let #Taxnum=0
  Let #Absnum=0
  Let #Netnum=0

end-procedure PayGroup-Change

!**************************************
begin-procedure Department-Change
!show '** Department-Change **'
!**************************************
  Let $PrintDeptid = 'Y'  
  Move 'Totales por Departamento' to $TotalType
  Move $Deptname to $Title
  Let $DeptId = rtrim($DeptId, ' ')
  Concat ' - ' With $DeptId
  Concat $Title With $DeptId
  Move $DeptId to $Title
  Move #TotAmt_Dep to #Amtnum
  Move #TaxAmt_Dep to #Taxnum
  Move #AbsDay_Dep to #Absnum
  Move #NetPay_Dep to #Netnum
  do Print-Totales
  Let #TotAmt_Dep=0
  Let #TaxAmt_Dep=0
  Let #AbsDay_Dep=0
  Let #NetPay_Dep=0
  Let #Amtnum=0
  Let #Taxnum=0
  Let #Absnum=0
  Let #Netnum=0

end-procedure Department-Change


begin-procedure Print-Totales
  print $TotalType (+2,15) BOLD
  print $Title      (0,44) BOLD
  do Format-Number(#Absnum, $out1, '999,999,999.99mi')
  do Format-Number(#Amtnum, $out2, '999,999,999.99mi')
  do Format-Number(#Taxnum, $out3, '999,999,999.99mi')
  do Format-Number(#Netnum, $out4, '999,999,999.99mi')
  print $out1 (0, 92)  BOLD
  print $out2 (0,124)  BOLD
  print $out3 (0,141)  BOLD
  print $out4 (0,159)  BOLD
end-procedure Print-Totales


Begin-Procedure Find-Deptname

Let $Deptname = ''
Begin-Select
DP.DESCR
  let $Deptname = &DP.DESCR
  
FROM PS_DEPT_TBL_LANG DP
WHERE DP.DEPTID = $DeptId 
AND DP.EFFDT = (SELECT MAX(L.EFFDT) 
                FROM PS_DEPT_TBL_LANG L 
                WHERE L.DEPTID = DP.DEPTID                 
                AND L.SETID = DP.SETID 
                AND L.LANGUAGE_CD = DP.LANGUAGE_CD
                AND L.EFFDT <= $PerEndDt )
AND DP.SETID = $SetIDDept      
AND DP.LANGUAGE_CD = $Curr_language_Cd   
End-Select

IF $Deptname = ''
Begin-Select
DP1.DESCR
  let $Deptname = &DP1.DESCR
FROM PS_DEPT_TBL DP1
WHERE DP1.DEPTID = $DeptId 
AND DP1.EFFDT = (SELECT MAX(EFFDT) FROM PS_DEPT_TBL WHERE
            DEPTID = $DeptId AND EFFDT <= $PerEndDt
            AND SETID = $SetIDDept)
AND DP1.SETID = $SetIDDept            
End-Select
END-IF
End-Procedure

!**************************************
Begin-Procedure Find-Jobname
!show '* Find-Jobname *'
!**************************************
!show '$Curr_language_Cd '  $Curr_language_Cd
!show '$SetIDJobcode ' $SetIDJobcode
!show '$Jobcode ' $Jobcode
!show '$PerEndDt ' $PerEndDt
let $JobName = ''

Begin-Select
JC.DESCR

   let $JobName = rtrim(&JC.DESCR, ' ')
   !show '$JobName: ' $JobName
   
FROM PS_JOBCODE_LANG JC
WHERE JC.SETID = $SetIDJobcode
AND JC.JOBCODE = $Jobcode 
AND JC.EFFDT = (SELECT MAX(EFFDT) 
                FROM PS_JOBCODE_LANG
                WHERE SETID = JC.SETID
                AND JOBCODE = JC.JOBCODE
                AND LANGUAGE_CD = JC.LANGUAGE_CD
                AND EFFDT <= $PerEndDt)
AND JC.LANGUAGE_CD = $Curr_language_Cd                
End-SELECT

if $JobName = ''
Begin-Select
JC2.DESCR

   let $JobName = &JC2.DESCR
!    show '$JobName: ' $JobName
   
FROM PS_JOBCODE_TBL JC2
WHERE JC2.SETID = $SetIDJobcode
AND JC2.JOBCODE = $Jobcode 
AND JC2.EFFDT = (SELECT MAX(EFFDT) 
                FROM PS_JOBCODE_TBL 
                WHERE SETID = JC2.SETID
                AND JOBCODE = JC2.JOBCODE  
                AND EFFDT <= $PerEndDt)
End-SELECT
end-if
End-Procedure


Begin-Procedure Find-PayeeName
Begin-Select
N.NAME
   let $PayeeName = &N.NAME
FROM PS_PERSON_NAME N
WHERE
N.EMPLID = $Emplid
End-SELECT
End-Procedure

Begin-Procedure Begin-Date
#Debug show 'PROCEDURE: Begin-Date'

!Check if $Cal_RunID is Off Cycle
Begin-Select
OFF_CYCLE  
    
    Let $OffCycle = Rtrim(&OFF_CYCLE, ' ')
    
FROM PS_GP_CAL_RUN 
WHERE CAL_RUN_ID = $Cal_RunID
End-Select

If $OffCycle = 'Y'
#Debug show '  Running Offcycle Payroll' 
!Get Data from PS_GP_CAL_RUN_OFF

#Debug Show '  $Cal_RunID  :' $Cal_RunID
Begin-Select DISTINCT
OFFP.PRD_BGN_DT
OFFP.PRD_END_DT
OFFD.CAL_PRD_ID
OFFD.GP_PAYGROUP
OFFCAL.CAL_ID
OFFP.FREQUENCY_ID
OFFCAL.RUN_TYPE

  Let $BeginDt      = Rtrim(&OFFP.PRD_BGN_DT, ' ') 
  do  Format-DateTime($BeginDt, $BeginDtF, {DEFDMY},'','') 

    #Debug Show '  $BeginDt      :' $BeginDt
    #Debug Show '  $BeginDtF     :' $BeginDtF

FROM PS_GP_CAL_RUN_OFF OFFD,
PS_GP_CAL_PRD OFFP,
PS_GP_PYE_CAL OFFCAL,
PS_GP_RUN_TYPE OFFT
WHERE OFFD.CAL_RUN_ID = $Cal_RunID
AND OFFP.CAL_PRD_ID = OFFD.CAL_PRD_ID
AND OFFCAL.GP_PAYGROUP = OFFD.GP_PAYGROUP
AND OFFCAL.CAL_PRD_ID = OFFD.CAL_PRD_ID
AND OFFCAL.OFF_CYCLE_ID = OFFD.CAL_RUN_ID
AND OFFT.RUN_TYPE       = OFFCAL.RUN_TYPE
AND OFFT.CALC_TYPE      = 'P'
End-Select

Else
#Debug show '  Running Normal Payroll'
!Get Data from PS_GP_CAL_RUN_DTL

Begin-Select
BD.PRD_BGN_DT

  Let $BeginDt      = Rtrim(&BD.PRD_BGN_DT, ' ') 
  do  Format-DateTime($BeginDt, $BeginDtF, {DEFDMY},'','') 
  
FROM PS_GP_CAL_RUN_DTL BD
WHERE BD.CAL_RUN_ID=$Cal_RunID AND
BD.GP_PAYGROUP=$Gp_Paygroup AND
BD.CAL_ID=$Cal_ID
End-SELECT

End-if

End-Procedure

!**************************************
begin-procedure Get-Company-Data-Mex-ant
!show '** Get-Company-Data-Mex-ant **'
!show '   $Company_ant: ' $Company_ant
!show '   $AsOfToday:    ' $AsOfToday
!**************************************
begin-SELECT
CO.COMPANY
CO.DESCR

   let $CompanyName      = rtrim(&CO.DESCR,' ')
 !  show '$CompanyName: ' $CompanyName
   
FROM  PS_COMPANY_TBL CO    
WHERE CO.COMPANY = $Company_ant
  AND CO.EFFDT = (SELECT MAX(EFFDT) FROM PS_COMPANY_TBL
                  WHERE  COMPANY = CO.COMPANY
                  AND  EFFDT  <= $AsOfToday)
end-SELECT

   
if $curr_language_cd <> $Psoptions_Language_Cd
   do Get-Rel-Lang-Comp
end-if

end-procedure Get-Company-Data-Mex-ant

#Include 'getcomex.sqc'  !Get selected fields from Company Table and Mexico Fields
#Include 'getlocnm.sqc'  !Get-Location-Name
#Include 'getpgdta.sqc'  !Get-PayGroup-Data procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'payrnctl.sqc'  !Get-Run-Control procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'
