!-----------------------------------------------------------------------
!  STSU007:  Daily Prices
!-----------------------------------------------------------------------
!
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2013/01/18:02:56:14                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!-----------------------------------------------------------------------
! Program Descr: Reports all daily prices within a given date range.
!-----------------------------------------------------------------------

  #include 'setenv.sqc'

!-----------------------------------------------------------------------
! Procedure: Begin-Setup
! Descr:     Main Report Procedure.
!-----------------------------------------------------------------------
Begin-Setup

  #include 'ptpsl177.sqc'

End-Setup

!-----------------------------------------------------------------------
! Procedure: Begin-Program
! Descr:     Main Report Procedure.
!-----------------------------------------------------------------------
Begin-Program

  do Init-Report
  do Process-Main
  do Reset
  do Successful-EOJ

  let $CurrentTime = datenow()
  show 'Process ended at:   ' $CurrentTime      !standard output info to sqr.log

End-Program

!-----------------------------------------------------------------------
! Procedure: Init-Report
! Descr:     Report initialization Procedure.  Set titles, parameters.
!            This is called from the Begin-Program Procedure.
!-----------------------------------------------------------------------
Begin-Procedure Init-Report

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Define-Prcs-Vars
  do Get-Run-Control-Parms
  let $ReportId = 'STSU007'
  do Get-Report-Language
  if $prcs_process_instance = ''
    do Ask-From-Thru-Date
  else
    do Get-SQR-Parms
  end-if
  do Init-Variables

End-Procedure

!-----------------------------------------------------------------------
! Procedure: Init-Variables
! Descr:     Initializes all report variables
!-----------------------------------------------------------------------
Begin-Procedure Init-Variables

  let $StockId     = rtrim($RC_STOCK_ID,' ')
  let $RC_FromDate = rtrim($RC_FromDate,' ')
  let $RC_ThruDate = rtrim($RC_ThruDate,' ')
  let #Count       = 0
  let #TotBid      = 0
  let #TotAsk      = 0
  let #TotOpen     = 0
  let #TotClose    = 0
  let #TotLow      = 0
  let #TotHigh     = 0
  let #TotPrivate  = 0

End-Procedure

!-----------------------------------------------------------------------
! Procedure: Report-Translation
! Descr:     This procedure is used to read language-dependent text from
!            the database.
!-----------------------------------------------------------------------
Begin-Procedure Report-Translation

  do Init_Report_Translation('STOCK', $Language_Cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK','FROM',           $FromLbl,        #DW)
  do Get_Field_Information ('STOCK','THRU',           $ThruLbl,        #DW)
  do Get_Field_Information ('STOCK','STOCK_ID',       $StockIdLbl,     #DW)

  do Get_Field_Information ('STSU007','STSU007',      $ReportTitle,    #DW)
  do Get_Field_Information ('STSU007','ASK',          $AskLbl,         #DW)
  do Get_Field_Information ('STSU007','BID',          $BidLbl,         #DW)
  do Get_Field_Information ('STSU007','OPEN',         $OpenLbl,        #DW)
  do Get_Field_Information ('STSU007','CLOSE',        $CloseLbl,       #DW)
  do Get_Field_Information ('STSU007','HIGH',         $HighLbl,        #DW)
  do Get_Field_Information ('STSU007','LOW',          $LowLbl,         #DW)
  do Get_Field_Information ('STSU007','PRIVATE',      $PrivateLbl,     #DW)
  do Get_Field_Information ('STSU007','FMV_DT',       $FmvDtLbl,       #DW)
  do Get_Field_Information ('STSU007','DAY_TYPE',     $TypeLbl,        #DW)
  do Get_Field_Information ('STSU007','AVERAGE',      $AvgLbl,         #DW)
  do Get_Field_Information ('STSU007','TOTAL_TRADING',$TotalTradingLbl,#DW)

End-Procedure

!-----------------------------------------------------------------------
! Procedure: Begin-Heading
! Descr:     Prints at the beginning of each page
!-----------------------------------------------------------------------
begin-heading 06

  #Include 'stdhdg01.sqc'

  print $StockIdLbl                                        (-1,001,008)
  print ':'                                                (  ,   ,001)
  print $StockId                                           (  , +3,010)

  do Format-DateTime ($RC_FromDate, $Date1, {DEFDATE}, '', '')
  do Format-DateTime ($RC_ThruDate, $Date2, {DEFDATE}, '', '')
  let $FromThruDate = $FromLbl || ': ' || $Date1 || '  ' || $ThruLbl || ': ' || $Date2
  print $FromThruDate                                      (  ,   ,   ) center

  print $FmvDtLbl                                          (+3,001,010)
  print $TypeLbl                                           (  ,012,008)
  print $BidLbl                                            (  ,037,003)
  print $AskLbl                                            (  ,057,003)
  print $OpenLbl                                           (  ,076,004)
  print $CloseLbl                                          (  ,095,005)
  print $LowLbl                                            (  ,117,003)
  print $HighLbl                                           (  ,136,004)
  print $PrivateLbl                                        (  ,153,007)
  print '-'                                                (+1,001,177) fill bold

End-Heading

!-----------------------------------------------------------------------
! Section: Process-Main
! Descr:   Selects all Grant info for paramteres passed by user.
!-----------------------------------------------------------------------
Begin-Procedure Process-Main

Begin-Select

A.STOCK_ID
A.ST_FMV_DT
A.ST_DAY_TYPE
A.ST_TRADING_DAY
A.BID_PRICE
A.ASK_PRICE
A.OPEN_PRICE
A.CLOSE_PRICE
A.LOW_PRICE
A.HIGH_PRICE
A.PRIVATE_PRICE

  do Print-Detail
  add &a.bid_price     to #TotBid
  add &a.ask_price     to #TotAsk
  add &a.open_price    to #TotOpen
  add &a.close_price   to #TotClose
  add &a.low_price     to #TotLow
  add &a.high_price    to #TotHigh
  add &a.private_price to #TotPrivate
  add 1 to #Count

from PS_ST_DAILY_PRICES A
where A.STOCK_ID   = $StockId
  and A.ST_FMV_DT >= $RC_FromDate
  and A.ST_FMV_DT <= $RC_ThruDate
order BY A.ST_FMV_DT

End-Select

  if #Count > 1
    do Process-Totals
    do Print-Totals
  end-if

End-Procedure

!-----------------------------------------------------------------------
! Section: Print-Detail
! Descr:   Prints the report detail.
!-----------------------------------------------------------------------
Begin-Procedure Print-Detail

  do Format-DateTime (&a.st_fmv_dt, $FmvDt, {DEFDATE}, '', '')
  print $FmvDt                                             (+1,001,010)
  print &a.st_day_type                                     (  ,012,008)
  print &a.bid_price                                       (  ,022,018) edit $$$,$$9,999.999999
  print &a.ask_price                                       (  ,042,018) edit $$$,$$9,999.999999
  print &a.open_price                                      (  ,062,018) edit $$$,$$9,999.999999
  print &a.close_price                                     (  ,082,018) edit $$$,$$9,999.999999
  print &a.low_price                                       (  ,102,018) edit $$$,$$9,999.999999
  print &a.high_price                                      (  ,122,018) edit $$$,$$9,999.999999
  print &a.private_price                                   (  ,142,018) edit $$$,$$9,999.999999

End-Procedure

!-----------------------------------------------------------------------
! Section: Process-Totals
! Descr:   Selects and prints High, Low and Averages for each type.
!-----------------------------------------------------------------------
Begin-Procedure Process-Totals

Begin-Select
MAX(BID_PRICE)  &maxbid
MIN(BID_PRICE)  &minbid

from PS_ST_DAILY_PRICES
where STOCK_ID     = $StockId
  and ST_FMV_DT   >= $RC_FromDate
  and ST_FMV_DT   <= $RC_ThruDate
  and ST_DAY_TYPE  = 'TRD'
End-Select


Begin-Select
MAX(ASK_PRICE)  &maxask
MIN(ASK_PRICE)  &minask

from PS_ST_DAILY_PRICES
where STOCK_ID     = $StockId
  and ST_FMV_DT   >= $RC_FromDate
  and ST_FMV_DT   <= $RC_ThruDate
  and ST_DAY_TYPE  = 'TRD'
End-Select


Begin-Select
MAX(OPEN_PRICE)  &maxopen
MIN(OPEN_PRICE)  &minopen

from PS_ST_DAILY_PRICES
where STOCK_ID     = $StockId
  and ST_FMV_DT   >= $RC_FromDate
  and ST_FMV_DT   <= $RC_ThruDate
  and ST_DAY_TYPE  = 'TRD'
End-Select


Begin-Select
MAX(CLOSE_PRICE)  &maxclose
MIN(CLOSE_PRICE)  &minclose

from PS_ST_DAILY_PRICES
where STOCK_ID     = $StockId
  and ST_FMV_DT   >= $RC_FromDate
  and ST_FMV_DT   <= $RC_ThruDate
  and ST_DAY_TYPE  = 'TRD'
End-Select


Begin-Select
MAX(LOW_PRICE)  &maxlow
MIN(LOW_PRICE)  &minlow

from PS_ST_DAILY_PRICES
where STOCK_ID     = $StockId
  and ST_FMV_DT   >= $RC_FromDate
  and ST_FMV_DT   <= $RC_ThruDate
  and ST_DAY_TYPE  = 'TRD'
End-Select


Begin-Select
MAX(HIGH_PRICE)  &maxhigh
MIN(HIGH_PRICE)  &minhigh

from PS_ST_DAILY_PRICES
where STOCK_ID     = $StockId
  and ST_FMV_DT   >= $RC_FromDate
  and ST_FMV_DT   <= $RC_ThruDate
  and ST_DAY_TYPE  = 'TRD'
End-Select


Begin-Select
MAX(PRIVATE_PRICE)  &maxprivate
MIN(PRIVATE_PRICE)  &minprivate

from PS_ST_DAILY_PRICES
where STOCK_ID     = $StockId
  and ST_FMV_DT   >= $RC_FromDate
  and ST_FMV_DT   <= $RC_ThruDate
  and ST_DAY_TYPE  = 'TRD'
End-Select

 let #AvgBid     = (#TotBid     / #Count)
 let #AvgAsk     = (#TotAsk     / #Count)
 let #AvgOpen    = (#TotOpen    / #Count)
 let #AvgClose   = (#TotClose   / #Count)
 let #AvgLow     = (#TotLow     / #Count)
 let #AvgHigh    = (#TotHigh    / #Count)
 let #AvgPrivate = (#TotPrivate / #Count)

End-Procedure

!-----------------------------------------------------------------------
! Section: Print-Totals
! Descr:   Prints High, Low and Averages for each type.
!-----------------------------------------------------------------------
Begin-Procedure Print-Totals

  print $TotalTradingLbl                                   (+2,001,018) underline
  print $HighLbl                                           (+1,012,004)
  print &maxbid                                            (  ,022,018) edit $$$,$$9,999.999999
  print &maxask                                            (  ,042,018) edit $$$,$$9,999.999999
  print &maxopen                                           (  ,062,018) edit $$$,$$9,999.999999
  print &maxclose                                          (  ,082,018) edit $$$,$$9,999.999999
  print &maxlow                                            (  ,102,018) edit $$$,$$9,999.999999
  print &maxhigh                                           (  ,122,018) edit $$$,$$9,999.999999
  print &maxprivate                                        (  ,142,018) edit $$$,$$9,999.999999

  print $LowLbl                                            (+1,012,004)
  print &minbid                                            (  ,022,018) edit $$$,$$9,999.999999
  print &minask                                            (  ,042,018) edit $$$,$$9,999.999999
  print &minopen                                           (  ,062,018) edit $$$,$$9,999.999999
  print &minclose                                          (  ,082,018) edit $$$,$$9,999.999999
  print &minlow                                            (  ,102,018) edit $$$,$$9,999.999999
  print &minhigh                                           (  ,122,018) edit $$$,$$9,999.999999
  print &minprivate                                        (  ,142,018) edit $$$,$$9,999.999999

  print $AvgLbl                                            (+1,012,007)
  print #AvgBid                                            (  ,022,018) edit $$$,$$9,999.999999
  print #AvgAsk                                            (  ,042,018) edit $$$,$$9,999.999999
  print #AvgOpen                                           (  ,062,018) edit $$$,$$9,999.999999
  print #AvgClose                                          (  ,082,018) edit $$$,$$9,999.999999
  print #AvgLow                                            (  ,102,018) edit $$$,$$9,999.999999
  print #AvgHigh                                           (  ,122,018) edit $$$,$$9,999.999999
  print #AvgPrivate                                        (  ,142,018) edit $$$,$$9,999.999999

End-Procedure

!-----------------------------------------------------------------------
!SQC Include Files
!!-----------------------------------------------------------------------
#include 'sqlerr.sqc'      !SQL error processing
#include 'prcsapi.sqc'     !Process Scheduler API
#include 'prcsdef.sqc'     !Update Process Open_Trans variable declaration
#include 'eoj.sqc'         !End of Report Format
#include 'curdttim.sqc'    !Get-Current-Date-Time
#include 'datetime.sqc'    !Get-Date-Time
#include 'datemath.sqc'    !Date Math Routines
#include 'reset.sqc'       !Reset printer procedure
#include 'number.sqc'      !Routines to format numbers
#include 'sqrtrans.sqc'    !Translate SQR strings to given language
#include 'getrplng.sqc'    !Get the report language
#include 'readxlat.sqc'    !Read the Translate Table for the desired values.
#include 'strunctl.sqc'    !Runcontrol
#include 'askftd.sqc'      !Ask the from-thru-date
#include 'stgetfmv.sqc'    !Get Grant FMV
