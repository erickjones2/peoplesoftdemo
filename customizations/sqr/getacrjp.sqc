!***********************************************************************
!  Getactrsjp:  Get ActionReason Name from the Action Reason Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!          $Date:  2013/01/17:13:38:36                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!
!************************************
begin-procedure Get-ActionReason-Name
!************************************

  !Keys passed in:  $ACTION, $ACTION_REASON, $AsOfDate
  !Values returned: $ActionReasonName, $ActionReasonNameAbbrv

  move '' to $ActionReasonName
  move '' to $ActionReasonNameAbbrv

  if Rtrim($ACTION,' ') = '' or Rtrim($ACTION_REASON,' ') = ''
    goto GetActReasonExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

  let $sql-statement = 'An error occurred in reading Action Reason Table(ACTN_REASON_TBL).'

begin-SELECT  ON-ERROR=SQL-Error
ACR.DESCR
ACR.DESCRSHORT

  let $ActionReasonName      = Rtrim(&ACR.DESCR,' ')
  let $ActionReasonNameAbbrv = Rtrim(&ACR.DESCRSHORT,' ')

FROM  PS_ACTN_REASON_TBL ACR
WHERE ACR.ACTION        = $ACTION
  AND ACR.ACTION_REASON = $ACTION_REASON
  AND ACR.EFFDT  =
       (SELECT MAX(EFFDT)
        FROM   PS_ACTN_REASON_TBL
        WHERE  ACTION        = ACR.ACTION
          AND  ACTION_REASON = ACR.ACTION_REASON
          AND  EFFDT <= $AsOfDate)
end-SELECT

  do Get-ActionReason-Name-Lang

GetActReasonExit:

end-procedure

!***************************************
begin-procedure Get-ActionReason-Name-Lang
!***************************************

  !Keys passed in:  $ACTION, $ACTION_REASON, $AsOfDate
  !Values returned: $ActionReasonName, $ActionReasonNameAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetActReasonLngExit
  end-if

  if Rtrim($ACTION,' ') = '' or Rtrim($ACTION_REASON,' ') = ''
    goto GetActReasonLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

  let $sql-statement = 'An error occurred in reading Action Reason Language Table(ACTN_RSN_LANG).'

begin-SELECT  ON-ERROR=SQL-Error
ACRL.DESCR
ACRL.DESCRSHORT

  if Rtrim(&ACRL.DESCR,' ') <> ''
    let $ActionReasonName = Rtrim(&ACRL.DESCR,' ')
  end-if
  if Rtrim(&ACRL.DESCRSHORT,' ') <> ''
    let $ActionReasonNameAbbrv = Rtrim(&ACRL.DESCRSHORT,' ')
  end-if

FROM  PS_ACTN_RSN_LANG ACRL
WHERE ACRL.ACTION        = $ACTION
  AND ACRL.ACTION_REASON = $ACTION_REASON
  AND ACRL.LANGUAGE_CD   = $Curr_Language_Cd
  AND ACRL.EFFDT =
        (SELECT MAX(EFFDT)
         FROM  PS_ACTN_RSN_LANG
         WHERE ACTION        = ACRL.ACTION
           AND ACTION_REASON = ACRL.ACTION_REASON
           AND LANGUAGE_CD   = ACRL.LANGUAGE_CD
           AND EFFDT        <= $AsOfDate)
end-SELECT

GetActReasonLngExit:

end-procedure
!***************************** End of Include File **************************
