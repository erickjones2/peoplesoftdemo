!***********************************************************************
!                   GPINLWF:  LWF Deduction Report                     *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!          $Date:  2013/01/18:04:00:06                                 *
!       $Release:  HR92                                                *
!      $Revision:  101                                                 *
!***********************************************************************

#Include 'setenv.sqc'  !Set environment

Begin-Setup

   declare-layout LWF
      paper-size   = (16.54,11.69)
      orientation  = LANDSCAPE
      line-height  = 8
   end-declare

   declare-report LWF_RPT
      layout = LWF
   end-declare

   declare-variable
       date $JobDate
       date $Emp_Begin_Dt
   end-declare

End-Setup

Begin-footing 1
    page-number (,148) 'Page '
End-footing

!***********************************************************************
begin-program

   do Init-Report
   do Process-Main
   do Stdapi-Term
end-program

!***********************************************************************
! Procedure:    Init_Val                                               *
!                                                                      *
! Description: Prints the Header of the report                         *
!                                                                      *
!***********************************************************************
begin-procedure   Init_Val
   let #Heading_Font      = 4
   let #HeadingPntSize    = 9
   let #HeadingPntSize1   = 14
   let #StdPntSize        = 6

   let #depth3     = 1
   let #Start_Line = 1
end-procedure

!***********************************************************************
!  Procedure: Init Report                                              *
!                                                                      *
!***********************************************************************
begin-procedure Init-Report
   move 'GPINPY03' to $ReportID
   do Init-DateTime
   do Init-Number
   do Get-Current-DateTime
   do Stdapi-Init

   if $proc__instance = ''
   else
      let #proc_instance=To_number($proc__instance)
   end-if

   do Get_Translat_Info
   do Get_Run_Cntrl_Dtls
   do Init_Val
end-procedure !Init-Report


!***************************************************************************************
!  Procedure: Get_Run_Cntrl_Dtls                                                       *
!  Purpose  : This procedure gets the run control values from the run control record   *
!                                                                                      *
!***************************************************************************************
Begin-Procedure Get_Run_Cntrl_Dtls
Begin-Select
RNCT.GPIN_LWF_CONTRIB
RNCT.GPIN_LWF_OFFICE
RNCT.GPIN_YEAR
RNCT.BGN_DT
RNCT.END_DT
RNCT.ESTABID
RNCT.GPIN_LWF_CHEQUE_NO
RNCT.GPIN_LWF_CHEQ_DATE
RNCT.GPIN_LWF_BANK_DTL

    move &RNCT.GPIN_LWF_CONTRIB to #contrib_id
    move &RNCT.GPIN_LWF_OFFICE to $LWF_Office
    let $Begin_Date = edit(&RNCT.BGN_DT,'dd/mm/yyyy')
    let $End_Date   = edit(&RNCT.END_DT,'dd/mm/yyyy')
    let $ChequeDD_No= &RNCT.GPIN_LWF_CHEQUE_NO
    let $chq_date   = edit(&RNCT.GPIN_LWF_CHEQ_DATE,'dd/mm/yyyy')
    let $bank       = &RNCT.GPIN_LWF_BANK_DTL

FROM PS_GPIN_FORMLWF_RC RNCT
WHERE  RNCT.OPRID    = $prcs_oprid
AND RNCT.RUN_CNTL_ID = $prcs_run_cntl_id
End-Select
End-Procedure    ! Get_Run_Cntrl_Dtls

!**************************************************************************
! Procedure:   Process-Main                                               *
! Description: The main process                                           *
!                                                                         *
!**************************************************************************
begin-procedure Process-Main

    do Init_Variables
    do Get_LWF_Data
    if #Tot_LWF <> 0
      do Print_Report
    end-if

End-Procedure

!***********************************************************************
! Procedure:    Init_Variables                                         *
!                                                                      *
! Description: Printing form 7                                         *
!                                                                      *
!***********************************************************************

begin-procedure  Init_Variables

    let $Prev_Emplid   = ''
    let $Prev_Reg_No   = ''
    let #Sl_No = 0

    let #Year1 = &RNCT.GPIN_YEAR
    let $Year = edit(#Year1,'9999')

    if rtrim(ltrim(&RNCT.GPIN_LWF_CONTRIB, ' '), ' ') = '1'
       let $WHERECL = 'AND A.GPIN_YEAR = ' || ''''||$Year||''''
       let $Period = 'January-December'
    end-if

    if rtrim(ltrim(&RNCT.GPIN_LWF_CONTRIB, ' '), ' ') = '2'

       let $WHERECL = 'AND(A.GPIN_YEAR = ' || ''''||$Year||'''' ||'  AND A.MONTH in (1,2,3,4,5,6))'
       let $Period = 'January-June'
    end-if

    if rtrim(ltrim(&RNCT.GPIN_LWF_CONTRIB, ' '), ' ') = '3'
       let $WHERECL = 'AND(A.GPIN_YEAR = ' || ''''||$Year||'''' ||'  AND A.MONTH in (7,8,9,10,11,12))'
       let $Period = 'July-December'
    end-if
end-procedure


!***********************************************************************
! Procedure:    Print_Report                                           *
!                                                                      *
! Description: Printing LWF deduction report                           *
!                                                                      *
!***********************************************************************

begin-procedure  Print_Report
    do Get_Header_Data
    do Print_Header
    do Print_table
    do Print_footer
end-procedure

!***********************************************************************
! Procedure:    Get_Header_Data                                        *
!                                                                      *
! Description: Get data for the header of report                       *
!                                                                      *
!***********************************************************************
begin-procedure Get_Header_Data
  let $Establishmentcode = &RNCT.ESTABID

Begin-select
DEPT.GPIN_LWF_DEPT_NUM
DEPT.GPIN_LWF_NBR
PN.NAME
JBT.DESCR
  let $local_lwfno = &DEPT.GPIN_LWF_DEPT_NUM
  let $accountno   = &DEPT.GPIN_LWF_NBR
  let $Estb_Head   = &PN.NAME
  let $desig       = &JBT.DESCR
FROM PS_ESTAB_TBL_GPIN DEPT , PS_PERSON_NAME PN, PS_GPIN_JBTITLE_VW JBT
WHERE DEPT.ESTABID=$Establishmentcode
AND DEPT.EFFDT =(SELECT MAX(DEPT1.EFFDT) FROM PS_ESTAB_TBL_GPIN DEPT1
                 WHERE DEPT1.ESTABID=$Establishmentcode
                 AND DEPT1.EFFDT <=&RNCT.END_DT)
AND DEPT.EMPLID=PN.EMPLID
AND DEPT.EMPLID=JBT.EMPLID
end-select

    let $Estab_Name = ''

Begin-select
ADDR2.GPIN_REG_NAME
ADDR.ADDRESS1
ADDR.ADDRESS2
ADDR.ADDRESS3
ADDR.ADDRESS4
ADDR.CITY
ADDR.POSTAL
ADDR.DESCR

    let $Estab_Name = ltrim(rtrim(&ADDR2.GPIN_REG_NAME,' '),' ')
    let $Address1   = ltrim(rtrim(&ADDR.ADDRESS1,' '),' ') ||', '||ltrim(rtrim(&ADDR.ADDRESS2, ' '),' ')
    let $Address2   = ltrim(rtrim(&ADDR.ADDRESS3,' '),' ') ||', '||ltrim(rtrim(&ADDR.ADDRESS4, ' '),' ')
    let $Address3   = ltrim(rtrim(&ADDR.CITY,' '),' ') ||' '||ltrim(rtrim(&ADDR.POSTAL, ' '),' ')
    let $Address4   = ltrim(rtrim(&ADDR.DESCR, ' '),' ')

FROM PS_GPIN_LWF_GEN_VW ADDR, PS_ESTAB_TBL_GPIN ADDR2
WHERE ADDR.ESTABID = $Establishmentcode
AND ADDR.EFFDT = (SELECT MAX(ADDR1.EFFDT) FROM PS_GPIN_LWF_GEN_VW ADDR1
                 WHERE ADDR1.ESTABID=$Establishmentcode
                 AND ADDR1.EFFDT <=&RNCT.END_DT)
AND ADDR2.ESTABID  = ADDR.ESTABID
and ADDR2.EFFDT    = (SELECT MAX(ADDR3.EFFDT) FROM PS_ESTAB_TBL_GPIN ADDR3
                       WHERE ADDR3.ESTABID = ADDR2.ESTABID
                       AND ADDR3.EFFDT <= &RNCT.END_DT)
end-select

end-procedure
!end of Get_Header_Data

!***********************************************************************
! Procedure:    Print_Header                                           *
!                                                                      *
! Description: Printing the report header                              *
!                                                                      *
!***********************************************************************

begin-procedure  Print_Header

         Let #FileCount = 1
         alter-printer  point-size=40  font=25

         Let #ROW =4
         print $Form_lbl                              (#ROW,70)  BOLD
         alter-printer  point-size=15  font=15
         ADD 3 TO #ROW
         print $Rule_lbl                              (#ROW,68)  BOLD
         ADD 6 TO #ROW
         alter-printer  point-size=15  font=20
         print $Head_dtl_lbl                          (#ROW,20)  BOLD
         ADD 6 TO #ROW
         alter-printer point-size=10  font=15
         print $year_lbl                              (#ROW,10)
         print $Year                                  (#ROW,35)
         print $period_lbl                            (#ROW,110)
         print $period                                (#ROW,125)
         ADD 2 TO #ROW
         print $Establishmentcode_lbl                 (#ROW,10)
         print $Establishmentcode                     (#ROW,35)
         print $accountno_lbl                         (#ROW,110)
         print $accountno                             (#ROW,125)
         ADD 2 TO #ROW
         print $local_lwfno_lbl                       (#ROW,10)
         print $local_lwfno                           (#ROW,35)
         ADD 2 TO #ROW
         Print $estb_lbl1                             (#ROW,10)
         ADD 1 TO #ROW
         Print $estb_lbl2                             (#ROW,10)
         print '.'                                    (,35,25)fill
         print $Estab_Name                            (#ROW,35)
         ADD 1 TO #ROW
         print '.'                                    (,35,25)fill
         print $Address1                              (#ROW,35)
         ADD 1 TO #ROW
         Print '.'                                    (#ROW,35,25)fill
         print $Address2                              (#ROW,35)
         ADD 1 TO #ROW
         Print '.'                                    (#ROW,35,25)fill
         print $Address3                              (#ROW,35)
         ADD 1 TO #ROW
         Print '.'                                    (#ROW,35,25)fill
         print $Address4                              (#ROW,35)
         ADD 4 TO #ROW
         Print $estb_head_lbl                         (#ROW,10)
         Print $Estb_Head                            (#ROW,35)
         Print $desig_lbl                             (#ROW,110)
         Print $desig                                 (#ROW,125)
         ADD 4 TO #ROW
end-procedure
! end of Print_Header


!***********************************************************************
! Procedure:    Print_table                                            *
!                                                                      *
! Description: Printing the table of the report                        *
!                                                                      *
!***********************************************************************
!
Begin-procedure Print_table

    let #VERTICLE_PT = #ROW - 3
    let #Box_Pt = #ROW - 2
    GRAPHIC (#Box_Pt,10,140) BOX  10

    print 'Total No. of Employees in Establishment.' (#ROW,13) BOLD wrap 20 4
    GRAPHIC (#VERTICLE_PT,35,10) VERT-LINE
    print 'No. of Employees Eligible for LWF.'       (#ROW,38) BOLD wrap 20 4
    GRAPHIC (#VERTICLE_PT,65,10) VERT-LINE
    print 'Amount of Employees Contribution.'        (#ROW,68) BOLD wrap 20 4
    GRAPHIC (#VERTICLE_PT,95,10) VERT-LINE
    print 'Amount of Employers Contribution.'        (#ROW,98) BOLD wrap 20 4
    GRAPHIC (#VERTICLE_PT,125,10) VERT-LINE
    print 'Total amount Remitted.'                   (#ROW,128) BOLD wrap 20 2
    add 3 to #ROW
    GRAPHIC                                          (#ROW,10,140) HORZ-LINE
    add 2 to #ROW
    !let $Tot_Empl_Estab = #Tot_Empl_Estab

    let $Tot_Empl_Estab = ltrim(edit(#Tot_Empl_Estab,'999999999999'), ' ')
    print $Tot_Empl_Estab                                         (#ROW,20)
    let $Tot_Empl_Elgbl = ltrim(edit(#Tot_Empl_Elgbl,'999999999999'), ' ')
    print $Tot_Empl_Elgbl                                         (#ROW,45)
    let $LWF_EE = ltrim(edit(#LWF_EE,'999999999999999.99'),' ')
    print $LWF_EE                                                 (#ROW,75)
    let $LWF_ER = ltrim(edit(#LWF_ER,'999999999999999.99'), ' ')
    print $LWF_ER                                                 (#ROW,105)
    let $Tot_LWF = ltrim(edit(#Tot_LWF,'9999999999999999.99'), ' ')
    print $Tot_LWF                                                (#ROW,135)
    add 13 to #ROW
End-procedure
! end of Print_table

!!***********************************************************************
!! Procedure:    Print_footer                                           *
!!                                                                      *
!! Description: Printing the footer of the report                       *
!!                                                                      *
!!***********************************************************************
!
Begin-procedure Print_footer

  print $ChequeDD_No_lbl                       (#ROW,70)
  print $ChequeDD_No                           (#ROW,93)
  GRAPHIC (#ROW,90,35) HORZ-LINE
  print $chq_date_lbl                          (,130,10)
  print $chq_date                              (#ROW,138)
  GRAPHIC (#ROW,135,15) HORZ-LINE
  add 3 to #ROW
  print $bank_lbl                              (#ROW,70,20)
  print $bank                                  (#ROW,93)
  GRAPHIC (#ROW,90,60) HORZ-LINE
  add 10 to #ROW
  print $decleration_lbl                       (#ROW,10)
  add 10 to #ROW
  print $footer_lbl1                           (#ROW,10,8)
  let $cur_date = ltrim(edit($current-date,'dd/mm/yyyy'),' ')
  print $cur_date                              (#ROW,18)
  GRAPHIC (#ROW,15,15) HORZ-LINE
  print $footer_lbl2                           (,115,25)
  GRAPHIC (#ROW,135,15) HORZ-LINE
end-Procedure
! end of Print_footer


!***********************************************************************
! Procedure:    Get_LWF_Data                                           *
!                                                                      *
! Description: Retrieving LWF Contributions for employee and employer  *
!                                                                      *
!***********************************************************************

begin-procedure  Get_LWF_Data
Begin-select
A.GPIN_LWF_EE
A.GPIN_LWF_ER
     let #LWF_EE = #LWF_EE + &A.GPIN_LWF_EE
     let #LWF_ER = #LWF_ER + &A.GPIN_LWF_ER
from PS_GPIN_LWF_RPT A,PS_GP_CAL_RUN F
WHERE A.ESTABID  = &RNCT.ESTABID
AND A.RSLT_REV_NUM = (SELECT MAX(A1.RSLT_REV_NUM)
                      FROM PS_GPIN_LWF_RPT A1 WHERE
                      A1.EMPLID=A.EMPLID
                      AND A1.CAL_RUN_ID=A.CAL_RUN_ID
                      AND A1.EMPL_RCD=A.EMPL_RCD
                      AND A1.GP_PAYGROUP=A.GP_PAYGROUP
                      AND A1.CAL_ID=A.CAL_ID
                      AND A1.ORIG_CAL_RUN_ID=A.ORIG_CAL_RUN_ID
                      AND A1.RSLT_SEG_NUM = A.RSLT_SEG_NUM
                      AND A1.RSLT_REV_NUM=A.RSLT_REV_NUM
                      AND A1.RSLT_VER_NUM=A.RSLT_VER_NUM
                      AND A1.GPIN_YEAR=A.GPIN_YEAR
                      AND A1.MONTH=A.MONTH)

AND A.CAL_RUN_ID = F.CAL_RUN_ID
AND F.RUN_FINALIZED_IND = 'Y'
[$WHERECL]
End-Select

     let #Tot_LWF = #LWF_EE + #LWF_ER

Begin-select DISTINCT
B.EMPLID
   Let #Tot_Empl_Estab = #Tot_Empl_Estab + 1
from PS_ESTAB_EMPLOY_VW B
WHERE B.ESTABID  = &RNCT.ESTABID
AND B.EFFDT <= &RNCT.END_DT
end-select

Begin-select DISTINCT
A.EMPLID
     let #Tot_Empl_Elgbl = #Tot_Empl_Elgbl + 1
from PS_GPIN_LWF_RPT A,PS_GP_CAL_RUN F
WHERE A.ESTABID  = &RNCT.ESTABID
and A.CAL_RUN_ID = F.CAL_RUN_ID
and F.RUN_FINALIZED_IND = 'Y'
[$WHERECL]
End-Select
!Begin-select DISTINCT
!C.EMPLID
!  Let #Tot_Empl_Elgbl = #Tot_Empl_Elgbl + 1
!from PS_GPIN_LWF_GEN_VW C
!where C.ESTABID  = &RNCT.ESTABID
!AND C.EFFDT <= &RNCT.END_DT
!AND C.GPIN_LWF_ELIGIBLE = 'Y'
!end-select
end-procedure
!End of procedure Get_LWF_Data


!***********************************************************************
!! Procedure:    Get_Translat_Info                                      *
!!                                                                      *
!! Description: Gets the strings printed in this report                 *
!!                                                                      *
!!***********************************************************************
!
begin-procedure Get_Translat_Info

 do Init_Report_Translation ($ReportID,$Language_Code)
 do Append_Report_Translation ('GPINPY03')

 do Get_Field_Information ('GPINPY03','LWF_FORM',            $Form_lbl,                  #DW)
 do Get_Field_Information ('GPINPY03','LWF_RULE',            $Rule_lbl,                  #DW)
 do Get_Field_Information ('GPINPY03','LWF_HEAD_DTL',        $Head_dtl_lbl,              #DW)

 do Get_Field_Information ('GPINPY03','LWF_YEAR',            $year_lbl,                  #DW)
 do Get_Field_Information ('GPINPY03','LWF_ESTBCODE',        $establishmentcode_lbl,     #DW)
 do Get_Field_Information ('GPINPY03','LWF_PERIOD',          $period_lbl,                #DW)
 do Get_Field_Information ('GPINPY03','LWF_ACCNO',           $accountno_lbl,             #DW)
 do Get_Field_Information ('GPINPY03','LWF_LWFNO',           $local_lwfno_lbl,           #DW)
 do Get_Field_Information ('GPINPY03','LWF_ESTAB_LBL1',      $estb_lbl1,                 #DW)
 do Get_Field_Information ('GPINPY03','LWF_ESTAB_LBL2',      $estb_lbl2,                 #DW)
 do Get_Field_Information ('GPINPY03','LWF_HEAD_ESTB',       $estb_head_lbl,             #DW)
 do Get_Field_Information ('GPINPY03','LWF_DESIGNATION',     $Desig_lbl,                 #DW)

 do Get_Field_Information ('GPINPY03','LWF_CHEQUE',          $ChequeDD_No_lbl,           #DW)
 do Get_Field_Information ('GPINPY03','LWF_CHQ_DATE',        $chq_date_lbl,              #DW)
 do Get_Field_Information ('GPINPY03','LWF_BANK',            $bank_lbl,                  #DW)
 do Get_Field_Information ('GPINPY03','LWF_DECLERATION',     $decleration_lbl,           #DW)
 do Get_Field_Information ('GPINPY03','LWF_FOOTER_LBL1',     $footer_lbl1,           #DW)
 do Get_Field_Information ('GPINPY03','LWF_FOOTER_LBL2',     $footer_lbl2,           #DW)


end-procedure !Get_Translat_Info

!#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for Date Arithmetic
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#include 'address.sqc'   !global address formating
#include 'readxlat.sqc'  ! Get shortname or longname from xlattable
#Include 'useprntr.sqc'
#Include 'sqrtrans.sqc'