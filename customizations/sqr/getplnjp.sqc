!***********************************************************************
!  Getplnjp:  Get SalAdminPlan Name from the Sal Admin Plan Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!          $Date:  2013/01/17:13:40:34                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!
!************************************
begin-procedure Get-SalAdminPlan-Name
!************************************

  !Keys passed in:  $SETID_SALARY, $SAL_ADMIN_PLAN, $AsOfDate
  !Values returned: $SalAdminPlanName, $SalAdminPlanNameAbbrv

  move '' to $SalAdminPlanName
  move '' to $SalAdminPlanNameAbbrv

  if Rtrim($SAL_ADMIN_PLAN,' ') = ''
    goto GetSalAdminExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

  let $sql-statement = 'An error occurred in reading Salary Admin Plan Table(SAL_PLAN_TBL).'

begin-SELECT  ON-ERROR=SQL-Error
PLN.DESCR
PLN.DESCRSHORT

  let $SalAdminPlanName      = Rtrim(&PLN.DESCR,' ')
  let $SalAdminPlanNameAbbrv = Rtrim(&PLN.DESCRSHORT,' ')

FROM  PS_SAL_PLAN_TBL PLN
WHERE PLN.SETID          = $SETID_SALARY
  AND PLN.SAL_ADMIN_PLAN = $SAL_ADMIN_PLAN
  AND PLN.EFFDT  =
       (SELECT MAX(EFFDT)
        FROM   PS_SAL_PLAN_TBL
        WHERE  SETID          = PLN.SETID
          AND  SAL_ADMIN_PLAN = PLN.SAL_ADMIN_PLAN
          AND  EFFDT <= $AsOfDate)
end-SELECT

  do Get-SalAdminPlan-Name-Lang

GetSalAdminExit:

end-procedure

!***************************************
begin-procedure Get-SalAdminPlan-Name-Lang
!***************************************

  !Keys passed in:  $SAL_ADMIN_PLAN, $AsOfDate
  !Values returned: $SalAdminPlanName, $SalAdminPlanNameAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetSalAdminLngExit
  end-if

  if Rtrim($SAL_ADMIN_PLAN,' ') = ''
    goto GetSalAdminLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

  let $sql-statement = 'An error occurred in reading Salary Admin Plan Language Table(SAL_PLAN_LANG).'

begin-SELECT  ON-ERROR=SQL-Error
PLNL.DESCR
PLNL.DESCRSHORT

  if Rtrim(&PLNL.DESCR,' ') <> ''
    let $SalAdminPlanName = Rtrim(&PLNL.DESCR,' ')
  end-if
  if Rtrim(&PLNL.DESCRSHORT,' ') <> ''
    let $SalAdminPlanNameAbbrv = Rtrim(&PLNL.DESCRSHORT,' ')
  end-if

FROM  PS_SAL_PLAN_LANG PLNL
WHERE PLNL.SETID          = $SETID_SALARY
  AND PLNL.SAL_ADMIN_PLAN = $SAL_ADMIN_PLAN
  AND PLNL.LANGUAGE_CD    = $Curr_Language_Cd
  AND PLNL.EFFDT =
        (SELECT MAX(EFFDT)
         FROM  PS_SAL_PLAN_LANG
         WHERE SETID          = PLNL.SETID
           AND SAL_ADMIN_PLAN = PLNL.SAL_ADMIN_PLAN
           AND LANGUAGE_CD    = PLNL.LANGUAGE_CD
           AND EFFDT         <= $AsOfDate)
end-SELECT

GetSalAdminLngExit:

end-procedure
!***************************** End of Include File **************************
