!*****************************************************
!  GetJobTl:  Get Job Title from the Job Code Table  *
!*****************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:31:33                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

begin-procedure Get-Job-Title
  if rtrim($JobCode,' ') = ''
    move '' to $JobTitle
    move '' to $JobTitleAbbrv
    goto GetJobTlExit
  end-if
  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
    move $AsOfToday to $AsOfDate
  end-if
begin-SELECT
JT.DESCR,JT.DESCRSHORT,JT.SETID
FROM  PS_JOBCODE_TBL JT
WHERE JT.SETID   = $SetID
  AND JT.JOBCODE = $JobCode
  AND JT.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_JOBCODE_TBL
       WHERE  JOBCODE = $JobCode
         AND  SETID = $SetID
         AND  EFFDT  <= $AsOfDate)
end-SELECT
  let $JobTitle       = rtrim(&JT.Descr,' ')
  let $JobTitleAbbrv  = rtrim(&JT.DescrShort,' ')

  let $SetIDLang = Rtrim(&JT.SETID,' ')
  do Get-Job-Title-Lang

GetJobTlExit:
end-procedure

!*********************************
begin-procedure Get-Job-Title-Lang
!*********************************

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetJobTlLngExit
  end-if

  if Rtrim($JobCode,' ') = '' or Rtrim ($SetIDLang,' ') = ''
    goto GetJobTlLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
JTL.DESCR
JTL.DESCRSHORT

  if rtrim(&JTL.Descr,' ') <> ''
    let $JobTitle = Rtrim(&JTL.Descr,' ')
  end-if

  if rtrim(&JTL.DescrShort,' ') <> ''
    let $JobTitleAbbrv = Rtrim(&JTL.DescrShort,' ')
  end-if

FROM PS_JOBCODE_LANG JTL
WHERE JTL.JOBCODE     = $JobCode
  AND JTL.SETID       = $SetIDLang
  AND JTL.LANGUAGE_CD = $Curr_Language_Cd
  AND JTL.EFFDT      <= $AsOfDate
  AND JTL.EFFDT =
         (SELECT MAX(EFFDT)
          FROM PS_JOBCODE_LANG
          WHERE JOBCODE     = $JobCode
          AND SETID         = JTL.SETID
          AND LANGUAGE_CD   = JTL.LANGUAGE_CD
          AND EFFDT        <= $AsOfDate)

end-SELECT

GetJobTlLngExit:

end-procedure
