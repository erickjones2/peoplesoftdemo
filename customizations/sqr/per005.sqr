!********************************************
!  PER005:  Employees on Leaves of Absence  *
!********************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2014, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2014/10/08:01:19:06                                 *
!       $Release:  HR92                                                *
!      $Revision:  102                                                 *
!                                                                      *
!***********************************************************************
!Porcedures ending with "Scrty" and Record alias ending with "SC" are
!using SQR Seurity.
!***********************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
#Include 'ptpsl177.sqc'   !Printer and page-size initialization
end-setup


begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report

  if $prcs_oprid=''
    goto last2
  end-if

  if $language_cd = 'JPN'
     let $qualifier = 'E.NAME_AC'
  else
     let $qualifier = 'E.NAME'
  end-if

  do Process-Main

  do Reset
  do Stdapi-Term
LAST2:
end-report


begin-heading 7
  do Get_Field_Information ('PER005',     'REPORT_TITLE',         $REPORTTITLE,   #DW)
  #Include 'stdhdgtr.sqc'
  do Get_Field_Information ('PER005',     'LEAVE_BEGAN1',         $LEAVE1,        #DW)
  do Get_Field_Information ('PER005',     'LEAVE_BEGAN2',         $LEAVE2,        #DW)
  do Get_Field_Information ('PER005',     'EXPECTED_RETURN1',     $RETURN1,       #DW)
  do Get_Field_Information ('PER005',     'EXPECTED_RETURN2',     $RETURN2,       #DW)
  do Get_Field_Information ('PER005',     'LEAVE_REASON',         $REASON,        #DW)
  do Get_Field_Information ('HR',         'EMPL_NAME',            $NAME,          #DW)
  do Get_Field_Information ('HR',         'EMPLID',               $EMPLID,        #DW)
  do Get_Field_Information ('HR',         'DEPARTMENT',           $DEPTNM,        #DW)
  do Get_Field_Information ('HR',         'PHONE',                $PHONE,         #DW)


  PRINT   $LEAVE1                 (+2,88,11)
  PRINT   $RETURN1                (0,100)
  PRINT   $NAME                   (+1,1,41)
  PRINT   $EMPLID                 (0,43,11)
  PRINT   $DEPTNM                 (0,56,31)
  PRINT   $LEAVE2                 (0,88,11)
  PRINT   $RETURN2                (0,100,11)
  PRINT   $REASON                 (0,112,31)
  PRINT   $PHONE                  (0,144)
end-heading


begin-procedure Init-Report
  Let $_TableAlias = 'E'
  move 'PER005' to $ReportID
  do Stdapi-Init

  if $prcs_oprid=''
     display ''
     display 'REPORT CAN NOT BE EXECUTED OUTSIDE OF PEOPLESOFT,PLEASE USE PROCESS SCHEDULER.'
     display ''
    goto last1
  end-if

  do Security-Param

  if $prcs_process_instance = ''
    !No Prompt
  else
    do Select-Parameters
  end-if

  do Init_Printer
  do Get-Values
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')
LAST1:
end-procedure


begin-procedure Get-Values
let $language_cd = $PRCS_LANGUAGE_CD
end-procedure


begin-procedure Process-Main

begin-SELECT DISTINCT
E.EMPLID
E.EMPL_RCD
E.NAME
E.DEPTNAME
E.EXPECTED_RETURN_DT
E.HOME_PHONE
E.NAME_AC

   do Get-Leave-Data

FROM  PS_EMPLOYEES E
WHERE E.EMPL_STATUS     IN ('L','P')
[$SecurityClauseWithoutERN]
ORDER BY [$qualifier]
end-SELECT
end-procedure

begin-procedure Get-Leave-Data
begin-SELECT

J.EFFDT
J.EFFSEQ
J.EMPL_STATUS
J.ACTION
J.ACTION_REASON

  move &J.Action        to $Action
  move &J.Action_Reason to $Action_Reason
  move &J.EffDt         to $AsOfDate
  do Get-Action-Reason

  if &J.EMPL_STATUS = 'L' OR &J.EMPL_STATUS = 'P'
    print &E.EMPLID                (+1,43)   on-break
    print &E.NAME                   (0,1)  on-break
    print &E.DEPTNAME               (0,56)  on-break
    do Format-DateTime($AsOfDate, $out, {DEFDATE}, '', '')
    print $out                   (0,88,10)
    do Format-DateTime(&E.Expected_Return_Dt, $out, {DEFDATE}, '', '')
    print $out (0,100,10)
    print $Action_Reason_Descr      (0,112)
    print &E.Home_Phone         (0,144,24)
  else
    EXIT-SELECT 
  end-if

FROM  PS_JOB J
WHERE J.EMPLID          = &E.EmplID
  AND J.EMPL_RCD       = &E.EMPL_RCD
  AND J.EFFDT <= $AsOfToday
  AND J.ACTION = 'LOA' 
ORDER BY J.EFFDT DESC, J.EFFSEQ DESC
end-SELECT
end-procedure

#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'hrsecty.sqc'   !Get SQR Security parameters
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'getactrs.sqc'  !Get-Action-Reason procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'