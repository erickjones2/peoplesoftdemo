!***********************************************************************
!  NVQ005: Employee NVQ Reviews and Unit Plan                          *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!
!          $Date:  2013/01/17:13:49:20                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!                                                                      *
! Populates PS_NVQ_TEMP with details on NVQ Unit Plans from tables     *
! listed below for an employee. This temporary table is then used by   *
! a Report.                                                            *
! (SQR used because of restriction on using outer joins)               *
!                                                                      *
! Tables Referenced:                                                   *
!                    PS_NVQ_REVIEW                                     *
!                    PS_COMPETENCIES                                   * 
!                    PS_REVW_RATING_TBL                                *
!                    PS_COMPETENCY_TBL                                 *
!                                                                      *
! Tables Updated:    PS_NVQ_TEMP                                       *
!                                                                      *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup

begin-report
   do Process-Main
   do Stdapi-Term
end-report

begin-procedure Init-Report
   move 'NVQ005' to $ReportID
   move 'Employee NVQ Review & Unit Plans' to $ReportTitle
   display $ReportTitle

   do Stdapi-Init

   if $prcs_process_instance = ''
!  Do nothing if not via Process Schedular
   else
     do Select-Parameters
   end-if

   do Init_Printer

end-procedure

!***********************************************************************
! Function:    Process-Main                                            *
! Description:  The main control subroutine.                           *
!***********************************************************************
begin-procedure Process-Main
 DISPLAY 'PROCESS MAIN'
   do Init-Report
   do InitNVQtemp
   do GetNVQData

end-procedure


begin-procedure Get-Values
  let $param_emplid=&RUN_CNTL_HR.EMPLID
  display 'Emplid: ' NOLINE
  display &RUN_CNTL_HR.EMPLID
end-procedure

!***********************************************************************
! Function:    InitNVQTemp                                             *
! Description: Clear ps_nvq_temp (output table)                        *
!***********************************************************************
begin-procedure InitNVQTemp
DISPLAY 'INITNVQTEMP'
let $sql-statement = 'NVQ005.SQR,Delete-TEMPtable,Delete,PS_NVQ_TEMP'
begin-sql on-error=sql-error
DELETE from PS_NVQ_TEMP
end-sql

end-procedure

!***********************************************************************
! Function:    GetNVQData                                              *
! Description: Retrieve NVQ Reviews, insert into NVQ_TEMP              *
!***********************************************************************
begin-procedure GetNVQData
DISPLAY 'GETNVQDATA'

begin-SELECT

 let #compcount=0

A.EMPLID                &emplid
A.ACCOMPLISHMENT        &accomplishment
A.REVIEW_DT             &review_dt
A.DESCRLONG             &descrlong

  do find_outer (&emplid,&accomplishment,&review_dt,&descrlong,#compcount)
  if #compcount=0
       do insert_temp (&emplid,&accomplishment,&review_dt,' ',' ',' ',' ',&descrlong)
  end-if


FROM PS_NVQ_REVIEW A
WHERE A.EMPLID=$PARAM_EMPLID
ORDER BY A.EMPLID,A.ACCOMPLISHMENT,A.REVIEW_DT

end-SELECT

end-procedure


!***********************************************************************
! Function:    find_outer                                              *
! Description: Retrieve Unit Plan, insert into NVQ_TEMP                *
!***********************************************************************
begin-procedure find_outer ($emplid,$accomplishment,$review_dt,$descrlong,:#compcount)
DISPLAY 'FIND OUTER'
begin-SELECT

C.COMPETENCY    &comp
C.PROFICIENCY   &progress
D.UNIT_TITLE    &unit_title
E.DESCR         &descr

  let #compcount=#compcount+1
  do insert_temp ($emplid,$accomplishment,$review_dt,&comp,&unit_title,&progress,&descr,$descrlong)

FROM PS_NVQ_UNITS_VW C,PS_COMPETENCY_TBL D, PS_REVW_RATING_TBL E
WHERE C.EMPLID=$EMPLID
AND C.EFFDT=$REVIEW_DT
AND D.COMPETENCY=C.COMPETENCY
AND E.RATING_MODEL= 'NVQ'
AND E.REVIEW_RATING=C.PROFICIENCY
AND E.EFFDT=(SELECT MAX(E2.EFFDT) FROM PS_REVW_RATING_TBL E2
             WHERE E2.RATING_MODEL = E.RATING_MODEL
             AND E2.EFFDT <= $_AsofToday
             AND E2.REVIEW_RATING = E.REVIEW_RATING)


end-SELECT

end-procedure

!***********************************************************************
! Function:    insert_temp                                             *
! Description: insert values into NVQ_TEMP                             *
!***********************************************************************
begin-procedure insert_temp ($emplid,$accomplishment,$review_dt,$c,$unit,$progress,$descr,$descrlong)
DISPLAY 'INSERT TEMP'
let $Sql-Statement = 'NVQ005: INSERT into PS_NVQ_TEMP'
BEGIN-SQL ON-ERROR=SQL-ERROR

INSERT INTO PS_NVQ_TEMP (EMPLID,ACCOMPLISHMENT,REVIEW_DT,COMPETENCY,UNIT_TITLE,PROFICIENCY,DESCR)
values ($emplid,$accomplishment,$review_dt,$c,$unit,$progress,$descr)

end-sql

let $Sql-Statement = 'NVQ005: UPDATE PS_NVQ_TEMP (Descr Long)'
Begin-SQL On-Error=SQL-Error

UPDATE PS_NVQ_TEMP
   SET DESCRLONG = $descrlong       
 WHERE EMPLID           = $emplid
   AND ACCOMPLISHMENT   = $accomplishment
   AND REVIEW_DT        = $review_dt
   AND COMPETENCY       = $c;
   
End-SQL
end-procedure


#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'

