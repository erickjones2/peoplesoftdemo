!***********************************************************************
!  PAYWHRPT:  Payroll State Withholding Form Error / Warning Report    *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2016, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2016/05/18:12:34:25                                 !
!       $Release:  HR92                                                !
!           $Bug:  23132576                                            !
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' !Printer and page-size initialization

!************
begin-report
!************

  do Init-Report
  do Process-Main
  do Stdapi-Term
end-report


!***************************
begin-procedure Init-Report
!***************************

  move 'PAYWHRPT' to $ReportID
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init
  do Get-Run-Control

  let $EndofReport = 'N'
  let $FirstEmpl = 'Y'
  let $EmptyReport = 'Y'
  let #prtline = 0

  move 'Updateable Tax Withholding Form Notifications' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '
  do Report-Translation

end-procedure


!****************
begin-heading 7
!****************

#Include 'stdhtr02.sqc'

  let $DateRange_Title = $PayWHRpt_DateFrom || ' ' || $DispDateFrom || ' ' || $PayWHRpt_DateTo || ' ' || $DispDateTo
  print $DateRange_Title            (3,,)      center

  print $PayWHRpt_SubmitDt1         (+3,135)
  print $PayWHRpt_UpdateSts1        (,147)
  print $PayWHRpt_ActTaken1         (,159)

  print $PayWHRpt_EmplID            (+1,1)
  print $PayWHRpt_Name              (,14)
  print $PayWHRpt_State             (,70)
  print $PayWHRpt_Jurisdiction      (,79)
  print $PayWHRpt_SubmitDt2         (,135)
  print $PayWHRpt_UpdateSts2        (,147)
  print $PayWHRpt_ActTaken2         (,159)

  print '-----------'                                            (+1,1)
  print '--------------------------------------------------'     (,14)
  print '-------'                                                (,70)
  print '------------------------------'                         (,79)
  print '----------'                                             (,135)
  print '----------'                                             (,147)
  print '--------'                                               (,159)


end-HEADING

!*********************************
begin-procedure Report-Translation
!*********************************

  let $language_cd = $PRCS_LANGUAGE_CD
  do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PAYWHRPT', 'DATEFROM',      $PayWHRpt_DateFrom,       #DW)
  do Get_Field_Information ('PAYWHRPT', 'DATETO',        $PayWHRpt_DateTo,         #DW)
  do Get_Field_Information ('PAYWHRPT', 'EMPLID',        $PayWHRpt_EmplID,         #DW)
  do Get_Field_Information ('PAYWHRPT', 'NAME',          $PayWHRpt_Name,           #DW)
  do Get_Field_Information ('PAYWHRPT', 'STATE',         $PayWHRpt_State,          #DW)
  do Get_Field_Information ('PAYWHRPT', 'JURISDICTION',  $PayWHRpt_Jurisdiction,   #DW)
  do Get_Field_Information ('PAYWHRPT', 'SUBMITDATE1',   $PayWHRpt_SubmitDt1,      #DW)
  do Get_Field_Information ('PAYWHRPT', 'SUBMITDATE2',   $PayWHRpt_SubmitDt2,      #DW)
  do Get_Field_Information ('PAYWHRPT', 'UPDATESTATUS1', $PayWHRpt_UpdateSts1,     #DW)
  do Get_Field_Information ('PAYWHRPT', 'UPDATESTATUS2', $PayWHRpt_UpdateSts2,     #DW)
  do Get_Field_Information ('PAYWHRPT', 'ACTIONTAKEN1',  $PayWHRpt_ActTaken1,      #DW)
  do Get_Field_Information ('PAYWHRPT', 'ACTIONTAKEN2',  $PayWHRpt_ActTaken2,      #DW)

end-procedure

!****************************
begin-procedure Process-Main
!****************************

begin-SELECT

W4HDR.COMPANY              () on-break print=never  level=1  procedure=change-page
W4HDR.EMPLID
PN.NAME_DISPLAY
W4HDR.STATE
SN.DESCR
W4HDR.DATE_FILED
W4HDR.SESSION_ID
W4HDR.PY_W4_STATUS
W4HDR.PY_W4_ACTN_TKN

  let $Company           = rtrim(&W4HDR.COMPANY, ' ')
  let $EmplID            = rtrim(&W4HDR.EMPLID,' ')
  let $State             = rtrim(&W4HDR.STATE,' ')
  let $SessionID         = &W4HDR.SESSION_ID
  let $DispDateSubmit    = datetostr(&W4HDR.DATE_FILED,'YYYY-MM-DD')

  move 'PY_W4_STATUS'         to $FieldName
  move &W4HDR.PY_W4_STATUS    to $FieldValue
  do Read-Translate-Table
  move $XlatShortName         to $DispW4Status

  move 'PY_W4_ACTN_TKN'       to $FieldName
  move &W4HDR.PY_W4_ACTN_TKN  to $FieldValue
  do Read-Translate-Table
  move $XlatShortName         to $DispW4ActnTkn

  do print-header

  do print-message-details

  let $EmptyReport = 'N'

FROM PS_PY_W4_NOTFY_HDR W4HDR, PS_PERSON_NAME PN, PS_STATE_US_W4_VW SN
[$WhereParam]
AND W4HDR.EMPLID = PN.EMPLID
AND W4HDR.STATE = SN.STATE
ORDER BY W4HDR.COMPANY, W4HDR.EMPLID, W4HDR.STATE, W4HDR.DATE_FILED

end-SELECT

  let $EndofReport = 'Y'

  if $EmptyReport = 'Y'
      display 'No notification reported with following run parameters: '
      display 'Date Range between ' noline
      display $DispDateFrom noline
      display ' and ' noline
      display $DispDateTo

      if $RC_Company <> ''
          display 'Company - ' noline
          display $RC_Company
      end-if

      if $RC_EmplID <> ''
          display 'Employee ID - ' noline
          display $RC_EmplID
      end-if

      if $RC_State <> ''
          display 'State - ' noline
          display $RC_State
      end-if

      if $RC_W4_Status <> ''
          display 'Status - ' noline
          display $RC_W4_Status
      end-if

      if $RC_W4_ActnTkn <> ''
          display 'Action Taken - ' noline
          display $RC_W4_ActnTkn
      end-if
  end-if


  let $RC_Company    = ltrim(rtrim(&RC.Company ,' '),' ')
  let $RC_EmplID     = ltrim(rtrim(&RC.EmplID ,' '),' ')
  let $RC_State      = ltrim(rtrim(&RC.State ,' '),' ')
  let $RC_W4_Status  = ltrim(rtrim(&RC.PY_W4_Status ,' '),' ')
  let $RC_W4_ActnTkn = ltrim(rtrim(&RC.PY_W4_Actn_Tkn ,' '),' ')

end-procedure

!*****************************
begin-procedure Print-Header
!*****************************

  if #prtline >= 46
      new-page

      let $FirstEmpl = 'Y'
      let #prtline = 0
  end-if

  if $FirstEmpl = 'Y'
      print &W4HDR.EMPLID             (+1,1,11)
      let $FirstEmpl = 'N'
      let #prtline = #prtline + 1
  else
      print &W4HDR.EMPLID             (+2,1,11)
      let #prtline = #prtline + 2
  end-if

  print &PN.NAME_DISPLAY              (,14,50)
  print &W4HDR.STATE                  (,70,6)
  print &SN.DESCR                     (,79,30)
  print $DispDateSubmit               (,135)
  print $DispW4Status                 (,147,10)
  print $DispW4ActnTkn                (,159,8)

end-procedure

!************************************
begin-procedure Print-Message-Details
!************************************

begin-SELECT
W4DTL.PY_W4_ERR_TYPE
W4DTL.MESSAGE_SET_NBR
W4DTL.MESSAGE_NBR
W4DTL.PARMVALUE

  move 'PY_W4_ERR_TYPE'        to $FieldName
  move &W4DTL.PY_W4_ERR_TYPE   to $FieldValue
  do Read-Translate-Table
  move $XlatShortName          to $DispW4ErrType

  move &W4DTL.MESSAGE_SET_NBR  to #Msg_SetNbr
  move &W4DTL.MESSAGE_NBR      to #Msg_Nbr
  let $Msg_Parm1 = rtrim(&W4DTL.PARMVALUE, ' ')
  do Get-Message-Catalog

  if $Msg_Parm1 <> ''
      let $Msg_Text = replace($Msg_Text, '%1', $Msg_Parm1)
  end-if
  let $Msg_Text = ltrim(rtrim($Msg_Text, ' '), ' ')

  if #prtline >= 46
      new-page

      print &W4HDR.EMPLID             (+1,1,11)
      print &PN.NAME_DISPLAY          (,14,50)
      print &W4HDR.STATE              (,70,6)
      print &SN.DESCR                 (,79,30)
      print $DispDateSubmit           (,135)
      print '(Cont..)'                (,147)

      let #prtline = 1
  end-if

  print $DispW4ErrType    (+1,16)
  print #Msg_SetNbr       (,24) edit 99999
  print '/'               (,29)
  print #Msg_Nbr          (,30) edit 99999
  print $Msg_Text         (,36,100)
  let #prtline = #prtline + 1

FROM PS_PY_W4_NOTFY_DTL W4DTL
WHERE W4DTL.EMPLID     = $EmplID
  AND W4DTL.COMPANY    = $Company
  AND W4DTL.STATE      = $State
  AND W4DTL.SESSION_ID = $SessionID
end-SELECT

end-procedure Print-Message-Details

!***************************
begin-procedure change-page
!***************************

  new-page
  let $FirstEmpl = 'Y'
  let #prtline = 0

end-procedure change-page


!******************************
begin-procedure Get-Run-Control
!******************************

  let $WhereParam = ''

begin-SELECT
RC.DATE_FROM
RC.DATE_TO
RC.COMPANY
RC.EMPLID
RC.STATE
RC.PY_W4_STATUS
RC.PY_W4_ACTN_TKN

  let $DateFrom      =  &RC.Date_From
  let $DateTo        =  &RC.Date_To
  let $WhereParam = 'WHERE W4HDR.DATE_FILED BETWEEN ' || '''' || $DateFrom || '''' || ' AND ' || '''' || $DateTo || ''''

  let $DispDateFrom    =  datetostr(&RC.Date_From,'YYYY-MM-DD')
  let $DispDateTo      =  datetostr(&RC.Date_To,'YYYY-MM-DD')

  let $RC_Company    = ltrim(rtrim(&RC.Company ,' '),' ')
  let $RC_EmplID     = ltrim(rtrim(&RC.EmplID ,' '),' ')
  let $RC_State      = ltrim(rtrim(&RC.State ,' '),' ')
  let $RC_W4_Status  = ltrim(rtrim(&RC.PY_W4_Status ,' '),' ')
  let $RC_W4_ActnTkn = ltrim(rtrim(&RC.PY_W4_Actn_Tkn ,' '),' ')

  if rtrim($RC_Company, ' ') <> ''
      let $WhereParam = $WhereParam || ' AND W4HDR.COMPANY =  ' || '''' || $RC_Company || ''''
  end-if

  if rtrim($RC_EmplID, ' ') <> ''
      let $WhereParam = $WhereParam || ' AND W4HDR.EMPLID =  ' || '''' || $RC_EmplID || ''''
  end-if

  if rtrim($RC_State, ' ') <> ''
      let $WhereParam = $WhereParam || ' AND W4HDR.STATE =  ' || '''' || $RC_State  || ''''
  end-if

  if rtrim($RC_W4_Status, ' ') <> ''
      let $WhereParam = $WhereParam || ' AND W4HDR.PY_W4_STATUS = ' || '''' || $RC_W4_Status  || ''''
  end-if

  if rtrim($RC_W4_ActnTkn, ' ') <> ''
      let $WhereParam = $WhereParam || ' AND W4HDR.PY_W4_ACTN_TKN = ' || '''' || $RC_W4_ActnTkn || ''''
  end-if


FROM PS_RUNCTL_W4_NOTFY RC
WHERE RC.OPRID = $prcs_oprid
  AND RC.RUN_CNTL_ID = $prcs_run_cntl_id
end-SELECT

end-procedure Get-Run-Control


begin-footing 2
  if $EndofReport = 'Y'
      print 'End of Report' (+1,1) center
  else
      print ' '             (+1,1) center
  end-if
end-footing

#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'getrplng.sqc'  !Gets the report language
#Include 'getmsgct.sqc'  !Gets Message Catalog

