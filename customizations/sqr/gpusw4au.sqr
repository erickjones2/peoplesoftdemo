!**********************************************************************!
!  gpusw4au: W-4 ePay Audit Report (USA)                               !
!**********************************************************************!
!                                                                      !
!                                                                      !
!                                                                      !
!                                                                      !
! This software and related documentation are provided under a         !
! license agreement containing restrictions on use and                 !
! disclosure and are protected by intellectual property                !
! laws. Except as expressly permitted in your license agreement        !
! or allowed by law, you may not use, copy, reproduce,                 !
! translate, broadcast, modify, license, transmit, distribute,         !
! exhibit, perform, publish or display any part, in any form or        !
! by any means. Reverse engineering, disassembly, or                   !
! decompilation of this software, unless required by law for           !
! interoperability, is prohibited.                                     !
! The information contained herein is subject to change without        !
! notice and is not warranted to be error-free. If you find any        !
! errors, please report them to us in writing.                         !
!                                                                      !
!                                                                      !
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              !
! All Rights Reserved.                                                 !
!**********************************************************************!
!                                                                      !
!       $Release:  HR92                                                !
!           $Bug:  30517706                                            !
!                                                                      !
!**********************************************************************!
! Change History
! Incident ID    Dated     Comments
!
! 1251532000     7/05      Add a new column for the date that user updated
!                          their W-4 information.
!
! 1445141000     6/06      Name format change for NAMES field. Use NAME_PSFORMAT.
!


#include 'setenv.sqc'    !Set Database Environment

begin-report
  do Stdapi-Init
  move 'GPUSW4AU' to $ReportID
  move 'W-4 ePay Audit Report' to $ReportTitle
  display $ReportTitle
  display ''

  do Get-Run-ID

  do Init-DateTime
  do Get-Current-DateTime
  do Format-DateTime($AsOfToday, $out, {DEFMDY},'','')
  move $out to $ReportDate
  display 'Report Began: ' noline
  display $AsOfNow
  display ''

  do Get-From-Thru-Date

  do Order_clause

  do get-w4audit

  do Get-Current-DateTime
  display ''
  display 'Report Ended: ' noline
  display $AsOfNow
  do Stdapi-Term
end-report

#include 'setup31.sqc'   !Printer and page-size initialization


begin-heading 7

#include 'stdhdg01.sqc'

  do Format-DateTime($Fromdate, $FmDate, {DEFMDY},'','')
  do Format-DateTime($ThruDate, $TuDate, {DEFMDY},'','')

  print 'List Of W-4 updated via ePay ' (+1,1)
  print $FmDate ()
  print '  to  '  ()
  print $TuDate ()

  print 'Employee Id and Name                      Filing Status'          (+2,1)
  print 'Total No Of Allowances  Extra W/H     Exempt Status  Updated On'  (0,59)
  print '----------------------------------------- --------------'         (+1,1)
  print '----------------------  ------------  -------------  ----------'  (0,59)


end-heading


begin-procedure Get-Run-ID
      do Select-Parameters
end-procedure

Begin-Procedure Order_clause

DISPLAY 'ORDER_OPTION'
DISPLAY $GPUS_RPT_TAX_RC.GPUS_ORDER_OPTION
  Let $OrderByClause = ''
  If $GPUS_RPT_TAX_RC.GPUS_ORDER_OPTION = '02'
      Let $OrderByClause = 'ORDER BY PERS.NAME_PSFORMAT'
  else
      Let $OrderByClause = 'ORDER BY W4.EMPLID'   !ORDER_OPTION = '01'
  End-If

End-Procedure


begin-procedure get-w4audit
  display 'Generating report . . . ' noline

begin-SELECT
W4.EMPLID
W4.GPUS_FWT_MAR_ST
W4.GPUS_FWT_ALLOW
W4.GPUS_FWT_ADD_AMT
W4.GPUS_FWT_STATUS
W4.GPUS_UPDATE_DT
PERS.NAME_PSFORMAT

  move &W4.EMPLID              to $Emplid
  move &W4.GPUS_FWT_ALLOW      to #Gpus_Fwt_Allowances
  move &W4.GPUS_FWT_ADD_AMT    to #Gpus_Fwt_Addl_Amt
  move &W4.GPUS_UPDATE_DT      to $Gpus_Update_Dt


  let $FieldName    = 'GPUS_FWT_MAR_ST'
  let $FieldValue   = &W4.GPUS_FWT_MAR_ST
  do Read-Translate-Table
  let $Gpus_Fwt_Mar_Status = $XLatShortName

  let $FieldName    = 'GPUS_FWT_STATUS'
  let $FieldValue   = &W4.GPUS_FWT_STATUS
  do Read-Translate-Table
  let $Gpus_Fwt_Status = $XLatShortName

  let $Name = rtrim(&PERS.NAME_PSFORMAT,' ')

  do Format-DateTime($Gpus_Update_Dt, $UpDate, {DEFMDY},'','')

  do  Print-W4Audit

FROM PS_GPUS_FWT_EE  W4, PS_PERSON_NAME PERS
WHERE W4.EMPLID = PERS.EMPLID
  AND W4.GPUS_UPDATE_DT BETWEEN $FromDate AND $ThruDate
  AND W4.GPUS_UPDATE_CD = '03'
[$OrderByClause]

end-SELECT
  display 'Done'
end-procedure


begin-procedure Print-W4Audit

  print $Emplid                 (+1,1)
  print $Name                   (0,13)
  print $Gpus_Fwt_Mar_Status    (0,43)
  print #Gpus_Fwt_Allowances    (0,59)
  print #Gpus_Fwt_Addl_Amt      (0,83)
  print $Gpus_Fwt_Status        (0,97)
  print $UpDate                 (0,112)

end-procedure


#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Update Process API
#include 'gpuspctl.sqc'  !Select Run Control Parameters
#include 'gpuspyvl.sqc'  !To Get as Of date
#include 'readxlat.sqc'  !Read-Translate-Table procedure
