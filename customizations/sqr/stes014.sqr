!-----------------------------------------------------------------------
! Report Name: STES014.SQR - Stock Purchase Messages 
!-----------------------------------------------------------------------
!
!
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!-----------------------------------------------------------------------
!
!          $Date:  2013/01/18:02:50:52                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!
!-----------------------------------------------------------------------
! Program Descr: Prints ESPP Messages
!-----------------------------------------------------------------------
! Prompts:       Purchase ID, Process Group
! Tables:        RUN_CNTL_ST, ST_ESPP_MESSAGES, 
!                ST_ESPP_MSG_TBL
! Sorts:         By Process Group, Process Type, Emplid
!-----------------------------------------------------------------------

  #include 'setenv.sqc'

!-----------------------------------------------------------------------
Begin-Setup
!-----------------------------------------------------------------------

  #include 'ptpsl177.sqc'

End-Setup

!-----------------------------------------------------------------------
! Procedure: Begin-Program
! Descr:     Main Report procedure.
!-----------------------------------------------------------------------
Begin-Program

  do Init-Report
  do Get-Purchase-ID
  do Print-Report
  do Reset
  do Successful-EOJ

End-Program

!-----------------------------------------------------------------------
! Section: Heading Procedures
! Descr:   This sections contains all procedures for
!          printing the optional heading.
!-----------------------------------------------------------------------
Begin-Heading 12

#Include 'stdhdg01.sqc'

 print $StockId               (0,1,9)
 print $PUR_StockId           (0,17,10)

 print $StockPlan             (+1,1,11)
 print $PUR_StPlan            (0,17,10)

 print $OfferId               (+1,1,12)
 print $PUR_Offering          (0,17,10)

 print $PurchaseDt            (+1,1,14)

 do Format-DateTime ($PUR_PurchDt, $Dt, {DEFDATE}, '', '')
 print $Dt                    (0,17,10) 

 print $ProcessGrp            (+2,1,14)
 print $ProcessGroup          (0,17,10)

 print $MsgData1              (+2,118,30)
 print $MsgData2              (0,149,30)
 print $ProcessType           (+1,1,10)
 print $Emplid                (0,12,11)
 print $EmplName              (0,24,30)
 print $GrantDt               (0,55,10)
 print $Msg                   (0,66,50)
 print $MsgData3              (0,118,30)
 print $MsgData4              (0,149,30)

 print '-'                    (+1,1,178) fill

End-Heading

!-----------------------------------------------------------------------
! Procedure: Init-Report
! Descr:     Report initialization procedure.  Set titles, parameters.
!            This is called from the Begin-Program procedure.
!-----------------------------------------------------------------------
Begin-Procedure Init-Report

  let $ReportId = 'STES014'
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Define-Prcs-Vars
  do Get-Run-Control-Parms
  do Init_Report_Translation ('STOCK', $language_cd)
  do Append_Report_Translation ($ReportID)

  if $prcs_process_instance = ''
    input $RC_PURCHASE_ID MAXLEN=10 'Purchase ID: ' Type=Char
    uppercase $RC_Purchase_ID
    input $RC_PrcsGrp MAXLEN=10 'Process Group: ' Type=Char
    uppercase $RC_PrcsGrp
    input $RC_EMPLID MAXLEN=11 'Emplid: ' Type=Char
    uppercase $RC_EMPLID
  else
    do Get-SQR-Parms                            ! Get Run Control Values
  end-if


  do Get_Field_Information ('STOCK',   'STOCK_ID',     $StockId,       #DW)
  do Get_Field_Information ('STOCK',   'STOCK_PLAN',   $StockPlan,     #DW)
  do Get_Field_Information ('STOCK',   'OFFERING_ID',  $OfferId,       #DW)
  do Get_Field_Information ('STOCK',   'PURCHASE_DT',  $PurchaseDt,    #DW)
  do Get_Field_Information ('STOCK',   'NAME',         $EmplName,      #DW)
  do Get_Field_Information ('STOCK',   'EMPLID',       $Emplid,        #DW)
  do Get_Field_Information ('STOCK',   'GRANT_DT',     $GrantDt,       #DW)
  do Get_Field_Information ('STOCK',   'PROCESS_GRP',  $ProcessGrp,    #DW)

  do Get_Field_Information ('STES014',   'REPORT_TITLE', $ReportTitle,   #DW)
  do Get_Field_Information ('STES014',   'PROCESS_TYPE', $ProcessType,   #DW)
  do Get_Field_Information ('STES014',   'DATA1',        $MsgData1,      #DW)
  do Get_Field_Information ('STES014',   'MSG',          $Msg,           #DW)
  do Get_Field_Information ('STES014',   'DATA2',        $MsgData2,      #DW)
  do Get_Field_Information ('STES014',   'DATA3',        $MsgData3,      #DW)
  do Get_Field_Information ('STES014',   'DATA4',        $MsgData4,      #DW)

  show   ' '
  show   $ReportTitle
  show   ' '

End-Procedure

!-----------------------------------------------------------------------
! Section: Print-Report
! Descr:   Main report
!-----------------------------------------------------------------------
Begin-Procedure Print-Report
  let $SQLQuote      = ''''
  let $WhereEmplid   = ''
  let $WherePrcsGrp = ''
 
  let $RC_EMPLID = rtrim($RC_EMPLID,' ')
  let $RC_PrcsGrp = rtrim($RC_PrcsGrp, ' ')
 
  if $RC_PrcsGrp <> ''
     let $WherePrcsGrp   = ' AND v1.PROCESS_GROUP  = ' || $SQLQuote || $RC_PrcsGrp || $SQLQuote
  else
    let $WherePrcsGrp   = ''
  end-if
 
  if $RC_EMPLID <> ''
    let $WhereEmplid   = ' AND v1.EMPLID  = ' || $SQLQuote || $RC_EMPLID || $SQLQuote
  else
    let $WhereEmplid   = ''
  end-if


Begin-Select
v1.PROCESS_GROUP      &Process_Group ()                   On-Break Print=never Level=1 
                                                          after= Process-Group-Heading save= $ProcessGroup 
v1.ESPP_PROCESS_TYPE         
T.XLATSHORTNAME                                        (+1,1,10) On-Break Level=2 Print=Change skiplines=2
v1.EMPLID                                              (0,12,11) on-break level=3 print=Change Skiplines=2
  if &v1.EMPLID <> ' '
    do Get-Empl-Name
  end-if
v1.GRANT_DT                                            (0,55,10)
E.DESCR50                                              (0,66,50)
v1.MSGDATA1                                            (0,118,30)
v1.MSGDATA2                                            (0,149,30)
v1.MSGDATA3                                            (+1,118,30)
v1.MSGDATA4                                            (0,149,30)

FROM PS_ST_ESPP_MESSAGE v1,
     PS_ST_ESPP_MSG_TBL E,
     PSXLATITEM T
WHERE v1.PURCHASE_ID = $RC_PURCHASE_ID
  AND v1.MSGID       = E.MSGID
  AND T.FIELDNAME    = 'ESPP_PROCESS_TYPE'
  AND T.FIELDVALUE   = v1.ESPP_PROCESS_TYPE
  AND T.EFF_STATUS   = 'A'
  AND T.EFFDT        = (SELECT MAX(T2.EFFDT)
                          FROM PSXLATITEM T2
                         WHERE T2.FIELDNAME  = T.FIELDNAME
                           AND T2.FIELDVALUE = T.FIELDVALUE
                           AND T2.EFFDT     <= $AsofToday)
[$WherePrcsGrp]
[$WhereEmplid]
ORDER BY v1.PROCESS_GROUP, v1.ESPP_PROCESS_TYPE, v1.EMPLID

End-Select

End-Procedure

!-----------------------------------------------------------------------
! Section: Process-Group-Header
! Descr:   Force a new page.
!-----------------------------------------------------------------------
Begin-Procedure Process-Group-Heading

  new-page

End-Procedure

!-----------------------------------------------------------------------
Begin-Procedure Get-Empl-Name
!-----------------------------------------------------------------------

Begin-Select

D.NAME         (0,24,30)

FROM PS_ST_PERSON_NAME D
WHERE D.EMPLID = &v1.EMPLID

End-Select

End-Procedure

!-----------------------------------------------------------------------
! SQC Includes
!-----------------------------------------------------------------------
#include 'strunctl.sqc'   ! Stock Admin's Run Control Record
#include 'prcsapi.sqc'
#include 'prcsdef.sqc'    ! Update Process Open_Trans variable declaration
#include 'eoj.sqc'        ! End of Report Format
#include 'sqrtrans.sqc'   ! Access Strings Table
#include 'reset.sqc'      ! Reset printer procedure
#include 'curdttim.sqc'   ! Get-Current-DateTime procedure
                          ! Procedures: Get-Current-DateTime
#include 'datemath.sqc'
#include 'datetime.sqc'   ! Routines for date and time formatting
                          ! Procedures: Init-Datetime
                          ! Procedures: Format-DateTime
                          ! Procedures: Diff-Date
                          ! Procedures: many others.
#include 'number.sqc'     ! Procedures: Init-Number
#include 'stescomn.sqc'   ! Procedures: Get-Purchase-ID
                          !             Build-Where-Clause
