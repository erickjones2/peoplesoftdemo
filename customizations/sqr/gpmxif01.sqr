!***********************************************************************
!  GPMXIF01:  Federal Taxes Summary                                    *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:03:50:32                                 *
!       $Release:  HR92                                                *
!      $Revision:  101                                                 *
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment
#Include 'setup32.sqc'   !printer and page-size initialization

#define c1 1
#define c2 12
#define c3 40
#define c4 58
#define c5 80
#define c6 108
#define c7 147

begin-report
   move '1' to $ReportDateType
   move '1' to $ReportYear4
   do Init-DateTime
   do Init-Number
   do Stdapi-Init
   do Get-Current-DateTime
   do Select-Parameters
   do Init-Report
   do Stdapi-Term
   do Reset
end-report

!************************************
!Procedure Init-Report
!************************************
begin-procedure Init-Report
   move 'GPMXIF01' to $ReportID
   move 'Impuestos Federales' to $ReportTitle
   display $ReportID
   display $ReportTitle
   date-time () hh:mi:ss &timeBegan
   display 'Report Began: ' noline
   display &timeBegan

   do Report-Translation
   do Fed-Tax

   date-time () hh:mi:ss &timeEnded
   display 'Report Ended: ' noline
   display &timeEnded
end-procedure Init-Report

begin-heading 9
    uppercase $ReportTitle
    print 'PeopleSoft'      (1,) center
    print $ReportTitle      (2,) center
    print $Report           (3,1)
    print $ReportID         ()
    let $pag = edit(#page-count,'9999')
    let $pag = $Head1 || $pag
    print $pag              (3,152)
    print $Param1           (4,1)
    print $Company          ()
    print ', '              ()
    print $CompanyName      ()
    print $Head2            (4,152)
    date-time () HH:MM:SS  &ReportTime
    print &ReportTime       ()
    print $Param2           (5,1)
    print $AsOfYear         ()
    print $Head3            (5,152)
    print $ReportDate       ()
    print $Param3           (6,1)
    print $AsOfMonth        ()

    print '='               (+2,{c1},169)fill
    print $Head4            (+1,{c1})
    print $Head5            ( ,{c3})
    print $Head6            ( ,{c5})
    print $Head7            ( ,{c6})
    print $Head8            ( ,{c7})
    print '='               (+1,{c1},169)fill
end-heading

!************************************
!Procedure Select-Parameters
!************************************
begin-procedure Select-Parameters
begin-SELECT
ARNCTL.OPRID
ARNCTL.RUN_CNTL_ID
ARNCTL.COMPANY
ARNCTL.BALANCE_YEAR
ARNCTL.MONTHCD

    let $Company   = &ARNCTL.COMPANY
    let #AsofYear  = &ARNCTL.BALANCE_YEAR
    let #AsofMonth = &ARNCTL.MONTHCD


FROM  PS_GPMX_RC_FEDTAX  ARNCTL
WHERE ARNCTL.OPRID        = $Prcs_OprID
AND   ARNCTL.RUN_CNTL_ID  = $Prcs_Run_Cntl_ID
end-select

    if mod(#AsofYear, 4) = 0
       let #bis = 1
    else
       let #bis = 0
    end-if

    let $AsofYear = edit(#AsofYear,'9999')
    let $AsofMonth = edit(#AsofMonth,'09')

    let $BgnDt = $AsofYear || '-' || $AsofMonth || '-' || '01'

    if  $AsofMonth = '01' OR $AsofMonth = '03' OR $AsofMonth = '05' OR $AsofMonth = '07' OR $AsofMonth = '08' OR $AsofMonth = '09' OR $AsofMonth = '11'
        let $EndDt =  $AsofYear || '-' || $AsofMonth || '-' || '31'
    end-if

    if  $AsofMonth ='02'
        if #bis = 1
           let $EndDt = $AsofYear || '-' || $AsofMonth || '-' || '29'
        else
           let $EndDt = $AsofYear || '-' || $AsofMonth || '-' || '28'
        end-if
    end-if

    if  $AsofMonth = '04' OR $AsofMonth = '06' OR $AsofMonth = '09' OR $AsofMonth = '10'
        let $EndDt =  $AsofYear || '-' || $AsofMonth || '-' || '30'
    end-if

    do Convert-From-DTU-Date($BgnDt,$BgnDt)
    do Convert-From-DTU-Date($EndDt,$EndDt)
    do Get-Company-Data-Mex
end-procedure Select-Parameters

!************************************
!Procedure Fed-Tax
!************************************
begin-procedure Fed-Tax
  let #tot_sum_loc = 0
  let #tot_cia = 0
begin-SELECT
B.LOCATION   (+1,{c1},8) on-break print=change level=1 save=$Lcode
L.DESCR      (0,{c2},25) on-break print=change level=2  save=$LOC after = Tot-Loc
A.CAL_RUN_ID (0,{c3},15) on-break print=change level=4
R.DESCR      (0,{c4},20) on-break print=change level=4
D.PIN_NUM
C.PIN_NM     (0,{c5},25)
N.DESCR      (0,{c6},30)
SUM(A.CALC_RSLT_VAL) &A.CALC_RSLT_VAL
SUM(A.USER_ADJ_VAL)  &A.USER_RSLT_VAL

  if &A.USER_RSLT_VAL = 0
     print &A.CALC_RSLT_VAL (0,{c7})  edit '999,999,999,999,999.999'
     let #sum_loc = &A.CALC_RSLT_VAL
  else
     print &A.USER_RSLT_VAL (0,{c7})  edit '999,999,999,999,999.999'
     let #sum_loc = &A.USER_RSLT_VAL
  end-if

  let #tot_sum_loc = #tot_sum_loc + #sum_loc
  let #tot_cia = #tot_cia  + #sum_loc

  FROM PS_GP_RSLT_ACUM A,  PS_JOB B, PS_GP_PIN C,
       PS_GPMX_DET_FEDTAX D,  PS_LOCATION_TBL L,
       PS_GP_CAL_RUN R, PS_GP_PIN N
  WHERE A.RSLT_SEG_NUM = (SELECT MAX(AM.RSLT_SEG_NUM) FROM PS_GP_RSLT_ACUM AM
          WHERE A.EMPLID = AM.EMPLID
            AND A.CAL_RUN_ID = AM.CAL_RUN_ID
            AND A.EMPL_RCD = AM.EMPL_RCD
            AND A.GP_PAYGROUP = AM.GP_PAYGROUP
            AND A.CAL_ID = AM.CAL_ID
            AND A.ORIG_CAL_RUN_ID = AM.ORIG_CAL_RUN_ID
            AND A.PIN_NUM = AM.PIN_NUM
            AND A.EMPL_RCD_ACUM = AM.EMPL_RCD_ACUM
            AND A.ACM_FROM_DT >= $BgnDt
            AND A.ACM_THRU_DT <= $EndDt)
     AND A.EMPLID = B.EMPLID
     AND A.EMPL_RCD = B.EMPL_RCD
     AND A.GP_PAYGROUP = B.GP_PAYGROUP
     AND B.EFFDT =
        (SELECT MAX(B_ED.EFFDT) FROM PS_JOB B_ED
        WHERE B.EMPLID = B_ED.EMPLID
          AND B.EMPL_RCD = B_ED.EMPL_RCD
          AND B_ED.EFFDT <= $EndDt )
    AND B.EFFSEQ =
        (SELECT MAX(B_ES.EFFSEQ) FROM PS_JOB B_ES
        WHERE B.EMPLID = B_ES.EMPLID
          AND B.EMPL_RCD = B_ES.EMPL_RCD
          AND B.EFFDT = B_ES.EFFDT)
     AND B.COMPANY = $Company
     AND A.PIN_NUM = C.PIN_NUM
     AND A.ACM_FROM_DT >= $BgnDt
     AND A.ACM_THRU_DT <= $EndDt
     AND D.OPRID = $Prcs_OprID
     AND D.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
     AND A.PIN_NUM = D.PIN_NUM
     AND L.EFFDT = (SELECT MAX(L_ED.EFFDT) FROM PS_LOCATION_TBL L_ED
        WHERE L.SETID = L_ED.SETID
          AND L.LOCATION = L_ED.LOCATION
          AND L_ED.EFFDT <= $EndDt)
     AND L.SETID = B.SETID_LOCATION
     AND L.LOCATION = B.LOCATION
     AND L.EFF_STATUS = 'A'
     AND A.CAL_RUN_ID = R.CAL_RUN_ID
     AND D.PIN_NUM = N.PIN_NUM
  GROUP BY B.LOCATION,  L.DESCR, A.CAL_RUN_ID, R.DESCR ,D.PIN_NUM, C.PIN_NM, N.DESCR
  ORDER BY B.LOCATION,  L.DESCR, A.CAL_RUN_ID, R.DESCR , D.PIN_NUM, C.PIN_NM, N.DESCR
end-select
  print '='               (+2,{c5},90)fill
  print $Head9            (+1,{c5})
  let $cia = rtrim($Company,' ') || '  ' || rtrim($CompanyName,' ')
  print $cia (0,{c6})
  print #tot_cia (0,{c7}) edit '999,999,999,999,999.999'
end-procedure Fed-Tax

!************************************
!Procedure Tot-Loc
!************************************
begin-procedure Tot-Loc
  print '='               (+1,{c5},90)fill
  print $Head10   (+1,{c5})
  let $loca = rtrim($Lcode,' ') || '  ' ||  rtrim($loc,' ')
  print $loca  (0,{c6})
  print #tot_sum_loc  (0,{c7}) edit '999,999,999,999,999.999'
  let #tot_sum_loc = 0
  print ' ' (+1,1)
End-procedure Tot-Loc

!**********************************
!Procedure Report-Translation
!**********************************
begin-Procedure Report-Translation
! Read from GPMXIF01 program IDS
 let $language_cd = $prcs_languaje_cd
 do Init_Report_Translation ('GPMXIF01', $language_cd)
 do Append_Report_Translation ('GPMXIF01')
 do Get_Field_Information ('GPMXIF01', 'REPORT',  $Report,   #DW)
 do Get_Field_Information ('GPMXIF01', 'REPORT_DESCR',  $ReportDescr,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD1',  $Head1,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD2',  $Head2,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD3',  $Head3,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD4',  $Head4,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD5',  $Head5,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD6',  $Head6,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD7',  $Head7,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD8',  $Head8,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD9',  $Head9,   #DW)
 do Get_Field_Information ('GPMXIF01', 'HEAD10',  $Head10,   #DW)
 do Get_Field_Information ('GPMXIF01', 'PARAM1',  $Param1,   #DW)
 do Get_Field_Information ('GPMXIF01', 'PARAM2',  $Param2,   #DW)
 do Get_Field_Information ('GPMXIF01', 'PARAM3',  $Param3,   #DW)
End-Procedure Report-Translation

#Include 'getcomex.sqc'  !Get selected fields from Company Table and Mexico Fields
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Datemath Routines
#Include 'sqrtrans.sqc'

