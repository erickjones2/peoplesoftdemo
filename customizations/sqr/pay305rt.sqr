!**************************************************************
!  PAY305RT:  Load Paysheet Transaction Report                *
!**************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  31780290                                            !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

begin-report

    do Init-DateTime
    do Init-Number
    do Get-Current-Datetime
    do Get-Language-Codes
    do Init-Report
    do Process-Main
    do Reset
    do Successful-EOJ

end-report


!******************** H E A D I N G      S E C T I O N  *****************

#include 'setup32.sqc'  !Printer and page-size initialization

begin-heading 10



#include 'stdhdg01.sqc'

    print $PAY305RT_PU_Source                (+4,1)
    print $PAY305RT_PU_Txn_Status            (0,20)
    print $PAY305RT_Emplid                   (0,29)
    print $PAY305RT_Empl_Rcd                 (0,73)
    print $PAY305RT_Company                  (0,85)
    print $PAY305RT_Paygroup                 (0,95)
    print $PAY305RT_Pay_End_Dt               (0,107)
    print $PAY305RT_PU_Txn_Type              (0,130)
    print $PAY305RT_Grossup                  (0,138)
    print $PAY305RT_Sepchk                   (0,150)



end-heading

begin-footing 4
  print $PAY305RT_FootNote                   (2, 1)

  if $End_of_Report = 'Y'
    !*******************************************************************
    !  Use a globalized string if one was gotten from stdhdgtr         *
    !*******************************************************************
    if $stdhdg_end_rep = ''
      print 'End of Report' (+0,1) center
    else
      print $stdhdg_end_rep (+0,1) center
    end-if
  end-if
end-footing


!******************  I N I T I A L I Z A T I O N   *********************

begin-procedure Init-Report

!********  Translations procedures & Strings Table retrievals  *********

  move 'PAY305RT' to $ReportID
  do Define-Prcs-Vars
  do Select-Parameters

  do Init_Report_Translation ('PAY305RT', $curr_language_cd)
  do Append_Report_Translation ('PAY305RT')

  do Get_Field_Information ('PAY305RT' ,'TITLE'             ,$PAY305RT_Title          ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PU_SOURCE'         ,$PAY305RT_PU_Source      ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PU_TXN_STATUS'     ,$PAY305RT_PU_Txn_Status  ,#DW)
  do Get_Field_Information ('PAY305RT', 'EMPLID'            ,$PAY305RT_Emplid         ,#DW)
  do Get_Field_Information ('PAY305RT' ,'EMPL_RCD'          ,$PAY305RT_Empl_Rcd       ,#DW)
  do Get_Field_Information ('PAY305RT' ,'COMPANY'           ,$PAY305RT_Company        ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PAYGROUP'          ,$PAY305RT_Paygroup       ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PAY_END_DT'        ,$PAY305RT_Pay_End_Dt     ,#DW)
  do Get_Field_Information ('PAY305RT' ,'PU_TXN_TYPE'       ,$PAY305RT_PU_Txn_Type    ,#DW)
  do Get_Field_Information ('PAY305RT' ,'GROSSUP'           ,$PAY305RT_Grossup        ,#DW)
  do Get_Field_Information ('PAY305RT' ,'SEPCHK'            ,$PAY305RT_Sepchk         ,#DW)
  do Get_Field_Information ('PAY305RT' ,'FOOTNOTE'          ,$PAY305RT_FootNote       ,#DW)


  move $PAY305RT_Title to $ReportTitle
  display $ReportTitle

end-procedure

!**************************  M A I N   P R O C E S S   ******************

begin-procedure Process-Main           !Main Report Processing

  let $PrevSource = ''
  let $SkipNewPage = 'Y'

begin-SELECT

A.PU_SOURCE
A.PU_TXN_STATUS
A.EMPLID
A.EMPL_RCD
A.COMPANY
A.PAYGROUP
A.PAY_END_DT
A.PU_TXN_TYPE
A.GROSSUP
A.SEPCHK

  let $ProcEE = 'N'
  if isNull(&A.PAY_END_DT)
      let $SlctPayEndDt = $AsOfToday
  else
      let $SlctPayEndDt = &A.PAY_END_DT
  end-if
  do Get-Employee-Name

  if $ProcEE = 'Y'
      if &A.PU_SOURCE <> $PrevSource
          let $PrevSource = &A.PU_SOURCE
          if $SkipNewPage = 'Y'
              let $SkipNewPage = 'N'
          else
              new-page
          end-if
      end-if

      print &A.PU_SOURCE                         (+2,1)
      print &A.PU_TXN_STATUS                     (0,20)
      print $EmplID_Name                         (0,29)
      print &A.EMPL_RCD                          (0,73)
      print &A.COMPANY                           (0,85)
      print &A.PAYGROUP                          (0,95)
      print &A.PU_TXN_TYPE                       (0,131)
      print &A.GROSSUP                           (0,141)
      print &A.SEPCHK                            (0,152)
      do Format-DateTime(&A.PAY_END_DT, $out, {DEFMDY}, '', '')
      print $out                                 (0,107)
  end-if

FROM PS_PSHUP_TXN A
[$WhereSource]
[$WhereStatus]

ORDER BY A.PU_SOURCE,
         A.PU_TXN_STATUS,
         A.COMPANY,
         A.PAYGROUP,
         A.EMPLID,
         A.EMPL_RCD

end-SELECT

  if $SkipNewPage = 'Y'
      display ' '
      display '** No transactions selected **'
  end-if

end-procedure

!*************  G E T   P E R S O N A L  D A T A  ***************

begin-procedure Get-Employee-Name

begin-SELECT

E.NAME

  do Get-Empl-Status
  let $EmplID = rtrim(&A.EMPLID, ' ')
  let $EmplID_Name = $EmplID || ' ' || &E.NAME

  if $ActiveEmpl = 'N'
      let $EmplID_Name = '* ' || $EmplID_Name
  end-if
  if length($EmplID_Name) > 43
    let $EmplID_Name = substr($EmplID_Name,1,43)
  end-if

  if (($Rqst-Pu-Active = 'A' OR $Rqst-Pu-Active = 'B') AND $ActiveEmpl = 'Y') OR
     (($Rqst-Pu-Active = 'I' OR $Rqst-Pu-Active = 'B') AND $ActiveEmpl = 'N')
      let $ProcEE = 'Y'
  else
      let $ProcEE = 'N'
  end-if

FROM PS_PERSON_NAME E

WHERE E.EMPLID = &A.EMPLID

end-SELECT

end-procedure

!********************************************************************
begin-procedure Get-Empl-Status
!********************************************************************

begin-SELECT
J.EMPL_STATUS

  if &J.EMPL_STATUS = 'A' OR
     &J.EMPL_STATUS = 'P' OR
     &J.EMPL_STATUS = 'Q' OR
     &J.EMPL_STATUS = 'U'
      let $ActiveEmpl = 'Y'
  else
      let $ActiveEmpl = 'N'
  end-if

FROM PS_JOB J
WHERE J.EMPLID = &A.EMPLID
AND J.EMPL_RCD = &A.EMPL_RCD
AND J.EFFDT = (SELECT MAX(J1.EFFDT) FROM PS_JOB J1
               WHERE J1.EMPLID = J.EMPLID
                 AND J1.EMPL_RCD = J.EMPL_RCD
                 AND J1.EFFDT <= $SlctPayEndDt)
AND J.EFFSEQ = (SELECT MAX(J2.EFFSEQ) FROM PS_JOB J2
                WHERE J2.EMPLID = J.EMPLID
                  AND J2.EMPL_RCD = J.EMPL_RCD
                  AND J2.EFFDT = J.EFFDT)
end-SELECT

end-procedure


!********************************************************************
begin-procedure Reset
!********************************************************************

  move 'Y' to $End_of_Report

end-procedure


!********************************************************************

#Include 'prcsapi.sqc'   !Update Process Scheduler Request Record
#Include 'prcsdef.sqc'   !Creates standard Process Scheduler variables
#Include 'pay305rt.sqc'  !Custom Run Control Parameters
#Include 'getpgdta.sqc'  !Get-PayGroup-Data procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'eoj.sqc'       !End of Job procedure
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'prcslng.sqc'   !Replaces getrplng.sqc
