!***********************************************
!  CHGFTDTS: Request FromDate and ThruDate     *
!            and Update the PSAsOfDate record  *
!***********************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:22:32                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

begin-procedure Change-From-Thru-Dates
  do Init-DateTime
  while $DateOK <> 'Y'
    do Set-Dates
  end-while
end-procedure

begin-procedure Set-Dates
  move 'Y' to $DateOK
  if $FromDateOK <> 'Y'
    input $FromDate 'Enter "From" Date ({Prompt-Date})'
    move {PDelimiter} to $DDelimiter
    do Format-Datetime($FromDate, $FromDate, {Prompt-Mask},'','native')
  end-if
  do Update-FromDate
  if $DateOK = 'Y'
    move 'Y' to $FromDateOK
    input $ThruDate 'Enter "Thru" Date ({Prompt-Date})'
    move {PDelimiter} to $DDelimiter
    do Format-Datetime($ThruDate, $ThruDate, {Prompt-Mask},'','native')
    do Update-ThruDate
    if $DateOK = 'Y'
      do Check-Dates
    end-if
  end-if
end-procedure

begin-procedure Update-FromDate
begin-SQL on-error=Bad-Date
UPDATE PSASOFDATE SET FROMDATE=$FROMDATE
end-SQL
end-procedure

begin-procedure Update-ThruDate
begin-SQL on-error=Bad-Date
UPDATE PSASOFDATE SET ASOFDATE=$THRUDATE
end-SQL
end-procedure

begin-procedure Check-Dates
begin-SELECT
PSO.FROMDATE,
PSO.ASOFDATE

  do Format-Datetime(&PSO.FromDate, $out1, {DEFCMP}, '', '')
  do Format-Datetime(&PSO.AsOfDate, $out2, {DEFCMP}, '', '')
  if $out1 > $out2
    display 'From Date cannot be greater than Thru Date'
    move 'N' to $DateOK
    move 'N' to $FromDateOK
  else
    move $ThruDate to $AsOfDate
  end-if

FROM PSASOFDATE PSO
end-SELECT
end-procedure

begin-procedure Bad-Date
  move 'N' to $DateOK
  display '**** This is an invalid date, please reenter ****'
end-procedure
