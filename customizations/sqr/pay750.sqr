!***********************************************************************
!  PAY750: Print contents of Tips Establishment Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:42:38                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   ! Set environment
#Include 'setup32.sqc'  ! Printer and page-size initialization


BEGIN-REPORT

  do Init-Report        ! Initialize the report
  do Process-Main       ! Main paragraph
  do Reset              ! Send RESET string to printer
  do Stdapi-Term        ! Standard SQR Termination

END-REPORT

#DEFINE  COL01  001      ! Column #01 - Company
#DEFINE  COL02  009      ! Column #02 - Company Name
#DEFINE  COL03  040      ! Column #03 - Business Unit
#DEFINE  COL04  057      ! Column #04 - Business Unit Name
#DEFINE  COL05  088      ! Column #05 - Effective Date
#DEFINE  COL06  101      ! Column #06 - Tips Establishment ID
#DEFINE  COL07  113      ! Column #07 - Establihment Descr
#DEFINE  COL08  146      ! Column #08 - Minimum Tips Percent
#DEFINE  COL09  162      ! Column #09 - Allocation Method


BEGIN-PROCEDURE INIT-REPORT

  move 'PAY750'                   to $ReportID
  move 'Tips Establishment Table' to $ReportTitle

  do Init-DateTime

  do Init-Number

  display '------------------------------------'
  display $ReportID
  display ' '
  display $ReportTitle
  display '------------------------------------'

  do Get-Current-DateTime

  do Stdapi-Init

end-procedure


BEGIN-HEADING 7

  #include 'stdhdg01.sqc'

  print 'Company'                               (+2,{COL01},)
  print 'Business Unit'                         (,{COL03},)
  print 'Effective'                             (,{COL05},)
  print 'Tips Establishment'                    (,{COL06},)
  print 'Minimum'                               (,{COL08},)
  print 'Allocation'                            (,{COL09},)

  print 'ID'                                    (+1,{COL01},)
  print 'Name'                                  (,{COL02},)
  print 'ID'                                    (,{COL03},)
  print 'Name'                                  (,{COL04},)
  print 'Date'                                  (,{COL05},)
  print 'ID'                                    (,{COL06},)
  print 'Name'                                  (,{COL07},)
  print 'Tips Percent'                          (,{COL08},)
  print 'Method'                                (,{COL09},)

  print '-------'                                (+1,{COL01},)
  print '------------------------------'         (,{COL02},)
  print '-------------'                          (,{COL03},)
  print '------------------------------'         (,{COL04},)
  print '----------'                             (,{COL05},)
  print '----------'                             (,{COL06},)
  print '------------------------------'         (,{COL07},)
  print '------------'                           (,{COL08},)
  print '----------'                             (,{COL09},)

end-HEADING

!***********************************************************************
!  PROCESS-MAIN
!     Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main

BEGIN-SELECT

TET.COMPANY                 (+1,{COL01},)
COM.DESCR                   (,{COL02},)
TET.BUSINESS_UNIT           (,{COL03},)

TET.EFFDT  &EffDt
  do Format-DateTime(&EffDt, $out, {DEFDATE}, '', '')
  print $out                   (0,{COL05},10)
  let $ASOFDATE=&EFFDT
TET.TIPS_ESTAB              (,{COL06},)
TET.MIN_TIPS_PCT            (,{COL08},)
TET.TIPS_ALLOC_METH

  MOVE &TET.BUSINESS_UNIT TO $BU
  DO GET-BUSINESS-UNIT-NAME
  PRINT $BU_DESCR           (,{COL04},)

  MOVE &TET.TIPS_ESTAB    TO $ESTAB
  MOVE &TET.COMPANY       TO $COMPANY

  DO GET-ESTABLISHMENT-NAME
  PRINT $ESTABDESCR         (,{COL07},)

  MOVE 'TIPS_ALLOC_METH'       TO $FIELDNAME
  MOVE &TET.TIPS_ALLOC_METH    TO $FIELDVALUE
  DO READ-TRANSLATE-TABLE
  PRINT $XLATSHORTNAME      (,{COL09},27)

FROM
     PS_TIPS_ESTAB_TBL TET,
     PS_COMPANY_TBL    COM

WHERE
      TET.COMPANY = COM.COMPANY
 AND  COM.EFFDT   =
            (SELECT MAX(COM1.EFFDT)
               FROM PS_COMPANY_TBL COM1
               WHERE COM1.COMPANY = COM.COMPANY
                 AND COM1.EFFDT <= TET.EFFDT)

ORDER BY TET.COMPANY, TET.BUSINESS_UNIT, TET.EFFDT DESC

END-SELECT

END-PROCEDURE

! *******************************************************************
! *  Include Section
! *******************************************************************
! *    This is where the SQC's are  included into the body of
! *    the program.
! *    NOTE:  All SQC's are called in lower-case.  For UNIX
! *           environment, make sure that the actual SQC files
! *           are stored in lower-case, or they will not be found.
! *******************************************************************

#Include 'getcodta.sqc'  !Get-Company-Name procedure
#Include 'getbunam.sqc'  !Get-Business-Unit-Name procedure
#Include 'getestnm.sqc'  !Get-Establishment-Name procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'readxlat.sqc'  !Routines to Retrieve XLATTABLE values
#Include 'stdapi.sqc'    !Update Process API

! *******************************************************************
!                   <<<   END OF FILE   >>>
! *******************************************************************
