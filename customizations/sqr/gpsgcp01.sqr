!***********************************************************************
!       PSGPCP01 : CPF Monthly Contributions Report                    *
!***********************************************************************
!  Created Based on a requirement from Singapore Payroll.              *
!                                                                      *
!  This report will print one or more of the following reports, based  *
!  on the run control selection:                                       *
!                                                                      *
!  1)  CDAC                                                            *
!  2)  CPF                                                             *
!  3)  ECF                                                             *
!  4)  FWL                                                             *
!  5)  MBMF
!  5)  MBF
!  6)  YMF                                                             *
!  6)  SDL                                                             *
!  7)  SHARE                                                           *
!  8)  SINDA                                                           *
!                                                                      *
!  The data in each of the report is in Employee Name order.           *
!                                                                      *
!  Totals for each of the contribution amount are printed at the end   *
!  of each of the report.                                              *
!                                                                      *
!  For the CPF Report only, a 'Group Total' is also printed after      *
!  each 100 employees.                                                 *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2018, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  28621584                                            !
!                                                                      *
!***********************************************************************
! Modification History                                                 *
! Dated      Author  ICE       Changes                                 *
! 28/11/02   KWH    549711000  Reset page numbers to zero between      *
!                              reports                                 *
! 17/12/03          673002000  SQR has been modify to replace MBMF     *
!                              deduction into two new deduction YMF    *
!                              and MBF.                                *
! 17/12/03          674283000  SQR has been modify to change the       *
!                              formatting.                             *
!                                                                      *
!***********************************************************************

#Include 'setenv.sqc'   ! Set environment
#Include 'setup31.sqc'  ! Printer and page-size initialization, SETUP31 and
                        !   SETUP32 SQCs to support HTML, HP PCL, PostScript,
                        !   PDF and Window in addition to Line Printer as
                        !   the output type.


!***********************************************************************
! Begin-Report                                                         *
!***********************************************************************
begin-report

  do Init-Processing
  do Stdapi-Init
  do Get-Parameters

  do Process-Main
  do Reset
  do Stdapi-Term

end-report

!***********************************************************************
! Procedure: Init-Processing                                           *
!***********************************************************************
begin-procedure Init-Processing

  do Init-DateTime
  do Get-Current-DateTime
  do Define-Prcs-Vars

end-procedure


!***********************************************************************
! Procedure: Get-Parameters                                            *
!***********************************************************************
begin-procedure Get-Parameters

Begin-SELECT
RC.GPSG_CPF_EMPLR_REF
RC.GPSG_YEAR
RC.MONTHCD
RC.BEGIN_DT
RC.END_DT
RC.GPSG_PRINT_CPF
RC.GPSG_PRINT_CDAC
RC.GPSG_PRINT_ECF
RC.GPSG_PRINT_MBF
RC.GPSG_PRINT_YMF
RC.GPSG_PRINT_SINDA
RC.GPSG_PRINT_SHARE
RC.GPSG_PRINT_FWL
RC.GPSG_PRINT_SDL

    let $RC_Emplr_Ref       = &RC.GPSG_CPF_EMPLR_REF
    let $RC_Year            = &RC.GPSG_YEAR
    let $RC_MonthCd         = &RC.MONTHCD
    let $RC_Begin_Dt        = &RC.BEGIN_DT
    let $RC_End_Dt          = &RC.END_DT
    let $Print_CPF          = &RC.GPSG_PRINT_CPF
    let $Print_CDAC         = &RC.GPSG_PRINT_CDAC
    let $Print_ECF          = &RC.GPSG_PRINT_ECF
    let $Print_MBF         = &RC.GPSG_PRINT_MBF
    let $Print_YMF         = &RC.GPSG_PRINT_YMF
    let $Print_SINDA        = &RC.GPSG_PRINT_SINDA
    let $Print_SHARE        = &RC.GPSG_PRINT_SHARE
    let $Print_FWL          = &RC.GPSG_PRINT_FWL
    let $Print_SDL          = &RC.GPSG_PRINT_SDL

    do Get_Emplr_Descr

FROM PS_GPSG_CPF_RUNCTL RC
WHERE RC.OPRID       = $PRCS_OPRID
  AND RC.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID
End-SELECT

end-procedure


!***********************************************************************
! Procedure: Get_Emplr_Descr                                           *
!***********************************************************************
begin-procedure Get_Emplr_Descr

Begin-SELECT
ER.GPSG_EMPLR_DESCR

    let $ER_Descr = &ER.GPSG_EMPLR_DESCR

FROM PS_GPSG_ER_CPF_REF ER
WHERE ER.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
End-SELECT

end-procedure


!***********************************************************************
! Procedure: Process-Main                                              *
!***********************************************************************
begin-procedure Process-Main

    do Get-Month-Name($RC_MonthCd, $MonthName)
    move 'GPSGCP01' to $ReportID
    let #Rept_Cnt = 0
    let $where_cpf = 'AND Z.CITIZENSHIP_STATUS <> '||'''||6||'''
    
    if $Print_CDAC = 'Y'
       let $Print_Now = 'CDAC'
       do Process-CDAC
       let #Rept_Cnt = #Rept_Cnt + 1
    end-if

    if $Print_CPF = 'Y'
    let $where_cpf = 'AND Z.CITIZENSHIP_STATUS not in ( '||'''||6||'''||','|| '''8'''||')'
       let $Print_Now = 'CPF'
       if #Rept_Cnt > 0
          let #page-count = 0
       end-if
       do Process-CPF
       let #Rept_Cnt = #Rept_Cnt + 1
    end-if

    if $Print_ECF = 'Y'
       let $Print_Now = 'ECF'
       if #Rept_Cnt > 0
          let #page-count = 0
       end-if
       do Process-ECF
       let #Rept_Cnt = #Rept_Cnt + 1
    end-if

    if $Print_FWL = 'Y'
       let $Print_Now = 'FWL'
       if #Rept_Cnt > 0
          let #page-count = 0
       end-if
       do Process-FWL
       let #Rept_Cnt = #Rept_Cnt + 1
    end-if

    if $Print_MBF = 'Y'

       let $Print_Now = 'MBF'
      if #Rept_Cnt > 0
           let #page-count = 0
      end-if
        do Process-MBF
       let #Rept_Cnt = #Rept_Cnt + 1
    end-if


    if $Print_YMF = 'Y'
       let $Print_Now = 'YMF'
      if #Rept_Cnt > 0
           let #page-count = 0
        end-if
        do Process-YMF
        let #Rept_Cnt = #Rept_Cnt + 1
    end-if

    if $Print_SDL = 'Y'
       let $Print_Now = 'SDL'
       if #Rept_Cnt > 0
          let #page-count = 0
       end-if
       do Process-SDL
       let #Rept_Cnt = #Rept_Cnt + 1
    end-if

    if $Print_SHARE = 'Y'
       let $Print_Now = 'SHARE'
       if #Rept_Cnt > 0
          let #page-count = 0
       end-if
       do Process-SHARE
       let #Rept_Cnt = #Rept_Cnt + 1
    end-if

    if $Print_SINDA = 'Y'
       let $Print_Now = 'SINDA'
       if #Rept_Cnt > 0
          let #page-count = 0
       end-if
       do Process-SINDA
       let #Rept_Cnt = #Rept_Cnt + 1
    end-if

end-procedure


!***********************************************************************
! Procedure: Process-CDAC                                              *
!***********************************************************************
begin-procedure Process-CDAC

    move 'CDAC FUND RETURN FOR THE MONTH OF ' to $xxx
    let $ReportTitle = $xxx || $MonthName || ' ' || $RC_Year
    UpperCase $ReportTitle
    display $ReportTitle

    Do Print-headings
    LET #Rept_Cnt = 1

    let $Footer_Text = 'CDAC RETURNS FOR ' || $MonthName || ' ' ||
                          $RC_Year || ' AS SHOWN ABOVE'
    UpperCase $Footer_Text

    Let #Total-CDAC-Amt = 0


Begin-SELECT
CDAC.EMPLID
CDAC.GPSG_EE_CPF_ACCT
CDAC.GPSG_CDAC
N1.NAME

    Let $Emplid = &CDAC.EMPLID
    Let $Name   = &N1.NAME

    Let $Employee_CPF_Ref = &CDAC.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    Let #CDAC_Amount = &CDAC.GPSG_CDAC

    If #LC > 77
       Do Print-Headings
    End-if
!    Else
       print $Name                     (+2,1,40)
       print $Translated_CPFNO         (0,+2,12)
       print #CDAC_Amount              (0,+2,18)   Edit 999,999,999,999.99
       Let #LC = #LC + 2


    Add #CDAC_Amount to #Total-CDAC-Amt

FROM PS_GPSG_CPF_RPTG CDAC,
     PS_NAMES N1
WHERE CDAC.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND CDAC.GPSG_YEAR = $RC_Year
  AND CDAC.MONTHCD = $RC_MonthCd
  AND CDAC.GPSG_CDAC <> 0
  AND N1.EMPLID = CDAC.EMPLID
  AND N1.NAME_TYPE = 'PRI'
  AND N1.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = N1.EMPLID
                 AND NAME_TYPE = N1.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)

ORDER BY N1.NAME
End-SELECT

    If #LC > 60
       Do Print-Headings
    End-if
!    Else
       print 'TOTAL'           (+2,43,12)
       print #Total-CDAC-Amt   (0,+2,18) Edit 999,999,999,999.99
       print 'THE EMPLOYER HEREBY GUARANTEES THE ACCURACY OF THE ' (+5,50)
       print $Footer_Text                                          (+1,50)
       print '_______________________________'                     (+4,50)
       print 'EMPLOYER''S AUTHORISED SIGNATORY'                    (+1,50)


end-procedure


!***********************************************************************
! Procedure: Process-CPF                                               *
!***********************************************************************
begin-procedure Process-CPF

    move 'CPF RETURN FOR THE MONTH OF ' to $xxx
    let $ReportTitle = $xxx || $MonthName || ' ' || $RC_Year
    UpperCase $ReportTitle
    display $ReportTitle

    move 'PART A - EMPLOYEES WHO DID NOT LEAVE EMPLOYMENT THIS MONTH ' to
          $Part_A_Title

    move 'PART B - EMPLOYEES WHO LEFT EMPLOYMENT THIS MONTH ' to
          $Part_B_Title

    move 'PART C - NEW EMPLOYEES ' to $Part_C_Title

    let $Footer_Text = 'OF THE CPF RETURNS FOR ' || $MonthName ||
                           ' ' ||  $RC_Year || ' AS'
    Uppercase $Footer_Text

    do Part-A-Report

    do Part-B-Report

    do Part-C-Report

    If #LC > 60
       Do Print-Headings
    end-if
!    Else
       print 'THE EMPLOYER HEREBY GUARANTEES THE ACCURACY' (+5,80)
       print $Footer_Text                                  (+1,80)
       print 'SHOWN ON THE SUBMITTED LISTING.'             (+1,80)
       print '_______________________________'             (+5,80)
       print 'EMPLOYER''S AUTHORISED SIGNATORY'            (+1,80)


end-procedure


!***********************************************************************
! Procedure: Part-A-Report                                             *
!                                                                      *
! Employees who did not leave Employment this month.                   *
!***********************************************************************
begin-procedure Part-A-Report

    let $CPF_Type                 = 'A'
    let $Group_Title              = $Part_A_Title

    let $Asterisk_fill            = ' '
    let #Employee_Count           = 0
    let #Hundred_Emp_Contribution = 0
    let #Group_A_Contribution     = 0

    Do Print-headings
    LET #Rept_Cnt = 1

    print $Group_title            (+1,1) Center

    let #LC = #LC + 2

Begin-SELECT
A.EMPLID
A.GPSG_EE_CPF_ACCT
A.GPSG_ER_ORD_CPF
A.GPSG_EE_ORD_CPF
A.GPSG_ER_ADD_CPF
A.GPSG_EE_ADD_CPF
A.GPSG_ER_VOL_CPF
A.GPSG_EE_VOL_CPF
A.GPSG_ORD_WAGES
A.GPSG_ADD_WAGES
N2.NAME

    Let $Emplid = &A.EMPLID
    Let $Name_A = &N2.NAME

    Let $Employee_CPF_Ref = &A.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    do Check-100-Employees

    let #Additional       = &A.GPSG_ADD_WAGES
    let #Ordinary         = &A.GPSG_ORD_WAGES
    let #Wages            = #Additional + #Ordinary
    let #CPF_Contribution = &A.GPSG_ER_ORD_CPF + &A.GPSG_EE_ORD_CPF +
                            &A.GPSG_ER_ADD_CPF + &A.GPSG_EE_ADD_CPF +
                            &A.GPSG_ER_VOL_CPF + &A.GPSG_EE_VOL_CPF

    If #LC > 75
       Do Print-Headings
    end-if
!    Else
       print $Name_A                 (+2,1,40)
       print $Asterisk_fill          (0,+2,1)
       print $Translated_CPFNO       (0,+2,12)
       print #CPF_Contribution       (0,+2,18)   edit 999,999,999,999.99
       print #Wages                  (0,+2,18)   edit 999,999,999,999.99
       print #Ordinary               (0,+2,18)   edit 999,999,999,999.99
       print #Additional             (0,+2,18)   edit 999,999,999,999.99
       Let #LC = #LC + 3

    let #Employee_count           = #Employee_count + 1
    let #Group_A_Contribution     = #Group_A_Contribution + #CPF_Contribution
    let #Hundred_Emp_Contribution = #Hundred_Emp_Contribution + #CPF_Contribution

FROM PS_GPSG_CPF_RPTG A,
     PS_NAMES N2,
     PS_CTZSHIP_SGP Z
WHERE A.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND A.GPSG_YEAR = $RC_Year
  AND A.MONTHCD = $RC_MonthCd
  AND A.GPSG_NEW_EMPLOYEE <> 'Y'
  AND A.TERMINATION_DT IS NULL
  AND Z.EMPLID = A.EMPLID
  !AND Z.DEPENDENT_ID = ' '
  AND Z.COUNTRY = 'SGP'
  [$where_cpf]
  AND Z.EFFDT =
       (SELECT MAX(CZ.EFFDT)
          FROM PS_CTZSHIP_SGP CZ
         WHERE CZ.EMPLID = Z.EMPLID
           AND CZ.COUNTRY = Z.COUNTRY
           AND CZ.EFFDT < &RC.END_DT)
  AND (A.GPSG_ER_ORD_CPF <> 0
  OR   A.GPSG_EE_ORD_CPF <> 0
  OR   A.GPSG_ER_ADD_CPF <> 0
  OR   A.GPSG_EE_ADD_CPF <> 0
  OR   A.GPSG_ER_VOL_CPF <> 0
  OR   A.GPSG_EE_VOL_CPF <> 0
  OR   A.GPSG_MBF        <> 0
  OR   A.GPSG_YMF        <> 0
  OR   A.GPSG_CDAC       <> 0
  OR   A.GPSG_SINDA      <> 0
  OR   A.GPSG_ECF        <> 0)
  AND N2.EMPLID = A.EMPLID
  AND N2.NAME_TYPE = 'PRI'
  AND N2.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = N2.EMPLID
                 AND NAME_TYPE = N2.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)
ORDER BY N2.NAME
End-SELECT

    if #Employee_Count <> 0
      let #Employee_Count = 0
      print '-'                           (+1,28,67) Fill
      print 'GROUP TOTAL'                 (+1,28,30)
      print #Hundred_Emp_contribution     (0,+4,18)  edit 999,999,999,999.99
      print '-'                           (+1,28,67) Fill
      let #Hundred_Emp_Contribution = 0
    end-if

    If #LC > 74
       Let $CPF_Type = '#'
       Do Print-Headings
    end-if
!    Else
       print 'PART A TOTAL'                  (+2,28,30)
       print #Group_A_Contribution           (0,+4,18)      edit 999,999,999,999.99


    New-Page    ! Print next part in new page
end-procedure


!***********************************************************************
! Procedure: Part-B-Report                                             *
!                                                                      *
! Employees who left Employment this month.                            *
!***********************************************************************
begin-procedure Part-B-Report

    let $CPF_Type                 = 'B'
    let $Group_Title              = $Part_B_Title
    let #Employee_Count           = 0
    let #Hundred_Emp_Contribution = 0
    let #Group_A_Contribution     = 0
    let $Asterisk_fill            = ' '

    print $Group_title            (+5,1) Center

    LET #LC = 6

Begin-SELECT
B.EMPLID
B.GPSG_EE_CPF_ACCT
B.GPSG_ER_ORD_CPF
B.GPSG_EE_ORD_CPF
B.GPSG_ER_ADD_CPF
B.GPSG_EE_ADD_CPF
B.GPSG_ER_VOL_CPF
B.GPSG_EE_VOL_CPF
B.GPSG_ORD_WAGES
B.GPSG_ADD_WAGES
N3.NAME

    Let $Emplid = &B.EMPLID
    Let $Name_B = &N3.NAME

    Let $Employee_CPF_Ref = &B.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    do Check-100-Employees

    let #Additional       = &B.GPSG_ADD_WAGES
    let #Ordinary         = &B.GPSG_ORD_WAGES
    let #Wages            = #Additional + #Ordinary
    let #CPF_Contribution = &B.GPSG_ER_ORD_CPF + &B.GPSG_EE_ORD_CPF +
                            &B.GPSG_ER_ADD_CPF + &B.GPSG_EE_ADD_CPF +
                            &B.GPSG_ER_VOL_CPF + &B.GPSG_EE_VOL_CPF


    If #LC > 75
       Do Print-Headings
    end-if
!    Else
       print $Name_B                 (+2,1,40)
       print $Asterisk_fill          (0,+2,1)
       print $Translated_CPFNO       (0,+2,12)
       print #CPF_Contribution       (0,+2,18)   edit 999,999,999,999.99
       print #Wages                  (0,+2,18)   edit 999,999,999,999.99
       print #Ordinary               (0,+2,18)   edit 999,999,999,999.99
       print #Additional             (0,+2,18)   edit 999,999,999,999.99
       Let #LC = #LC + 3

    let #Employee_count           = #Employee_count + 1
    let #Group_B_Contribution     = #Group_B_Contribution + #CPF_Contribution
    let #Hundred_Emp_Contribution = #Hundred_Emp_Contribution + #CPF_Contribution

FROM PS_GPSG_CPF_RPTG B,
     PS_NAMES N3,
     PS_CTZSHIP_SGP Z
WHERE B.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND B.GPSG_YEAR = $RC_Year
  AND B.MONTHCD = $RC_MonthCd
!  AND B.GPSG_NEW_EMPLOYEE <> 'Y'
  AND B.TERMINATION_DT IS NOT NULL
  AND Z.EMPLID = B.EMPLID
!  AND Z.DEPENDENT_ID = ' '
  AND Z.COUNTRY = 'SGP'
  [$where_cpf]
  AND Z.EFFDT =
       (SELECT MAX(CZ.EFFDT)
          FROM PS_CTZSHIP_SGP CZ
         WHERE CZ.EMPLID = Z.EMPLID
           AND CZ.COUNTRY = Z.COUNTRY
           AND CZ.EFFDT < $RC_End_Dt)
  AND (B.GPSG_ER_ORD_CPF <> 0
  OR   B.GPSG_EE_ORD_CPF <> 0
  OR   B.GPSG_ER_ADD_CPF <> 0
  OR   B.GPSG_EE_ADD_CPF <> 0
  OR   B.GPSG_ER_VOL_CPF <> 0
  OR   B.GPSG_EE_VOL_CPF <> 0
  OR   B.GPSG_MBF        <> 0
  OR   B.GPSG_YMF        <> 0
  OR   B.GPSG_CDAC       <> 0
  OR   B.GPSG_SINDA      <> 0
  OR   B.GPSG_ECF        <> 0)
  AND N3.EMPLID = B.EMPLID
  AND N3.NAME_TYPE = 'PRI'
  AND N3.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = N3.EMPLID
                 AND NAME_TYPE = N3.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)
ORDER BY N3.NAME
End-SELECT

    if #Employee_Count <> 0
      let #Employee_Count = 0
      print '-'                           (+1,28,67) Fill
      print 'GROUP TOTAL'                 (+1,28,30)
      print #Hundred_Emp_contribution     (0,+4,18)  edit 999,999,999,999.99
      print '-'                           (+1,28,67) Fill
      let #Hundred_Emp_Contribution = 0
    end-if

    If #LC > 74
       Let $CPF_Type = '#'
       Do Print-Headings
    end-if
       print 'PART B TOTAL'                  (+2,28,30)
       print #Group_B_Contribution           (0,+4,18)      edit 999,999,999,999.99


    New-Page   ! Print next part in new page

end-procedure


!***********************************************************************
! Procedure: Part-C-Report                                             *
!                                                                      *
! Employees who joined Employment this month.                          *
!***********************************************************************
begin-procedure Part-C-Report

    let $CPF_Type                 = 'C'
    let $Group_Title              = $Part_C_Title
    let #Employee_Count           = 0
    let #Hundred_Emp_Contribution = 0
    let #Group_A_Contribution     = 0
    let $Asterisk_fill            = ' '

    print $Group_title            (+5,1) Center

    LET #LC = 6

Begin-SELECT
C.EMPLID
C.GPSG_EE_CPF_ACCT
C.GPSG_ER_ORD_CPF
C.GPSG_EE_ORD_CPF
C.GPSG_ER_ADD_CPF
C.GPSG_EE_ADD_CPF
C.GPSG_ER_VOL_CPF
C.GPSG_EE_VOL_CPF
C.GPSG_ORD_WAGES
C.GPSG_ADD_WAGES
N4.NAME

    Let $Emplid = &C.EMPLID
    Let $Name_C = &N4.NAME

    Let $Employee_CPF_Ref = &C.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    do Check-100-Employees

    let #Additional       = &C.GPSG_ADD_WAGES
    let #Ordinary         = &C.GPSG_ORD_WAGES
    let #Wages            = #Additional + #Ordinary
    let #CPF_Contribution = &C.GPSG_ER_ORD_CPF + &C.GPSG_EE_ORD_CPF +
                            &C.GPSG_ER_ADD_CPF + &C.GPSG_EE_ADD_CPF +
                            &C.GPSG_ER_VOL_CPF + &C.GPSG_EE_VOL_CPF

    If #LC > 75
       Do Print-Headings
    end-if
       print $Name_C                 (+2,1,40)
       print $Asterisk_fill          (0,+2,1)
       print $Translated_CPFNO       (0,+2,12)
       print #CPF_Contribution       (0,+2,18)   edit 999,999,999,999.99
       print #Wages                  (0,+2,18)   edit 999,999,999,999.99
       print #Ordinary               (0,+2,18)   edit 999,999,999,999.99
       print #Additional             (0,+2,18)   edit 999,999,999,999.99
       Let #LC = #LC + 3

    let #Employee_count           = #Employee_count + 1
    let #Group_C_Contribution     = #Group_C_Contribution + #CPF_Contribution
    let #Hundred_Emp_Contribution = #Hundred_Emp_Contribution + #CPF_Contribution

FROM PS_GPSG_CPF_RPTG C,
     PS_NAMES N4,
     PS_CTZSHIP_SGP Z
WHERE C.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND C.GPSG_YEAR = $RC_Year
  AND C.MONTHCD = $RC_MonthCd
  AND C.GPSG_NEW_EMPLOYEE = 'Y'
  AND C.TERMINATION_DT IS NULL
  AND Z.EMPLID = C.EMPLID
  !AND Z.DEPENDENT_ID = ' '
  AND Z.COUNTRY = 'SGP'
  [$where_cpf]
  AND Z.EFFDT =
       (SELECT MAX(CZ.EFFDT)
          FROM PS_CTZSHIP_SGP CZ
         WHERE CZ.EMPLID = Z.EMPLID
           AND CZ.COUNTRY = Z.COUNTRY
           AND CZ.EFFDT <= $RC_End_Dt)
  AND (C.GPSG_ER_ORD_CPF <> 0
  OR   C.GPSG_EE_ORD_CPF <> 0
  OR   C.GPSG_ER_ADD_CPF <> 0
  OR   C.GPSG_EE_ADD_CPF <> 0
  OR   C.GPSG_ER_VOL_CPF <> 0
  OR   C.GPSG_EE_VOL_CPF <> 0
  OR   C.GPSG_MBF        <> 0
  OR   C.GPSG_YMF        <> 0
  OR   C.GPSG_CDAC       <> 0
  OR   C.GPSG_SINDA      <> 0
  OR   C.GPSG_ECF        <> 0)
  AND N4.EMPLID = C.EMPLID
  AND N4.NAME_TYPE = 'PRI'
  AND N4.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = N4.EMPLID
                 AND NAME_TYPE = N4.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)
ORDER BY N4.NAME
End-SELECT

    if #Employee_Count <> 0
      let #Employee_Count = 0
      print '-'                           (+1,28,67) Fill
      print 'GROUP TOTAL'                 (+1,28,30)
      print #Hundred_Emp_contribution     (0,+4,18)  edit 999,999,999,999.99
      print '-'                           (+1,28,67) Fill
      let #Hundred_Emp_Contribution = 0
    end-if

    If #LC > 65
       Let $CPF_Type = '#'
       Do Print-Headings
    end-if
       print 'PART C TOTAL'                  (+2,28,30)
       print #Group_C_Contribution           (0,+4,18)      edit 999,999,999,999.99


end-procedure


!***********************************************************************
! Procedure: Check-100-Employees                                       *
!                                                                      *
! After every 100 Employees are printed a group total is required to   *
! to be printed.                                                       *
!***********************************************************************
begin-procedure Check-100-Employees

  if #Employee_Count = 100
    let #Employee_Count = 0
    print '-'                        (+1,20,67) Fill
    print 'GROUP TOTAL'              (+1,20,30)
    print #Hundred_Emp_contribution  (0,+4,18)  edit 999,999,999,999.99
    print '-'                        (+1,20,67) Fill
    print ' '                        (+1,20,67)
    let #Hundred_Emp_Contribution = 0
    LET #LC = #LC + 5
  end-if

end-procedure


!***********************************************************************
! Procedure: Process-ECF                                               *
!***********************************************************************
begin-procedure Process-ECF

    move 'ECF FUND RETURN FOR THE MONTH OF ' to $xxx
    let $ReportTitle = $xxx || $MonthName || ' ' || $RC_Year
    UpperCase $ReportTitle
    display $ReportTitle

    let $Footer_Text = 'ECF RETURNS FOR ' || $MonthName || ' ' ||
                          $RC_Year || ' AS SHOWN ABOVE'
    UpperCase $Footer_Text

    Do Print-headings
    LET #Rept_Cnt = 1

    Let #Total-ECF-Amt = 0

    let #LC = 8

Begin-SELECT
ECF.EMPLID
ECF.GPSG_EE_CPF_ACCT
ECF.GPSG_ECF
N5.NAME

    Let $Emplid = &ECF.EMPLID
    Let $Name   = &N5.NAME

    Let $Employee_CPF_Ref = &ECF.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    Let #ECF_Amount = &ECF.GPSG_ECF

    If #LC > 77
       Do Print-Headings
    end-if
       print $Name                     (+2,1,40)
       print $Translated_CPFNO         (0,+2,12)
       print #ECF_Amount               (0,+2,18)   Edit 999,999,999,999.99
       Let #LC = #LC + 2


    Add #ECF_Amount to #Total-ECF-Amt

FROM PS_GPSG_CPF_RPTG ECF,
     PS_NAMES N5
WHERE ECF.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND ECF.GPSG_YEAR = $RC_Year
  AND ECF.MONTHCD = $RC_MonthCd
  AND ECF.GPSG_ECF <> 0
  AND N5.EMPLID = ECF.EMPLID
  AND N5.NAME_TYPE = 'PRI'
  AND N5.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = N5.EMPLID
                 AND NAME_TYPE = N5.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)
ORDER BY N5.NAME
End-SELECT

    If #LC > 60
       Do Print-Headings
    end-if
       print 'TOTAL'           (+2,43,12)
       print #Total-ECF-Amt    (0,+2,18) Edit 999,999,999,999.99
       print 'THE EMPLOYER HEREBY GUARANTEES THE ACCURACY OF THE ' (+5,50)
       print $Footer_Text                                          (+1,50)
       print '_______________________________'                     (+4,50)
       print 'EMPLOYER''S AUTHORISED SIGNATORY'                    (+1,50)


end-procedure


!***********************************************************************
! Procedure: Process-FWL                                               *
!***********************************************************************
begin-procedure Process-FWL

    move 'FOREIGN WORKER LEVY LIST FOR THE MONTH OF ' to $xxx
    let $ReportTitle = $xxx || $MonthName || ' ' || $RC_Year
    UpperCase $ReportTitle
    display $ReportTitle

    Do Print-headings
    LET #Rept_Cnt = 1

    Let #Total-FWL-Amt = 0

Begin-SELECT
FWL.EMPLID
FWL.GPSG_EE_CPF_ACCT
FWL.GPSG_FWL
FWL.GPSG_ORD_WAGES
FWL.GPSG_ADD_WAGES
N6.NAME

    Let $Emplid = &FWL.EMPLID
    Let $Name   = &N6.NAME

    Let $Employee_CPF_Ref = &FWL.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    Let #FWL_Amount = &FWL.GPSG_FWL

    Let #Total_Wage = &FWL.GPSG_ORD_WAGES + &FWL.GPSG_ADD_WAGES

    If #LC > 77
       Do Print-Headings
    end-if
       print $Name                     (+2,1,40)
       print $Translated_CPFNO         (0,+2,12)
       print #Total_Wage               (0,+2,13)   Edit 99,999,999.99
       print #FWL_Amount               (0,+2,18)   Edit 999,999,999,999.99
       Let #LC = #LC + 2


    Add #FWL_Amount to #Total-FWL-Amt

FROM PS_GPSG_CPF_RPTG FWL,
     PS_NAMES N6
WHERE FWL.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND FWL.GPSG_YEAR = $RC_Year
  AND FWL.MONTHCD = $RC_MonthCd
  AND FWL.GPSG_FWL <> 0
  AND N6.EMPLID = FWL.EMPLID
  AND N6.NAME_TYPE = 'PRI'
  AND N6.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = N6.EMPLID
                 AND NAME_TYPE = N6.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)
ORDER BY N6.NAME
End-SELECT

    If #LC > 74
       Do Print-Headings
    end-if
       print 'TOTAL'             (+2,57,13)
       print #Total-FWL-Amt      (0,+2,18)  Edit 999,999,999,999.99


end-procedure


!***********************************************************************
! Procedure: Process-MBF                                              *
!***********************************************************************
begin-procedure Process-MBF

    move 'MOSQUE BUILDING FUND RETURN FOR THE MONTH OF ' to $xxx
    let $ReportTitle = $xxx || $MonthName || ' ' || $RC_Year
    UpperCase $ReportTitle
    display $ReportTitle

    let $Footer_Text = 'MBF RETURNS FOR ' || $MonthName || ' ' ||
                          $RC_Year || ' AS SHOWN ABOVE'
    UpperCase $Footer_Text

    Do Print-headings
    LET #Rept_Cnt = 1

    Let #Total-MBF-Amt = 0

Begin-SELECT
MBF.EMPLID
MBF.GPSG_EE_CPF_ACCT
MBF.GPSG_MBF
N7.NAME

    Let $Emplid = &MBF.EMPLID
    Let $Name   = &N7.NAME

    Let $Employee_CPF_Ref = &MBF.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    Let #MBF_Amount = &MBF.GPSG_MBF

    If #LC > 77
       Do Print-Headings
    end-if
       print $Name                     (+2,1,40)
       print $Translated_CPFNO         (0,+2,12)
       print #MBF_Amount              (0,+2,18)   Edit 999,999,999,999.99
       Let #LC = #LC + 2


    Add #MBF_Amount to #Total-MBF-Amt

FROM PS_GPSG_CPF_RPTG MBF,
     PS_NAMES N7
WHERE MBF.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND MBF.GPSG_YEAR = $RC_Year
  AND MBF.MONTHCD = $RC_MonthCd
  AND MBF.GPSG_MBF <> 0
  AND N7.EMPLID = MBF.EMPLID
  AND N7.NAME_TYPE = 'PRI'
  AND N7.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = N7.EMPLID
                 AND NAME_TYPE = N7.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)
ORDER BY N7.NAME
End-SELECT

    If #LC > 60
       Do Print-Headings
    end-if
       print 'TOTAL'             (+2,43,12)
       print #Total-MBF-Amt     (0,+2,18)  Edit 999,999,999,999.99
       print 'THE EMPLOYER HEREBY GUARANTEES THE ACCURACY OF THE ' (+5,50)
       print $Footer_Text                                          (+1,50)
       print '_______________________________'                     (+4,50)
       print 'EMPLOYER''S AUTHORISED SIGNATORY'                    (+1,50)


end-procedure


!***********************************************************************
! Procedure: Process-YMF                                              *
!***********************************************************************
begin-procedure Process-YMF

    move 'YAYASAN MENDAKI FUND RETURN FOR THE MONTH OF ' to $xxx
    let $ReportTitle = $xxx || $MonthName || ' ' || $RC_Year
    UpperCase $ReportTitle
    display $ReportTitle

    let $Footer_Text = 'YMF RETURNS FOR ' || $MonthName || ' ' ||
                          $RC_Year || ' AS SHOWN ABOVE'
    UpperCase $Footer_Text

    Do Print-headings
    LET #Rept_Cnt = 1

    Let #Total-YMF-Amt = 0

Begin-SELECT
YMF.EMPLID
YMF.GPSG_EE_CPF_ACCT
YMF.GPSG_YMF
N7S.NAME  &N7S.NAME

    Let $Emplid = &YMF.EMPLID
    Let $Name   = &N7S.NAME

    Let $Employee_CPF_Ref = &YMF.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    Let #YMF_Amount = &YMF.GPSG_YMF

    If #LC > 77
       Do Print-Headings
    end-if
       print $Name                     (+2,1,40)
       print $Translated_CPFNO         (0,+2,12)
       print #YMF_Amount              (0,+2,18)   Edit 999,999,999,999.99
       Let #LC = #LC + 2


    Add #YMF_Amount to #Total-YMF-Amt

FROM PS_GPSG_CPF_RPTG YMF,
     PS_NAMES N7S
WHERE YMF.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND YMF.GPSG_YEAR = $RC_Year
  AND YMF.MONTHCD = $RC_MonthCd
  AND YMF.GPSG_YMF <> 0
  AND N7S.EMPLID = YMF.EMPLID
  AND N7S.NAME_TYPE = 'PRI'
  AND N7S.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = N7S.EMPLID
                 AND NAME_TYPE = N7S.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)
ORDER BY N7S.NAME
End-SELECT

    If #LC > 60
       Do Print-Headings
    end-if
       print 'TOTAL'             (+2,43,12)
       print #Total-YMF-Amt     (0,+2,18)  Edit 999,999,999,999.99
       print 'THE EMPLOYER HEREBY GUARANTEES THE ACCURACY OF THE ' (+5,50)
       print $Footer_Text                                          (+1,50)
       print '_______________________________'                     (+4,50)
       print 'EMPLOYER''S AUTHORISED SIGNATORY'                    (+1,50)


end-procedure


!***********************************************************************
! Procedure: Process-SDL                                               *
!***********************************************************************
begin-procedure Process-SDL

    move 'SKILL DEVELOPMENT LEVY LIST FOR THE MONTH OF ' to $xxx
    let $ReportTitle = $xxx || $MonthName || ' ' || $RC_Year
    UpperCase $ReportTitle
    display $ReportTitle

    Do Print-headings
    LET #Rept_Cnt = 1

    Let #Total-SDL-Amt = 0

Begin-SELECT
SDL.EMPLID
SDL.GPSG_EE_CPF_ACCT
SDL.GPSG_SDL
SDL.GPSG_ORD_WAGES
SDL.GPSG_ADD_WAGES
N8.NAME

    Let $Emplid = &SDL.EMPLID
    Let $Name   = &N8.NAME

    Let $Employee_CPF_Ref = &SDL.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    Let #SDL_Amount = &SDL.GPSG_SDL

    Let #Total_Wage = 0
    Add &SDL.GPSG_ORD_WAGES to #Total_Wage
    Add &SDL.GPSG_ADD_WAGES to #Total_Wage

    If #LC > 77
       Do Print-Headings
    end-if
       print $Name                     (+2,1,40)
       print $Translated_CPFNO         (0,+2,12)
       print #Total_Wage               (0,+2,13)   Edit 99,999,999.99
       print #SDL_Amount               (0,+2,18)   Edit 999,999,999,999.99
       Let #LC = #LC + 2


    Add #SDL_Amount to #Total-SDL-Amt

FROM PS_GPSG_CPF_RPTG SDL,
     PS_NAMES N8
WHERE SDL.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND SDL.GPSG_YEAR = $RC_Year
  AND SDL.MONTHCD = $RC_MonthCd
  AND SDL.GPSG_SDL <> 0
  AND N8.EMPLID = SDL.EMPLID
  AND N8.NAME_TYPE = 'PRI'
  AND N8.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = N8.EMPLID
                 AND NAME_TYPE = N8.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)
ORDER BY N8.NAME
End-SELECT

    If #LC > 74
       Do Print-Headings
    end-if
       print 'TOTAL'             (+2,57,13)
       print #Total-SDL-Amt      (0,+2,18)  Edit 999,999,999,999.99


end-procedure


!***********************************************************************
! Procedure: Process-SHARE                                             *
!***********************************************************************
begin-procedure Process-SHARE

    move 'SHARE FUND RETURN FOR THE MONTH OF ' to $xxx
    let $ReportTitle = $xxx || $MonthName || ' ' || $RC_Year
    UpperCase $ReportTitle
    display $ReportTitle

    let $Footer_Text = 'SHARE RETURNS FOR ' || $MonthName || ' ' ||
                          $RC_Year || ' AS SHOWN ABOVE'
    UpperCase $Footer_Text

    Do Print-headings
    LET #Rept_Cnt = 1

    Let #Total-SHARE-Amt = 0

Begin-SELECT
SHARE1.EMPLID
SHARE1.GPSG_EE_CPF_ACCT
SHARE1.GPSG_SHARE
N9.NAME

    Let $Emplid = &SHARE1.EMPLID
    Let $Name   = &N9.NAME

    Let $Employee_CPF_Ref = &SHARE1.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    Let #SHARE_Amount = &SHARE1.GPSG_SHARE

    If #LC > 77
       Do Print-Headings
    end-if
       print $Name                     (+2,1,40)
       print $Translated_CPFNO         (0,+2,12)
       print #SHARE_Amount              (0,+2,18)   Edit 999,999,999,999.99
       Let #LC = #LC + 2


    Add #SHARE_Amount to #Total-SHARE-Amt

  FROM PS_GPSG_CPF_RPTG SHARE1,
       PS_PERSON_NAME N9
   WHERE SHARE1.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
     AND SHARE1.GPSG_YEAR = $RC_Year
     AND SHARE1.MONTHCD = $RC_MonthCd
     AND SHARE1.GPSG_SHARE <> 0
     AND N9.EMPLID = SHARE1.EMPLID
   ORDER BY N9.NAME
End-SELECT


    If #LC > 60
       Do Print-Headings
    end-if
       print 'TOTAL'             (+2,43,12)
       print #Total-SHARE-Amt     (0,+2,18)  Edit 999,999,999,999.99
       print 'THE EMPLOYER HEREBY GUARANTEES THE ACCURACY OF THE ' (+5,50)
       print $Footer_Text                                          (+1,50)
       print '_______________________________'                     (+4,50)
       print 'EMPLOYER''S AUTHORISED SIGNATORY'                    (+1,50)


end-procedure


!***********************************************************************
! Procedure: Process-SINDA                                             *
!***********************************************************************
begin-procedure Process-SINDA

    move 'SINDA FUND RETURN FOR THE MONTH OF ' to $xxx
    let $ReportTitle = $xxx || $MonthName || ' ' || $RC_Year
    UpperCase $ReportTitle
    display $ReportTitle

    let $Footer_Text = 'SINDA RETURNS FOR ' || $MonthName || ' ' ||
                          $RC_Year || ' AS SHOWN ABOVE'
    UpperCase $Footer_Text

    Do Print-headings
    LET #Rept_Cnt = 1

    Let #Total-SINDA-Amt = 0

Begin-SELECT
SINDA.EMPLID
SINDA.GPSG_EE_CPF_ACCT
SINDA.GPSG_SINDA
M.NAME

    Let $Emplid = &SINDA.EMPLID
    Let $Name   = &M.NAME

    Let $Employee_CPF_Ref = &SINDA.GPSG_EE_CPF_ACCT
    do Translate-CPF-No

    Let #SINDA_Amount = &SINDA.GPSG_SINDA

    If #LC > 77
       Do Print-Headings
    end-if
       print $Name                     (+2,1,40)
       print $Translated_CPFNO         (0,+2,12)
       print #SINDA_Amount             (0,+2,18)   Edit 999,999,999,999.99
       Let #LC = #LC + 2


    Add #SINDA_Amount to #Total-SINDA-Amt

FROM PS_GPSG_CPF_RPTG SINDA,
     PS_NAMES M
WHERE SINDA.GPSG_CPF_EMPLR_REF = $RC_Emplr_Ref
  AND SINDA.GPSG_YEAR = $RC_Year
  AND SINDA.MONTHCD = $RC_MonthCd
  AND SINDA.GPSG_SINDA <> 0
  AND M.EMPLID = SINDA.EMPLID
  AND M.NAME_TYPE = 'PRI'
  AND M.EFFDT = (SELECT MAX(EFFDT)
                 FROM PS_NAMES
                 WHERE EMPLID = M.EMPLID
                 AND NAME_TYPE = M.NAME_TYPE
                 AND EFFDT <= &RC.END_DT)
ORDER BY M.NAME
End-SELECT

    If #LC > 60
       Do Print-Headings
    end-if
       print 'TOTAL'             (+2,43,12)
       print #Total-SINDA-Amt    (0,+2,18)  Edit 999,999,999,999.99
       print 'THE EMPLOYER HEREBY GUARANTEES THE ACCURACY OF THE ' (+5,50)
       print $Footer_Text                                          (+1,50)
       print '_______________________________'                     (+4,50)
       print 'EMPLOYER''S AUTHORISED SIGNATORY'                    (+1,50)


end-procedure


!***********************************************************************
! Procedure: Translate-CPF-No                                          *
!***********************************************************************
Begin-Procedure Translate-CPF-No

    Let $CPF_Account_No = substr($Employee_CPF_Ref,1,8)
    Let $Official_Ref   = substr($Employee_CPF_Ref,9,1)

!   Translate Official Reference from alpha code to numeric code
!   ------------------------------------------------------------
    Let $Translated_Ref_No =
          translate($Official_Ref ,'ABCDEFGHIJZ','1234567890-')

    if $Translated_Ref_No = ' ' or $Translated_Ref_No = ''
       let $Translated_CPFNO = $CPF_Account_No
    else
       let $Translated_CPFNO = $CPF_Account_No || '/' || $Translated_Ref_No
    end-if

end-procedure


!***********************************************************************
! Print-headings                                                       *
!***********************************************************************
begin-procedure Print-headings

if #Rept_Cnt > 0
   NEW-PAGE
End-If

#Include 'stdhdg01.sqc'

  if $Print_Now = 'CDAC'
     print 'CPF Emplr Ref.No./CPF Submission No.:'        (+2,1)
     print $RC_Emplr_ref           (0,+2)
     print $ER_Descr               (0,+5)
     print 'EMPLOYEE NAME'         (+2,1,40)
     print 'ACCOUNT NO'            (0,+2,12)
     print ' CDAC CONTRIBUTION'    (0,+2,18)
  end-if

  if $Print_Now = 'CPF'
     !print 'SAMIR '               (+1,1) Center

     print 'EMPLOYER REF #'        (+2,10)
     print $RC_Emplr_ref           (0,+2)
     print $ER_Descr               (0,+5)
     If $CPF_Type <> '#'
         print 'EMPLOYEE NAME'         (+2,1,40)
         print 'ACCOUNT NO'            (0,+5,12)
         print 'TOTAL CONTRIBUTION'    (0,+2,18)
         print '       TOTAL WAGES'    (0,+2,18)
         print '    ORDINARY WAGES'    (0,+2,18)
         print '  ADDITIONAL WAGES'    (0,+2,18)
     end-if
  end-if

  if $Print_Now = 'ECF'
     print 'EMPLOYER REF #'        (+2,1)
     print $RC_Emplr_ref           (0,+2)
     print $ER_Descr               (0,+5)
     print 'EMPLOYEE NAME'         (+2,1,40)
     print 'ACCOUNT NO'            (0,+2,12)
     print '  ECF CONTRIBUTION'    (0,+2,18)
  end-if

  if $Print_Now = 'FWL'
     print 'EMPLOYER REF #'        (+2,1)
     print $RC_Emplr_ref           (0,+2)
     print $ER_Descr               (0,+5)
     print 'EMPLOYEE NAME'         (+2,1,40)
     print 'ACCOUNT NO'            (0,+2,12)
     print 'MONTHLY GROSS'         (0,+2,13)
     print '  FWL CONTRIBUTION'    (0,+2,18)
  end-if

  if $Print_Now = 'MBF'
     print 'EMPLOYER REF #'        (+2,1)
     print $RC_Emplr_ref           (0,+2)
     print $ER_Descr               (0,+5)
     print 'EMPLOYEE NAME'         (+2,1,40)
     print 'ACCOUNT NO'            (0,+2,12)
     print ' MBF CONTRIBUTION'    (0,+2,18)
  end-if

  if $Print_Now = 'YMF'
     print 'EMPLOYER REF #'        (+2,1)
     print $RC_Emplr_ref           (0,+2)
     print $ER_Descr               (0,+5)
     print 'EMPLOYEE NAME'         (+2,1,40)
     print 'ACCOUNT NO'            (0,+2,12)
     print ' YMF CONTRIBUTION'    (0,+2,18)
  end-if

  if $Print_Now = 'SDL'
     print 'EMPLOYER REF #'        (+2,1)
     print $RC_Emplr_ref           (0,+2)
     print $ER_Descr               (0,+5)
     print 'EMPLOYEE NAME'         (+2,1,40)
     print 'ACCOUNT NO'            (0,+2,12)
     print 'MONTHLY GROSS'         (0,+2,13)
     print '  SDL CONTRIBUTION'    (0,+2,18)
  end-if

  if $Print_Now = 'SHARE'
     print 'EMPLOYER REF #'        (+2,1)
     print $RC_Emplr_ref           (0,+2)
     print $ER_Descr               (0,+5)
     print 'EMPLOYEE NAME'         (+2,1,40)
     print 'ACCOUNT NO'            (0,+2,12)
     print 'SHARE CONTRIBUTION'    (0,+2,18)
  end-if

  if $Print_Now = 'SINDA'
     print 'EMPLOYER REF #'        (+2,1)
     print $RC_Emplr_ref           (0,+2)
     print $ER_Descr               (0,+5)
     print 'EMPLOYEE NAME'         (+2,1,40)
     print 'ACCOUNT NO'            (0,+2,12)
     print 'SINDA CONTRIBUTION'    (0,+2,18)
  end-if

  Let #LC = 8

End-Procedure

!***********************************************************************

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getmonnm.sqc'  !Routine to Get Name of the month
#Include 'stdapi.sqc'    !Routines to Update Run Status
