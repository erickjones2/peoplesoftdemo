!************************************************************************
!  STSU017:  Stock Agent Detail
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:57:03                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
   #include 'ptpsp125.sqc'  !Printer and page-size init
end-setup

#define  col2    5
#define  col3    8
#define  col4    10

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let     $ReportID     =   'STSU017'

  do Get-Report-Language

  if $prcs_process_instance = ''
     do Prompts             !Prompt user

  else
     do Get-SQR-Parms
     do Get-Values
  end-if

  show    'FLOW: Init-Report'
  show    'Report Title: ' $REPORTTITLE

end-procedure

!******************************************************************************
begin-procedure Prompts
!******************************************************************************

!-----------------------------------------------------------------------------!
! Procedure: Prompts                                                          !
! Descr:     Prompts for all input parameters in the event the program is run !
!            directly from SQR.  In normal use, this should not happen.       !
!-----------------------------------------------------------------------------!

end-procedure

!************************************************************************
begin-procedure Get-Values
!************************************************************************

end-procedure

!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ('STOCK',$language_cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK','EFFDT',           $EFFDT,           #DW)
  do Get_Field_Information ('STOCK','STOCK_ID',        $STOCK_ID,        #DW)
  do Get_Field_Information ('STOCK','NAME',            $NAME,            #DW)

  do Get_Field_Information ('STSU017','STSU017',         $REPORTTITLE,     #DW)
  do Get_Field_Information ('STSU017','ST_AGENCY_ID',    $ST_AGENCY_ID,    #DW)
  do Get_Field_Information ('STSU017','EFF_STATUS',      $EFF_STATUS,      #DW)
  do Get_Field_Information ('STSU017','AGENCY_TYPE',     $AGENCY_TYPE,     #DW)
  do Get_Field_Information ('STSU017','LAST_CONTROL_NBR',$LAST_CONTROL_NBR,#DW)
  do Get_Field_Information ('STSU017','CONTACT',         $CONTACT,         #DW)
  do Get_Field_Information ('STSU017','ADDRESS',         $ADDRESS,         #DW)
  do Get_Field_Information ('STSU017','PHONE',           $PHONE,           #DW)
  do Get_Field_Information ('STSU017','EMAIL_ADDR',      $EMAIL_ADDR,      #DW)
  do Get_Field_Information ('STSU017','FAX_NBR',         $FAX_NBR,         #DW)

end-procedure

!************************************************************************
begin-heading 4
!************************************************************************

#include 'stdhdgtr.sqc'

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

begin-Select

A.STOCK_ID                () ON-BREAK Print=never before=Select-Dtl
A.ST_AGENCY_ID
A.LAST_CONTROL_NBR

  do Print-Info
  do Select-Detail
  print ' '                    (+1,1,1)

from      PS_STOCK_AGENT  A
order by  A.STOCK_ID

end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

B.STOCK_ID
B.DESCRSHORT

  do Print-data

 from  PS_STOCK_TBL B
where  B.STOCK_ID   =  &A.STOCK_ID

end-Select

end-procedure

!***********************************************************************
begin-procedure Select-Detail
!***********************************************************************

begin-Select

C.STOCK_ID
C.ST_AGENCY_ID
C.EFFDT
C.EFF_STATUS
C.AGENCY_TYPE

  do Print-Detail
  do Select-LowerDetail

 from  PS_STOCK_AGENT_DTL   C
where  C.STOCK_ID           =  &A.STOCK_ID
  and  C.ST_AGENCY_ID       =  &A.ST_AGENCY_ID

end-Select

end-procedure

!************************************************************************
begin-procedure Select-LowerDetail
!************************************************************************

begin-Select

D.STOCK_ID
D.ST_AGENCY_ID
D.EFFDT
D.ST_CONTACT_TYPE
D.COUNTRY
D.ADDRESS1
D.CITY
D.STATE
D.POSTAL
D.COUNTY
D.CONTACT_NAME
D.PHONE
D.EMAIL_ADDR
D.FAX

  do Print-LowerDetail

 from  PS_ST_AGENT_CNTCT  D
where  D.STOCK_ID         =  &C.STOCK_ID
  and  D.ST_AGENCY_ID     =  &C.ST_AGENCY_ID
  and  D.EFFDT            =  &C.EFFDT

end-Select

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  print $STOCK_ID              (+1,1,08)
  print ':'                    (,,1)
  print &A.STOCK_ID            (0,+2,10)
  print &B.DESCRSHORT          (0,+5,10)

end-procedure

!************************************************************************
begin-procedure Print-Info
!************************************************************************

  print $ST_AGENCY_ID          (+2,{col2},9)
  print ':'                    (,,1)
  print &A.ST_AGENCY_ID        (0,+2,10)

  print $LAST_CONTROL_NBR      (+1,{col2},14)
  print ':'                    (,,1)
  print &A.LAST_CONTROL_NBR    (0,+2,06)

end-procedure

!***********************************************************************
begin-procedure Print-Detail
!***********************************************************************

  do Format-DateTime(&C.EFFDT, $EFFDT1, {DEFYMD}, '', '')

  print $EFFDT                 (+2,{col3},08)
  print ':'                    (,,1)
  print $EFFDT1                (0,22,10)

  do Get-Data

  print $EFF_STATUS            (+1,{col3},06)
  print ':'                    (,,1)
  print $Status                (0,22,10)

  print $AGENCY_TYPE           (+1,{col3},11)
  print ':'                    (,,1)
  print $AgencyType            (0,22,10)

end-procedure

!************************************************************************
begin-procedure Print-LowerDetail
!************************************************************************

  print $CONTACT              (+2,{col3},07)

  print $NAME                 (+1,{col4},04)
  print ':'                   (,,1)
  print &D.CONTACT_NAME       (0,18,50)

  print $PHONE                (+1,{col4},05)
  print ':'                   (,,1)
  print &D.PHONE              (0,18,24)

  print $EMAIL_ADDR           (+1,{col4},05)
  print ':'                   (,,1)
  print &D.EMAIL_ADDR         (0,18,70)

  print $FAX_NBR              (+1,{col4},06)
  print ':'                   (,,1)
  print &D.FAX                (0,18,15)

  print $ADDRESS              (+2,{col3},07)

  print &D.ADDRESS1           (+1,{col4},35)

  let $TempCity = rtrim(&D.CITY,' ') || ', ' || rtrim(&D.STATE,' ') || ' ' || &D.POSTAL

  print $TempCity             (+1,{col4},51)

  let $TempCounty = rtrim(&D.COUNTY,' ') || ' ' || &D.COUNTRY

  print   $TempCounty          (+1,{col3},34)

  print   ' '                  (+1,1,1)

end-procedure

!************************************************************************
begin-procedure Get-Data
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Data                                              !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'EFF_STATUS'
  let $FieldValue = &C.EFF_STATUS

  do Read-Translate-Table

  let $Status = RTRIM($XlatShortName,' ')


  let $FieldName = 'AGENCY_TYPE'
  let $FieldValue = &C.AGENCY_TYPE

  do Read-Translate-Table

  let $AgencyType = RTRIM($XlatShortName,' ')

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************

#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
#include 'strunctl.sqc'  !Get run control parameter values
