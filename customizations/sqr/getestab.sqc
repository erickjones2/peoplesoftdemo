!*************************************************************
!  GetEstab:  Get Establishment Data from the GPJP_TAX_ESTAB
!*************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2015, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2015/12/03:20:45:36                                 
!       $Release:  HR92                                                
!           $Bug:  22307211                                            
!                                                                      *
!***********************************************************************
!
!**********************************
begin-procedure Get-Estab-Name
!**********************************

  !Keys passed in:  $EstabType, $Estab, $AsOfDate
  !Values returned: $EstabName, $EstabNameAbbrv, $EstabMngID, $SCEstabRef1, $SCEstabRef2, $SCEstabNO, $LAEstabInd,
  !                 $EstabCountry, $EstabAddr1,  $EstabAddr2, $EstabAddr3, $EstabAddr4, $EstabCity, $EstabCounty,
  !                 $EstabState, $EstabStCd, $EstabPostal, $EstabPhone, #FirmNumber

  move '' to $EstabName
  move '' to $EstabNameAbbrv
  move '' to $EstabMngID
  move '' to $SCEstabRef1
  move '' to $SCEstabRef2
  move '' to $SCEstabNO
  move '' to $LAEstabInd
  move '' to $EstabCountry
  move '' to $EstabAddr1
  move '' to $EstabAddr2
  move '' to $EstabAddr3
  move '' to $EstabAddr4
  move '' to $EstabCity
  move '' to $EstabCounty
  move '' to $EstabState
  move '' to $EstabStCd
  move '' to $EstabPostal
  move '' to $EstabPhone
  move 0 to #FirmNumber

  if Rtrim($EstabType,' ') = '' or Rtrim($Estab,' ') = ''
    goto GetEstabExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if
  
  if $JPN-TE-FirmNbr-Installed = 'Y'        !gpjppnbr.sqc
    let $FIRM_NUM_JPN = 'TE.FIRM_NUM_JPN'
  end-if
  
begin-SELECT
TE.GPJP_TAX_ESTAB
TE.GPJP_TAX_ESTAB_TYP
TE.DESCR
TE.DESCRSHORT
TE.GPJP_NAME
TE.GPJP_SC_ESTAB_REF1
TE.GPJP_SC_ESTAB_REF2
TE.GPJP_SC_ESTAB_NO
TE.GPJP_LA_IND_TYPE
TE.COUNTRY
TE.ADDRESS1
TE.ADDRESS2
TE.ADDRESS3
TE.ADDRESS4
TE.CITY
TE.COUNTY
TE.STATE
TE.POSTAL
TE.PHONE
TE.GPJP_PENS_FUND_FLG
[$FIRM_NUM_JPN]   &Firm_Num=number

  let $EstabName        = Rtrim(&TE.DESCR,' ')
  let $EstabNameAbbrv   = Rtrim(&TE.DESCRSHORT,' ')
  let $EstabMngID       = Rtrim(&TE.GPJP_NAME,' ')
  let $SCEstabRef1      = Rtrim(&TE.GPJP_SC_ESTAB_REF1,' ')
  let $SCEstabRef2      = Rtrim(&TE.GPJP_SC_ESTAB_REF2,' ')
  let $SCEstabNO        = Rtrim(&TE.GPJP_SC_ESTAB_NO,' ')
  let $LAEstabInd       = Rtrim(&TE.GPJP_LA_IND_TYPE,' ')
  let $EstabCountry     = Rtrim(&TE.COUNTRY,' ')
  let $EstabAddr1       = Rtrim(&TE.ADDRESS1,' ')
  let $EstabAddr2       = Rtrim(&TE.ADDRESS2,' ')
  let $EstabAddr3       = Rtrim(&TE.ADDRESS3,' ')
  let $EstabAddr4       = Rtrim(&TE.ADDRESS4,' ')
  let $EstabCity        = Rtrim(&TE.CITY,' ')
  let $EstabCounty      = Rtrim(&TE.COUNTY,' ')
  let $EstabStCd        = Rtrim(&TE.STATE,' ')
  let $EstabPostal      = Rtrim(&TE.POSTAL,' ')
  let $EstabPhone       = Rtrim(&TE.PHONE,' ')
  let $PensFundFlg      = Rtrim(&TE.GPJP_PENS_FUND_FLG,' ')
  let #FirmNumber       = &Firm_Num

  do Get-State-Name

FROM  PS_GPJP_TAX_ESTAB TE
WHERE TE.GPJP_TAX_ESTAB = $Estab
  AND TE.GPJP_TAX_ESTAB_TYP = $EstabType
  AND TE.EFFDT <= $AsOfDate
  AND TE.EFFDT  =
       (SELECT MAX(EFFDT)
        FROM   PS_GPJP_TAX_ESTAB
        WHERE  GPJP_TAX_ESTAB           = TE.GPJP_TAX_ESTAB
          AND  GPJP_TAX_ESTAB_TYP       = TE.GPJP_TAX_ESTAB_TYP
          AND  EFFDT <= $AsOfDate)
end-SELECT

  do Get-Estab-Name-Lang

GetEstabExit:

end-procedure

!***************************************
begin-procedure Get-State-Name
!***************************************

  !Keys passed in:  $Country, $EstabStCd
  !Values returned: $EstabState

  let $Country = 'JPN'

  if Rtrim($EstabStCd,' ') = ''
    goto GetStateExit
  end-if

begin-SELECT
SN.DESCR

  let $EstabState = Rtrim(&SN.DESCR,' ')

FROM  PS_STATE_NAMES_TBL SN
WHERE SN.COUNTRY  = $Country
  AND SN.STATE    = $EstabStCd
end-SELECT

  do Get-State-Name-Lang

GetStateExit:

end-procedure

!***************************************
begin-procedure Get-State-Name-Lang
!***************************************

  !Keys passed in:  $Country, $EstabStCd
  !Values returned: $EstabState

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetStateLngExit
  end-if

  if Rtrim($EstabStCd,' ') = ''
    goto GetStateLngExit
  end-if

begin-SELECT
SNL.DESCR

  if Rtrim(&SNL.DESCR,' ') <> ''
    let $EstabState = Rtrim(&SNL.DESCR,' ')
  end-if

FROM  PS_STATE_NAMES_LNG SNL
WHERE SNL.COUNTRY      = $Country
  AND SNL.STATE        = $EstabStCd
  AND SNL.LANGUAGE_CD  = $Curr_Language_Cd
end-SELECT

GetStateLngExit:

end-procedure

!***************************************
begin-procedure Get-Estab-Name-Lang
!***************************************

  !Keys passed in:  $EstabType, $Estab, $AsOfDate
  !Values returned: $EstabName, $EstabNameAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetEstabLngExit
  end-if

  if Rtrim($Estab,' ') = '' or Rtrim($EstabType,' ') = ''
    goto GetEstabLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
TEL.DESCR
TEL.DESCRSHORT

  if Rtrim(&TEL.DESCR,' ') <> ''
    let $EstabName = Rtrim(&TEL.DESCR,' ')
  end-if
  if Rtrim(&TEL.DESCRSHORT,' ') <> ''
    let $EstabNameAbbrv = Rtrim(&TEL.DESCRSHORT,' ')
  end-if

FROM  PS_GPJP_TAXEST_LNG TEL
WHERE TEL.GPJP_TAX_ESTAB        = $Estab
  AND TEL.GPJP_TAX_ESTAB_TYP    = $EstabType
  AND TEL.LANGUAGE_CD           = $Curr_Language_Cd
  AND TEL.EFFDT                 <= $AsOfDate
  AND TEL.EFFDT =
        (SELECT MAX(EFFDT)
         FROM  PS_GPJP_TAXEST_LNG
         WHERE GPJP_TAX_ESTAB      = TEL.GPJP_TAX_ESTAB
           AND GPJP_TAX_ESTAB_TYP  = TEL.GPJP_TAX_ESTAB_TYP
           AND LANGUAGE_CD = TEL.LANGUAGE_CD
           AND EFFDT      <= $AsOfDate)
end-SELECT

GetEstabLngExit:

end-procedure



!***********************************************************************
! Check-JPN-TE-Firm-Num-Install                                        *
! Called By:    begin-report                                           *
! Description:  The procedure checks whether JPN Firm Number changes   *
!               exist in a customer database or not. The variables set *
!               are then checked in getestab.sqc (or can be checked in *
!               any sqc/sqr that has already included the sqc),based on*
!               which the column FIRM_NUM_JPN is dynamically set for   *
!               the SELECT SQL.                                        *
!***********************************************************************
begin-procedure Check-JPN-TE-Firm-Num-Install
let $JPN-TE-FirmNbr-Installed = 'N'
BEGIN-SELECT
'Y'  
    let $JPN-TE-FirmNbr-Installed = 'Y'
FROM PSRECFIELDDB
WHERE RECNAME='GPJP_TAX_ESTAB' AND FIELDNAME = 'FIRM_NUM_JPN'
END-SELECT

end-procedure !Check-JPN-TE-Firm-Num-Install


!***************************** End of Include File **************************