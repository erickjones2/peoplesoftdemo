!****************************************
! CBASKVAL: COBRA Ask Values
!****************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:12:26:40                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!*********************************
Begin-Procedure Ask-Reprint-Prompt
!*********************************

  let $Reprint = ''

  While ($Reprint <> 'Y') And ($Reprint <> 'N')
    input $Reprint 'Is this a Reprint [Y/N] ?'
    let $Reprint = UPPER(RTRIM($Reprint, ' '))
  End-While

  if ($Reprint = 'Y')
    do Ask-Reprint-Id
    do Ask-Print-Reprint
  end-if

End-Procedure

!******************************
Begin-Procedure Ask-Reprint-Id
!******************************

  let $Reprint_Match = 'N'
  input $Reprint_Id 'Enter Reprint Id'
  let $Reprint_Id = UPPER(RTRIM($Reprint_Id, ' '))

  While ($Reprint_Match <> 'Y') And ($Reprint_Id <> '')
    do Validate-Reprint-Id
    if ($Reprint_Match = 'N')
      input $Reprint_Id '(Reprint Id does not exist). Enter Reprint Id'
      let $Reprint_Id = UPPER(RTRIM($Reprint_Id, ' '))
    end-if
  End-While

End-Procedure

!**********************************
Begin-Procedure Validate-Reprint-Id
!**********************************

  let $Reprint_Match = 'N'

Begin-Select
VR.PRINT_ID
VR.REPRINT_ALL_PQBS
VR.FROMDATE
VR.THRUDATE

  let $Reprint_Match = 'Y'
  let $ReprintAll = &VR.REPRINT_ALL_PQBS
  let $FromDate = &VR.FROMDATE
  let $ThruDate = &VR.THRUDATE

FROM  PS_CBR_PRNT VR
WHERE VR.PRINT_ID = $Reprint_Id
End-Select

End-Procedure

!********************************
Begin-Procedure Ask-Print-Reprint
!********************************

  let $PrintReprint = ''
  While ($PrintReprint <> 'Y') And ($PrintReprint <> 'N')
    input $PrintReprint 'Do you wish to print REPRINT on output [Y/N] ?'
    let $PrintReprint = UPPER(RTRIM($PrintReprint, ' '))
  End-While

End-Procedure

!*****************************
Begin-Procedure Ask-Sort-Parm1
!*****************************

  display 'Enter the number of the sort sequence to use '
  display 'for the COBRA Event Report:'
  display ''
  display '1. Employee ID'
  display '1. COBRA Event Date/Employee ID'
  display '3. COBRA Event/COBRA Event Date/Employee ID'
  display '4. COBRA Qualified Status/Employee ID'
  display '5. Employee Name'
  display '6. COBRA Event Date/Employee Name'
  display '7. COBRA Event/COBRA Event Date/Employee Name'
  display '8. COBRA Qualified Status/Employee Name'
  display ''

  let $done = 'N'

  While $done = 'N'
    input $SortParm1 '     ENTER NUMBER >'
    let $SortParm1 = UPPER($SortParm1)
    let $option = $SortParm1
    if ($option = '1' or $option = '2' or $option = '3' or $option = '4'
         or $option = '5' or $option = '6' or $option = '7' or $option = '8')
      let $done = 'Y'
    end-if
  End-While

End-Procedure

!*****************************
Begin-Procedure Ask-Sort-Parm2
!*****************************

  display 'Enter the number of the sort sequence to use '
  display 'for the COBRA Enrollment Report:'
  display ''
  display '1. Employee ID'
  display '2. Plan Type/Employee ID'
  display '3. Employee Name'
  display '4. Plan Type/Employee Name'
  display ''

  let $done = 'N'

  While $done = 'N'
    input $SortParm2 '     ENTER NUMBER >'
    let $SortParm2 = UPPER($SortParm2)
    let $option = $SortParm2
    if ($option = '1' or $option = '2' or $option = '3' or $option = '4')
      let $done = 'Y'
    end-if
  End-While

End-Procedure
!**************************** End of Report ********************************
