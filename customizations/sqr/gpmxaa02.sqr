!***********************************************************************
!  GPMXAA02:  Annual Tax Adjustment                                    *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                *
!           $Bug:  30533220                                            *
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment

Begin-Setup
#include 'setupdb.sqc'
#include 'ptset02.sqc'
End-Setup
 

begin-report
   move '1' to $ReportDateType
   move '1' to $ReportYear4
   do Init-DateTime
   do Init-Number
   do Stdapi-Init
   do Get-Current-DateTime
   do Select-Parameters
   do Init-Report
   do Stdapi-Term
   do Reset
end-report


begin-procedure Init-Report
   move 'GPMXAA02' to $ReportID
   move 'AJUSTE ANUAL DE IMPUESTOS' to $ReportTitle
   display $ReportID
   display $ReportTitle
   move 'Ver. PUM-34.01'  to $ReportVersion 
    Show $ReportVersion 
   date-time () hh:mi:ss &timeBegan
   display 'Report Began: ' noline
   display &timeBegan
   
   
    do Init_Report_Translation ($ReportID, $curr_language_cd)
    do Append_Report_Translation ('GPMXAA02')
    
    do Get_Field_Information ('GPMXAA02', 'REPORT_TITLE_LBL'    , $ReportTitle,         #DW)
    do Get_Field_Information ('GPMXAA02', 'STDHDG_CO_NM'        , $PeopleSoft,          #DW)
    do Get_Field_Information ('GPMXAA02', 'STDHDG_REP_ID'       , $Reporte,             #DW)
    do Get_Field_Information ('GPMXAA02', 'COMPANY'             , $Compania,            #DW)
    do Get_Field_Information ('GPMXAA02', 'YEAR'                , $Anio_Proceso,        #DW)
    do Get_Field_Information ('GPMXAA02', 'STDHDG_PAGE_NO'      , $Pagina_No,           #DW)
    do Get_Field_Information ('GPMXAA02', 'STDHDG_RUN_TM'       , $Hora,                #DW)
    do Get_Field_Information ('GPMXAA02', 'STDHDG_RUN_DT'       , $Fecha,               #DW)
    do Get_Field_Information ('GPMXAA02', 'ID_LBL'              , $Clave_NO,            #DW)
    do Get_Field_Information ('GPMXAA02', 'INFO1'               , $Informativo,         #DW)
    do Get_Field_Information ('GPMXAA02', 'EMPLID'              , $C_Empleado,          #DW)
    do Get_Field_Information ('GPMXAA02', 'NAME'                , $Nombre_Empleado,     #DW)
    do Get_Field_Information ('GPMXAA02', 'ADJUSTMENT_MTD_LBL'  , $Ajuste,              #DW)
    do Get_Field_Information ('GPMXAA02', 'TOTALS_ONLY'         , $Total_de,            #DW)
    do Get_Field_Information ('GPMXAA02', 'TOTAL_ACMPS'         , $Total,               #DW)
    do Get_Field_Information ('GPMXAA02', 'GPMX_ISR_POS_BAL'    , $ISR_Calculo,         #DW)
    do Get_Field_Information ('GPMXAA02', 'GPMX_EMPL_SUBSIDY'   , $Subsidio,            #DW)
    do Get_Field_Information ('GPMXAA02', 'CHARGE'              , $Cargo,               #DW)
    do Get_Field_Information ('GPMXAA02', 'HIRE_DT'             , $F_Ingreso,           #DW)
    do Get_Field_Information ('GPMXAA02', 'TERMINATION_DT'      , $Fecha_Baja,          #DW)
    do Get_Field_Information ('GPMXAA02', 'GPMX_NID_RFC'        , $RFC,                 #DW)
    do Get_Field_Information ('GPMXAA02', 'CALCULATION_TYPE'    , $T_Calculo,           #DW)
    do Get_Field_Information ('GPMXAA02', 'CMP_RT_ANNUAL'       , $Ingreso_Anual,       #DW)
    do Get_Field_Information ('GPMXAA02', 'GPMX_RSLT_NO_TAX'    , $Total_Exentos,       #DW)
    do Get_Field_Information ('GPMXAA02', 'GPMX_RSLT_TAX'       , $Total_Gravados,      #DW)
    do Get_Field_Information ('GPMXAA02', 'GPMX_ISR_RET'        , $ISR_Retenido,        #DW)
    do Get_Field_Information ('GPMXAA02', 'LABEL1'              , $Articulo_177,        #DW)
    do Get_Field_Information ('GPMXAA02', 'LABEL2'              , $al_Empleo,           #DW)
    do Get_Field_Information ('GPMXAA02', 'ADJ_AMOUNT'          , $Ajuste_Favor,        #DW)
    do Get_Field_Information ('GPMXAA02', 'TOTAL_EE_COUNT'      , $Total_Empleados,     #DW)
    do Get_Field_Information ('GPMXAA02', 'TOTAL_AMOUNT'        , $Totales_Compania,    #DW)
    do Get_Field_Information ('GPMXAA02', 'LABEL3'              , $Claves_No_Ajuste,    #DW)
    do Get_Field_Information ('GPMXAA02', 'INFO1_LABEL'         , $Clave_0010,          #DW)
    do Get_Field_Information ('GPMXAA02', 'INFO2_LABEL'         , $Clave_0020,          #DW)
    do Get_Field_Information ('GPMXAA02', 'INFO3_LABEL'         , $Clave_0030,          #DW)
    do Get_Field_Information ('GPMXAA02', 'INFO4_LABEL'         , $Clave_0040,          #DW)
    do Get_Field_Information ('GPMXAA02', 'LABEL4'              , $Mecanica_Ajuste,     #DW)
    do Get_Field_Information ('GPMXAA02', 'INFO5_LABEL'         , $Procedmiento_Act,    #DW)

   do Process-Main

   date-time () hh:mi:ss &timeEnded
   display 'Report Ended: ' noline
   display &timeEnded
end-procedure Init-Report


begin-heading 10

    
    uppercase $ReportTitle
    print $ReportTitle      (2,) center
    print $PeopleSoft       (1,) center
    print $Reporte          (3,1)
    print $ReportID         (,+2)
    print $Compania         (4,1)
    print $Company          (,+2)
    print ', '              ()
    print $CompanyName      ()
    print $Anio_Proceso     (5,1)
    print $AsOfYear         (,+2)

    page-number             (3,157) 'Página No. '
    print $Hora             (4,157)
    date-time () HH:MM:SS  &ReportTime
    print &ReportTime       (,+2)

    print $Fecha            (5,157)
    print $ReportDate       (,+2)
    
    
    print '='                      (+2,1,175)fill
    print $Clave_NO                (+1,42)
    print '(A)'                    ( ,105)
    print '(B)'                    ( ,122)
    print $Informativo             ( ,133)
    print '(B-A)'                  ( ,151)
    print '(B-A)'                  ( ,166)

    print $C_Empleado              (+1,1)
    print $Nombre_Empleado         ( ,14,35)
    print $Ajuste                  ( ,42)
    print $Total_de                ( ,58)
    print $Total                   ( ,69)
    print $ISR_Calculo             ( ,118)
    print $al_Empleo               ( ,133)
    print $Cargo                   ( ,148)

    print $F_Ingreso               (+1,4)
    print $Fecha_Baja              ( ,17)
    print $RFC                     ( ,30)
    print $T_Calculo               ( ,42)
    print $Ingreso_Anual           ( ,53)
    print $Total_Exentos           ( ,69)
    print $Total_Gravados          ( ,84)
    print $ISR_Retenido            ( ,101)
    print $Articulo_177            ( ,118)
    print $Subsidio                ( ,133)
    print $Ajuste                  ( ,148)
    print $Ajuste_Favor            ( ,163)
    print '='                      (+1,1,175)fill
end-heading


begin-procedure Process-Main
begin-select
A.EMPLID
A.GPMX_CALC_TYPE
A.GPMX_TOT_EARN
A.GPMX_TOT_TXBLE
A.GPMX_ISR_RET
A.GPMX_TOT_EXE
A.GPMX_SUB_RET
A.GPMX_EMPL_SUBSIDY
A.GPMX_SUB_PERCENT
A.GPMX_TAX_AMT
A.GPMX_WORST_TAX
A.GPMX_ADJ_TYPE

  move &A.EMPLID         to $Emplid
  move &A.GPMX_TOT_EARN  to #GPMX_TOT_EARN     !Ingreso Anual
  move &A.GPMX_TOT_TXBLE to #GPMX_TOT_TXBLE    !Total Gravados
  move &A.GPMX_ISR_RET   to #GPMX_ISR_RET      !ISR Retenido
  move &A.GPMX_TOT_EXE   to #GPMX_TOT_EXE      !Total Excentos
  move &A.GPMX_WORST_TAX to #GPMX_WST_TAX      !IPST Art. 141
  move &A.GPMX_EMPL_SUBSIDY  to #GPMX_EMPL_SUBSIDY     !Credito Efectivo
  move &A.GPMX_TAX_AMT   to #GPMX_TAX_AMT      !Saldo Cargo/Favor
  
  Let $Calc_Type = ' '
  If &A.GPMX_CALC_TYPE = '0010'
    Let $Calc_Type = 'Act'                     !Tipo Calculo
  end-if
  If &A.GPMX_CALC_TYPE = '0020'
    Let $Calc_Type = '1991'                    !Tipo Calculo
  end-if
  

  move &A.GPMX_ADJ_TYPE  to $GPMX_ADJ_TYPE

  do Get-Names
   if rtrim($GPMX_ADJ_TYPE,' ') = ''

       If #GPMX_WST_TAX > #GPMX_ISR_RET
          Let #ISPT_Cargo = #GPMX_WST_TAX - #GPMX_ISR_RET
          move 0 to #Ajuste_Favor
       Else
          move 0 to #ISPT_Cargo           
          If #GPMX_WST_TAX < 0
             move #GPMX_ISR_RET to #Ajuste_Favor
          Else
             Let #Ajuste_Favor = #GPMX_ISR_RET - #GPMX_WST_TAX  
          End-If 
      End-If        
    else
      move 0 to #GPMX_WST_TAX
      move 0 to #Ajuste_Favor
      move 0 to #ISPT_Cargo
   end-if

  print $Emplid                    (+1,1)
  print $Name                      ( ,14,30)
  print #GPMX_TOT_EARN             ( ,52)  edit 999,999,999.99
  print #GPMX_TOT_EXE              ( ,68)  edit 999,999,999.99
  print #GPMX_TOT_TXBLE            ( ,84)  edit 999,999,999.99
  print #GPMX_ISR_RET              ( ,100) edit 999,999,999.99
  print #GPMX_WST_TAX              ( ,116) edit 999,999,999.99
  print #GPMX_EMPL_SUBSIDY             ( ,132) edit 99,999,999.99
  print #ISPT_Cargo                ( ,147) edit 99,999,999.99
  print #Ajuste_Favor              ( ,162) edit 99,999,999.99

  add #GPMX_TOT_EARN to #Totales_Ing_Anual
  add #GPMX_TOT_EXE  to #Totales_Exentos
  add #GPMX_TOT_TXBLE  to #Totales_Gravados
  add #GPMX_ISR_RET  to #Totales_ISPT_Retenido
  add #GPMX_WST_TAX  to #Totales_ISPT_Art141
  add #GPMX_EMPL_SUBSIDY to #Totales_Cred_Efect
  add #ISPT_Cargo    to #Totales_ISPT_Cargo
  add #Ajuste_Favor  to #Totales_Ajuste_Favor

  ! Variables Cleanup
  move 0 TO #GPMX_TOT_EARN
  move 0 TO #GPMX_TOT_TXBLE
  move 0 TO #GPMX_ISR_RET
  move 0 TO #GPMX_TOT_EXE
  move 0 TO #GPMX_WST_TAX
  move 0 TO #GPMX_EMPL_SUBSIDY
  move 0 TO #GPMX_TAX_AMT
  move 0 TO #ISPT_Cargo
  move 0 TO #Ajuste_Favor

  add 1 to #Contador_Empleados

  do Format-DateTime($Hire_Dt, $Hire_Dt1,{DEFDATE},'','')
  do Format-DateTime($Termination_DT, $Termination_DT1,{DEFDATE},'','')
  print $GPMX_ADJ_TYPE             ( ,46)
  print $Hire_Dt1                  (+1,4)
  print $Termination_DT1           ( ,17)
  print $RFC_Id                    ( ,30)
  print $Calc_Type                 ( ,46)

FROM PS_GPMX_ANL_ADJ_WA A
WHERE A.COMPANY = $Company
  AND A.GPMX_BALANCE_YEAR = #AsofYear
  AND (A.GPMX_CALC_TYPE <> ' ' or A.GPMX_ADJ_TYPE <> ' ')
  AND A.RSLT_SEG_NUM IN (SELECT MAX(B.RSLT_SEG_NUM)
                         FROM PS_GPMX_ANL_ADJ_WA B
                         WHERE B.EMPLID = A.EMPLID AND B.CAL_RUN_ID = A.CAL_RUN_ID
                         AND B.EMPL_RCD = A.EMPL_RCD 
                         AND B.GP_PAYGROUP = A.GP_PAYGROUP
                         AND B.CAL_ID = A.CAL_ID
                         AND B.ORIG_CAL_RUN_ID = A.ORIG_CAL_RUN_ID
                         AND B.RSLT_SEG_NUM = A.RSLT_SEG_NUM
                         AND B.COMPANY = A.COMPANY)
ORDER BY A.EMPLID, A.GPMX_CALC_TYPE
end-select
  do Print-Totals
end-procedure Process-Main


begin-procedure Get-Names
begin-select
P.NAME
E.HIRE_DT
E.TERMINATION_DT

  let $name           = &P.NAME
  let $Hire_Dt        = &E.HIRE_DT
  let $Termination_DT = &E.TERMINATION_DT

  do Get-Max-TermDt
  do Get-RFC

FROM PS_JOB J,
     PS_PERSON_NAME P,
     PS_PER_ORG_ASGN_VW E

WHERE J.EMPLID = P.EMPLID
AND   E.EMPLID = J.EMPLID
AND   J.EMPLID = $Emplid
AND   J.COMPANY = $Company
AND   J.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOB
                 WHERE COMPANY = J.COMPANY
                 AND   EMPLID = J.EMPLID
                 AND   EMPL_RCD = J.EMPL_RCD
                 AND   EFFDT <= $EndDt)
AND   J.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB
                  WHERE COMPANY = J.COMPANY
                  AND   EMPLID = J.EMPLID
                  AND   EMPL_RCD = J.EMPL_RCD
                  AND   EFFDT = J.EFFDT)
AND   J.EMPL_RCD = (SELECT MAX(EMPL_RCD) FROM PS_JOB
                     WHERE COMPANY   = J.COMPANY
                     AND   EMPLID    = J.EMPLID
                     AND   EMPL_RCD = J.EMPL_RCD
                     AND   EFFDT     = J.EFFDT
                     AND    EFFSEQ   = J.EFFSEQ)
end-select
end-procedure Get-Names


begin-procedure Get-RFC
begin-select
PC.NATIONAL_ID
  let $RFC_Id = rtrim(&PC.NATIONAL_ID,' ')
FROM PS_PERS_NID PC
WHERE PC.NATIONAL_ID_TYPE = 'RFC'
AND PC.COUNTRY = 'MEX'
AND PC.EMPLID = $Emplid
end-select
end-procedure Get-RFC


begin-procedure Get-Max-TermDt
begin-select
J2.EFFDT
   let $Termination_dt = &J2.EFFDT
FROM PS_JOB J2
WHERE J2.EMPLID = $Emplid AND
      J2.COMPANY = $Company AND
      J2.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOB
                  WHERE COMPANY = J2.COMPANY AND
                        EMPLID = J2.EMPLID   AND
                        EFFDT >= $BeginDt    AND
                        EFFDT <= $EndDt)     AND
      J2.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB
                   WHERE COMPANY = J2.COMPANY AND
                         EMPLID = J2.EMPLID   AND
                         EFFDT = J2.EFFDT)    AND
      J2.EMPL_RCD = (SELECT MAX(EMPL_RCD) FROM PS_JOB
                      WHERE COMPANY   = J2.COMPANY AND
                            EMPLID    = J2.EMPLID  AND
                            EFFDT     = J2.EFFDT   AND
                            EFFSEQ    = J2.EFFSEQ) AND
      J2.ACTION = 'TER'
end-select
end-procedure Get-Max-TermDt


begin-procedure Print-Totals

  print $Total_Empleados            (+2,14)
  print #Contador_Empleados         ( ,+6) edit 888,888,888
  print '_'                         (+1,52,124)fill
  print $Totales_Compania           (+1,14)
  print #Totales_Ing_Anual          ( ,52)  edit 999,999,999.99
  print #Totales_Exentos            ( ,68)  edit 999,999,999.99
  print #Totales_Gravados           ( ,84)  edit 999,999,999.99
  print #Totales_ISPT_Retenido      ( ,100) edit 999,999,999.99
  print #Totales_ISPT_Art141        ( ,116) edit 999,999,999.99
  print #Totales_Cred_Efect         ( ,132) edit 99,999,999.99
  print #Totales_ISPT_Cargo         ( ,147) edit 99,999,999.99
  print #Totales_Ajuste_Favor       ( ,162) edit 99,999,999.99

  print $Claves_No_Ajuste  (+4,1)
  print $Clave_0010        (+2,3)
  print $Clave_0020        (+1,3)
  print $Clave_0030        (+1,3)
  print $Clave_0040        (+1,3)

end-procedure Print-Totals


begin-procedure Select-Parameters
begin-SELECT
MEXRNCTL.OPRID
MEXRNCTL.RUN_CNTL_ID
MEXRNCTL.COMPANY
MEXRNCTL.GPMX_BALANCE_YEAR
MEXRNCTL.LANGUAGE_CD

    let $Company  = &MEXRNCTL.COMPANY
    let #AsofYear = &MEXRNCTL.GPMX_BALANCE_YEAR
    let $AsofYear = To_char (#AsofYear)
    let $lang_opr = &MEXRNCTL.LANGUAGE_CD

    move $AsofYear to $BgnDt
    move $AsofYear to $EndDt

    concat '-01-01' with $BgnDt
    concat '-12-31' with $EndDt

    do Convert-From-DTU-Date($BgnDt,$BgnDt)
    do Convert-From-DTU-Date($EndDt,$EndDt)
    do Get-Company-Data-Mex

FROM  PS_GPMX_RC_ANNLADJ MEXRNCTL
WHERE MEXRNCTL.OPRID       = $Prcs_OprID
  AND MEXRNCTL.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
end-select
end-procedure Select-Parameters


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Datemath Routines
#Include 'getcomex.sqc'  !Datemath Routines
#include 'sqrtrans.sqc'     ! Translate SQR strings to given language
