!***********************************************************************
!  RotName6:  Convert from "Last Suffix,First" to "Last First Mid In"  *
!             Remove Title and Suffix from Name                        *
!                                                                      *
!             Note: This procedure will only find a member of the      *
!             comma-delimited suffixes defined below in the variable   *
!             $SuffixString.  Additional suffixes may be added to the  *
!             variable as necessary.  If two or more suffixes, e.g.    *
!             'JR.' and 'JR', in which the second is a substring of    *
!             the first are included, the longest suffix must be the   *
!             first in the list.                                       *
!                                                                      *
!             If the variable $Remove_Blanks_From_Surname is set to    *
!             'Y' before this procedure is called, any blanks in the   *
!             surname will be removed, e.g. 'ST JOHN' will be formatted*
!             as 'STJOHN'.                                             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:46:58                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

begin-procedure Rotate-Name-for-Mag-Media
  move '' to $Suffix
  move 'N' to $SuffixFound
  uppercase $Name

  let $SuffixString =
    'JR.,JR,SR.,SR,III,II,I,IV,MD,M.D.,CPA,C.P.A.,ESQ.,ESQ,' ||
    '2ND.,2ND,SECOND,3RD.,3RD,THIRD,4TH.,4TH,FOURTH'

  let $Name = rtrim($Name,' ')
  find ',' in $Name 0 #locn
  let $LastName = {ps-substr}($Name,1,#locn + 1)
  find ' ' in $LastName 0 #locnblank
  if #locnblank <> -1
    let #i = 1
    while INSTR($SuffixString,',',#i) > 0
      let #j = INSTR($SuffixString,',',#i)
      let $Suffix = {ps-substr}($SuffixString,#i,#j - #i)

      let #locnsuffix = INSTR($LastName, $Suffix || ',', 1)
      if #locnsuffix > 0
        let $LastName = {ps-substr}($LastName,1,#locnsuffix - 2)
        move 'Y' to $SuffixFound
      end-if

  let #i = #j + 1
      if $SuffixFound = 'Y'
        break
      end-if
    end-while
  end-if

  add 1 to #locn
  extract $FirstName from $Name #locn 40  !40 is the maximum length of Name

  if $Remove_Blanks_From_Surname = 'Y'
    while INSTR($LastName,' ',1)
      let #locnblank = INSTR($LastName,' ',1)
      let $LastName = {ps-substr}($LastName,1,#locnblank - 1)   ||
                      {ps-substr}($LastName,#locnblank + 1,40)
    end-while
  end-if

  if $SuffixFound <> 'Y'

   let $Name = {ps-substr}($LastName,1,LENGTH($LastName) - 1) || ' ' || $FirstName
  else
     let $Name = $LastName || ' ' || $FirstName
 end-if
end-procedure
