!************************************************************************
!  STSU003:  Agency Information
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:55:55                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
  #include 'ptpsp125.sqc'  !Printer and page-size init
end-setup

#define  col2    37
#define  col3    52
#define  col4    88
#define  col5    100
#define  col6    117


!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let     $ReportID     =   'STSU003'

  do Get-Report-Language

  show    'FLOW: Init-Report'
  show    'Report Title: ' $ReportTitle

end-procedure

!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

 do Init_Report_Translation ('STOCK',$language_cd)
 do Append_Report_Translation ($ReportId)

 do Get_Field_Information ('STOCK','EFFDT',            $EffDt,             #DW)
 do Get_Field_Information ('STOCK','DTC_ACCOUNT_NBR',  $DtcAccountNbr,     #DW)

 do Get_Field_Information ('STSU003','STSU003',        $ReportTitle,       #DW)
 do Get_Field_Information ('STSU003','EFF_STATUS',     $EffStatus,         #DW)
 do Get_Field_Information ('STSU003','ST_AGENCY_ID',   $StAgencyID,        #DW)
 do Get_Field_Information ('STSU003','DESCR',          $Descr,             #DW)
 do Get_Field_Information ('STSU003','AGENCY_TYPE',    $HAgencyType,       #DW)

end-procedure

!************************************************************************
begin-heading 6
!************************************************************************

#include 'stdhdgtr.sqc'

  print $StAgencyID         (+2,1,06)
  print $EffDt              (0,37,08)
  print $Descr              (0,52,11)
  print $EffStatus          (0,88,06)
  print $DtcAccountNbr      (0,100,04)
  print $HAgencyType        (0,117,11)

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

begin-Select

A.ST_AGENCY_ID
A.DTC_ACCOUNT_NBR

  do Select-Dtl

from PS_ST_AGENT_TBL A

end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

B.ST_AGENCY_ID
B.EFFDT
B.EFF_STATUS
B.DESCR
B.AGENCY_TYPE
B.ADDRESS1
B.HOUSE_TYPE
B.COUNTY
B.GEO_CODE
B.IN_CITY_LIMIT
B.ADDRESS2
B.ADDRESS3
B.ADDRESS4
B.CITY
B.STATE
B.POSTAL
B.COUNTRY
B.PHONE

 do Print-Data

from     PS_ST_AGENT_DTL  B
where    B.ST_AGENCY_ID   = &A.ST_AGENCY_ID
order by B.EFFDT          desc

end-Select

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  print   &B.ST_AGENCY_ID   (+1,1,10)       ON-BREAK level=1

  do Format-DateTime(&B.EFFDT, $EFFDT1, {DEFYMD}, '', '')

  print $EFFDT1             (0,{col2},10)   ON-BREAK level=2

  do Get-Value

  print   &B.DESCR           (0,{col3},30)
  print   $STATUS            (0,{col4},30)
  print   &A.DTC_ACCOUNT_NBR (0,{col5},10)
  print   $AGENCYTYPE        (0,{col6},10)
  print   &B.ADDRESS1        (+1,1,35)
  
  if rtrim (&B.ADDRESS2,' ') <> ''
     print   &B.ADDRESS2        (+1,1,35)
  end-if

  let $TempCity = rtrim(&B.CITY,' ') || ', ' || rtrim(&B.STATE,' ') || ' ' || &B.POSTAL
  
  if rtrim($TempCity, ' ') <> ','
   print   $TempCity          (+1,1,51)
  else
   print ' '                  (+1,1,51)
  end-if

  let $tempCounty = rtrim(&B.COUNTY,' ')
  
  if $tempCounty <> ''
     let $tempCounty = rtrim(&B.COUNTY,' ') || ' ' || &B.COUNTRY
  else
     let $tempCounty = &B.COUNTRY
  end-if

  print $tempCounty           (+1,1,34)

  print &B.PHONE              (+1,1,24)
  print ' '                   (+1,1,1)

end-procedure

!************************************************************************
begin-procedure Get-Value
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Value                                             !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'EFF_STATUS'
  let $FieldValue = &B.EFF_STATUS

  do Read-Translate-Table

  let $STATUS = RTRIM($XlatLongName,' ')

  let $FieldName = 'AGENCY_TYPE'
  let $FieldValue = &B.AGENCY_Type

  do Read-Translate-Table

  let $AGENCYTYPE = RTRIM($XlatShortName,' ')

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************
#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
