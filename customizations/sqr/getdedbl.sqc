!**************************************
!  GetDedBl:  Get Deduction Balances  *
!**************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:30:40                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

begin-procedure Get-Deduction-Balances

  move 0 to #DedMTD
  move 0 to #DedQTD
  move 0 to #DedYTD
  move -1 to #Prev-Ben-Rcd

  evaluate $Country
    when = 'USA'
      do Get-USA-Deduction-Balances
    when = 'CAN'
      do Get-CAN-Deduction-Balances
  end-evaluate

end-procedure


begin-procedure Get-USA-Deduction-Balances
  move $AsOfYYYY  to #AsOfYYYY
  move $AsOfMonth to #AsOfMonth
  move $AsOfQtr   to #AsOfQtr
begin-SELECT
DB.DED_MTD,DB.DED_QTD,DB.DED_YTD,DB.BALANCE_QTR,DB.BALANCE_PERIOD

  move &DB.BALANCE_PERIOD to #MONTHCD
  move &DB.BALANCE_QTR    to #QTRCD
  move &DB.Ded_MTD        to #MTD
  move &DB.Ded_QTD        to #QTD
  move &DB.Ded_YTD        to #YTD

  do Accumulate-Deductions

FROM  PS_DEDUCTION_BAL DB
WHERE EMPLID        = $EmplID
  AND COMPANY       = $Company
  AND PLAN_TYPE     = $PlanType
  AND DEDCD         = $DedCd
  AND DED_CLASS     = $DedClass
  AND BALANCE_YEAR = #AsOfYYYY
  AND BALANCE_ID   = $Calendar_Year_Id
  AND BALANCE_PERIOD =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_DEDUCTION_BAL
       WHERE  EMPLID        = $EmplID
         AND  COMPANY       = $Company
         AND  PLAN_TYPE     = $PlanType
         AND  DEDCD         = $DedCd
         AND  DED_CLASS     = $DedClass
         AND  BENEFIT_RCD_NBR = DB.BENEFIT_RCD_NBR
         AND  BALANCE_YEAR  = #AsOfYYYY
         AND  BALANCE_ID    = $Calendar_Year_Id
         AND  BALANCE_PERIOD  <= #AsOfMonth)
end-SELECT
end-procedure


begin-procedure Get-CAN-Deduction-Balances
  move $AsOfYYYY  to #AsOfYYYY
  move $AsOfMonth to #AsOfMonth
  move $AsOfQtr   to #AsOfQtr
begin-SELECT
CDB.DED_MTD,CDB.DED_QTD,CDB.DED_YTD,CDB.BALANCE_QTR,CDB.BALANCE_PERIOD

  move &CDB.BALANCE_PERIOD to #MONTHCD
  move &CDB.BALANCE_QTR    to #QTRCD
  move &CDB.DED_MTD        to #MTD
  move &CDB.DED_QTD        to #QTD
  move &CDB.DED_YTD        to #YTD

  do Accumulate-Deductions

FROM  PS_CAN_DED_BALANCE CDB
WHERE CDB.EMPLID           = $EmplID
  AND CDB.COMPANY          = $Company
  AND CDB.PLAN_TYPE        = $PlanType
  AND CDB.DEDCD            = $DedCd
  AND CDB.DED_CLASS        = $DedClass
  AND CDB.DED_SLSTX_CLASS  = $DedSlsTxClass
  AND CDB.BALANCE_ID       = $Calendar_Year_Id
  AND CDB.BALANCE_YEAR     = #AsOfYYYY
  AND CDB.BALANCE_PERIOD =
      (SELECT MAX(BALANCE_PERIOD)
       FROM   PS_CAN_DED_BALANCE
       WHERE  EMPLID          = CDB.EMPLID
         AND  COMPANY         = CDB.COMPANY
         AND  PLAN_TYPE       = CDB.PLAN_TYPE
         AND  DEDCD           = CDB.DEDCD
         AND  DED_CLASS       = CDB.DED_CLASS
         AND  DED_SLSTX_CLASS = CDB.DED_SLSTX_CLASS
         AND  BALANCE_ID      = CDB.BALANCE_ID
         AND  BENEFIT_RCD_NBR = CDB.BENEFIT_RCD_NBR
         AND  BALANCE_YEAR    = CDB.BALANCE_YEAR
         AND  WAGE_LOSS_PLAN  = CDB.WAGE_LOSS_PLAN
         AND  PROVINCE        = CDB.PROVINCE
         AND  BALANCE_PERIOD  <= #AsOfMonth)
end-SELECT

end-procedure


begin-procedure Accumulate-Deductions
  if #MonthCd = #AsOfMonth
    add #MTD to #DedMTD
  end-if

  if #QtrCd = #AsOfQtr
    add #QTD to #DedQTD
  end-if

  add #YTD   to #DedYTD

end-procedure





