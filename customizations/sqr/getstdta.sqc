!*************************************************************
!  GetStDta:  Get selected fields from the State Tax Tables  *
!*************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2020, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  30897428                                            !
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************

!*********************************!
begin-procedure Get-State-Tax-Data
!*********************************!

  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
PS_STATE_NAMES_TBL.DESCR,PS_STATE_NAMES_TBL.NUMERIC_CD
FROM  PS_STATE_NAMES_TBL
WHERE COUNTRY = {NID_COUNTRY}
  AND STATE   = $State
end-SELECT

  let $StateName = RTRIM(&PS_State_Names_Tbl.Descr, ' ')
  move &PS_State_Names_Tbl.Numeric_Cd to $StateNumber

!---------------------------------------------------------------!
! Only retrieve data from the related language table if         !
! the operator language is not the same to the installation     !
! language or $curr_language_cd  is different from $language_cd !
!---------------------------------------------------------------!

  if $curr_language_cd <> $Psoptions_Language_Cd
     do Get-Rel-Lang-State
  end-if


begin-SELECT
EMPLOYER_ID_SWT,EMPLOYER_ID_SUT,EMPLOYER_ID_DIS                                !HP99999
GL_SWT,GL_SDI,GL_SUT,GL_SDI_ER                                             !HP99999
GL_SUT_ER,BRANCH_CD,SUT_EXPERIENCE_RT,DIS_EXPERIENCE_RT,GL_VDI_EE,GL_VDI_ER
GL_ST_EIC, GL_TRANSIT, PNA_GL_FLI_ER, PNA_GL_MLI_EE, PNA_GL_MLI_ER, PNA_GL_VMLI_EE,
PNA_GL_VMLI_ER, PY_GL_FML_EE, PY_GL_FML_ER, PY_GL_VFML_EE, PY_GL_VFML_ER
UBI_NUMBER,EAF_RATE,HEALTH_CARE_COV,PNA_GL_FLI_EE,PNA_GL_VFLI_EE,PNA_GL_VFLI_ER,
PNA_MLI_EE_PCT,PNA_FLI_EE_PCT,PFML_ACCOUNT_ID
FROM  PS_CO_STATETAX_TBL
WHERE COMPANY = $Company
  AND STATE   = $State
  AND EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_CO_STATETAX_TBL
       WHERE  COMPANY = $Company
         AND  STATE   = $State
         AND  EFFDT  <= $AsOfDate)
end-SELECT

  move 0 to #OASDIRate
  move 0 to #MEDIRate
  move 0 to #FUTARate
  move 0 to #SDIRate
  move 0 to #SpecialUIRate
  move 0 to #EmployeeUIRate



begin-SELECT
OT.STATE
OT.TAX_CLASS
OT.TAX_RT * 1  &OT.Tax_Rt

  let $Tax_Class = rtrim(&OT.Tax_Class, ' ')
  if rtrim(&OT.State, ' ') =  '$U'
  
     evaluate $Tax_Class
       when = 'U'
         move &OT.Tax_Rt to #FUTARate
       when = 'F'
         move &OT.Tax_Rt to #MediRate
       when = 'D'
         move &OT.Tax_Rt to #OASDIRate
     end-evaluate

  else
 
     evaluate $Tax_Class
       when = 'D'
         move &OT.Tax_Rt to #SDIRate
       when = 'S'
         move &OT.Tax_Rt to #SpecialUIRate
       when = 'L'
         move &OT.Tax_Rt to #SWAFRate
       when = 'M'
         move &OT.Tax_Rt to #WFDPRate
       when = 'N'
         move &OT.Tax_Rt to #HCSFRate
       when = 'V'
         move &OT.Tax_Rt to #EmployeeUIRate
     end-evaluate
     
  end-if

FROM  PS_ST_OTH_TAX_TBL OT
WHERE OT.STATE     IN  ('$U',$State)
  AND OT.TAX_CLASS IN  ('U','D','F','L','M','N','S','V')
  AND OT.EFFDT     =
      (SELECT MAX(EFFDT)
       FROM   PS_ST_OTH_TAX_TBL
       WHERE  STATE   = OT.STATE
         AND  EFFDT  <= $AsOfDate)
end-SELECT

end-procedure


!*********************************!
begin-procedure Get-Rel-Lang-State
!*********************************!

begin-SELECT
STL.DESCR

    if rtrim(&STL.Descr, ' ') <> ''
       let $StateName = RTRIM(&STL.Descr, ' ')
    end-if

FROM  PS_STATE_NAMES_LNG STL
WHERE STL.COUNTRY     = {NID_COUNTRY}
  AND STL.LANGUAGE_CD = $Curr_language_cd
  AND STL.STATE       = $State

end-SELECT

end-procedure
