
!**********************************************************************
!  Function Result Values.   Retrieve the function result names for
!                            specified function
!                                                                     *
!  Parameters passed:  $Functn_name      (name of function result to  *
!                                         to retrieve)                *
!
!  Parameters returned $functn_result_nm
!**********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!Revision Notes:                                                       *
!                                                                      *
! 2/19/2006 PY - Fix for Calculations with All Plans checked           *
!                                                                      *
!06/16/03 Lee - ICE 579654000 - select one row by effdt                *
!                                                                      *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:14:58:51                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************



begin-procedure get-function-result
let $return_flag = 'N'
let $return_value = ' '
move 0 to #counter
let $sql-statement = 'PAFUNCTN-Function Name&Result'

BEGIN-SELECT ON-ERROR=SQL-ERROR
FUNCTN.FUNCTN_NAME
FUNCTN.FUNCTN_RESULT_NM
FUNCTNDESCR.EACCT_TYPE
FUNCTNDESCR.DESCR
INPT.USE_PROCESS_SECT
PROC1.EXEC_ONLY_CD
   if &INPT.USE_PROCESS_SECT = 'Y'
      if &PROC1.EXEC_ONLY_CD  = 'Y'
         put &FUNCTN.FUNCTN_RESULT_NM &FUNCTNDESCR.DESCR &FUNCTNDESCR.EACCT_TYPE
             into function-result(#counter)
         add 1 to #counter
     end-if
   else
      put &FUNCTN.FUNCTN_RESULT_NM &FUNCTNDESCR.DESCR &FUNCTNDESCR.EACCT_TYPE
          into function-result(#counter)
      add 1 to #counter
   end-if

FROM PS_PA_FNC_RSLT_DFN FUNCTN,
     PS_PA_FNC_RSLT_TBL FUNCTNDESCR,
     PS_PA_CALCULATION  CALC1,
     PS_PA_CLC_PROCLIST PROC1,
     PS_PA_CLC_PLN_INPT INPT
WHERE CALC1.CALC_NAME          = $calc_name
  AND FUNCTN.BENEFIT_PLAN     = &PA_PLAN.BENEFIT_PLAN
  AND FUNCTN.FUNCTN_NAME      = $functn_name
!* ICE 579654000
!* AND FUNCTN.EFFDT           <= CALC1.ASOFDATE

AND FUNCTN.EFFDT = (SELECT MAX(FUNCTN2.EFFDT) FROM PS_PA_FNC_RSLT_DFN FUNCTN2
WHERE FUNCTN2.EFFDT <= CALC1.ASOFDATE AND FUNCTN2.FUNCTN_NAME = $functn_name
AND FUNCTN2.BENEFIT_PLAN=FUNCTN.BENEFIT_PLAN
AND FUNCTN2.FUNCTN_NAME=FUNCTN.FUNCTN_NAME
AND FUNCTN2.FUNCTN_RESULT_NM=FUNCTN.FUNCTN_RESULT_NM)

  AND FUNCTN.BENEFIT_PLAN     = FUNCTNDESCR.BENEFIT_PLAN
  AND FUNCTN.FUNCTN_NAME      = FUNCTNDESCR.FUNCTN_NAME
  AND FUNCTN.FUNCTN_RESULT_NM = FUNCTNDESCR.FUNCTN_RESULT_NM
  AND PROC1.CALC_NAME          = $calc_name
  AND FUNCTN.BENEFIT_PLAN     = PROC1.BENEFIT_PLAN
  AND FUNCTN.FUNCTN_RESULT_NM = PROC1.PROC_NAME
  AND INPT.CALC_NAME          = $calc_name
  AND INPT.BENEFIT_PLAN       = &PA_PLAN.BENEFIT_PLAN
ORDER BY FUNCTN.FUNCTN_NAME, FUNCTNDESCR.EACCT_TYPE, FUNCTN.FUNCTN_RESULT_NM
end-select
end-procedure

begin-procedure get-function-result-all-plans
let $return_flag = 'N'
let $return_value = ' '
move 0 to #counter
let $sql-statement = 'PAFUNCTN-Function Name&Result'

BEGIN-SELECT ON-ERROR=SQL-ERROR
FUNCTN_ALL_PLANS.FUNCTN_NAME
FUNCTN_ALL_PLANS.FUNCTN_RESULT_NM
FR_TBL_ALL_PLANS.EACCT_TYPE
FR_TBL_ALL_PLANS.DESCR
    put &FUNCTN_ALL_PLANS.FUNCTN_RESULT_NM &FR_TBL_ALL_PLANS.DESCR 
    &FR_TBL_ALL_PLANS.EACCT_TYPE
        into function-result(#counter)
    add 1 to #counter

FROM PS_PA_FNC_RSLT_DFN FUNCTN_ALL_PLANS,
     PS_PA_FNC_RSLT_TBL FR_TBL_ALL_PLANS,
     PS_PA_CALCULATION  CALC1_ALL_PLANS
WHERE CALC1_ALL_PLANS.CALC_NAME          = $calc_name
  AND FUNCTN_ALL_PLANS.BENEFIT_PLAN     = &PA_PLAN.BENEFIT_PLAN
  AND FUNCTN_ALL_PLANS.FUNCTN_NAME      = $functn_name
AND FUNCTN_ALL_PLANS.EFFDT = (SELECT MAX(FUNCTN2_ALL_PLANS.EFFDT) 
FROM PS_PA_FNC_RSLT_DFN FUNCTN2_ALL_PLANS
WHERE FUNCTN2_ALL_PLANS.EFFDT <= CALC1_ALL_PLANS.ASOFDATE 
AND FUNCTN2_ALL_PLANS.FUNCTN_NAME = $functn_name
AND FUNCTN2_ALL_PLANS.BENEFIT_PLAN=FUNCTN_ALL_PLANS.BENEFIT_PLAN
AND FUNCTN2_ALL_PLANS.FUNCTN_NAME=FUNCTN_ALL_PLANS.FUNCTN_NAME
AND FUNCTN2_ALL_PLANS.FUNCTN_RESULT_NM=FUNCTN_ALL_PLANS.FUNCTN_RESULT_NM)
AND FUNCTN_ALL_PLANS.BENEFIT_PLAN     = FR_TBL_ALL_PLANS.BENEFIT_PLAN
AND FUNCTN_ALL_PLANS.FUNCTN_NAME      = FR_TBL_ALL_PLANS.FUNCTN_NAME
AND FUNCTN_ALL_PLANS.FUNCTN_RESULT_NM = FR_TBL_ALL_PLANS.FUNCTN_RESULT_NM
ORDER BY FUNCTN_ALL_PLANS.FUNCTN_NAME, FR_TBL_ALL_PLANS.EACCT_TYPE, 
FUNCTN_ALL_PLANS.FUNCTN_RESULT_NM
end-select
end-procedure


