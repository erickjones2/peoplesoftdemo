!************************************************************************
!  STSU018:  Stock Branch Detail - Contact
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:57:08                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
   #include 'ptpsp125.sqc'  !Printer and page-size init
end-setup

#define  col2    5
#define  col3    8
#define  col4    10
#define  col5    50
#define  col6    53

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let     $ReportID     =   'STSU018'

  do Get-Report-Language

  if $prcs_process_instance = ''
     do Prompts             !Prompt user

  else
     do Get-SQR-Parms
     do Get-Values
  end-if

  show    'FLOW: Init-Report'
  show    'Report Title: ' $REPORTTITLE

end-procedure

!******************************************************************************
begin-procedure Prompts
!******************************************************************************

!-----------------------------------------------------------------------------!
! Procedure: Prompts                                                          !
! Descr:     Prompts for all input parameters in the event the program is run !
!            directly from SQR.  In normal use, this should not happen.       !
!-----------------------------------------------------------------------------!

end-procedure

!************************************************************************
begin-procedure Get-Values
!************************************************************************

end-procedure

!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ('STOCK',$language_cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK','STOCK_ID',          $STOCK_ID,           #DW)
  do Get_Field_Information ('STOCK','ST_BROKERAGE_ID',   $ST_BROKERAGE_ID,    #DW)
  do Get_Field_Information ('STOCK','ST_BRANCH_ID',      $ST_BRANCH_ID,       #DW)
  do Get_Field_Information ('STOCK','EFFDT',             $EFFDT,              #DW)
  do Get_Field_Information ('STOCK','NAME',              $NAME,               #DW)

  do Get_Field_Information ('STSU018','STSU018',           $REPORTTITLE,        #DW)
  do Get_Field_Information ('STSU018','LAST_CONTROL_NBR',  $LAST_CONTROL_NBR,   #DW)
  do Get_Field_Information ('STSU018','EFF_STATUS',        $EFF_STATUS,         #DW)
  do Get_Field_Information ('STSU018','MANAGE_FEES',       $MANAGE_FEES,        #DW)
  do Get_Field_Information ('STSU018','AUTHORIZE',         $AUTHORIZE,          #DW)
  do Get_Field_Information ('STSU018','CONTACT',           $CONTACT,            #DW)
  do Get_Field_Information ('STSU018','ADDRESS',           $ADDRESS,            #DW)
  do Get_Field_Information ('STSU018','BRANCH_DTL',        $BRANCH_DTL,         #DW)
  do Get_Field_Information ('STSU018','BROKERAGE_FEES',    $BROKERAGE_FEES,     #DW)
  do Get_Field_Information ('STSU018','BROKERAGE_CMSN',    $BROKERAGE_CMSN,     #DW)
  do Get_Field_Information ('STSU018','SEC_FEES',          $SEC_FEES,           #DW)
  do Get_Field_Information ('STSU018','CAPTIVE_OPTIONS',   $CAPTIVE_OPTIONS,    #DW)
  do Get_Field_Information ('STSU018','CAPTIVE_ESPP',      $CAPTIVE_ESPP,       #DW)
  do Get_Field_Information ('STSU018','CONTACT_TYPE',      $CONTACT_TYPE,       #DW)
  do Get_Field_Information ('STSU018','PHONE',             $PHONE,              #DW)
  do Get_Field_Information ('STSU018','EMAIL_ADDR',        $EMAIL_ADDR,         #DW)
  do Get_Field_Information ('STSU018','FAX_NBR',           $FAX_NBR,            #DW)
  do Get_Field_Information ('STSU018','CNTRL_NBR_PREFIX',  $PREFIXLBL,          #DW)

end-procedure

!************************************************************************
begin-heading 3
!************************************************************************

#include 'stdhdgtr.sqc'

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

begin-Select

A.STOCK_ID                 () ON-BREAK Print=never before=Select-Dtl
A.ST_BROKERAGE_ID
A.ST_BRANCH_ID
A.LAST_CONTROL_NBR

  do Print-Info
  do Select-Detail
  print ' '                (+1,1,1)

from      PS_STOCK_BRANCH  A
order by  A.STOCK_ID

end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

B.STOCK_ID
B.DESCRSHORT

  do Print-data

 from  PS_STOCK_TBL B
where  B.STOCK_ID   =  &A.STOCK_ID

end-Select

end-procedure

!***********************************************************************
begin-procedure Select-Detail
!***********************************************************************

begin-Select

C.STOCK_ID
C.ST_BROKERAGE_ID
C.ST_BRANCH_ID
C.EFFDT
C.EFF_STATUS
C.CAPTIVE_OPTIONS
C.CAPTIVE_ESPP
C.BROKERAGE_FEES_IND
C.BROKERAGE_CMSN_IND
C.SEC_FEES_IND
C.CONTROL_NBR_PREFIX

  do Print-Detail
  do Select-LowerDetail

 from  PS_STOCK_BRNCH_DTL   C
where  C.STOCK_ID           =  &A.STOCK_ID
  and  C.ST_BRANCH_ID       =  &A.ST_BRANCH_ID

end-Select

end-procedure

!************************************************************************
begin-procedure Select-LowerDetail
!************************************************************************

begin-Select

D.STOCK_ID
D.ST_BROKERAGE_ID
D.ST_BRANCH_ID
D.EFFDT
D.ST_CONTACT_TYPE
D.ADDRESS1
D.CITY
D.STATE
D.POSTAL
D.COUNTY
D.COUNTRY
D.CONTACT_NAME
D.PHONE
D.EMAIL_ADDR
D.FAX

  do Print-LowerDetail

 from  PS_ST_BRANCH_CNTCT D
where  D.STOCK_ID         =  &C.STOCK_ID
  and  D.ST_BROKERAGE_ID  =  &C.ST_BROKERAGE_ID
  and  D.ST_BRANCH_ID     =  &C.ST_BRANCH_ID
  and  D.EFFDT            =  &C.EFFDT

end-Select

end-procedure

!************************************************************************
begin-procedure Print-Info
!************************************************************************

  do Get-Descr

  print $ST_BROKERAGE_ID       (+2,{col3},12)
  print ':'                    (,,1)
  print &A.ST_BROKERAGE_ID     (0,+3,10)
  print &F.DESCR               (0,+2,30)

  print $ST_BRANCH_ID          (+1,{col3},09)
  print ':'                    (,,1)
  print &A.ST_BRANCH_ID        (0,+6,10)
  print &G.DESCR               (0,+2,30)

  print $LAST_CONTROL_NBR      (+1,{col3},14)
  print ':'                    (,,1)
  print &A.LAST_CONTROL_NBR    (0,+1,06)

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  print $STOCK_ID              (+1,1,08)
  print ':'                    (,,1)
  print &A.STOCK_ID            (0,+2,10)
  print &B.DESCRSHORT          (0,+5,10)

end-procedure

!***********************************************************************
begin-procedure Print-Detail
!***********************************************************************

  do Format-DateTime(&C.EFFDT, $EFFDT1, {DEFYMD}, '', '')

  print $EFFDT                 (+2,{col3},8)
  print ':'                    (,,1)
  print $EFFDT1                (0,+2,10)

  do Get-Data

  print $EFF_STATUS            (0,{col6},06)
  print ':'                    (,,1)
  print $Status                (0,+2,10)

  print $PrefixLbl             (+1,{col3},18)
  print ':'                    (,,1)
  print &C.CONTROL_NBR_PREFIX  (0,+2,4)

  print $BRANCH_DTL            (+2,1,13)            !BRANCH DETAIL

  print $MANAGE_FEES           (+2,{col3},11)
  print $AUTHORIZE             (0,{col5},13)        !Authorization

  print $BROKERAGE_FEES        (+2,{col3},15)
  print ':'                    (,,1)
  print &C.BROKERAGE_FEES_IND  (0,+7,01)

  print $CAPTIVE_OPTIONS       (0,{col6},16)
  print ':'                    (,,1)
  print &C.CAPTIVE_OPTIONS     (0,+1,01)

  print $SEC_FEES              (+1,{col3},09)
  print ':'                    (,,1)
  print &C.SEC_FEES_IND        (0,+13,01)

  print $CAPTIVE_ESPP          (0,{col6},13)
  print ':'                    (,,1)
  print &C.CAPTIVE_ESPP        (0,+4,13)

  print $BROKERAGE_CMSN        (+1,{col3},21)
  print ':'                    (,,1)
  print &C.BROKERAGE_CMSN_IND  (0,+1,01)

end-procedure

!************************************************************************
begin-procedure Print-LowerDetail
!************************************************************************

  print $CONTACT               (+2,{col3},07)

  do Get-Data
  
  print $CONTACT_TYPE          (+2,{col4},12)
  print ':'                    (,,1)
  print $ContactType           (0,+2,10)

  print $NAME                  (+1,{col4},04)
  print ':'                    (,,1)
  print &D.CONTACT_NAME        (0,20,10)

  print $PHONE                 (+1,{col4},06)
  print ':'                    (,,1)
  print &D.PHONE               (0,20,24)

  print $EMAIL_ADDR            (+1,{col4},06)
  print ':'                    (,,1)
  print &D.EMAIL_ADDR          (0,20,70)

  print $FAX_NBR               (+1,{col4},07)
  print ':'                    (,,1)
  print &D.FAX                 (0,20,15)

  print $ADDRESS               (+2,{col3},07)

  print &D.ADDRESS1            (+1,{col4},35)

  let $TempCity = rtrim(&D.CITY,' ') || ', ' || rtrim(&D.STATE,' ') || ' ' || &D.POSTAL

  print $TempCity              (+1,{col4},51)

  let $TempCounty = rtrim(&D.COUNTY,' ') || ' ' || &D.COUNTRY

  print   $TempCounty          (+1,{col4},34)

  print   ' '                  (+1,1,1)

end-procedure

!************************************************************************
begin-procedure Get-Descr
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Descr                                             !
! Descr:      This procedure gets the description of the specific   !
!             field from the prompt tables                          !
!-------------------------------------------------------------------!

begin-Select

F.DESCR

 from  PS_ST_BROKER_DTL    F
where  F.EFF_STATUS   =   'A'
and    F.ST_BROKERAGE_ID   =    &A.ST_BROKERAGE_ID
and    F.EFFDT             =    (Select   MAX(EFFDT)
                                   from   PS_ST_BROKER_DTL
                                  where   ST_BROKERAGE_ID =  F.ST_BROKERAGE_ID)

end-Select


begin-Select

G.DESCR

 from  PS_ST_BRKR_BRANCH   G
where  G.EFF_STATUS        =  'A'
  and  G.ST_BROKERAGE_ID   =  &A.ST_BROKERAGE_ID
  and  G.ST_BRANCH_ID      =  &A.ST_BRANCH_ID
  and  G.EFFDT             =  (Select   MAX(EFFDT)
                                 from   PS_ST_BRKR_BRANCH
                                where   ST_BROKERAGE_ID =  G.ST_BROKERAGE_ID
                                  and   ST_BRANCH_ID    =  G.ST_BRANCH_ID)

end-Select

end-procedure


!************************************************************************
begin-procedure Get-Data
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Data                                              !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'EFF_STATUS'
  let $FieldValue = &C.EFF_STATUS

  do Read-Translate-Table

  let $Status = RTRIM($XlatShortName,' ')


  let $FieldName = 'ST_CONTACT_TYPE'
  let $FieldValue = &D.ST_CONTACT_TYPE

  do Read-Translate-Table

  let $ContactType = RTRIM($XlatLongName,' ')

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************

#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
#include 'strunctl.sqc'  !Get run control parameter values
