!***********************************************************************
!  GPBRSC01:  INSS Salary Contribution Report                          *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2014, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2014/10/09:08:29:42                                 *
!       $Release:  HR92                                                *
!      $Revision:  103                                                 *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   ! set enviroment

Begin-Setup    
  #include 'setupdb.sqc'   ! Database specific setup    
  Declare-Printer   THIS_REPORT    
       Type=HPLASERJET    
       Font=5
           
       Point-Size=12    
       Pitch=17    
       Symbol-Set=0U     ! ASCII symbol set    
  End-Declare    
     
  Declare-Layout THIS_REPORT    
       Orientation=Portrait    
       Top-Margin= .4    
       Left-Margin=.60    
       Max-Lines=82    
       Max-Columns=130    
       Char-Width=4.3    ! 7.2    
       Line-Height=9     ! 72/printer_point-size    
  End-Declare    
     
  Declare-Report THIS_REPORT    
      Printer-Type=HPLASERJET    
      LayOut=THIS_REPORT    
  End-Declare    
  
end-Setup    

!**************************************
begin-program
!**************************************
  Alter-Report
  HEADING='Report1'
  FOOTING='Footing'  
  move '1' to $ReportDateType   ! Set for date to format as DMY
  move '1' to $ReportYear4      ! Set for year to be formatted YYYY
  
    #include 'setupdb.sqc'               ! Database specific setup
    let #FontStd   = 5  
    let #SizeGrd1  = 17  
    
    let #pos  = instr($sqr-program,'gpbrsc01',0)
    let #pos  = #pos - 1 
    let $path = substr($sqr-program,1,#pos)
    let $FileDir = $path || 'inss.gif'
        
      #ifdef MVS
       let #pos  = instr($sqr-program,'gpbrsc01',0)
       let #pos  = #pos - 1
       let $path = substr($sqr-program,1,#pos)
       let $FileDir = $path || 'inss)'
      #end-if
        
      #ifdef OS390
       let #pos  = instr($sqr-program,'gpbrsc01',0)
       let #pos  = #pos - 1
       let $path = substr($sqr-program,1,#pos)
       let $FileDir = $path || 'inss)'
      #end-if
        
      #ifdef OS400
       let #pos  = instr($sqr-program,'gpbrsc01',0)
       let #pos  = #pos - 1
       let $path = substr($sqr-program,1,#pos)
       let $FileDir = $path || 'inss)'
      #end-if
        
      #ifdef UNIX
       let $FileDir = $path || 'inss.gif'
      #end-if
    
  #debug show $FileDir '---' $sqr-program

  do Init-Report
  do Select-Parameters
  do Process-Report
  do Stdapi-Term
  do Reset
  
  date-time () hh:mi:ss &timeEnd
  display 'Report Ended: ' noline
  display &timeEnd
end-program


!***************************** 
Begin-Procedure Init-Report
#debug show '** Init-Report **'
!***************************** 
    do Init-DateTime
    do Init-Number
    do Stdapi-Init
    do Get-Current-DateTime

    move 'GPBRSC01' to $ReportID
    move 'INSS Salary Contribution Report' to $ReportTitle
    move 'Ver. 2009071200' to $ReportVersion
    display $ReportID
    display $ReportTitle
    display $ReportVersion
    date-time () hh:mi:ss &timeBegan
    display 'Report Began: ' noline
    display &timeBegan

    Alter-Locale MONEY-SIGN = 'R$'    
End-Procedure Init-Report

begin-heading 1 name = 'Report1'
  print-image (1,2)
  type         = GIF-FILE
  image-size   = (35,4)
  source       = $FileDir
end-heading 1

begin-heading 3 name = 'Report2'
   Alter-Printer Point-size = 10 Font-Type = Fixed Font = 22
   Print 'AUMENTOS SALARIAIS' (3,53) Bold  
    
   Alter-Printer Point-size = 8 Font-Type = Proportional Font = 22   
   Print 'MÊS/ANO'              ( 5,  03) Bold      
   Print 'MOTIVO'               ( 5,  59) Bold         
   Print 'PERCENTUAL'           ( 5, 105) Bold             
end-heading 3

begin-footing 8 name = 'Footing' 
  
   Alter-Printer Point-size = 9 Font-Type = Proportional Font = 22
   Print $CityAndDate     ( ,1) Bold 
   
   graphic ( , 75, 52) horz-line

   Alter-Printer Point-size = 8 Font-Type = Proportional Font = 22        
   Print 'ASSINATURA DO RESPONSÁVEL E CARIMBO DA EMPRESA'   ( +1, 75) Bold

   Alter-Printer Point-size = 7 Font-Type = Proportional Font = 22     
   Print 'IMPORTANTE'           (+2,  57) Bold
!   Print 'A INEXATIDÃO DAS DECLARAÇÕES CONSTANTES DESTE DOCUMENTO CONSTITUI CRIME PREVISTO NOS ARTIGOS 171 e 299 DO CÓDIGO PENAL' ( +1, 3)

   move   'A INEXATIDÃO DAS DECLARAÇÕES CONSTANTES DESTE DOCUMENTO CONSTITUI CRIME PREVISTO NOS ARTIGOS 171 e 299 DO CÓDIGO PENAL' to $ReportFooter
   Print $ReportFooter ( +1, 3)
   
   Alter-Printer Point-size = 6 Font-Type = Proportional Font = 22     
   Print $ReportDateFmtBra      (+2,   1) Bold 
   Print $ReportTime            (  ,  +2) Bold 

   Print $EmplId                (  , 110) Bold
   Print #EmplRcd               (  ,  +2) Edit '99' Bold

   Print 'PeopleSoft '          (+1,   1)
   Print 'GPBRSC01'             (  , 117)
end-footing 8   

!***************************** 
Begin-Procedure Select-Parameters
#debug show '** Select-Parameters **'
#debug show '$AsOfToday ' $AsOfToday
#debug show '$prcs_oprid ' $prcs_oprid
#debug show '$prcs_run_cntl_id ' $prcs_run_cntl_id
!***************************** 
Begin-Select
RC.LEGAL_ENT_TYPE_BRA
RC.GPBR_LIST_OPTION
RC.ESTABID
RC.GB_GROUP_ID
RC.GPBR_CRED_BGN_DT
RC.GPBR_CRED_END_DT


  Let $strLegaltp = &RC.LEGAL_ENT_TYPE_BRA  
  Let $strlstOptn = &RC.GPBR_LIST_OPTION
  Let $strEstabId = &RC.ESTABID
  Let $strGroupId = &RC.GB_GROUP_ID
  Let $strBeginDt = &RC.GPBR_CRED_BGN_DT
  Let $strEndDt   = &RC.GPBR_CRED_END_DT
  
  #debug show '$strLegaltp ' $strLegaltp
  #debug show '$strlstOptn ' $strlstOptn
  #debug show '$strEstabId ' $strEstabId
  #debug show '$strGroupId ' $strGroupId

  Evaluate $strlstOptn
  When = '1' !* All establishments & Payees
     Let $WhereClause = 'WHERE '
     Break
  When = '2' !* Establishment
     Let $WhereClause = 'WHERE JOB.ESTABID = ' || '''' || Rtrim($strEstabId, ' ') || ''''
     Let $WhereClause = $WhereClause || ' AND '
     Break
  When = '3' !* Employees
     Let $WhereClause = ', PS_GPBR_RC_SALCONE SCE WHERE JOB.EMPLID = SCE.EMPLID AND JOB.EMPL_RCD = SCE.EMPL_RCD'
     Let $WhereClause = $WhereClause || ' AND SCE.OPRID = ' || '''' || Rtrim($prcs_oprid, ' ') || ''''
     Let $WhereClause = $WhereClause || ' AND SCE.RUN_CNTL_ID = ' || '''' || Rtrim($prcs_run_cntl_id, ' ') || ''''
     Let $WhereClause = $WhereClause || ' AND '
     Break
  When = '4' !* Group ID
     Let $WhereClause = ', PS_GB_GRP_RES_TBL GRP WHERE JOB.EMPLID = GRP.EMPLID AND JOB.EMPL_RCD = GRP.EMPL_RCD' 
     Let $WhereClause = $WhereClause || ' AND GRP.GB_GROUP_ID = ' || '''' || Rtrim($strGroupId, ' ') || ''''
     Let $WhereClause = $WhereClause || ' AND GRP.VERSIONGBQDM = (SELECT MAX(VERSIONGBQDM) FROM PS_GB_GRP_RES_TBL ' 
     Let $WhereClause = $WhereClause || ' WHERE GB_GROUP_ID = GRP.GB_GROUP_ID)'
     Let $WhereClause = $WhereClause || ' AND JOB.EFFDT = GRP.JOB_EFFDT'
     Let $WhereClause = $WhereClause || ' AND JOB.EFFSEQ = GRP.JOB_EFFSEQ'
     Break
  End-Evaluate
  
  Evaluate $strlstOptn
  When = '1' !* All establishments & Payees
  When = '2' !* Establishment
  When = '3' !* Employees
     Let $WhereClause = $WhereClause || ' JOB.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOB WHERE EMPLID = JOB.EMPLID AND EMPL_RCD = JOB.EMPL_RCD AND EFFDT <= ' || '''' || $AsOfToday || '''' || ')'
     Let $WhereClause = $WhereClause || ' AND JOB.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB WHERE EMPLID = JOB.EMPLID AND EMPL_RCD = JOB.EMPL_RCD AND EFFDT = JOB.EFFDT)'
  Break
  End-Evaluate

  Do Convert-To-Dtu-Date($AsOfToday, $AsOfTodayDTU)  
  Do Dtu-Parse-Date($AsOfTodayDTU, #Year, #Month, #Day)      
 
   Evaluate #Month
   When = 1
      Let $Month = 'Janeiro'
   Break
   When = 2
      Let $Month = 'Fevereiro'
   Break
   When = 3
      Let $Month = 'Março'
   Break
   When = 4
      Let $Month = 'Abril'
   Break
   When = 5
      Let $Month = 'Maio'
   Break
   When = 6
      Let $Month = 'Junho'
   Break
   When = 7
      Let $Month = 'Julho'
   Break
   When = 8
      Let $Month = 'Agosto'
   Break
   When = 9
      Let $Month = 'Setembro'
   Break
   When = 10
      Let $Month = 'Outubro'
   Break
   When = 11
      Let $Month = 'Novembro'
   Break
   When = 12
      Let $Month = 'Dezembro'
   Break
  End-Evaluate

  Let $ReportDateFmtBra = Edit(#Day, '09') || '/' || Edit(#Month,'09') || '/' || Edit(#Year,'9999') 
  Let $ReportDateBra = ' , ' || Edit(#Day, '09') || ' de ' || $Month || ' de ' || Edit(#Year,'9999')  
  
FROM  PS_GPBR_RC_SALCON RC
WHERE RC.OPRID       = $prcs_oprid
  AND RC.RUN_CNTL_ID = $prcs_run_cntl_id
End-Select
End-Procedure Select-Parameters

!*****************************   
Begin-Procedure Process-Report
#debug show '** Process-Report **'
#debug show '$WhereClause ' $WhereClause
!*****************************  
Begin-Select
JOB.EMPLID
JOB.EMPL_RCD
JOB.ESTABID
JOB.COMPANY
EMP.HIRE_DT
EMP.TERMINATION_DT

   Let $EmplId          = Rtrim(&JOB.EMPLID, ' ')
   Let #EmplRcd         = &JOB.EMPL_RCD
   Let $EstabId         = Rtrim(&JOB.ESTABID, ' ')
   Let $Company         = Rtrim(&JOB.COMPANY, ' ')
   Let $HireDt          = Rtrim(&EMP.HIRE_DT, ' ')
   Let $TerminationDt   = Rtrim(&EMP.TERMINATION_DT, ' ')
   Let $TermDt_FmtBra   = '' 
   Let $HireDt_FmtBra   = ''
   
   If $HireDt <> ''
     Do Convert-To-Dtu-Date($HireDt , $HireDtDTU)  
     Do Dtu-Parse-Date($HireDtDTU, #Year, #Month, #Day)     
     Let $HireDt_FmtBra = Edit(#Day, '09') || '/' || Edit(#Month,'09') || '/' || Edit(#Year,'9999') 
   End-If
   If $TerminationDt <> ''
     Do Convert-To-Dtu-Date($TerminationDt , $TerminationDtDTU)  
     Do Dtu-Parse-Date($TerminationDtDTU, #Year, #Month, #Day)
     Let $TermDt_FmtBra = Edit(#Day, '09') || '/' || Edit(#Month,'09') || '/' || Edit(#Year,'9999') 
   End-If
   
   #debug show '$Emplid ' $Emplid
   #debug show '#EmplRcd ' #EmplRcd

   If $strLegaltp = 'COM'
      Do Get-Company-Data
   Else
      Do Get-Estab-Data
   End-If
   
   Do Get-Estab-IDs
   Do Get-Employee-Name
   Do Get-Employee-IDs
   Do Print-Layout-Page1
   Do Print-Data-Page1
   Do Print-Contributions 
   New-Page
   Do Print-Data-Page2
   New-Page
   
FROM PS_JOB JOB
, PS_EMPLOYMENT EMP
[$WhereClause]
AND EMP.EMPLID = JOB.EMPLID
AND EMP.EMPL_RCD = JOB.EMPL_RCD
ORDER BY JOB.EMPLID, JOB.EMPL_RCD
End-Select
End-Procedure Process-Report

!*****************************   
Begin-Procedure Get-Company-Data
#debug show '** Get-Company-Data **'
#debug show '$Company ' $Company
#debug show '$AsOfToday ' $AsOfToday
!*****************************  
Let $Space = ' '
Let $ComaSpace = ', '
Let $CompanyLegalName = ''
Let $CompanyAddress = ''
Begin-Select
COM.DESCR
COM.ADDRESS1
COM.ADDRESS2
COM.ADDRESS4
COM.NUM1
COM.STATE
COM.CITY

   Let $CompanyLegalName = Rtrim(&COM.DESCR, ' ')
   Let $CompanyAddress = Rtrim(&COM.ADDRESS1, ' ') || $Space || Rtrim(&COM.NUM1, ' ') || $ComaSpace 
   Let $CompanyAddress = $CompanyAddress || Rtrim(&COM.ADDRESS2, ' ') || $ComaSpace || Rtrim(&COM.ADDRESS4, ' ') 
   Let $CompanyAddress = $CompanyAddress || $ComaSpace || Rtrim(&COM.STATE, ' ')
   Let $CompanyAddress = Substr(Rtrim($CompanyAddress, ' '),1,73)
   Let $CityAndDate    = Rtrim(&COM.CITY, ' ') || $ReportDateBra

FROM PS_COMPANY_TBL COM
WHERE COM.COMPANY = $Company
AND   COM.EFFDT   = (SELECT MAX(CO1.EFFDT) 
                     FROM PS_COMPANY_TBL CO1 
                     WHERE CO1.COMPANY = COM.COMPANY 
                     AND CO1.EFFDT <= $AsOfToday)
End-Select
End-Procedure Get-Company-Data

!*****************************   
Begin-Procedure Get-Estab-Data
#debug show '** Get-Estab-Data **'
#debug show '$EstabId ' $EstabId
#debug show '$AsOfToday ' $AsOfToday
!*****************************  
Let $Space = ' '
Let $ComaSpace = ', '
Let $CompanyLegalName = ''
Let $CompanyAddress = ''
Begin-Select
EST.DESCR
EST.ADDRESS1
EST.ADDRESS2
EST.ADDRESS4
EST.NUM1
EST.STATE
EST.CITY

   Let $CompanyLegalName = Rtrim(&EST.DESCR, ' ')
   Let $CompanyAddress = Rtrim(&EST.ADDRESS1, ' ') || $Space || Rtrim(&EST.NUM1, ' ') || $ComaSpace 
   Let $CompanyAddress = $CompanyAddress || Rtrim(&EST.ADDRESS2, ' ') || $ComaSpace || Rtrim(&EST.ADDRESS4, ' ') 
   Let $CompanyAddress = $CompanyAddress || $ComaSpace || Rtrim(&COM.STATE, ' ')
   Let $CompanyAddress = Substr(Rtrim($CompanyAddress, ' '),1,146)
   Let $CityAndDate    = Rtrim(&EST.CITY, ' ') || $ReportDateBra

FROM PS_ESTAB_TBL EST
WHERE EST.ESTABID = $EstabId
AND   EST.EFFDT   = (SELECT MAX(ES1.EFFDT) 
                     FROM PS_ESTAB_TBL ES1 
                     WHERE ES1.ESTABID = EST.ESTABID 
                     AND ES1.EFFDT <= $AsOfToday)
End-Select
End-Procedure Get-Estab-Data

!*****************************   
Begin-Procedure Get-Estab-IDs
#debug show '** Get-Estab-IDs **'
#debug show '$EstabId ' $EstabId
!*****************************  
Let $EstabIdType = ''
Let $CompanyCNPJ = ''
Begin-Select
EID.ESTAB_ID_TYPE_BRA 
EID.ESTAB_ID_BRA

   Let $EstabIdType = Rtrim(&EID.ESTAB_ID_TYPE_BRA , ' ')   

   Evaluate $EstabIdType
   When = 'CNPJ'
      Let $CompanyCNPJ = Rtrim(&EID.ESTAB_ID_BRA, ' ')
   Break
   End-Evaluate
   
FROM PS_ESTAB_ID_BRA EID
WHERE EID.ESTABID = $EstabId
AND   EID.ESTAB_ID_TYPE_BRA IN ('CNPJ')
AND   EID.EFFDT   = (SELECT MAX(EI1.EFFDT) 
                     FROM PS_ESTAB_ID_BRA EI1 
                     WHERE EI1.ESTABID = EID.ESTABID 
                     AND EI1.EFFDT <= $AsOfToday)
End-Select
End-Procedure Get-Estab-IDs

!*****************************   
Begin-Procedure Get-Employee-Name
#debug show '** Get-Employee-Name **'
#debug show '$EmplId ' $EmplId
#debug show '$AsOfToday ' $AsOfToday
!*****************************  
Let $EmployeeName = ''
Begin-Select
NAME_DISPLAY

   Let $EmployeeName = Substr(Rtrim(&NAME_DISPLAY, ' '),1,48)
   
FROM PS_NAMES NAM
WHERE NAM.EMPLID  = $EmplId
AND NAM.NAME_TYPE = 'PRI'
AND NAM.EFFDT = (SELECT MAX(NA1.EFFDT) 
                 FROM PS_NAMES NA1 
                 WHERE NA1.EMPLID  = NAM.EMPLID 
                 AND NA1.NAME_TYPE = NAM.NAME_TYPE AND 
                 NA1.EFFDT        <= $AsOfToday)
End-Select
End-Procedure Get-Employee-Name

!*****************************   
Begin-Procedure Get-Employee-IDs
#debug show '** Get-Employee-IDs **'
#debug show '$EmplId ' $EmplId
!*****************************  
Let $EmplIDType = ''
Let $EmployeeCPF = ''
Let $EmployeePISP = ''
Let $EmplCTPSSer = ''
Let $EmplCTPSNum = ''
Begin-Select
PID.NATIONAL_ID_TYPE
PID.NATIONAL_ID

   Let $EmplIDType = Rtrim(&PID.NATIONAL_ID_TYPE, ' ')
   Evaluate $EmplIDType
   When = 'CPF'
      Let $EmployeeCPF = Rtrim(&PID.NATIONAL_ID, ' ')
   Break
   When = 'PIS'
   When = 'PASEP'
      Let $EmployeePISP = Rtrim(&PID.NATIONAL_ID, ' ')
   Break   
   When = 'CTPS'
      Let $EmplCTPSNum = Rtrim(&PID.NATIONAL_ID, ' ')
   Break
   End-Evaluate

FROM PS_PERS_NID PID
WHERE PID.EMPLID = $EmplId
AND   PID.COUNTRY = 'BRA'
AND   PID.NATIONAL_ID_TYPE IN ('CPF','PIS', 'PASEP', 'CTPS')
End-Select

Begin-Select
PBR.CTPS_SERIES_BRA

   Let $EmplCTPSSer = Rtrim(&PBR.CTPS_SERIES_BRA, ' ')
  
FROM PS_PERSON_BRA PBR
WHERE PBR.EMPLID = $EmplId
End-Select
End-Procedure Get-Employee-IDs



!*****************************   
Begin-Procedure Print-Layout-Page1
#debug show '** Print-Layout-Page1 **'
!*****************************  
   
   Alter-Report
   HEADING = 'Report1'
  
   graphic ( 6,  1, 74) box  3 5
   graphic ( 6, 76, 19) box  3 5
   graphic ( 6, 96, 10) box  3 5
   graphic ( 6,107, 16) box  3 5

   graphic (10,  1, 94) box  3 5
   graphic (10, 96, 27) box  3 5
   
   graphic (14,  1, 94) box  3 5
   graphic (14, 96, 27) box  3 5

   graphic (18,  1, 32) box  3 5
   graphic (18, 34, 61) box  3 5
   graphic (18, 96, 27) box  3 5
   
   graphic (22,  1, 32) box  3 5
   graphic (22, 34, 30) box  3 5
   graphic (22, 65, 30) box  3 5
   graphic (22, 96, 27) box  3 5

   graphic (26,  1,122) box  3 5
   graphic (26, 61, 62) box  3 5
                 
   Alter-Printer Point-size = 10 Font-Type = Fixed Font = 22
!   print 'RELAÇÃO DOS SALÁRIOS DE CONTRIBUIÇÃO' (7,2) Bold  
 
   move   'RELAÇÃO DOS SALÁRIOS DE CONTRIBUIÇÃO' to $ReportTitle
   Print $ReportTitle (7, 2) Bold

   Alter-Printer Point-size = 6 Font-Type = Proportional Font = 22   
   Print 'OL'                   ( 6,  77) Bold
   Print 'E'                    ( 6,  98) Bold
   Print 'NB'                   ( 6, 108) Bold
   
   Print 'Razão Social'         (10,  41) Bold   
   Print 'Número de C.N.P.J.'   (10, 101) Bold
   
   Print 'Endereço'             (14,  43) Bold   
   Print 'Matrícula I.N.S.S.'   (14, 101) Bold   
   
   Print 'Unidade    /     Registro'(18,   2) Bold   
   Print 'Nome do Segurado'     (18,  56) Bold   
   Print 'Número de C.P.F.'     (18, 102) Bold   
   
   Print 'Doc. Inscrição Número e Série' (22,   2) Bold   
   Print 'Data Admissão'        (22,  43) Bold   
   Print 'Data Desligamento'    (22,  73) Bold   
   Print 'Número PIS / PASEP'   (22, 101) Bold      
   
   Alter-Printer Point-size = 8 Font-Type = Proportional Font = 22   
   Print 'MÊS/ANO'              (27,  03) Bold      
   Print 'VALOR'                (27,  27) Bold         
   Print 'RECOH.'               (27,  51) Bold         

   Print 'MÊS/ANO'              (27,  63) Bold      
   Print 'VALOR'                (27,  87) Bold         
   Print 'RECOH.'               (27, 111) Bold      
   
End-Procedure Print-Layout-Page1


!*****************************   
Begin-Procedure Print-Data-Page1
#debug show '** Print-Data-Page1 **'
#debug show '#EmplNamePos ' #EmplNamePos
!*****************************      
   Alter-Printer Point-size = 9 Font-Type = Proportional Font = 22
   Print $CompanyLegalName (12, 2) Bold
   Print $CompanyCNPJ      (12,97) Bold

   let #lengthC = length($CompanyAddress)
  
   If #lengthC > 73
     Let $CompanyAddress1 = Substr(Rtrim($CompanyAddress, ' '),1,73)
     Let $CompanyAddress2 = Substr(Rtrim($CompanyAddress, ' '),74,73)
     
     Print $CompanyAddress1    (15,2) Bold
     Print $CompanyAddress2    (16,2) Bold
   else
     Print $CompanyAddress     (16,2) Bold
   end-if
 
!  Print $CompanyAddress  (16, 2) Bold
!  Print $CompanyINSS     (16,97) Bold
 
   Print $EstabId         (20, 2) Bold
   Print $EmplId          (20,19) Bold   
   Print $EmployeeName    (20,35) Bold
   Print $EmployeeCPF     (20,97) Bold   
   
   Print $EmplCTPSNum     (24, 2) Bold
   Print $EmplCTPSSer     (24,19) Bold   
      
   Print $HireDt_FmtBra   (24,35) Bold   
   Print $TermDt_FmtBra   (24,67) Bold
   Print $EmployeePISP    (24,97) Bold  
   
End-Procedure Print-Data-Page1

!*****************************   
Begin-Procedure Print-Contributions
#debug show '** Print-Contributions **'
#debug show '$EmplId ' $EmplId
#debug show '#EmplRcd ' #EmplRcd
!*****************************  

Let #Line = 1
Print ' '    (29, 2)
Begin-Select
GPR.CALC_RSLT_VAL
GPC.PYMT_DT  

  Let $Contribution = &GPR.CALC_RSLT_VAL
  Let $PaymentDt    = &GPC.PYMT_DT
  
  Do Convert-To-Dtu-Date($PaymentDt, $PaymentDtDTU)  
  Do Dtu-Parse-Date($PaymentDtDTU, #Year, #Month, #Day)  
  Let $MonthYearDt   = Edit(#Month, '09') || '/' || Edit(#Year, '9999')
  Do Dtu-Add-Months($PaymentDtDTU, 1, $CollectionDtDTU)
  Do Dtu-Parse-Date($CollectionDtDTU, #Year, #Month, #Day)  
  Let $CollectionDt  = Edit(#Month, '09') || '/' || Edit(#Year, '9999')
  
  Evaluate #Line 
  When < 31
    Print $MonthYearDt   (+1,  2)
    Print $Contribution  (  , 24) Edit '99,999.00c'
    Print $CollectionDt  (  , 50)
    Break
  When = 31
    Print ' '    (29, 2)    
  When < 61 
    Print $MonthYearDt   (+1, 62)
    Print $Contribution  (  , 84) Edit '99,999.00c'
    Print $CollectionDt  (  ,110)  
    if #Line  = 60
       New-Page
       do Print-Layout-Page1
       do Print-Data-Page1
       Let #Line = 1
       Print ' '    (29, 2)
    end-if
    Break 
 End-Evaluate

  Let #Line = #Line + 1
  

FROM PS_GP_RSLT_ED_VW GPR
, PS_GP_PIN GPP
, PS_GP_CALENDAR GPC 
WHERE GPR.EMPLID       = $EmplId
AND GPR.EMPL_RCD       = #EmplRcd
AND GPR.PIN_NUM        = GPP.PIN_NUM
AND GPC.CAL_ID         = GPR.CAL_ID
AND GPC.GP_PAYGROUP    = GPR.GP_PAYGROUP
AND GPR.CAL_RUN_ID     = GPR.ORIG_CAL_RUN_ID
AND GPR.SLICE_BGN_DT  >= $strBeginDt
AND GPR.SLICE_END_DT  <= $strEndDt
AND (GPP.PIN_CODE      = 'INSS SAL CON BRA' 
OR  (GPR.CALC_RSLT_VAL = 0 AND
NOT EXISTS (SELECT 'X' FROM PS_GP_RSLT_ED_VW GPR2 , PS_GP_PIN GPP2
            WHERE GPR.EMPLID   = GPR2.EMPLID 
            AND GPR.EMPL_RCD   = GPR2.EMPL_RCD 
            AND GPR.CAL_RUN_ID = GPR2.CAL_RUN_ID
            AND GPR2.PIN_NUM   = GPP2.PIN_NUM 
            AND GPP2.PIN_CODE  = 'INSS SAL CON BRA')))  
ORDER BY GPC.PYMT_DT ASC
End-Select

End-Procedure Print-Contributions

!*****************************   
Begin-Procedure Print-Data-Page2
#debug show '** Print-Data-Page2 **'
#debug show '$EmplId ' $EmplId
#debug show '#EmplRcd ' #EmplRcd
!*****************************  
Let $ActnRsnDesc = ''
Alter-Report
HEADING='Report2'

Alter-Printer Point-size = 9 Font-Type = Proportional Font = 22
Print ' '   (3, 1)
Begin-Select
COP.EFFDT
COP.CHANGE_PCT
JBA.ACTION
JBA.ACTION_REASON

   Let $CompChgDt    = &COP.EFFDT
   Let #CompChgPct   = &COP.CHANGE_PCT
   Let $CompChgAct   = Rtrim(&JBA.ACTION, ' ')
   Let $CompChgRsn   = Rtrim(&JBA.ACTION_REASON, ' ')

   Do Get-Action-Rsn
   Let $ActnRsnDesc  = Substr($ActnRsnDesc, 1, 90)
   
   Do Convert-To-Dtu-Date($CompChgDt, $CompChgDtDTU)  
   Do Dtu-Parse-Date($CompChgDtDTU, #Year, #Month, #Day)  
   Let $CompChgDt  = Edit(#Month, '09') || '/' || Edit(#Year, '9999')
     
   Print $CompChgDt    ( +1,  2)
   Print $ActnRsnDesc  (   , 14)
   Print #CompChgPct   (   ,106) Edit '9990.00c'

FROM PS_COMPENSATION COP
, PS_JOB JBA
WHERE COP.EMPLID   = $Emplid
AND   COP.EMPL_RCD = #EmplRcd
AND   COP.CHANGE_PCT <> 0
AND   COP.EMPLID   = JBA.EMPLID
AND   COP.EMPL_RCD = JBA.EMPL_RCD
AND   COP.EFFDT    = JBA.EFFDT
AND   COP.EFFSEQ   = JBA.EFFSEQ
End-Select
End-Procedure Print-Data-Page2

!*****************************   
Begin-Procedure Get-Action-Rsn
#debug show '** Get-Action-Rsn **'
#debug show '$CompChgAct ' $CompChgAct
#debug show '$CompChgRsn ' $CompChgRsn
!***************************** 
Let $ActnRsnDesc = ''
Let $ActionDesc = ''
Let $ReasonDesc = ''
Begin-Select
ACT.ACTION_DESCR
   
   Let $ActionDesc = Rtrim(&ACT.ACTION_DESCR, ' ')

FROM PS_ACTION_TBL ACT
WHERE ACT.ACTION = $CompChgAct
AND   ACT.EFFDT  = (SELECT MAX(AC1.EFFDT) 
                    FROM PS_ACTION_TBL AC1 
                    WHERE AC1.ACTION = ACT.ACTION 
                    AND   AC1.EFFDT <= $AsOfToday)
End-Select

Begin-Select
ACL.ACTION_DESCR
   
   Let $ActionDesc = Rtrim(&ACL.ACTION_DESCR, ' ')

FROM PS_ACTION_LNG ACL
WHERE ACL.ACTION      = $CompChgAct
AND   ACL.LANGUAGE_CD = 'POR'
AND   ACL.EFFDT       = (SELECT MAX(AL1.EFFDT) 
                         FROM PS_ACTION_LNG AL1 
                         WHERE AL1.ACTION      = ACL.ACTION 
                         AND   AL1.LANGUAGE_CD = ACL.LANGUAGE_CD
                         AND   AL1.EFFDT      <= $AsOfToday)
End-Select

Begin-Select
RSN.DESCR

   Let $ReasonDesc = Rtrim(&RSN.DESCR, ' ')
   
FROM PS_ACTN_REASON_TBL RSN
WHERE RSN.ACTION        = $CompChgAct
AND   RSN.ACTION_REASON = $CompChgRsn
AND   RSN.EFFDT         = (SELECT MAX(RS1.EFFDT) 
                           FROM PS_ACTN_REASON_TBL RS1 
                           WHERE RS1.ACTION        = RSN.ACTION 
                           AND   RS1.ACTION_REASON = RSN.ACTION_REASON 
                           AND   RS1.EFFDT        <= $AsOfToday)
End-Select

Begin-Select
RNL.DESCR

   Let $ReasonDesc = Rtrim(&RNL.DESCR, ' ')
   
FROM PS_ACTN_RSN_LANG RNL
WHERE RNL.ACTION        = $CompChgAct
AND   RNL.ACTION_REASON = $CompChgRsn
AND   RNL.LANGUAGE_CD   = 'POR'
AND   RNL.EFFDT         = (SELECT MAX(RL1.EFFDT) 
                           FROM PS_ACTN_RSN_LANG RL1 
                           WHERE RL1.ACTION        = RNL.ACTION 
                           AND   RL1.ACTION_REASON = RNL.ACTION_REASON 
                           AND   RL1.LANGUAGE_CD   = RNL.LANGUAGE_CD 
                           AND   RL1.EFFDT        <= $AsOfToday)
End-Select
Let $ActnRsnDesc = $ActionDesc || ' - ' || $ReasonDesc
End-Procedure Get-Action-Rsn

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'  !Indicate which printer to use for reports
#include 'hrsecty.sqc'   !Get SQR Security parameters
!#include 'brfile01.sqc'  !File Utilities
