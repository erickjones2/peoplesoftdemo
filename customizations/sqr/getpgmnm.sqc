!*******************************************************
! GetPgmNm: Get the Name of the current Benefit Program
!*******************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:12:29:42                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!***************************************
begin-procedure Get-Benefit-Program-Name
!***************************************

  !Keys passed in:  $$BenProgram, $EffDate
  !Values returned: $BenefitProgramName, $BenefitProgramAbbrv, $BenefitProgramCurr

  move '' to $BenefitProgramName
  move '' to $BenefitProgramAbbrv
  move '' to $BenefitProgramCurr

  if Rtrim($BenProgram, ' ') = ''
    goto GetPgmNmExit
  end-if

  if $EffDate = ''
    move $AsOfToday to $EffDate
  end-if

begin-SELECT
PGM.DESCR
PGM.DESCRSHORT
PGM.CURRENCY_CD

  let $BenefitProgramName  = Rtrim(&PGM.Descr, ' ')
  let $BenefitProgramAbbrv = Rtrim(&PGM.DescrShort, ' ')
  let $BenefitProgramCurr  = Rtrim(&PGM.Currency_Cd, ' ')

FROM PS_BEN_DEFN_PGM PGM
WHERE PGM.BENEFIT_PROGRAM = $BenProgram
  AND PGM.EFFDT          <= $EffDate
  AND PGM.EFFDT =
         (SELECT MAX(EFFDT)
          FROM  PS_BEN_DEFN_PGM
          WHERE BENEFIT_PROGRAM = PGM.BENEFIT_PROGRAM
            AND EFFDT          <= $EffDate)
end-SELECT

  do Get-Benefit-Program-Name-Lang

GetPgmNmExit:

end-procedure


!********************************************
begin-procedure Get-Benefit-Program-Name-Lang
!********************************************

  !Keys passed in:  $$BenProgram, $EffDate
  !Values returned: $BenefitProgramName, $BenefitProgramAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetPgmNmLngExit
  end-if

  if Rtrim($BenProgram, ' ') = ''
    goto GetPgmNmLngExit
  end-if

  if $EffDate = ''
    move $AsOfToday to $EffDate
  end-if

begin-SELECT
PGML.DESCR
PGML.DESCRSHORT

  if Rtrim(&PGML.DESCR, ' ') <> ''
    let $BenefitProgramName  = Rtrim(&PGML.DESCR, ' ')
  end-if
  if Rtrim(&PGML.DESCRSHORT, ' ') <> ''
    let $BenefitProgramAbbrv = Rtrim(&PGML.DESCRSHORT, ' ')
  end-if

FROM PS_BENEF_PGM_LANG PGML
WHERE PGML.BENEFIT_PROGRAM = $BenProgram
  AND PGML.LANGUAGE_CD     = $Curr_Language_Cd
  AND PGML.EFFDT          <= $EffDate
  AND PGML.EFFDT =
          (SELECT MAX(EFFDT)
           FROM  PS_BENEF_PGM_LANG
           WHERE BENEFIT_PROGRAM = PGML.BENEFIT_PROGRAM
             AND LANGUAGE_CD     = PGML.LANGUAGE_CD
             AND EFFDT          <= $EffDate)
end-SELECT

GetPgmNmLngExit:

end-procedure

!***************************** End of Include File **************************
