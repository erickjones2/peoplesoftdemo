!*********************************************************
!  PAY300RT:  Retro Pay Request and Trigger Report       *
!*********************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/06/18:12:20:34                                 !
!       $Release:  HR92                                                !
!      $Revision:  102                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment

begin-report

    do Init-DateTime
    do Init-Number
    do Get-Current-Datetime
    do Init-Report
    do Process-Main
    do Reset
    do Stdapi-Term

end-report

!***********************************************************************



!*******************  H E A D I N G     S E C T I O N  *****************

#include 'setup32.sqc'  !Printer and page-size initialization

begin-heading 10

#include 'stdhdg02.sqc'

    print $Pay300rt_Empl                   (+4,51)
    print $Pay300rt_Empl                    (0,93)
    print $Pay300rt_Retro_Pay               (0,111)
    print $Pay300rt_Process                 (0,136)
    print $Pay300rt_Retro                   (0,147)
    print $Pay300rt_Trigger                 (0,158)

    print $Pay300rt_Rqst_Type              (+1,1)
    print $Pay300rt_Mass_ID                 (0,12)
    print $Pay300rt_Retro_Seq               (0,21)
    print $Pay300rt_Emplid                  (0,38)
    print $Pay300rt_Rcd#                    (0,51)
    print $Pay300rt_Name                    (0,57)
    print $Pay300rt_Status                  (0,93)
    print $Pay300rt_Paygroup                (0,102)
    print $Pay300rt_Prcs_Flag               (0,111)
    print $Pay300rt_Retro_Effdt             (0,124)
    print $Pay300rt_End_Dt                  (0,136)
    print $Pay300rt_Pgm_ID                  (0,147)
    print $Pay300rt_Pgm_ID                  (0,158)
    print $Pay300rt_Duplicate               (0,170)

    If $Trigger = 'I' and $RqstType <> 'M'
       print $Pay300rt_Trigger             (+2,5)
       print $Pay300rt_Trigger              (0,16)
       print $Pay300rt_Trigger              (0,27)
       print $Pay300rt_Trigger              (0,145)
       print $Pay300rt_Trigger              (0,157)

       print $Pay300rt_Trg_ID              (+1,5)
       print $Pay300rt_Trg_Seq              (0,16)
       print $Pay300rt_Trg_Effdt            (0,27)
       print $Pay300rt_Field                (0,40)
       print $Pay300rt_Old_Value            (0,61)
       print $Pay300rt_New_Value            (0,89)
       print $Pay300rt_Trg_Value            (0,117)
       print $Pay300rt_Trg_Action           (0,145)
       print $Pay300rt_Trg_Status           (0,157)

       print '-'                           (+1,1,174) fill
    end-if


end-heading

!******************  I N I T I A L I Z A T I O N   *********************

begin-procedure Init-Report

    move 'PAY300RT' to $ReportID
    do Stdapi-Init
    do Get-Report-Language
    move $Pay300rt_ReportTitle  to $ReportTitle
    display $ReportTitle

    do Select-Parameters

    do Build-Where-Clause
    do Build-Order-Clause
    move 'N' to $DataFound
    let $Prev_Company = ' '
    let $Prev_Rqst    = ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
begin-procedure Report-Translation
! Read from PAY300RT program ids

  do Init_Report_Translation($ReportID,$language_cd)

  do Get_Field_Information ('PAY300RT', 'REPORTTITLE',  $Pay300rt_ReportTitle, #dummy_width)
  do Get_Field_Information ('PAY300RT', 'RQST_TYPE',    $Pay300rt_Rqst_Type,   #dummy_width)
  do Get_Field_Information ('PAY300RT', 'RQST_TYPE_A',  $Pay300rt_Rqst_Type_A, #dummy_width)
  do Get_Field_Information ('PAY300RT', 'RQST_TYPE_J',  $Pay300rt_Rqst_Type_J, #dummy_width)
  do Get_Field_Information ('PAY300RT', 'RQST_TYPE_M',  $Pay300rt_Rqst_Type_M, #dummy_width)
  do Get_Field_Information ('PAY300RT', 'MASS_ID',      $Pay300rt_Mass_ID,     #dummy_width)
  do Get_Field_Information ('PAY300RT', 'RETRO_SEQ',    $Pay300rt_Retro_Seq,   #dummy_width)
  do Get_Field_Information ('PAY300RT', 'EMPLID',       $Pay300rt_Emplid,      #dummy_width)
  do Get_Field_Information ('PAY300RT', 'EMPL',         $Pay300rt_Empl,        #dummy_width)
  do Get_Field_Information ('PAY300RT', 'RCD#',         $Pay300rt_Rcd#,        #dummy_width)
  do Get_Field_Information ('PAY300RT', 'NAME',         $Pay300rt_Name,        #dummy_width)
  do Get_Field_Information ('PAY300RT', 'STATUS',       $Pay300rt_Status,      #dummy_width)
  do Get_Field_Information ('PAY300RT', 'PAYGROUP',     $Pay300rt_Paygroup,    #dummy_width)
  do Get_Field_Information ('PAY300RT', 'RETRO_PAY',    $Pay300rt_Retro_Pay,   #dummy_width)
  do Get_Field_Information ('PAY300RT', 'PRCS_FLAG',    $Pay300rt_Prcs_Flag,   #dummy_width)
  do Get_Field_Information ('PAY300RT', 'RETRO_EFFDT',  $Pay300rt_Retro_Effdt, #dummy_width)
  do Get_Field_Information ('PAY300RT', 'PROCESS',      $Pay300rt_Process,     #dummy_width)
  do Get_Field_Information ('PAY300RT', 'END_DT',       $Pay300rt_End_Dt,      #dummy_width)
  do Get_Field_Information ('PAY300RT', 'RETRO',        $Pay300rt_Retro,       #dummy_width)
  do Get_Field_Information ('PAY300RT', 'PGM_ID',       $Pay300rt_Pgm_ID,      #dummy_width)
  do Get_Field_Information ('PAY300RT', 'TRIGGER',      $Pay300rt_Trigger,     #dummy_width)
  do Get_Field_Information ('PAY300RT', 'DUPLICATE',    $Pay300rt_Duplicate,   #dummy_width)
  do Get_Field_Information ('PAY300RT', 'TRG_ID',       $Pay300rt_Trg_ID,      #dummy_width)
  do Get_Field_Information ('PAY300RT', 'TRG_SEQ',      $Pay300rt_Trg_Seq,     #dummy_width)
  do Get_Field_Information ('PAY300RT', 'TRG_EFFDT',    $Pay300rt_Trg_Effdt,   #dummy_width)
  do Get_Field_Information ('PAY300RT', 'FIELD',        $Pay300rt_Field,       #dummy_width)
  do Get_Field_Information ('PAY300RT', 'OLD_VALUE',    $Pay300rt_Old_Value,   #dummy_width)
  do Get_Field_Information ('PAY300RT', 'NEW_VALUE',    $Pay300rt_New_Value,   #dummy_width)
  do Get_Field_Information ('PAY300RT', 'TRG_VALUE',    $Pay300rt_Trg_Value,   #dummy_width)
  do Get_Field_Information ('PAY300RT', 'TRG_ACTION',   $Pay300rt_Trg_Action,  #dummy_width)
  do Get_Field_Information ('PAY300RT', 'TRG_STATUS',   $Pay300rt_Trg_Status,  #dummy_width)
  do Get_Field_Information ('PAY300RT', 'NO_DATA',      $Pay300rt_No_Data,     #dummy_width)

end-procedure



!*********************   M A I N   P R O C E S S   *********************

begin-procedure Process-Main

begin-SELECT

J.COMPANY
R.RETROPAY_RQST_TYPE

    if &R.RETROPAY_RQST_TYPE = 'A'
        move $Pay300rt_Rqst_Type_A to $Rqst-Descr
    end-if
    if &R.RETROPAY_RQST_TYPE = 'J'
        move $Pay300rt_Rqst_Type_J to $Rqst-Descr
    end-if
    if &R.RETROPAY_RQST_TYPE = 'M'
        move $Pay300rt_Rqst_Type_M to $Rqst-Descr
    end-if

R.MASS_RETRO_RQST_ID
R.RETROPAY_SEQ_NO
R.EMPLID
R.EMPL_RCD
N.NAME
N.LAST_NAME
N.FIRST_NAME
J.EMPL_STATUS

    move 'EMPL_STATUS'  to $FieldName
    move &J.Empl_Status to $FieldValue
    do Read-Translate-Table
    let $Empl_Status = $XlatShortName

J.PAYGROUP
R.RETROPAY_PRCS_FLAG

    move 'RETROPAY_PRCS_FLAG'  to $FieldName
    move &R.Retropay_Prcs_Flag to $FieldValue
    do Read-Translate-Table
    let $Prcs_Flag = $XlatShortName

R.RETROPAY_EFFDT
R.PRCS_END_DT
R.RETROPAY_PGM_ID
R.RETRO_TRG_PGM_ID
R.DUPLICATE_FLAG

  if $PrimSort = 'Y'
     if &R.RETROPAY_RQST_TYPE <> $Prev_Rqst
        if $Prev_Rqst <> ' '
            new-page
        end-if

        move &R.RETROPAY_RQST_TYPE to $Prev_Rqst
        move &J.COMPANY            to $Prev_Company
     end-if
  end-if

  if &J.COMPANY <> $Prev_Company
     if $Prev_Company <> ' '
        new-page
     end-if

     move &J.COMPANY to $Prev_Company
  end-if

  do Print-Employee-Info

  move 'Y' to $DataFound

FROM PS_RETROPAY_RQST R,
     PS_JOB J,
     PS_PERSON_NAME N

WHERE [$Rqst_Type_Where]
      [$Prcs_Flag_Where]
      [$Company_Where]
      [$Paygroup_Where]
  AND J.EMPLID = R.EMPLID
  AND J.EMPL_RCD = R.EMPL_RCD
  AND J.EFFDT = (SELECT MAX(EFFDT)  FROM PS_JOB
                 WHERE EMPLID   = J.EMPLID
                   AND EMPL_RCD = J.EMPL_RCD
                   AND EFFDT  <= R.RETROPAY_EFFDT)
  AND J.EFFSEQ = (SELECT MAX(EFFSEQ)  FROM PS_JOB
                  WHERE EMPLID = J.EMPLID
                    AND EMPL_RCD = J.EMPL_RCD
                    AND EFFDT  = J.EFFDT)
  AND N.EMPLID = R.EMPLID

ORDER BY [$Sort_Order1]
         [$Sort_Order2]

end-SELECT

  if $DataFound = 'N'
    print $Pay300rt_No_Data        (+2,) center
  end-if

end-procedure


!**************  P R I N T   E M P L O Y E E   I N F O  ****************

begin-procedure Print-Employee-Info

  move  &J.COMPANY to $Company       !for the Heading
  print $Rqst-Descr                (+2,3)
  print &R.MASS_RETRO_RQST_ID       (0,12)
  print &R.RETROPAY_SEQ_NO          (0,21)
  print &R.EMPLID                   (0,38)
  print &R.EMPL_RCD                 (0,52)
  print &N.NAME                     (0,57,35)
  print $Empl_Status                (0,93)
  print &J.PAYGROUP                 (0,105)
  print $Prcs_Flag                  (0,111)

  do Format-DateTime(&R.RETROPAY_EFFDT, $out, {DEFMDY}, '', '')
  print $out                        (0,124,10)

  do Format-DateTime(&R.PRCS_END_DT, $out, {DEFMDY}, '', '')
  print $out                        (0,136,10)

  print &R.RETROPAY_PGM_ID          (0,150)
  print &R.RETRO_TRG_PGM_ID         (0,161)
  print &R.DUPLICATE_FLAG           (0,172)


  if $Trigger = 'I' and $RqstType <> 'M'
     do Get-Trigger-Detail
     print '-'                     (+1,1,174) fill
  end-if

end-procedure


!***************  G E T   T R I G G E R   D E T A I L  *****************

begin-procedure Get-Trigger-Detail

begin-SELECT

T.RETRO_TRG_EVENT_ID
T.SEQNUM
T.FIELDNAME
T.TRGR_FLD_PRIOR_VAL
T.TRGR_FLD_VAL_CHAR
T.NONTRIG_FLD_VAL
T.TRIGGER_ACTION
T.TRIGGER_STATUS
D.EFFDT_USED

  move 'TRIGGER_ACTION'  to $FieldName
  move &T.TRIGGER_ACTION to $FieldValue
  do Read-Translate-Table
  let $TriggerAction = $XlatShortName

  move 'TRIGGER_STATUS'  to $FieldName
  move &T.TRIGGER_STATUS to $FieldValue
  do Read-Translate-Table
  let $TriggerStatus = $XlatShortName

  print &T.RETRO_TRG_EVENT_ID      (+2,7)
  print &T.SEQNUM                   (0,19)

  do Format-DateTime(&D.EFFDT_USED, $out, {DEFMDY}, '', '')
  print $out                        (0,27,10)

  print &T.FIELDNAME                (0,40)
  print &T.TRGR_FLD_PRIOR_VAL       (0,61)
  print &T.TRGR_FLD_VAL_CHAR        (0,89)
  print &T.NONTRIG_FLD_VAL          (0,117)
  print $TriggerAction              (0,145)
  print $TriggerStatus              (0,157)

FROM PS_RETROPAY_TRGVAL T, PS_RETROPAY_TRGDTL D
WHERE T.RETROPAY_SEQ_NO =  &R.RETROPAY_SEQ_NO
AND   T.EMPLID   = &R.EMPLID
AND   T.RETROPAY_SEQ_NO = D.RETROPAY_SEQ_NO
AND   T.EMPLID   = D.EMPLID
AND   T.RETROPAY_EFFDT = D.RETROPAY_EFFDT
AND   T.RETRO_TRG_EVENT_ID = D.RETRO_TRG_EVENT_ID
AND   T.SEQNUM   = D.SEQNUM
AND   T.RECNAME  = D.RECNAME
AND   T.ERNCD    = D.ERNCD

end-SELECT

end-procedure


!***************  B U I L D   W H E R E   C L A U S E  *****************

begin-procedure Build-Where-Clause


  let $W1  = 'R.RETROPAY_RQST_TYPE = '''
  let $W2  = $Rqst-TypeA
  let $W3  = $Rqst-TypeJ
  let $W4  = $Rqst-TypeM
  let $W5  = ''' AND R.MASS_RETRO_RQST_ID = '''
  let $W6  = $Mass_ID
  let $W7  = ''''
  let $W8  = '(R.RETROPAY_RQST_TYPE = '''
  let $W9  = ''' OR R.RETROPAY_RQST_TYPE = '''
  let $W10 = ''')'
  let $W11 = 'AND R.RETROPAY_PRCS_FLAG = '''
  let $W12 = $PrcsFlag
  let $W13 =  'AND J.COMPANY = '''
  let $W14 = $PromptCompany
  let $W15 = 'AND J.PAYGROUP = '''
  let $W16 = $PromptPaygroup

  if $RqstType = 'A'
     let $Rqst_Type_Where = $W1 || $W2 ||$W7
  end-if

  if $RqstType = 'J'
     let $Rqst_Type_Where = $W1 || $W3 || $W7
  end-if

  if $RqstType = 'M'
     let $Rqst_Type_Where = $W1 || $W4 || $W5 || $W6 || $W7
  end-if

  if $RqstType = 'Z'
     let $Rqst_Type_Where = $W8 || $W2 || $W9 || $W3 || $W9 || $W4 || $W10
  end-if

  if $PrcsFlag = 'Z'
     let $Prcs_Flag_Where = ' '
  else
     let $Prcs_Flag_Where =  $W11 || $W12 || $W7
  end-if

  if $PromptCompany = ''
      let $Company_Where = ''
  else
      let $Company_Where = $W13 || $W14 || $W7
  end-if

  if $PromptPaygroup = ''
     let $Paygroup_Where = ''
  else
     let $Paygroup_Where = $W15 || $W16 || $W7
  end-if

end-procedure



!***************  B U I L D   O R D E R   C L A U S E  *****************

begin-procedure Build-Order-Clause

  let $O0  = 'R.RETROPAY_RQST_TYPE, '
  let $O1  = 'J.COMPANY, R.EMPLID, R.EMPL_RCD, R.RETROPAY_EFFDT'
  let $O2  = 'J.COMPANY, N.LAST_NAME, N.FIRST_NAME, R.EMPLID, R.EMPL_RCD, R.RETROPAY_EFFDT'
  let $O3  = 'J.COMPANY, J.PAYGROUP, R.EMPLID, R.EMPL_RCD, R.RETROPAY_EFFDT'
  let $O4  = 'J.COMPANY, R.RETROPAY_SEQ_NO, R.EMPLID, R.EMPL_RCD'

  if $PrimSort = 'Y'
     let $Sort_Order1 = $O0
  else
     let $Sort_Order1 = ' '
  end-if

  if $SortOption = '1'
     let $Sort_Order2 = $O1
  end-if

  if $SortOption = '2'
     let $Sort_Order2 = $O2
  end-if

  if $SortOption = '3'
     let $Sort_Order2 = $O3
  end-if

  if $SortOption = '4'
     let $Sort_Order2 = $O4
  end-if

end-procedure



!***********************************************************************

#Include 'payrtrpt.sqc'  !Custom Run Control Parameters
#Include 'getpgdta.sqc'  !Get-PayGroup-Data procedure
#Include 'getcodta.sqc'  !Get-Company-Data procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
#Include 'readxlat.sqc'  !Read-Translate-Table procedure

