!***********************************************************************
!  PER105CN: Workforce Survey Statistics
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:13:58:56                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
Begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup


#define ActiveEmp  ('A')

!***********************************************************************
! BEGIN-REPORT
!***********************************************************************
begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Report
  do Reset
  do Stdapi-Term
end-report


!***********************************************************************
! BEGIN-HEADING
!***********************************************************************
begin-heading 4
  #Include 'stdhdg03.sqc'

end-heading

!***********************************************************************
! BEGIN-PROCEDURE Init-Report
!***********************************************************************
begin-procedure Init-Report

   move 'PER105CN' to $ReportID
   move 'Workforce Survey Statistics' to $ReportTitle
   display $ReportTitle

   do Stdapi-Init

   if $prcs_process_instance = ''
      do Ask-As-Of-Date
      do Ask-AreaCd($AreaList,#Dummy)
   else
      do Select-Parameters
   end-if

end-procedure

!***********************************************************************
! BEGIN-PROCEDURE Get-Values
!***********************************************************************
begin-procedure Get-Values

   do Get-As-Of-Date
   do Get-Can-OEE-Parameters

end-procedure

!***********************************************************************
! BEGIN-PROCEDURE Report
!***********************************************************************
begin-procedure Report

begin-SELECT
C.CAN_OEE_AREACD
A.COMPLETED
A.EMPLID

   if &C.Can_OEE_AreaCd <> $Area
      if not isnull($Area)
         let $FieldName = 'CAN_OEE_AREACD'
         let $FieldValue = $Area
         do Read-Translate-Table
         print $XlatLongName              (+2,2)
         print 'Total Surveys submitted:' (+2,47,26)
         print #Total                     ( 0,74) edit '99999'
         print 'Total Surveys completed:' (+2,47,26)
         print #Completed                 ( 0,74) edit '99999'
         let #Total = 0
         let #Completed = 0
      end-if
      let $Area = &C.Can_OEE_AreaCd
   end-if

   add 1 to #Total
   if &A.Completed = 'Y'
      add 1 to #Completed
   end-if

FROM PS_CAN_OEE_SURVEY A,
     PS_JOB B,
     PS_LOCATION_TBL C

WHERE A.RECEIVEDT = (SELECT MAX(A1.RECEIVEDT) FROM PS_CAN_OEE_SURVEY A1
        WHERE A1.EMPLID = A.EMPLID
          AND A1.RECEIVEDT <= $AsOfDate)
      AND B.EMPLID = A.EMPLID
      AND B.EFFDT = (SELECT MAX(B1.EFFDT) FROM PS_JOB B1
        WHERE B1.EMPLID = B.EMPLID
          AND B1.EMPL_RCD = B.EMPL_RCD
          AND B1.EFFDT <= $AsOfDate)
      AND B.EFFSEQ = (SELECT MAX(B2.EFFSEQ) FROM PS_JOB B2
        WHERE B2.EMPLID = B.EMPLID
          AND B2.EFFDT    = B.EFFDT)
      AND B.HR_STATUS IN {ACTIVEEMP}
      AND C.LOCATION = B.LOCATION
      AND C.SETID = (SELECT SETID FROM PS_SET_CNTRL_REC
                     WHERE SETCNTRLVALUE = B.BUSINESS_UNIT
                           AND RECNAME = 'LOCATION_TBL' )
      AND C.EFFDT = (SELECT MAX(C1.EFFDT) FROM PS_LOCATION_TBL C1
        WHERE C1.LOCATION = C.LOCATION
          AND C1.SETID = C.SETID
          AND C1.EFFDT <= $AsOfDate)
      AND C.CAN_OEE_AREACD IN [$AreaList]

ORDER BY C.CAN_OEE_AREACD
end-select

   let $FieldName = 'CAN_OEE_AREACD'
   let $FieldValue = $Area
   do Read-Translate-Table
   print $XlatLongName              (+2,2)
   print 'Total Surveys submitted:' (+2,47,26)
   print #Total                     ( 0,74) edit '99999'
   print 'Total Surveys completed:' (+2,47,26)
   print #Completed                 ( 0,74) edit '99999'

end-procedure


#Include 'hrrnctl1.sqc'  !Get run control parameter values
#Include 'hrgetval.sqc'  !Get values mask routines

#Include 'askaod.sqc'    !Get-As-Of-Date procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'askarea.sqc'   !Get-AreaCd procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'stdapi.sqc'    !Routines to Update Run Status
