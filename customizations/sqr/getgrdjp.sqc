!*************************************************************
!  Getgrdjp:  Get Grade Name from the Sal Grade Table
!*************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!          $Date:  2013/01/17:13:39:50                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!
!**********************************
begin-procedure Get-Grade-Name
!**********************************

  !Keys passed in:  $SETID_SALARY, $SAL_ADMIN_PLAN, $GRADE, $AsOfDate
  !Values returned: $GradeName, $GradeNameAbbrv

  move '' to $GradeName
  move '' to $GradeNameAbbrv

  if Rtrim($GRADE,' ') = ''
    goto GetGradeExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

  let $sql-statement = 'An error occurred in reading Salary Grade Table(SAL_GRADE_TBL).'

begin-SELECT  ON-ERROR=SQL-Error
GRD.DESCR
GRD.DESCRSHORT

  let $GradeName      = Rtrim(&GRD.DESCR,' ')
  let $GradeNameAbbrv = Rtrim(&GRD.DESCRSHORT,' ')

FROM  PS_SAL_GRADE_TBL GRD
WHERE GRD.SETID          = $SETID_SALARY
  AND GRD.SAL_ADMIN_PLAN = $SAL_ADMIN_PLAN
  AND GRD.GRADE          = $GRADE
  AND GRD.EFFDT          = (SELECT MAX(EFFDT) FROM   PS_SAL_GRADE_TBL
                            WHERE SETID          = GRD.SETID
                              AND SAL_ADMIN_PLAN = GRD.SAL_ADMIN_PLAN
                              AND GRADE          = GRD.GRADE
                              AND EFFDT         <= $AsOfDate)
end-SELECT

  do Get-Grade-Name-Lang

GetGradeExit:

end-procedure

!***************************************
begin-procedure Get-Grade-Name-Lang
!***************************************

  !Keys passed in:  $GRADE, $AsOfDate
  !Values returned: $GradeName, $GradeNameAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetGradeLngExit
  end-if

  if Rtrim($GRADE,' ') = ''
    goto GetGradeLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

  let $sql-statement = 'An error occurred in reading Salary Grade Language Table(SAL_GRADE_LANG).'

begin-SELECT  ON-ERROR=SQL-Error
GRDL.DESCR
GRDL.DESCRSHORT

  if Rtrim(&GRDL.DESCR,' ') <> ''
    let $GradeName = Rtrim(&GRDL.DESCR,' ')
  end-if
  if Rtrim(&GRDL.DESCRSHORT,' ') <> ''
    let $GradeNameAbbrv = Rtrim(&GRDL.DESCRSHORT,' ')
  end-if

FROM  PS_SAL_GRADE_LANG GRDL
WHERE GRDL.SETID          = $SETID_SALARY
  AND GRDL.SAL_ADMIN_PLAN = $SAL_ADMIN_PLAN
  AND GRDL.GRADE          = $GRADE
  AND GRDL.LANGUAGE_CD    = $Curr_Language_Cd
  AND GRDL.EFFDT          =
        (SELECT MAX(EFFDT)
         FROM  PS_SAL_GRADE_LANG
         WHERE SETID          = GRDL.SETID
           AND SAL_ADMIN_PLAN = GRDL.SAL_ADMIN_PLAN
           AND GRADE          = GRDL.GRADE
           AND LANGUAGE_CD    = GRDL.LANGUAGE_CD
           AND EFFDT         <= $AsOfDate)
end-SELECT

GetGradeLngExit:

end-procedure
!***************************** End of Include File **************************
