!***********************************************************************
! GPRNCTL1.SQC:  Retrieve Run Control for the parameters of the
!                Global Payroll Result Register Report
!   This SQC is used to pull run parms entered by the user.
!   Therse are used by calling SQR to determine the content of the
!   report.
!   It uses several sub-procedures to string paygroups or employees
!   selected by the user on the run control page.
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!          $Date:  2013/01/18:04:11:18                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!***********************************************************************


!***********************************************************************
! Select-Parameters                                                    *
!                                                                      *
! Calls:        Select-Paygroups                                       *
!               Select-Employees                                       *
!                                                                      *
! Description:  Selects level zero information off of run control.     *
!               Calls the appropriate sub-procedure, if necessary,     *
!               to string the selected paygroup or employee data.      *
!***********************************************************************
begin-procedure Select-Parameters


#ifdef debugy
    show 'Entering GPRUNCTL1.SQC'
    show 'IN PROCEDURE SELECT-PARAMETERS'
#end-if


    let $Quote                  = ''''
    let $Multiple_Calendars     = 'N'
    let $CalendarID             = ' '
    let $EE_Select_Clause       = ' '
    let $Employee_List          = ' '
    let $Paygroup_Select_Clause = ' '


#ifdef debugy
    show 'Run control select variables initialized.'
#end-if



begin-SELECT


#ifdef debugy
    show 'BEGINNING RUNCONTROL SELECT'
#end-if


LVL0.OPRID                    &LVL0.OPRID
LVL0.RUN_CNTL_ID              &LVL0.RUN_CNTL_ID
LVL0.CAL_RUN_ID               &LVL0.CAL_RUN_ID
LVL0.RUN_OPTIONS              &LVL0.RUN_OPTIONS
LVL0.EE_LVL_SORT_OPTION       &LVL0.EE_LVL_SORT_OPTION
LVL0.PYGRP_LIST_OPTION        &LVL0.PYGRP_LIST_OPTION

    let $Oprid                = &LVL0.OPRID
    let $Run_Cntl_ID          = &LVL0.RUN_CNTL_ID
    let $Cal_Run_ID           = &LVL0.CAL_RUN_ID
    let #Run_Sections         = &LVL0.RUN_OPTIONS
    let #EE_Lvl_Sort          = &LVL0.EE_LVL_SORT_OPTION
    let #Population           = &LVL0.PYGRP_LIST_OPTION

#ifdef debugy
    show '#Population:   ' noline
    show #Population
#end-if

    let $Run_Sections         = edit(#Run_Sections, '88')
    let $EE_Lvl_Sort          = edit(#EE_Lvl_Sort, '88')
    let $Population           = edit(#Population, '8')


#ifdef debugz
    show 'Run Control Select Oprid       :   ' noline
    show $Oprid
    show 'Run Control Select Run_Cntl_ID :   ' noline
    show $Run_Cntl_ID
    show 'Run Control Calendar Group     :   ' noline
    show $Cal_Run_ID
    show 'Run Control Reporting Option   :   ' noline
    show $Run_Sections
    show 'Run Control Payee Sort Order   :   ' noline
    show $EE_Lvl_Sort
    show 'Run Control Population         :   ' noline
    show $Population
#end-if


    if $Population = '2'
        do Select-Paygroups
    end-if

    if $Population = '3'
        do Select-Employees
    end-if


    FROM PS_GP_RUNCTL_SQR LVL0

    WHERE LVL0.OPRID        = $PRCS_OPRID
      AND LVL0.RUN_CNTL_ID  = $PRCS_RUN_CNTL_ID

END-SELECT


end-procedure ! Select-Parameters


!***********************************************************************
!                                                                      *
! Select-Paygroups                                                     *
!                                                                      *
!                                                                      *
! Calls:        none                                                   *
!                                                                      *
! Description:  Selects paygroups off run control.  Strings them into  *
!                   a variable or use in the main select clause in     *
!                   the calling report.                                *
!                                                                      *
!***********************************************************************


begin-procedure Select-Paygroups


    let $Multiple = 'N'

begin-SELECT

LVL1A.OPRID           &LVL1A.OPRID
LVL1A.RUN_CNTL_ID     &LVL1A.RUN_CNTL_ID
LVL1A.GP_PAYGROUP     &LVL1A.GP_PAYGROUP


    let $PaygroupID = &LVL1A.GP_PAYGROUP


!***********************************************************************
!                                                                      *
!  Note the difference between the condition of which report is being  *
!  called.  The difference is in the WHERE clause that is created.     *
!  Different reports calling this SQC do selects from different        *
!  tables.                                                             *
!                                                                      *
!***********************************************************************


    if $ReportID = 'GP000001' or $ReportID = 'GP000003'
        if $Multiple = 'N'
            let $PaygroupID         = $Quote || $PaygroupID || $Quote
            let $Paygroup_Select_Clause  = 'AND SEGSTAT.GP_PAYGROUP IN (' || $PaygroupID || ')'
            let $Paygroup_List      = $PaygroupID

                #ifdef debugz
                    show 'Run Control Select $PaygroupID:         ' noline
                    show $PaygroupID
                    show 'Run Control Select $Paygroup_Select_Clause:  ' noline
                    show $Paygroup_Select_Clause
                #end-if


        else
            let $Paygroup_List      = $Paygroup_List || ', ' || $Quote || $PaygroupID || $Quote
            let $Paygroup_Select_Clause  = 'AND SEGSTAT.GP_PAYGROUP IN (' || $Paygroup_List || ')'

                #ifdef debugz
                    show 'Run Control Select $Paygroup_List:      ' noline
                    show $Paygroup_List
                    show 'Run Control Select $Paygroup_Select_Clause:  ' noline
                    show $Paygroup_Select_Clause
                #end-if

        end-if
    end-if


    if $ReportID = 'GP000002'
        if $Multiple = 'N'
            let $PaygroupID         = $Quote || $PaygroupID || $Quote
            let $Paygroup_Select_Clause  = 'AND A.GP_PAYGROUP IN (' || $PaygroupID || ')'
            let $Paygroup_List      = $PaygroupID

                #ifdef debugz
                    show 'Run Control Select $PaygroupID:         ' noline
                    show $PaygroupID
                    show 'Run Control Select $Paygroup_Select_Clause:  ' noline
                    show $Paygroup_Select_Clause
                #end-if


        else
            let $Paygroup_List      = $Paygroup_List || ', ' || $Quote || $PaygroupID || $Quote
            let $Paygroup_Select_Clause  = 'AND A.GP_PAYGROUP IN (' || $Paygroup_List || ')'

                #ifdef debugz
                    show 'Run Control Select $Paygroup_List:      ' noline
                    show $Paygroup_List
                    show 'Run Control Select $Paygroup_Select_Clause:  ' noline
                    show $Paygroup_Select_Clause
                #end-if

        end-if
    end-if



    let $Multiple = 'Y'


    FROM PS_GP_RUNCTL_SQR01 LVL1A

    WHERE OPRID        = $Oprid
      AND RUN_CNTL_ID  = $Run_Cntl_ID


end-SELECT


end-procedure ! Select-Paygroups


!***********************************************************************
!                                                                      *
! Select-Employees                                                     *
!                                                                      *
!                                                                      *
! Calls:        none                                                   *
!                                                                      *
! Description:  Selects employees off run control.  Strings them into  *
!                   a variable for use in the main select clause in    *
!                   the calling report.                                *
!                                                                      *
!***********************************************************************


begin-procedure Select-Employees


    #ifdef debugz
        show 'In procedure Select-Employees'
    #end-if


    let $Multiple = 'N'     ! Controls the stringing format for the Select_Clause.



begin-SELECT


    #ifdef debugz
        show 'In SELECT, procedure Select-Employees'
    #end-if


LVL1B.OPRID                 &LVL1B.OPRID
LVL1B.RUN_CNTL_ID           &LVL1B.RUN_CNTL_ID
LVL1B.EMPLID                &LVL1B.EMPLID


    let $Runctl_Emplid = &LVL1B.EMPLID


!***********************************************************************
!                                                                      *
!  Note the difference between the condition of which report is being  *
!  called.  The difference is in the WHERE clause that is created.     *
!  Different reports calling this SQC do selects from different        *
!  tables.                                                             *
!                                                                      *
!***********************************************************************


    if $ReportID = 'GP000001' or $ReportID = 'GP000003'
        if $Multiple = 'N'
            let $Employee_List = $Quote||$Runctl_Emplid||$Quote
            let $EE_Select_Clause  = 'AND SEGSTAT.EMPLID IN ('||$Employee_List||')'

                #ifdef debugz
                    show 'Run Control Select $Employee_List:      ' noline
                    show $Employee_List
                    show 'Run Control Select $EE_Select_Clause:  ' noline
                    show $EE_Select_Clause
                #end-if

        else
            let $Employee_List = $Employee_List||', '||$Quote||$Runctl_Emplid||$Quote
            let $EE_Select_Clause  = 'AND SEGSTAT.EMPLID IN ('||$Employee_List||')'

                #ifdef debugz
                    show 'Run Control Select $Employee_List:      ' noline
                    show $Employee_List
                    show 'Run Control Select $EE_Select_Clause:  ' noline
                    show $EE_Select_Clause
                #end-if

        end-if
    end-if


    if $ReportID = 'GP000002'
        if $Multiple = 'N'
            let $Employee_List = $Quote||$Runctl_Emplid||$Quote
            let $EE_Select_Clause  = 'AND A.EMPLID IN ('||$Employee_List||')'

                #ifdef debugz
                    show 'Run Control Select $Employee_List:      ' noline
                    show $Employee_List
                    show 'Run Control Select $EE_Select_Clause:  ' noline
                    show $EE_Select_Clause
                #end-if

        else
            let $Employee_List = $Employee_List||', '||$Quote||$Runctl_Emplid||$Quote
            let $EE_Select_Clause  = 'AND A.EMPLID IN ('||$Employee_List||')'

                #ifdef debugz
                    show 'Run Control Select $Employee_List:      ' noline
                    show $Employee_List
                    show 'Run Control Select $EE_Select_Clause:  ' noline
                    show $EE_Select_Clause
                #end-if

        end-if
    end-if


    let $Multiple = 'Y'


    FROM PS_GP_RUNCTL_SQR02       LVL1B

    WHERE LVL1B.OPRID        = $Oprid
      AND LVL1B.RUN_CNTL_ID  = $Run_Cntl_ID


end-SELECT

end-procedure ! Select-Employees
