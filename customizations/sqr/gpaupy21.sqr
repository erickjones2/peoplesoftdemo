!***********************************************************************
!  GPAUPY21 :  List PSM Exceptions - discrepencies between             *
!              accumulator value and  value of Earnings and Deductions *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/06/14:10:46:08                                 
!       $Release:  HR92                                                
!      $Revision:  105                                                 
!                                                                      *
!***********************************************************************
!                                                                      *
! Modification History                                                 *
! Dated      Author  ICE       Changes                                 *
! 17/12/02   KWH    539412000  Empl Rcd removed, headings aligned &    *
!                              "End of Report" msg moved to after Pay  *
!                              Entity totals                           *
!                                                                      *
!***********************************************************************


#Include 'setenv.sqc' !Set environment

#define Emplid_Pos      1
#define Name_Pos        12
#define Occur_Pos       42
#define Bal_Id_Pos      50
#define Category_Pos    58
#define Elem_Name_Pos   80
#define Calc_Rsl_Pos    100
#define YTD_Accum_Pos   120
#define Diff_Pos        140

begin-report
  move 'GPAUPY21' to $ReportID
  move 'Payment Summary Exception Report' to $ReportTitle
  display $ReportTitle
  display ' '


  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime

  do Stdapi-Init

  do Init-Report
  do Report
  do Prn-Pay-Entity-Total

  print '** End of Report **'  (+2,70)

  do Stdapi-Term
end-report


#Include 'setup02.sqc'                 ! Printer and page-size initialization

begin-heading 8
#Include 'stdhdg01.sqc'

  print 'Tax Year: '      (+1,1)
  print #Tax_Year         (0,16)  edit 9999
  print 'Pay Entity:   '  (+1,1)
  print $Pay_Entity       (0,16)
  print $Pay_Entity_Nm    (0,26) wrap 60 4

  print 'Emplid '                               (+2,{Emplid_Pos}
  print 'Employee Name'                         (0,{Name_Pos})
  print 'Bal Id'                                (0,{Bal_Id_Pos})
  print 'Category'                              (0,{Category_Pos})
  print 'Element Name'                          (0,{Elem_Name_Pos})
  print '          Calc Rslt'                   (0,{Calc_Rsl_Pos})
  print '          YTD Accum'                   (0,{YTD_Accum_Pos})
  print '        Difference'                    (0,{Diff_Pos})
 
  print '-'                                     (+1,1,176)       fill
end-heading


!***********************************************************************
! Procedure: Init-Report                                               *
!            Initialises certain variables and selects parameters.     *
!***********************************************************************
begin-procedure Init-Report

  do Select-Parameters
 
  if &RC.GPAU_PSM_PAYEE_SW = 'Y'
     let $Order_By = 'ORDER BY A.PAY_ENTITY , C.NAME, A.SOVR_VAL_CHAR'  
  else
     let $Order_By = 'ORDER BY A.PAY_ENTITY , A.EMPLID, A.SOVR_VAL_CHAR' 
  end-if   

end-procedure



!***********************************************************************
! Procedure: Report                                                    *
!            The main procedure in the report.                         *
!            Process records from GPAU_PSM_EXCEPT table                *
!***********************************************************************
begin-procedure Report

  let $Old_Emplid     = ' '
  let #Old_Empl_Rcd   = 0
  let $Old_Bal_Grp_Num = ' ' 
  let $Old_Categ      = ' '
  let $Old_Pay_Entity = ' '
  
begin-select 
B.PAY_ENTITY 
D.GPAU_REGISTERED_NM

   if &B.PAY_ENTITY <> $Old_Pay_Entity
      if $Old_Pay_Entity <> ' '
         let $Pay_Entity = $Old_Pay_Entity
         do Prn-Pay-Entity-Total
         new-page
      end-if
      let $Pay_Entity = &B.PAY_ENTITY
   end-if
   let $Old_Pay_Entity = &B.PAY_ENTITY
   let $Pay_Entity_Nm=&D.GPAU_REGISTERED_NM
   
   do Generate-Data

FROM PS_GPAU_RC_PSM_PYE B ,PS_GP_PYENT_SGPAU D
     WHERE B.OPRID = &RC.OPRID
       AND B.RUN_CNTL_ID = &RC.RUN_CNTL_ID
       AND D.PAY_ENTITY= B.PAY_ENTITY
End-select
end-procedure !Report

!***********************************************************************
Begin-procedure Generate-Data
begin-SELECT

A.PAY_ENTITY

A.EMPLID
C.NAME
A.EMPL_RCD
A.BALANCE_GRP_NUM

   if &A.EMPLID  <> $Old_Emplid
      print &A.EMPLID       (+5,{Emplid_Pos})
      print &C.NAME         (0,{Name_Pos})
      print &A.BALANCE_GRP_NUM   (0,{Bal_Id_Pos})
   else
      print ' '             (+1,{Emplid_Pos})
   end-if
      
   if &A.BALANCE_GRP_NUM  <> $Old_Bal_Grp_Num  
    and  &A.EMPLID  = $Old_Emplid  
      print &A.BALANCE_GRP_NUM   (0,{Bal_Id_Pos})
   end-if

   let $Old_Bal_Grp_Num = &A.BALANCE_GRP_NUM
   let $Old_Emplid = &A.EMPLID
   let #Old_Empl_Rcd = &A.EMPL_RCD

A.SOVR_VAL_CHAR
   if &A.SOVR_VAL_CHAR <> $Old_Categ
      let $Sovr_Val_Char = &A.SOVR_VAL_CHAR
      do Get-Category
      print $Category_Name  (0,{Category_Pos})
   end-if
   let $Old_Categ = &A.SOVR_VAL_CHAR

A.GPAU_SRC_PIN_CODE         (0,{Elem_Name_Pos})
A.GPAU_ERN_DED_TOT        
  print &A.GPAU_ERN_DED_TOT (0,{Calc_Rsl_Pos})  EDIT ,999,999,999,999.99 
A.GPAU_ACCUM_TOT          
  print &A.GPAU_ACCUM_TOT   (0,{YTD_Accum_Pos}) EDIT ,999,999,999,999.99 

  let #Difference = &A.GPAU_ERN_DED_TOT -  &A.GPAU_ACCUM_TOT
  print #Difference         (0,{Diff_Pos})      EDIT 999,999,999,999.99 
 

FROM  PS_GPAU_PSM_EXCEPT A,
      PS_NAMES C

WHERE  A.PAY_ENTITY  = $Pay_Entity
AND C.EMPLID = A.EMPLID

    AND C.NAME_TYPE = 'PRI'
    AND C.EFFDT = (SELECT MAX(C1.EFFDT) 
        FROM PS_NAMES C1
        WHERE C1.EMPLID = C.EMPLID)
[$Order_By]

end-SELECT

end-procedure !Generate-Data



!***********************************************************************
! Procedure: Get-Category                                              *
!            Set up Category Description.                              *
!***********************************************************************
begin-procedure  Get-Category

  let $Sovr_Val_Char = rtrim($Sovr_Val_Char,' ')
  Evaluate $Sovr_Val_Char
    When = 'A'
      let $Category_Name = 'Allowances'
    When = 'C'
      let $Category_Name = 'CDEP Salary or Wages'
    When = 'F'
      let $Category_Name = 'Reportable FBT'
    When = 'G'
      let $Category_Name = 'Gross Pay'
    When = 'O'
      let $Category_Name = 'Other Income'
    When = 'T'
      let $Category_Name = 'Total Tax Withheld'
    When = 'U'
      let $Category_Name = 'Union Fees'
    When = 'LA'
      let $Category_Name = 'Lump Sum A Payments'
    When = 'LB'
      let $Category_Name = 'Lump Sum B Payments'
    When = 'LD'
      let $Category_Name = 'Lump Sum D Payments'
    When = 'LE'
      let $Category_Name = 'Lump Sum E Payments'
    When = 'E1'
      let $Category_Name = 'ETP Pre 83 Payments'
    When = 'E2'
      let $Category_Name = 'ETP Post 83 Non-Taxed'
    When = 'E3'
      let $Category_Name = 'Post 94 Invalidity'
    When = 'E4'
      let $Category_Name = 'ETP Post 83 Taxed'
    When = 'ET'
      let $Category_Name = 'ETP Tax Withheld'
    When = 'PT'
      let $Category_Name = 'Pre Tax Deduction'
    When = 'E4W'
      let $Category_Name = 'ETP Whole Income Cap'
    When = 'E1W'
      let $Category_Name = 'Non Taxable ETP WIC'
    When = 'ETW'
      let $Category_Name = 'ETP Tax on WIC'


  End-Evaluate

end-procedure


!***********************************************************************
! Procedure: Prn-Pay-Entity-Total                                      *
!            Prints all the Pay Entity totals.                         *
!***********************************************************************
begin-procedure Prn-Pay-Entity-Total

  let $T_Old_Categ = ' '
  let $First_line = 'Y'

begin-SELECT

T.PAY_ENTITY
T.SOVR_VAL_CHAR
T.GPAU_SRC_PIN_CODE
SUM(T.GPAU_ERN_DED_TOT) &Tot_Ern_Ded
SUM(T.GPAU_ACCUM_TOT)   &Tot_Accum

  if $First_line = 'Y'
       print '='                   (+4,1,176)       fill
       print 'Pay Entity Totals '  (+2,1)
  else
     print ' '                   (+1,1)
  end-if
  let $First_line = 'N'

  if &T.SOVR_VAL_CHAR <> $T_Old_Categ
      let $Sovr_Val_Char = &T.SOVR_VAL_CHAR
      do Get-Category
      print $Category_Name  (0,{Category_Pos})
  end-if
  let $T_Old_Categ = &T.SOVR_VAL_CHAR

  print &T.GPAU_SRC_PIN_CODE (0,{Elem_Name_Pos})
       
  print &Tot_Ern_Ded (0,{Calc_Rsl_Pos})   EDIT ,999,999,999,999.99           
  print &Tot_Accum   (0,{YTD_Accum_Pos})  EDIT ,999,999,999,999.99 

  let #Difference = &Tot_Ern_Ded -  &Tot_Accum
  print #Difference  (0,{Diff_Pos})       EDIT 999,999,999,999.99 


FROM  PS_GPAU_PSM_EXCEPT T
WHERE   T.PAY_ENTITY  = $Pay_Entity

GROUP BY T.PAY_ENTITY , T.SOVR_VAL_CHAR, T.GPAU_SRC_PIN_CODE

end-SELECT

end-procedure

!
#Include 'gpaups00.sqc'  ! Select-Parameters procedure
#Include 'curdttim.sqc'  ! Get-Current-DateTime procedure
#Include 'datetime.sqc'  ! Routines for date and time formatting
#Include 'number.sqc'    ! Routines to format numbers
#Include 'stdapi.sqc'    ! Routines to Update Run Status


