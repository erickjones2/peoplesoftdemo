!***********************************************************************
!  GPNZIR02:  IR 345 Report                                            *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!          $Date:  2013/12/06:09:35:05                                 !
!       $Release:  HR92                                                !
!      $Revision:  104                                                 !
!***********************************************************************
! Program Description                                                  *
! -------------------                                                  *
!                                                                      *
! This report produces the IR 345 report for the Pay Entity provided   *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment

!************************************************************************
!  Define Variables                                                     *
!************************************************************************
#define BLANK                       ' '
#define NULL                        ''
#define TRUE                        1
#define FALSE                       0
#define IR345_REPORT                '00'
#define IR346_REPORT                '01'
#define REPORT_ID                   'GPNZIR02'


begin-setup
#include 'setupdb.sqc'   ! Database specific setup
Declare-Layout This_Report
      PAPER-SIZE    = (a4)
      orientation   = portrait
      top-margin    = 0.30
      left-margin   = 0.2
      right-margin   = 0.2
      Line-Height   = 11.3
End-Declare

Declare-Report This_Report
       LayOut=This_Report
End-Declare

End-Setup

!************************************************************************
!  Report Structure                                                     *
!************************************************************************
begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  Let $AsOfDate = $AsOfToday
  do Init-Report
  do Stdapi-Term

end-report


!************************************
!Init-Report                        *
!************************************
begin-procedure Init-Report

  let $ReportID = {REPORT_ID}

  !*** Translations procedures & Strings Table retrievals.
  do Init_Report_Translation ($ReportID, $curr_language_cd)
  do Append_Report_Translation ($ReportID)

  do Get_Field_Information ($ReportID, 'IRD_NUMBER', $IRD_NUMBER_lbl, #DW)
  do Get_Field_Information ($ReportID, 'PERIOD_ENDED', $PERIOD_ENDED_lbl, #DW)
  do Get_Field_Information ($ReportID, 'STUDENT_LOAN_DED', $STUDENT_LOAN_DED_lbl, #DW)
  do Get_Field_Information ($ReportID, 'CHILD_SUPPORT_DED', $CHILD_SUPPORT_DED_lbl, #DW)
  do Get_Field_Information ($ReportID, 'PAYEE_DED', $PAYEE_DED_lbl, #DW)
  do Get_Field_Information ($ReportID, 'INLAND_REVENUE', $INLAND_REV_lbl, #DW)
  do Get_Field_Information ($ReportID, 'TE_TARI_TAAKE', $TE_TARI_lbl, #DW)
  do Get_Field_Information ($ReportID, 'KIWI_DED', $KIWI_DED_lbl, #DW)
  do Get_Field_Information ($ReportID, 'KIWI_ER', $KIWI_ER_lbl, #DW)
  !do Get_Field_Information ($ReportID, 'SSCWT', $SSCWT_lbl, #DW)
  do Get_Field_Information ($ReportID, 'ESCT_LBL_1', $ESCT_lbl_1, #DW)
  do Get_Field_Information ($ReportID, 'ESCT_LBL_2', $ESCT_lbl_2, #DW)
  do Get_Field_Information ($ReportID, 'KIWI_ETC', $KIWI_ETC_lbl, #DW)
  do Get_Field_Information ($ReportID, 'COMP_FUND_ETC', $COMP_FUND_ETC_lbl, #DW)
  do Get_Field_Information ($ReportID, 'TOTAL_ETC', $TOTAL_ETC_lbl, #DW)
  do Get_Field_Information ($ReportID, 'TOT_AMT_PAYABLE', $TOT_AMT_PAYABLE_lbl, #DW)

  do Stdapi-Init
  do Select-Parameters  !get run control parameters
  do Get_Field_Information ($ReportID, 'TOTAL_DED_PYBL_1', $TOTAL_DED_PYBL_lbl, #DW)

  let #bigboxc=6
  let #bigboxr=9
  let #desc1r=10
  let #desc1c=47
  let #num1_col=#desc1c+7
  let #ird1_row=#desc1r
  let #ird1_col=#desc1c+11
  let #ird2_col=#ird1_col+4
  let #ird3_col=#ird1_col+9

  let #desc2r=#desc1r+2
  let #DD_row=#ird1_row+2
  let #DD_col=#desc1c+11
  let #MM_col=#DD_col+4
  let #YYYY_col=#DD_col+8

  let #desc3r=#desc2r+2
  let #desc3c=39
  let #box3r=#desc3r
  let #num3_row=#box3r+1
  let #num3_col=54
  let #num4_row=#num3_row+2
  let #num5_row=#num4_row+2
  let #num6_row=#num5_row+2
  let #num7_row=#num6_row+2
  let #num8_row=#num7_row+2
  let #num9_row=#num8_row+3
  let #num10_row=#num9_row+3
  let #num11_row=#num10_row+2
  let #num12_row=#num11_row+4
  let #num13_row=#num12_row+3

  let #box3_col=#ird1_col
  let #dollar_col = #box3_col - 1
  let #val3_col = #box3_col+2
  let #box3_len=12
  let #box4r=14
  let #box5r=16
  let #box6r=19

  show 'before main'
  if $Run_Report_Type = '00'
    do Process-Main
  else
    show 'Please Select the same report chosen on the Run control page'
  end-if

end-procedure

!************************************************************************
! Select-Parameters                                                     *
! Obtain the running parameters                                         *
!************************************************************************
Begin-Procedure Select-Parameters

!#debuga show 'In Get-Run-Parms'
!let $Error-Message = $ReportID || ', Select-Parameters, PS_GPNZ_RC_IR348, Select'

begin-SELECT
R.PAY_ENTITY
R.GPNZ_YEARCD
R.GPNZ_MONTHCD
R.GPNZ_IR345_6_SEL
R.GPNZ_IR_LATE_PYMT
R.GPNZ_IRD_HALF_SEL

   let $Run_Pay_Entity = rtrim(&R.PAY_ENTITY, ' ')
   let $Run_Year = rtrim(&R.GPNZ_YEARCD, ' ')
   move $Run_Year to #Run_year
   let $Run_Month = rtrim(&R.GPNZ_MONTHCD, ' ')
   move $Run_Month to #Run_month
   let $Run_Report_Type = rtrim(&R.GPNZ_IR345_6_SEL, ' ')
   let #late_pymt_pen = &R.GPNZ_IR_LATE_PYMT
   Let $Half_Month_Sel = &R.GPNZ_IRD_HALF_SEL

FROM PS_GPNZ_RC_IR345_6 R
WHERE R.OPRID = $Prcs_Oprid
AND R.RUN_CNTL_ID = $Prcs_Run_cntl_ID
end-SELECT

do Get-Period-end
!let $IRD_number = '123456789'
do Get-IRD-Number

End-Procedure Select-Parameters


!************************************
!Process-Main                       *
!************************************
begin-procedure Process-Main

do Print-Heading
do Print-EMPLR-Copy
do Print-IRD-Copy
do Print-Pymt-slip
end-procedure ! Process-Main

!************************************
!Print-Header                       *
!************************************
begin-procedure Print-Heading
graphic (1,1,75) box 66  ! line around the page

alter-printer font=5 point-size=12
PRINT $INLAND_REV_lbl (2,#bigboxc)
alter-printer font=32 point-size=10
PRINT $TE_TARI_lbl (3,#bigboxc)
alter-printer font=4 point-size=16
PRINT 'Employer deductions' (5,#bigboxc)
alter-printer font=4 point-size=9
PRINT 'IR 345' (4,70) bold
alter-printer font=4 point-size=5
!if #Run_year >= 2009 and #Run_month >= 04
!or
IF #Run_year >= 2010 and #Run_month >= 01
PRINT 'January 2010'     (5,70) bold
else
PRINT 'April 2009'     (5,70) bold
END-IF
graphic (6,#bigboxc,70) horz-line !line below the heading
alter-printer font=4 point-size=6
print 'Please see notes on the back'  (7,64)
print 'to help you complete this'     (8,66)
print 'form and the EMS schedule'     (9,65)
alter-printer font=32 point-size=6
print 'For more information:'         (11,67)
alter-printer font=4 point-size=6
print 'website'                       (12,66)
print 'www.ird.govt.nz'               (12,69) bold
print 'telephone 0800 377 772'        (13,66)
print 'INFOexpress 0800 257 773'      (14,65) bold

end-procedure ! Print-Header



!********************************************************************
! Get-IRD-Number
! Procedure to extract IRD Number
! called by: Select-Parameters
!********************************************************************
begin-procedure Get-IRD-Number

begin-select
PYENT.PAY_ENTITY
PYENT.EFFDT
PYENT.GPNZ_IRD_NUMBER

    let $IRD_number = &PYENT.GPNZ_IRD_NUMBER

FROM PS_GPNZ_PYENT_DTL PYENT
WHERE PYENT.PAY_ENTITY = $Run_Pay_Entity
AND PYENT.EFFDT = (SELECT MAX(EFFDT) FROM PS_GPNZ_PYENT_DTL
                     WHERE EFFDT <= $period_end_dt
                     AND PAY_ENTITY = PYENT.PAY_ENTITY)
end-select

EXTRACT $IRD_NUM1 FROM $IRD_number 0 2
EXTRACT $IRD_NUM2 FROM $IRD_number 2 3
EXTRACT $IRD_NUM3 FROM $IRD_number 5 4


end-procedure !Get-IRD-Number

!********************************************************************
! Get-Period-end
! Procedure to get Period End date
! called by: Select-Parameters
!********************************************************************
begin-procedure Get-Period-end
let #period_year =0
if $Half_Month_Sel = 'F' or $Half_Month_Sel = '2'
  EVALUATE $Run_month
    WHEN = '02'
      move $Run_year to #period_year
      let #period_year_rem = #period_year % 4
      if #period_year % 4 = 0
        let $period_end_DD='29'
      else
        let $period_end_DD='28'
      end-if
      break
    WHEN = '01'
    WHEN = '03'
    WHEN = '05'
    WHEN = '07'
    WHEN = '08'
    WHEN = '10'
    WHEN = '12'
      LET $period_end_DD='31'
      break
    WHEN-OTHER
      let $period_end_DD='30'
      break
  END-EVALUATE
else
 let $period_end_DD='15'
end-if
LET $period_end_dt_str= $Run_Year||'-'||$run_month||'-'||$period_end_DD

Let $period_end_dt=strtodate($period_end_dt_str,'YYYY-MM-DD')

    if $sqr-database = 'DB2' or $sqr-database = 'INFORMIX'
        Let $period_end_dt = substr($period_end_dt, 1 , 10)
    end-if

end-procedure !Get-Period-end

!********************************************************************
! Print-EMPLR-Copy
! Procedure to print the employers copy
! called by: Process-Main
!********************************************************************
begin-procedure Print-EMPLR-Copy

alter-printer font=4 point-size=6 !reset to default font
print 'Name:' (9,8) wrap 8 2
print 'Address:'(12,8)
Add 6 to #bigboxc
graphic (#bigboxr,#bigboxc,29) BOX 7  ! BIG rectangle
graphic (+1,#bigboxc,29) horz-line
Subtract 6 from #bigboxc  !resetting the variable to original value
let #line1_row = #DD_row + 1


BEGIN-SELECT
HDR.PAY_ENTITY
HDR.GPNZ_YEARCD
HDR.GPNZ_MONTHCD
HDR.GPNZ_PAYE_TAX_1ST
HDR.GPNZ_STUD_LOAN_1ST
HDR.GPNZ_CHILD_SUP_1ST
HDR.GPNZ_PAYE_TAX_2ND
HDR.GPNZ_STUD_LOAN_2ND
HDR.GPNZ_CHILD_SUP_2ND
HDR.GPNZ_TAX_GROSS
HDR.GPNZ_GROSS_NO_EP
HDR.GPNZ_ISSUE_STATUS
HDR.GPNZ_DT_1ST_CREATE
HDR.GPNZ_DT_2ND_CREATE
HDR.GPNZ_DT_IR_ISSUED
HDR.GPNZ_KIWI_EE_1ST
HDR.GPNZ_KIWI_EE_2ND
HDR.GPNZ_KIWI_ER_1ST
HDR.GPNZ_KIWI_ER_2ND
HDR.GPNZ_SSCWT_1ST
HDR.GPNZ_SSCWT_2ND
HDR.GPNZ_KIWI_ETC_1ST
HDR.GPNZ_KIWI_ETC_2ND

  Evaluate $Half_Month_Sel
     WHEN  = '1'
      let #PAYE_ded = &HDR.GPNZ_PAYE_TAX_1ST
      let #child_support = &HDR.GPNZ_CHILD_SUP_1ST
      let #student_loan = &HDR.GPNZ_STUD_LOAN_1ST
      let #kiwi_ded_ee = &HDR.GPNZ_KIWI_EE_1ST
      let #kiwi_ded_er = &HDR.GPNZ_KIWI_ER_1ST
      Let #sscwt   = &HDR.GPNZ_SSCWT_1ST
      Let #kiwi_etc = &HDR.GPNZ_KIWI_ETC_1ST

    WHEN  = '2'
      let #PAYE_ded = &HDR.GPNZ_PAYE_TAX_2ND
      let #child_support = &HDR.GPNZ_CHILD_SUP_2ND
      let #student_loan = &HDR.GPNZ_STUD_LOAN_2ND
      let #kiwi_ded_ee = &HDR.GPNZ_KIWI_EE_2ND
      let #kiwi_ded_er = &HDR.GPNZ_KIWI_ER_2ND
      let #sscwt   = &HDR.GPNZ_SSCWT_2ND
      Let #kiwi_etc = &HDR.GPNZ_KIWI_ETC_2ND

    WHEN  = 'F'
      let #PAYE_ded = &HDR.GPNZ_PAYE_TAX_1ST + &HDR.GPNZ_PAYE_TAX_2ND
      let #child_support = &HDR.GPNZ_CHILD_SUP_1ST + &HDR.GPNZ_CHILD_SUP_2ND
      let #student_loan = &HDR.GPNZ_STUD_LOAN_1ST + &HDR.GPNZ_STUD_LOAN_2ND
      let #kiwi_ded_ee = &HDR.GPNZ_KIWI_EE_1ST + &HDR.GPNZ_KIWI_EE_2ND
      let #kiwi_ded_er = &HDR.GPNZ_KIWI_ER_1ST + &HDR.GPNZ_KIWI_ER_2ND
      let #sscwt   = &HDR.GPNZ_SSCWT_1ST + &HDR.GPNZ_SSCWT_2ND
      Let #kiwi_etc = &HDR.GPNZ_KIWI_ETC_1ST + &HDR.GPNZ_KIWI_ETC_2ND
  End-Evaluate

from PS_GPNZ_IRD_HEADER HDR
WHERE HDR.PAY_ENTITY=$Run_Pay_Entity
AND HDR.GPNZ_YEARCD=$Run_Year
AND HDR.GPNZ_MONTHCD=$Run_Month
END-SELECT

let #total_ded = #PAYE_ded + #child_support + #student_loan + #kiwi_ded_ee + #kiwi_ded_er + #sscwt
let #amt_payable = #total_ded - #kiwi_etc

end-procedure ! Print-EmplR-COPY

!*********************************************************************
!Print-IRD-Copy
!Prints the IRD copy
!*********************************************************************
begin-procedure Print-IRD-Copy
  LET #ER_IRD_DIF=6
  graphic (20,#bigboxc,69) box 46 0 4  ! print the gray box
  graphic (20,#bigboxc,69) box 46 0    ! bordering the gray box

Add #ER_IRD_DIF to #ird1_row

GRAPHIC (#ird1_row,#ird1_col,3) BOX 1 ! box for IRD1
GRAPHIC (,#ird2_col,4) BOX 1          ! box for IRD2
GRAPHIC (,#ird3_col,5) BOX 1          ! box for IRD3

Add #ER_IRD_DIF to #DD_row

GRAPHIC (#DD_row,#DD_col,3) BOX 1 ! box for DD
GRAPHIC (,#MM_col,3) BOX 1        ! box for MM
GRAPHIC (,#YYYY_col,5) BOX 1      ! box for YYYY

!*****PRINTING THE DESCRIPTIONS
alter-printer font=4 point-size=7 !reset to default font
Add #ER_IRD_DIF to #desc1r
PRINT $IRD_NUMBER_lbl (#desc1r,#desc1c)
Add #ER_IRD_DIF to #desc2r
PRINT $PERIOD_ENDED_lbl (#desc2r,#desc1c)
Add #ER_IRD_DIF to #desc3r
print 'If your postal address is different from that printed above, please enter your new address below.' (#desc3r,7) wrap 53 2

! Printing the lables of all the deductions.
PRINT $PAYEE_DED_lbl (#desc3r,#desc3c) wrap 25 2
PRINT $CHILD_SUPPORT_DED_lbl (+2,#desc3c)
PRINT $STUDENT_LOAN_DED_lbl (+2,#desc3c)
PRINT $KIWI_DED_lbl (+2,#desc3c)
PRINT $KIWI_ER_lbl (+2,#desc3c)
!PRINT $SSCWT_lbl    (+2,#desc3c)
!GRAPHIC (+1,#desc3c,35) HORZ-LINE
PRINT $ESCT_lbl_1    (+2,#desc3c)
PRINT $ESCT_lbl_2    (+1,#desc3c)
GRAPHIC (,#desc3c,35) HORZ-LINE
PRINT $TOTAL_DED_PYBL_lbl (+2,#desc3c)
if #Run_year >= 2009 and #Run_month >= 04
or
#Run_year >= 2010 and #Run_month >= 01
PRINT 'This is the amount you need ' (+1,#desc3c)
PRINT 'to pay.' (+1,#desc3c)
end-if
GRAPHIC (+1,#desc3c,35) HORZ-LINE

if #Run_year >= 2009 and #Run_month >= 04
or
#Run_year >= 2010 and #Run_month >= 01
else
PRINT $KIWI_ETC_lbl (+1,#desc3c) Wrap 25 2
PRINT $COMP_FUND_ETC_lbl (+2,#desc3c) Wrap 30 2
GRAPHIC (+1,#desc3c,35) HORZ-LINE
PRINT $TOTAL_ETC_lbl (+1,#desc3c) Wrap 30 2
GRAPHIC (+1,#desc3c,35) HORZ-LINE
PRINT $TOT_AMT_PAYABLE_lbl (+1,#desc3c) Wrap 25 3
end-if


Let #Desc3_line_row = #desc3r - 1
graphic (#Desc3_line_row,#DESC3c,35) HORZ-LINE !line below ird NUMBER
Add #ER_IRD_DIF to #num3_row
Add #ER_IRD_DIF to #num4_row
Add #ER_IRD_DIF to #num5_row
Add #ER_IRD_DIF to #num6_row
Add #ER_IRD_DIF to #num7_row
Add #ER_IRD_DIF to #num8_row
Add #ER_IRD_DIF to #num9_row

if #Run_year >= 2009 and #Run_month >= 04
or
#Run_year >= 2010 and #Run_month >= 01
else
Add #ER_IRD_DIF to #num10_row
Add #ER_IRD_DIF to #num11_row
Add #ER_IRD_DIF to #num12_row
Add #ER_IRD_DIF to #num13_row
end-if

Let #pymt_qn_row = #num7_row + 20
Let #pymnt_Y_col = #box3_col + 5
Let #pymnt_N_col = #box3_col + 8
Print 'Has the Payment been made electronically? (Tick one)      Yes     No' (#pymt_qn_row,#desc3c)
GRAPHIC (#pymt_qn_row,#pymnt_Y_col,1) BOX 1 0 1
GRAPHIC (#pymt_qn_row,#pymnt_N_col,1) BOX 1 0 1
!******PRINTING THE NUMBERS
alter-printer font=4 point-size=7 !set to LARGER font for box numbers
PRINT '  1  ' (#desc1r,#num1_col) bold background=('black') foreground=('white')
PRINT '  2  ' (#desc2r,#num1_col) bold background=('black') foreground=('white')
PRINT '  3  ' (#num3_row,#num3_col) bold background=('black') foreground=('white')
PRINT '  4  ' (+2,#num3_col) bold background=('black') foreground=('white')
PRINT '  5  ' (+2,#num3_col) bold background=('black') foreground=('white')
PRINT '  6  ' (+2,#num3_col) bold background=('black') foreground=('white')
PRINT '  7  ' (+2,#num3_col) bold background=('black') foreground=('white')
PRINT '  8  ' (+2,#num3_col) bold background=('black') foreground=('white')
PRINT '  9  ' (+3,#num3_col) bold background=('black') foreground=('white')

if #Run_year >= 2009 and #Run_month >= 04
or
#Run_year >= 2010 and #Run_month >= 01
else
PRINT '  10  ' (+3,#num3_col) bold background=('black') foreground=('white')
PRINT '  11  ' (+2,#num3_col) bold background=('black') foreground=('white')
PRINT '  12  ' (+4,#num3_col) bold background=('black') foreground=('white')
PRINT '  13  ' (+3,#num3_col) bold background=('black') foreground=('white')
end-if

!PRINTING BOXES 3,4,5,6
GRAPHIC (#num3_row,#box3_col,#box3_len) BOX 1
GRAPHIC (#num4_row,#box3_col,#box3_len) BOX 1
GRAPHIC (#num5_row,#box3_col,#box3_len) BOX 1
GRAPHIC (#num6_row,#box3_col,#box3_len) BOX 1
GRAPHIC (#num7_row,#box3_col,#box3_len) BOX 1
GRAPHIC (#num8_row,#box3_col,#box3_len) BOX 1
GRAPHIC (#num9_row,#box3_col,#box3_len) BOX 1

if #Run_year >= 2009 and #Run_month >= 04
or
#Run_year >= 2010 and #Run_month >= 01
else
GRAPHIC (#num10_row,#box3_col,#box3_len) BOX 1
GRAPHIC (#num11_row,#box3_col,#box3_len) BOX 1
GRAPHIC (#num12_row,#box3_col,#box3_len) BOX 1
GRAPHIC (#num13_row,#box3_col,#box3_len) BOX 1
end-if

!PRINTING THE DOLLAR SIGNS
alter-printer font=4 point-size=8 !set to LARGER font for DOLLAR sign
PRINT '$' (#num3_row,#dollar_col) BOLD
PRINT '$' (+2,#dollar_col) BOLD
PRINT '$' (+2,#dollar_col) BOLD
PRINT '$' (+2,#dollar_col) BOLD
PRINT '$' (+2,#dollar_col) BOLD
PRINT '$' (+2,#dollar_col) BOLD
PRINT '$' (+3,#dollar_col) BOLD

if #Run_year >= 2009 and #Run_month >= 04
or
#Run_year >= 2010 and #Run_month >= 01
else
PRINT '$' (+3,#dollar_col) BOLD
PRINT '$' (+2,#dollar_col) BOLD
PRINT '$' (+4,#dollar_col) BOLD
PRINT '$' (+3,#dollar_col) BOLD
end-if

!Print Office Use only section
Let #Offbox_col = #bigboxc + 1
Let #offbox_len=18
let #offbox_wid = 8
Let #offbox_row = #desc3r + 13
Let #desc3r = #desc3r + 3
GRAPHIC (#desc3r,#Offbox_col,25) BOX 3 0 1 !Address Box
alter-printer font=4 point-size=6
print 'Street or PO Box' (+2,9)
GRAPHIC (+2,#Offbox_col,25) BOX 2 0 1
print 'Suburb' (+1,9)
GRAPHIC (+2,#Offbox_col,25) BOX 2 0 1
print 'Town or city' (+1,9)

GRAPHIC (#offbox_row,#Offbox_col,#offbox_len) BOX #offbox_wid !office box
Let #Offwhitearea_row = #offbox_row + 4
let #Offwhitearea_col = #Offbox_col + 1
alter-printer font=4 point-size=8 !change font for OFFICE USE ONLY
let #off_desc_col = #bigboxc+2
PRINT '   >   '  (#offbox_row,#off_desc_col) bold background=('black') foreground=('white')
Add 3 to #off_desc_col
alter-printer font=4 point-size=5 !change font for OFFICE USE ONLY
PRINT 'OFFICE USE ONLY' (#offbox_row,#off_desc_col)

!Printing Office Use Options
alter-printer font=4 point-size=7
Let #off_opt_col = #Offwhitearea_col
Let #off_opt_row = #offbox_row + 1
PRINT 'Operator Code' (#off_opt_row,#off_opt_col) wrap 9 2
Add 4 to #off_opt_col
GRAPHIC (#off_opt_row,#off_opt_col,2) box 2 0 1
Add 3 to #off_opt_col
PRINT 'Corresp. Indicator' (#off_opt_row,#off_opt_col) wrap 9 2
Add 4 to #off_opt_col
GRAPHIC (#off_opt_row,#off_opt_col,2) box 2 0 1
Subtract 11 from #off_opt_col
Add 3 to #off_opt_row
PRINT 'Payment Attached' (#off_opt_row,#off_opt_col) wrap 9 2
Add 4 to #off_opt_col
GRAPHIC (#off_opt_row,#off_opt_col,2) box 2 0 1
Add 3 to #off_opt_col
PRINT 'Return Cot.' (#off_opt_row,#off_opt_col) wrap 9 2
Add 4 to #off_opt_col
GRAPHIC (#off_opt_row,#off_opt_col,2) box 2 0 1
Add 3 to #off_opt_col

alter-printer font=32 point-size=6
!Print declaration
Let #decl_row = #offbox_row + #offbox_wid
PRINT 'Decalaration: I declare that the information given in this return is true and correct' (#decl_row,#Offbox_col) wrap 50 2

!Print Signature box
Let #signbox_row = #decl_row + 2
GRAPHIC (#signbox_row,#Offbox_col,25) BOX 3 0 1  !Signature white box
Let #signtxt_row = #signbox_row + 1
PRINT 'Signature' (#signbox_row,#Offbox_col)
LET #signdate_col = #off_opt_col + 2
PRINT '   /   /    ' (+2, #signdate_col)
PRINT 'Date' (+1,#signdate_col)
alter-printer font=4 point-size=10
Print 'Inland Revenue copy' (+2,#Offbox_col) bold
alter-printer font=4 point-size=7
Print 'Please make a copy for your own records' (+1,#Offbox_col)
alter-printer font=4 point-size=7 !set to normal font for IRD number and Period end

!PRINT THE IRD NUMBER
LET #ird1_val_col=#ird1_col + 1
LET #ird2_val_col=#ird2_col + 1
LET #ird3_val_col=#ird3_col + 1

PRINT $IRD_NUM1 (#ird1_row,#ird1_val_col)
PRINT $IRD_NUM2 (,#ird2_val_col)
PRINT $IRD_NUM3 (,#ird3_val_col)

!PRINT THE IRD AND PERIOD ENDED
let #DD_val_col=#DD_col+1
PRINT $PERIOD_END_DD (#DD_row,#DD_val_col)
let #MM_val_col=#MM_col+1
PRINT $Run_month (,#MM_val_col)
let #YYYY_val_col=#YYYY_col+1
PRINT $Run_year (,#YYYY_val_col)

alter-printer font=4 point-size=8 !set to LARGER font for DOLLAR sign
PRINT #PAYE_ded (#num3_row,#val3_col)  edit '999999999999.99'
PRINT #child_support (#num4_row,#val3_col) edit '999999999999.99'
PRINT #student_loan (#num5_row,#val3_col) edit '999999999999.99'
PRINT #kiwi_ded_ee (#num6_row,#val3_col) edit '999999999999.99'
PRINT #kiwi_ded_er (#num7_row,#val3_col) edit '999999999999.99'
PRINT #sscwt       (#num8_row,#val3_col) edit '999999999999.99'
PRINT #total_ded   (#num9_row,#val3_col) edit '999999999999.99'

if #Run_year >= 2009 and #Run_month >= 04
or
#Run_year >= 2010 and #Run_month >= 01
else
PRINT #kiwi_etc    (#num10_row,#val3_col) edit '999999999999.99'
PRINT '0.00'       (#num11_row,#val3_col) edit '999999999999.99'
PRINT #kiwi_etc    (#num12_row,#val3_col) edit '999999999999.99'
if #amt_payable >=0
 PRINT #amt_payable (#num13_row,#val3_col) edit '999999999999.99'
end-if
end-if

end-procedure !Print-IRD-Copy

!*********************************************************************
!Print-PYMNT_SLIP
!Prints the Payment Slip
!*********************************************************************
begin-procedure Print-Pymt-slip
Let #pymt_row = 50

graphic (#pymt_row,#bigboxc,69) horz-line
alter-printer font=5 point-size=12
Add 1 to #bigboxc
PRINT $INLAND_REV_lbl (+2,#bigboxc)
alter-printer font=32 point-size=10
PRINT $TE_TARI_lbl (+1,#bigboxc)
GRAPHIC (+2,10,20) BOX 1
alter-printer font=4 point-size=6
print 'Name:' (,7)
alter-printer font=4 point-size=7 !set to LARGER font for printing the heading
Add 1 to #pymt_row
Print 'DED' (#pymt_row,72)
alter-printer font=4 point-size=10 !set to LARGER font for printing the heading
Add 1 to #pymt_row
PRINT 'Payment Slip' (#pymt_row,1) center
Let #IRD_PYMT_DIF = 23

Let #ird1_row = #ird1_row + 38
alter-printer font=4 point-size=8 !set to normal
GRAPHIC (#ird1_row,#ird1_col,3) BOX 1 ! box for IRD1
GRAPHIC (,#ird2_col,4) BOX 1          ! box for IRD2
GRAPHIC (,#ird3_col,5) BOX 1          ! box for IRD3

Let #DD_row = #DD_row + 38
GRAPHIC (#DD_row,#DD_col,3) BOX 1 ! box for DD
GRAPHIC (,#MM_col,3) BOX 1        ! box for MM
GRAPHIC (,#YYYY_col,5) BOX 1      ! box for YYYY

!PRINTING THE DESCRIPTIONS
alter-printer font=4 point-size=7 !reset to default font
Let #desc1r = #desc1r + 38
PRINT $IRD_NUMBER_lbl (#desc1r,#desc1c)
Let #desc2r = #desc2r + 38
PRINT $PERIOD_ENDED_lbl (#desc2r,#desc1c)

alter-printer font=4 point-size=7 !set to normal font for IRD number and Period end
!****** PRINT THE IRD NUMBER
LET #ird1_val_col=#ird1_col + 1
LET #ird2_val_col=#ird2_col + 1
LET #ird3_val_col=#ird3_col + 1
PRINT $IRD_NUM1 (#ird1_row,#ird1_val_col)
PRINT $IRD_NUM2 (,#ird2_val_col)
PRINT $IRD_NUM3 (,#ird3_val_col)

!****** PRINT THE IRD AND PERIOD ENDED
let #DD_val_col=#DD_col+1
PRINT $PERIOD_END_DD (#DD_row,#DD_val_col)
let #MM_val_col=#MM_col+1
PRINT $Run_month (,#MM_val_col)
let #YYYY_val_col=#YYYY_col+1
PRINT $Run_year (,#YYYY_val_col)

if #late_pymt_pen > 0
    let #Amnt_of_pymt = #late_pymt_pen + #amt_payable
else
    let #Amnt_of_pymt = #amt_payable
end-if

PRINT 'Amount of Payment' (+2,#desc1c) wrap 10 2

if #Run_year >= 2009 and #Run_month >= 04
or
#Run_year >= 2010 and #Run_month >= 01
PRINT '  10  ' (,#num3_col) bold background=('black') foreground=('white')
alter-printer font=4 point-size=8
PRINT '$' (,#dollar_col) BOLD
let #pymt_amt_col = #DD_col + 1
if #total_ded >= 0
  print #total_ded  (,#pymt_amt_col ) edit '9999999999999.99'
end-if
else
PRINT '  14  ' (,#num3_col) bold background=('black') foreground=('white')
alter-printer font=4 point-size=8
PRINT '$' (,#dollar_col) BOLD
let #pymt_amt_col = #DD_col + 1
if #amnt_of_pymt >= 0
  print #amnt_of_pymt (,#pymt_amt_col ) edit '9999999999999.99'
end-if
end-if

GRAPHIC (,#box3_col,#box3_len) BOX 1

alter-printer font=4 point-size=7 !reset to default font

if #Run_year >= 2009 and #Run_month >= 04
or
#Run_year >= 2010 and #Run_month >= 01
let $temp1 ='Copy your total from Box 9 and include any'
else
let $temp1 ='Copy your total from Box 13 and include any'
end-if

let $temp1 = ltrim(rtrim($temp1,' '),' ')
let $temp2 ='late payment penalties and Interest, for this period only.'
let $temp2 = ltrim(rtrim($temp2,' '),' ')
PRINT $temp1 (61,#desc1c)
PRINT $temp2 (,#desc1c)
PRINT 'This return and any payment are due' (,#offbox_col) bold
GRAPHIC (+1,#offbox_col,18) box 1


end-procedure !Print-Pymt-slip


#include 'curdttim.sqc'  ! Get-Current-DateTime procedure
#include 'datetime.sqc'  ! Routines for date and time forma
#include 'datemath.sqc'  ! Routines for date and time maths
#include 'number.sqc'    ! Routines to format numbers
#include 'reset.sqc'     ! Reset Printer
#include 'stdapi.sqc'    ! Routines to Update Run Status
#include 'sqrtrans.sqc'  ! Translate SQR strings to given language