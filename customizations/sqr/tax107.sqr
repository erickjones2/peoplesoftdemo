!***********************************************************************
!  TAX107 : W4 - EMPLOYEE WITHHOLDING ALLOWANCE AUDIT REPORT.         *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2019, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  30613921                                            !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set Database Environment

begin-report
  do Stdapi-Init
  move 'TAX107' to $ReportID
  move 'EMPLOYEE W4 AUDIT REPORT' to $ReportTitle
  display $ReportTitle
  display ''

  do Get-Run-ID

  do Init-DateTime
  do Get-Current-DateTime
  do Format-DateTime($AsOfToday, $out, {DEFMDY},'','')
  move $out to $ReportDate
  display 'Report Began: ' noline
  display $AsOfNow
  display ''

  do Get-From-Thru-Date

  do Order_clause

  Let $PrintHeader2020 = 'N'

  move 0 to #CountVersionA
  move 0 to #CountVersionB
  do get-w4audit

  do Get-Current-DateTime
  display ''
  display 'Report Ended: ' noline
  display $AsOfNow
  do Stdapi-Term
end-report

#include 'setup31.sqc'   !Printer and page-size initialization


begin-heading 4

#include 'stdhdg01.sqc'

  do Format-DateTime($Fromdate, $FmDate, {DEFMDY},'','')
  do Format-DateTime($ThruDate, $TuDate, {DEFMDY},'','')

end-heading


begin-procedure Get-Run-ID
  if $Prcs_Process_Instance = ''
     do Prompt
  else
    do Select-Parameters
  end-if
end-procedure


Begin-Procedure Prompt

while 1 = 1
    input $RunID maxlen=3 'Enter Valid Run ID' type=char

    if RTRIM($RunID, ' ') = ''
      display 'Exiting....'
      Stop
    end-if

    do edit-Run

    if $Break_YN = 'Y'
      Break
    End-If
End-While

End-Procedure


begin-procedure Edit-Run

  move 0 to #Found

begin-select

Z.FROMDATE
Z.THRUDATE
Z.ORDER_OPTION

  add 1 to #Found

FROM PS_RC_PAY Z
where Z.RUN_CNTL_ID = $RunID
end-select
  if #Found = 0
    display 'Error --  Not a Valid Run ID.'
    move 'N' to $Break_YN
  else

    if #Found = 1
    move 'Y' to $Break_YN

         Let $RC_PAY.FROMDATE = &Z.FROMDATE
         Let $RC_PAY.THRUDATE = &Z.THRUDATE
         Let $RC_PAY.ORDER_OPTION = &Z.ORDER_OPTION

    end-if

  end-if
end-procedure


Begin-Procedure Order_clause

DISPLAY 'ORDER_OPTION'
DISPLAY $RC_PAY.ORDER_OPTION
  Let $OrderByClause = ''
  If $RC_PAY.ORDER_OPTION = 'N'
      Let $OrderByClause = 'ORDER BY W4.FWT_FORM_VERSION, PERS.NAME, W4.EFFDT'
  else
      Let $OrderByClause = 'ORDER BY W4.FWT_FORM_VERSION, W4.EMPLID, W4.EFFDT'   !ORDER_OPTION = 'I'
  End-If

End-Procedure


begin-procedure get-w4audit
  display 'Generating report . . . ' noline

  move ' ' to $Fwt_Form_Version_Prior

begin-SELECT
W4.EMPLID
W4.FWT_MAR_STATUS
W4.FWT_ALLOWANCES
W4.FWT_ADDL_AMT
W4.SPECIAL_FWT_STATUS
W4.LAST_ACTION
W4.FWT_FORM_VERSION
W4.FWT_MULTI_JOBS
W4.FWT_TTL_DEP_AMT
W4.FWT_OTH_INCOME
W4.FWT_DED_ADJ
PERS.NAME

  move &W4.EMPLID              to $Emplid
  move &W4.FWT_ALLOWANCES      to $Fwt_Allowances
  move &W4.FWT_ADDL_AMT        to #Fwt_Addl_Amt

  let $FieldName    = 'FWT_MAR_STATUS'
  let $FieldValue   = &W4.FWT_MAR_STATUS
  do Read-Translate-Table
  let $Fwt_Mar_Status = $XLatShortName

  let $FieldName    = 'SPECIAL_FWT_STATUS'
  let $FieldValue   = &W4.SPECIAL_FWT_STATUS
  do Read-Translate-Table
  let $Special_Fwt_Status = $XLatShortName

  let $Name = rtrim(&PERS.NAME,' ')
  
  move &W4.FWT_FORM_VERSION   to $Fwt_Form_Version  
  move &W4.FWT_MULTI_JOBS     to $Fwt_Multi_Jobs   
  move &W4.FWT_TTL_DEP_AMT    to #Fwt_Ttl_Dep_Amt   
  move &W4.FWT_OTH_INCOME     to #Fwt_Oth_Income    
  move &W4.FWT_DED_ADJ        to #Fwt_Ded_Adj     
  
  let $FieldName    = 'SPECIAL_FWT_STATUS'
  let $FieldValue   = &W4.SPECIAL_FWT_STATUS
  do Read-Translate-Table
  let $Special_Fwt_Status = $XLatShortName
  
  let $FieldName    = 'LAST_ACTION'
  let $FieldValue   = &W4.LAST_ACTION
  do Read-Translate-Table
  let $Last_action = $XLatShortName

  if $Fwt_Form_Version = 'B'
     if ($Fwt_Form_Version_Prior = 'B') or ($Fwt_Form_Version_Prior = ' ')
        do  Print-W4Audit-2020
        move $Fwt_Form_Version to $Fwt_Form_Version_Prior
     else 
        if #CountVersionA >= 70    
           new-page
           move 0 to #CountVersionB
        else 
           move #CountVersionA to #CountVersionB
           let $PrintHeader2020 = 'Y'
        end-if
        do  Print-W4Audit-2020
        let $PrintHeader2020 = 'N'
        move $Fwt_Form_Version to $Fwt_Form_Version_Prior
     end-if
   else  
     if ($Fwt_Form_Version_Prior = 'A') or ($Fwt_Form_Version_Prior = ' ')
        do  Print-W4Audit-2019
        move $Fwt_Form_Version to $Fwt_Form_Version_Prior
     end-if
  end-if    

FROM PS_FED_TAX_DATA  W4, PS_NAMES PERS
WHERE W4.EMPLID = PERS.EMPLID
  AND PERS.NAME_TYPE = 'PRI'
  AND PERS.EFFDT =
      (SELECT MAX(PX.EFFDT)
       FROM   PS_NAMES PX
       WHERE  PX.EMPLID    = PERS.EMPLID
         AND  PX.NAME_TYPE = PERS.NAME_TYPE
         AND  PX.EFFDT    <= $AsOfToday)
  AND LAST_UPDATE_DATE BETWEEN $FromDate AND $ThruDate
  AND LAST_ACTION = 'W'
[$OrderByClause]

end-SELECT
  display 'Done'
end-procedure

begin-procedure Print-W4Audit-2019

   If #CountVersionA = 0 

     do Format-DateTime($Fromdate, $FmDate, {DEFMDY},'','')
     do Format-DateTime($ThruDate, $TuDate, {DEFMDY},'','')
    
     print 'List of updated Form W-4 version 2019 or Earlier from ' (+2,1)
     print $FmDate ()
     print '  to  '  ()
     print $TuDate ()
     print 'Empl ID      Name                         Tax Status  '          (+2,1)
     print 'Allowances  Addl Amount  Special W/H Tax Status  Updated Method' (0,55)
     print '-----------  ---------------------------- ----------  '          (+1,1)
     print '----------  -----------  ----------------------  --------------' (0,55)

     add 5 to #CountVersionA

  end-if

  print $Emplid                 (+1,1)
  print $Name                   (0,14)
  print $Fwt_Mar_Status         (0,43)
  print $Fwt_Allowances         (0,55)
  print #Fwt_Addl_Amt           (0,67)
  print $Special_Fwt_Status     (0,80)
  print $Last_action            (0,104)
  
  add 1 to #CountVersionA
  
  If #CountVersionA = 76
     move 0 to #CountVersionA
  end-if
  
end-procedure

begin-procedure Print-W4Audit-2020

  If #CountVersionB = 0 or $PrintHeader2020 = 'Y'
    
     do Format-DateTime($Fromdate, $FmDate, {DEFMDY},'','')
     do Format-DateTime($ThruDate, $TuDate, {DEFMDY},'','')
     
     print 'List of updated Form W-4 version 2020 or Later from ' (+2,1)
     print $FmDate ()
     print '  to  '  ()
     print $TuDate ()

     print 'MJ/  Dependent   Other                     '            (+2,55)
     print 'Extra       Special W/H  Updated'                       (0,96)
     print 'Empl ID      Name                         Tax Status  ' (+1,1)
     print 'SW   Amount      Income      Deductions   '             (0,55)
     print 'W/H         Tax Satus    Method'                        (0,96)
     print '-----------  ---------------------------- ----------  ' (+1,1)
     print '---  ----------- ----------- -----------  '             (0,55)
     print '----------  -----------  ----------'                    (0,96)

     add 6 to #CountVersionB
     
  end-if

  print $Emplid                 (+1,1)
  print $Name                   (0,14)
  print $Fwt_Mar_Status         (0,43)
  
  print $Fwt_Multi_Jobs         (0,55)
  print #Fwt_Ttl_Dep_Amt        (0,60)  
  print #Fwt_Oth_Income         (0,72)
  print #Fwt_Ded_Adj            (0,84)
  print #Fwt_Addl_Amt           (0,96)
  print $Special_Fwt_Status     (0,108)
  print $Last_action            (0,121)
  
  add 1 to #CountVersionB

  If #CountVersionB = 76
     move 0 to #CountVersionB
  end-if
  
end-procedure

#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Update Process API
#include 'payrctl2.sqc'  !Select Run Control Parameters
#include 'pygetval.sqc'  !To Get as Of date
#include 'readxlat.sqc'  !Read-Translate-Table procedure
