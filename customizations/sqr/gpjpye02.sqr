!***********************************************************************
! GPJPYE02.sqr:  Legal Payment Summary Report                          *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2016, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2016/03/28:12:48:41                                 !
!       $Release:  HR92                                                !
!           $Bug:  22565082                                            !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#include 'setup32.sqc'  !page layout and printer definition


begin-report

        do Check-JPN-TE-Firm-Num-Install          !getestab.sqc

        do Init-Processing

        do Process-Main

        do Reset

        do Update-Prcs-Run-Status

end-report


!***********************************************************************
!                                                                      *
! Init-Processing                                                      *
!                                                                      *
!                                                                      *
! Called By:    begin-report                                           *
!                                                                      *
! Calls:        Init-Datetime           - datetime.sqc                 *
!               Get-Current-Datetime    - prcsapi.sqc (curdttim.sqc)   *
!               Get-Run-Control-Parms   - prcsapi.sqc                  *
!               Get-Language-Codes      - prcslng.sqc                  *
!               Get-GPJPYE02-runctl                                    *
!               Init-Report                                            *
!               Get-Estab-Name          - Getestab.sqc                 *
!                                                                      *
! Description:  Standard PeopleSoft initialization processing.         *
!                                                                      *
!***********************************************************************
begin-procedure Init-Processing


        do Init-Datetime

        do Get-Current-Datetime

        do Define-Prcs-Vars

        do Get-Run-Control-Parms

        do Get-Language-Codes

        do Get-GPJPYE02-runctl

        do Init-Report

        let $Estab  = $Rc_Gpjp_tax_estab
        let $EstabType = 'IT'
        do Get-Estab-Name


end-procedure ! Init-Processing


!***********************************************************************
!                                                                      *
! Get-GPJPYE02-runctl                                                  *
!                                                                      *
!                                                                      *
! Called By:    Init-Processing                                        *
!                                                                      *
! Description:  Get data from GPJP_RC_YEA runcontrol record.          *
!                                                                      *
!***********************************************************************
begin-procedure Get-GPJPYE02-runctl


begin-select

RC.GPJP_TAX_ESTAB
RC.DATE_YEAR_VAL


FROM  PS_GPJP_RC_YEA RC
WHERE RC.OPRID          = $Prcs_OprID
AND   RC.RUN_CNTL_ID    = $Prcs_Run_Cntl_ID

end-select

     let #Rc_date_year_val  = &RC.Date_year_val
     move #Rc_date_year_val to $Rc_date_year_val 9999
     let $Rc_Gpjp_tax_estab = RTRIM(&RC.Gpjp_tax_estab, ' ')


end-procedure !Get-GPJPYE02-runctl


!***********************************************************************
!                                                                      *
! Init-Report                                                          *
!                                                                      *
!                                                                      *
! Called By:    Init-Processing                                        *
!                                                                      *
! Calls:        Init_Report_Translation    - sqrtrans.sqc              *
!               Append_Report_Translation  - sqrtrans.sqc              *
!               Get_Field_Information                                  *
!                                                                      *
! Description:  Initializes variables, defines values for variables    *
!               used in report header & throughout report.  Performs   *
!               Strings Table translation.  #Defines variables for     *
!               use throughout report in printing columns.             *
!                                                                      *
!***********************************************************************
begin-procedure Init-Report


!***********************************************************************
!                                                                      *
! Translations procedures & Strings Table retrievals.                  *
!                                                                      *
!***********************************************************************

    do Init_Report_Translation ($ReportID, $curr_language_cd)

    do Append_Report_Translation ('GPJPYE02')

    do Get_Field_Information ('GPJPYE02','TITLE',              $GPJPYE02_Title,                    #dummy_width)
    do Get_Field_Information ('GPJPYE02','SALARY_PAYER',       $GPJPYE02_Salary_payer,             #dummy_width)

    do Get_Field_Information ('GPJPYE02','SALARY',             $GPJPYE02_Salary,                   #dummy_width)
    do Get_Field_Information ('GPJPYE02','TOTAL',              $GPJPYE02_Total,                    #dummy_width)
    do Get_Field_Information ('GPJPYE02','SUBMIT TAX OFFICE',  $GPJPYE02_Submitted_to_Tax_Office,  #dummy_width)
    do Get_Field_Information ('GPJPYE02','NUMBER OF PAYEES',   $GPJPYE02_Number_of_Payees,         #dummy_width)
    do Get_Field_Information ('GPJPYE02','NO WITHHOLDING TAX', $GPJPYE02_No_Withholding_Tax,       #dummy_width)
    do Get_Field_Information ('GPJPYE02','EARNINGS',           $GPJPYE02_Earnings,                 #dummy_width)
    do Get_Field_Information ('GPJPYE02','WITHHOLDING TAX',    $GPJPYE02_Withholding_Tax,          #dummy_width)
    do Get_Field_Information ('GPJPYE02','RET ALW',            $GPJPYE02_Ret_Alw,                  #dummy_width)
    do Get_Field_Information ('GPJPYE02','NA',                 $GPJPYE02_Not_applicable,           #dummy_width)
    do Get_Field_Information ('GPJPYE02','FIRM_NBR',           $GPJPYE02_FIRM_NBR,                 #dummy_width)

!***********************************************************************
!                                                                      *
! Report variables.                                                    *
!                                                                      *
!***********************************************************************

    let $ReportID              = 'GPJPYE02'          ! Printed in header
    let $ReportTitle           = $GPJPYE02_Title     ! Printed in header
    let $ReportDate            = $AsOfToday          ! Printed in header
    let $ReportTime            = $AsOfNow            ! Printed in header
    let $Report_Language       = $curr_language_cd   ! curr_language_cd resolved in prcslng.sqc
    !do Security-Param

end-procedure ! Init-Report


!***************************
begin-heading 6
!***************************

#include 'stdhdgtr.sqc'
  print $Rc_date_year_val          (3,,) center
  print $GPJPYE02_Salary_payer     (+1,1,35)
  print $Estab                     (0,37,15)
  print $EstabName                 (0,52,30)
  
  if #FirmNumber <> 0 
    !Print Firm Number if it is not equal to 0 (default)
    print $GPJPYE02_FIRM_NBR        (+1,1,25)
    print #FirmNumber               (0,37,13) edit 9999999999999
    position (-1)
  end-if

end-heading


!***********************************************************************
!                                                                      *
! Process-Main                                                         *
!                                                                      *
!                                                                      *
! Called By:    Begin-Report                                           *
!                                                                      *
! Description:  Main processing                                        *
!                                                                      *
!***********************************************************************
begin-procedure Process-Main

let #Num_sal_payees     = 0
let #Num_zero_withhold  = 0
let #Total_earnings     = 0
let #Withh_tax_amount   = 0
let #Tax_amt_difference = 0
let #Num_tax_agent      = 0
let #Tot_earn_tax_agent = 0
let #Withholding_tax_sub = 0
let $_TableAlias = 'A'
  do Security-Param


begin-select
A.GPJP_YEA_SUBMIT
A.GPJP_IT_EARNS
A.GPJP_IT_INCTAX
A.GPJP_YEA_YEATX2
A.GPJP_YEA_TXDIFF



     let #Gpjp_it_inctax        = &A.Gpjp_it_inctax
     let #Gpjp_yea_yeatx2       = &A.Gpjp_yea_yeatx2
     let #Gpjp_with_tax_amt     = #Gpjp_it_inctax
     let #Gpjp_it_earns         = &A.Gpjp_it_earns
     let #Gpjp_yea_txdif        = &A.Gpjp_yea_txdiff
     let #Gpjp_yea_submit       = &A.Gpjp_yea_submit


     add 1 to #Num_sal_payees

     if #Gpjp_with_tax_amt = 0
       add 1 to #Num_zero_withhold
     end-if

     add #Gpjp_it_earns to #Total_earnings

     add #Gpjp_with_tax_amt to #Withh_tax_amount

     add #Gpjp_yea_txdif to #Tax_amt_difference

     if #Gpjp_yea_submit = 1
        add 1 to #Num_tax_agent
        add #Gpjp_it_earns to #Tot_earn_tax_agent
        add #Gpjp_with_tax_amt to #Withholding_tax_sub
     end-if


FROM PS_GPJP_YEA_TABLE A
WHERE A.DATE_YEAR_VAL = #Rc_date_year_val
AND A.GPJP_TAX_ESTAB = $Rc_Gpjp_tax_estab
[$SecurityClausewithERN]


end-select

let #Tot_Earnings_prior_employ = 0
let #Tot_Withhold_prior_employ = 0


begin-select

C.GPJP_YEA_PRIGRS
C.GPJP_YEA_PRITAX

     let #Gpjp_prior_earn = &C.GPJP_YEA_PRIGRS
     let #Gpjp_prior_withhold = &C.GPJP_YEA_PRITAX

     add #Gpjp_prior_earn to #Tot_Earnings_prior_employ
     add #Gpjp_prior_withhold to #Tot_Withhold_prior_employ

FROM PS_GPJP_YEA_TABLE A ,PS_GPJP_YEA_PYEADJ C
WHERE A.DATE_YEAR_VAL = #Rc_date_year_val
AND A.GPJP_TAX_ESTAB = $Rc_Gpjp_tax_estab
AND A.EMPLID = C.EMPLID
AND A.EMPL_RCD = C.EMPL_RCD
AND A.DATE_YEAR_VAL = C.DATE_YEAR_VAL
AND C.EFFDT = (SELECT MAX(C1.EFFDT) FROM PS_GPJP_YEA_PYEADJ C1 WHERE C.EMPLID  = C1.EMPLID
AND C.EMPL_RCD = C1.EMPL_RCD )
[$SecurityClausewithERN]

end-select

let #Num_ret_alw_payees = 0
let #Tot_ret_alw        = 0
let #Tot_wh_tax_ret_alw = 0
let #Num_ret_sub_payees = 0
let #Tot_ret_alw_sub    = 0
let #Tot_wh_tax_ret_sub = 0

let $_TableAlias = 'B'
 do Security-Param

begin-select

B.GPJP_RA_EARN1
B.GPJP_RA_IT1
B.GPJP_RA_EARN2
B.GPJP_RA_IT2
B.GPJP_RA_EARN3
B.GPJP_RA_IT3
B.GPJP_YEA_SUBMIT

     let #Gpjp_ra_earn     = &B.GPJP_RA_EARN1 + &B.GPJP_RA_EARN2 + &B.GPJP_RA_EARN3
     let #Gpjp_ra_it       = &B.GPJP_RA_IT1 + &B.GPJP_RA_IT2 + &B.GPJP_RA_IT3

     add 1 to #Num_ret_alw_payees
     add #Gpjp_ra_earn to #Tot_ret_alw
     add #Gpjp_ra_it to #Tot_wh_tax_ret_alw

     if &B.Gpjp_yea_submit = 1
       add 1 to #Num_ret_sub_payees
       add #Gpjp_ra_earn to #Tot_ret_alw_sub
       add #Gpjp_ra_it to #Tot_wh_tax_ret_sub
     end-if

FROM PS_GPJP_RA_WH_RPT B
WHERE B.DATE_YEAR_VAL = #Rc_date_year_val
AND B.GPJP_TAX_ESTAB = $Rc_Gpjp_tax_estab
[$SecurityClausewithERN]
end-select

subtract #Tot_Earnings_prior_employ from #Total_earnings
subtract #Tot_Withhold_prior_employ from #Withh_tax_amount

do Print-Details





end-procedure ! Process-Main


!***********************************************************************
!                                                                      *
! Print-Details                                                        *
!                                                                      *
!                                                                      *
! Called By:                                                           *
!                                                                      *
! Description:  Prints the detail lines                                *
!                                                                      *
!***********************************************************************
begin-procedure Print-Details

    Columns 3 15 43 60 62 80 83 100
    Print $GPJPYE02_Salary (1,1)
    Print $GPJPYE02_Ret_Alw (+7,1)

    Next-Column
    Print $GPJPYE02_Total (3,1,)
    Print $GPJPYE02_Submitted_to_Tax_Office (+1,1)
    Print $GPJPYE02_Total (+6,1,)
    Print $GPJPYE02_Submitted_to_Tax_Office (+1,1)

    Next-Column
    Print $GPJPYE02_Number_of_Payees (2,1)
    Print #Num_sal_payees (+1,1) edit 999,999,990
    Print #Num_tax_agent  (+1,1) edit 999,999,990
    print $GPJPYE02_Number_of_Payees (+5,1)
    Print #Num_ret_alw_payees  (+1,1) edit 999,999,990
    Print #Num_ret_sub_payees  (+1,1) edit 999,999,990

    Next-Column
    Print '(' (3,2)

    Next-Column
    Print $GPJPYE02_No_Withholding_Tax (2,1)
    Print #Num_zero_withhold (+1,1) edit 999,999,990
    Print $GPJPYE02_Not_applicable (+1,1)
    Print $GPJPYE02_Earnings (+5,1)
    Print #Tot_ret_alw (+1,1) edit 999,999,999,990
    Print #Tot_ret_alw_sub (+1,1) edit 999,999,999,990

    Next-Column
    Print ')' (3,1)

    Next-Column
    Print $GPJPYE02_Earnings (2,1)
    Print #Total_earnings (+1,1) edit 999,999,999,990
    Print #Tot_earn_tax_agent (+1,1) edit 999,999,999,990
    Print $GPJPYE02_Withholding_Tax (+5,1)
    Print #Tot_wh_tax_ret_alw (+1,1) edit 999,999,999,990
    Print #Tot_wh_tax_ret_sub (+1,1) edit 999,999,999,990

    Next-Column
    Print $GPJPYE02_Withholding_Tax (2,1)
    Print #Withh_tax_amount (+1,1) edit 999,999,999,990
    Print #Withholding_tax_sub (+1,1) edit 999,999,999,990






end-procedure  !Print-Details


!***********************************************************************
!                                                                      *
!                                                                      *
! SQCS included in the report                                          *
!                                                                      *
!***********************************************************************
#include 'getestab.sqc'     ! Get Establishment Data from the GPJP_TAX_ESTAB
#include 'datemath.sqc'     ! Date conversion procedures
#include 'hrsecty.sqc'      ! Get SQR Security parameters
#include 'number.sqc'       ! Routines to format numbers
#include 'datetime.sqc'     ! Routines for date and time formatting
#include 'sqrtrans.sqc'     ! Translate SQR strings to given language
#include 'prcsapi.sqc'      ! Replaces stdapi.sqc
#include 'prcslng.sqc'      ! Replaces getrplng.sqc
#include 'readxlat.sqc'     ! Procedure to read values from xlattable
#include 'reset.sqc'        ! Reset printer procedure
