!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:03:44:35                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!***********************************************************************
!
!***********************************************************************
!                                                                      *
! Global Payroll       Release 8                                       *
!***********************************************************************

!***********************************************************************
!                                                                      *
!  This SQR generates the Recipient Payments Report                    *
!                                                                      *
!  The user has several the option of generating a summary or detail   *
!  report.                                                             *
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'   !Set environment
#include 'setup32.sqc'  !page layout and printer definition
#include 'number.sqc'   !Routines to format numbers
#include 'datetime.sqc' !Routines for date and time formatting
#include 'prcsapi.sqc'  !Replaces stdapi.sqc
!
#define ColR   155
begin-report

#IFDEF DEBUGA
 lET $PRCS_OPRID = 'PS'
 LET $PRCS_RUN_CNTL_ID = '1'
#ENDIF
      do Init-Processing
      Do Stdapi-Init
      do Get-Parameters

      Let $ReportTitle = 'Recipient Payments Report'

      if rtrim($DebitDt,' ') <> ''
         Do Format-DateTime($DebitDt, $PrintDebitDt, {DEFDATE},'','')
      End-If

      If $ReportLvl = '1'
         Let $LevelHdr = 'Detail Report for Debit Date : ' || $PrintDebitDt
      else
         Let $LevelHdr = 'Summary Report for Debit Date : ' || $PrintDebitDt
      end-if 

      Do Check-RCP-Entered
      if  $RCPEntered = 'Y'
          Do Process-Ind-RCP
      else
          Let $WhereClause = ''
          do Process-RCP
      End-if
      Do Reset                        ! called from reset.sqc
        do Delete-RunControl
      Do Stdapi-Term
end-report
!
!***********************************************************************
! Init-Processing                                                      *
!***********************************************************************
begin-procedure Init-Processing
      do Init-Datetime
      do Get-Current-Datetime
      do Init-Report
      do Define-Prcs-Vars
end-procedure ! Init-Processing
!
!***********************************************************************
! Init-Report                                                          *
!***********************************************************************
begin-procedure Init-Report

#define Col1   1
#define Col2   14
#define Col3   32
#define Col4   45
#define Col5   55
#define Col6   107
#define Col7   118

    Let $ReportID = 'GPHKEFT2'

    Let $Curr_Language_Cd = 'ENG'

    do Init_Report_Translation ($ReportID, $curr_language_cd)

    do Get_Field_Information ($ReportID, 'HKID'             , $HKID_Title,    #DW)
    do Get_Field_Information ($ReportID, 'GPHK_EE_MEMBER_ID', $Member_ID ,    #DW)
    do Get_Field_Information ($ReportID, 'EMPLID'           , $EMPLID_Title,  #DW)
    do Get_Field_Information ($ReportID, 'EMPL_RCD'         , $ERCD_Title,    #DW)
    do Get_Field_Information ($ReportID, 'NAME'             , $NAME_Title,    #DW)
    do Get_Field_Information ($ReportID, 'GP_AMT'           , $AMT_Title,     #DW)
    do Get_Field_Information ($ReportID, 'MODE_OF_TRANSFER' , $Mode_Title,    #DW)
end-procedure ! Init-Report
!
!***************************
! Process-Ind-RCP
!***************************
Begin-Procedure Process-Ind-RCP

Begin-SELECT
D.RECIPIENT_ID
   Let $RecipientID = &D.RECIPIENT_ID
   Let $RecpID = rtrim(&D.RECIPIENT_ID,' ')
   Let $WhereClause = 'AND B.RECIPIENT_ID = ' || '''' || $RecpID || ''''
   Do Process-RCP
   NEW-PAGE
   
  FROM PS_GPHK_RCP_RUNCTL D
    WHERE D.OPRID         = $PRCS_OPRID
      AND D.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID
End-SELECT
End-Procedure
!
!HK ID        Membership Number Employee ID  Empl Rcd  Name                                                Amount     Mode of Transfer 
!***************************
! Check-RCP-Entered
!***************************
Begin-Procedure Check-RCP-Entered
  Let $RCPEntered = 'N'
Begin-SELECT
RP.RECIPIENT_ID
   if rtrim(&RP.RECIPIENT_ID,' ')  <> ''
      Let $RCPEntered = 'Y'
   End-if
  FROM PS_GPHK_RCP_RUNCTL RP
    WHERE RP.OPRID         = $PRCS_OPRID
      AND RP.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID
End-SELECT
End-Procedure
!
!***************************
! Print-Heading
!***************************
Begin-Procedure Print-Heading
  Let #DL = {Colr} + 25
#include 'stdhdg01.sqc'

  Print $LevelHdr () CENTER
  
  print  '-'  (+1,1,#DL)  fill
  print 'Recipient ID      : ' (+1,1)
  print $RecipientID           (0,23) 
  print $RecipientName         (0,33)
  print 'Pay Entity        : ' (+1,1)
  Print $PayEntity             (0,23)
  Print $PayEntityName         (0,33)
  Print 'Group Number :' (0,70)
  Print $RecpntRegNum           (0,+2)
  print  '-'                   (+1,1,#DL)  fill

  Let #LC = 13
  
  print $HKID_Title   (+1,{Col1})
  print $Member_ID    (0,{Col2})
  print $EMPLID_Title (0,{Col3})
  print $ERCD_Title   (0,{Col4})
  print $NAME_Title   (0,{Col5})
  print $AMT_Title    (0,{Col6})
  print $Mode_Title   (0,{Col7})
  print  '-'          (+1,1,#DL)  fill

End-Procedure
!
!***********************************************************************
! Get-Parameters                                                    *
!***********************************************************************
begin-procedure Get-Parameters
Begin-SELECT
RC.DEBIT_DT
RC.GPHK_REPORT_LVL
     Let $DebitDt   = &RC.DEBIT_DT
     Let $ReportLvl = &RC.GPHK_REPORT_LVL
     If $ReportLvl = '1'
        Let $Print = 'Y'
     Else
        Let $Print = 'N'
     End-if

  FROM PS_GPHK_EFT_RUNCTL RC
    WHERE RC.OPRID         = $PRCS_OPRID
        AND RC.RUN_CNTL_ID = $PRCS_RUN_CNTL_ID
End-SELECT

end-procedure ! Get-Parameters
!
!
!***********************************************************************
! Get-RecipientName                                                    *
!***********************************************************************
begin-procedure Get-RecipientName
     Let $RecipientName  = ''
Begin-SELECT
RCP.DESCR
     Let $RecipientName  = &RCP.DESCR

  FROM PS_RECIPIENT RCP
    WHERE RCP.RECIPIENT_ID = $RecipientID
End-SELECT

end-procedure ! Get-Parameters
!
!***********************************************************************
! Process-RCP                                                          *
!***********************************************************************
begin-procedure Process-RCP
   Let #PageNo = 1

BEGIN-SELECT DISTINCT  
B.PAY_ENTITY
B.RECIPIENT_ID
   Let $PayEntity   = &B.PAY_ENTITY
   Let $RecipientID = &B.RECIPIENT_ID

   Do Get-RecipientName
   Do Pay-Entity-Details
   Do Recp-Member-Details
   Do Detail-Report

   IF $ReportLvl = '1' ! Detail Report
      Add 2 to #LC 
   Else
      Do Print-Heading
      Add 2 to #LC 
   End-if

   Print 'Total Records : ' (+2,1)
   Print #TotRows         (0,20) edit '9999'

   Let #LC = #LC + 1
   Print 'Total Amount  : ' (+1,1)
   Print #PAmount           (0,20) edit '9999999.99'

   if $RecpntCommType <> '30' and rtrim($RecpntCommType,' ') <> ''
      Let #LC = #LC + 2
      Print 'Commission Method : ' (+2,1)
      Print $CommOptn           (0,25)

      Let #LC = #LC + 1
      Print 'Commission Type   : ' (+1,1)
      Print $CommType           (0,25)

      Let #LC = #LC + 1
      Print 'Commission Value  : ' (+1,1)
      Let #CommValue = 0
      if $RecpntCommType = '10' !Flat
         Let #CommValue = #RecpntCommAmt
      end-if
      if $RecpntCommType = '20' !Percentage
         Let #CommValue = (#PAmount * #RecpntCommAmt)/100
      end-if
      Print #CommValue           (0,25)  edit '9999999.99'
      Let #LC = #LC + 1
      Print 'Net Payable Amount : ' (+1,1)
      Let #NetPayable = #PAmount - #CommValue
      Print #NetPayable           (0,25)  edit '9999999.99'
   end-if

   IF $ReportLvl <> '1' ! Summary Report
      new-page
   End-if


   FROM PS_GP_PAYMENT B 
       ,PS_GP_PIN C
 WHERE B.DEBIT_DT = $DebitDt
   AND B.PIN_NUM = C.PIN_NUM
   AND C.COUNTRY = 'HKG'
   AND B.PMT_TYPE IN ('02','03')
[$WhereClause]
End-SELECT
End-Procedure ! Process-RCP
!
!***********************************************************************
! Detail-Report                                                        *
!***********************************************************************
begin-procedure Detail-Report
   Let #TotRows = 0
   Let #PAmount = 0
   Let #LC = 65
   Let $FieldName = 'PAYMENT_MTHD'


BEGIN-SELECT
SUM(G.GP_PMT_AMT)  &Amount
G.EMPLID
G.EMPL_RCD
G.PAYMENT_MTHD

    Let $Emplid  = &G.EMPLID
    Let $EmplRcd = &G.EMPL_RCD
    Let #EmplRcd = &G.EMPL_RCD
    Let $PayMthd = &G.PAYMENT_MTHD
    Let #Amount  = &Amount

    if $Print = 'Y'
       let $FieldValue = $PayMthd
       do Read-Translate-Table
       Do Get-HK-ID
       Do Get-Name
       Do Get-Membership
       Do Print-Values
    End-if

    Add 1 to #TotRows
    Add #Amount to #PAmount

    FROM PS_GP_PAYMENT G
    WHERE G.RECIPIENT_ID = $RecipientID
      AND G.PAY_ENTITY = $PayEntity
      AND G.DEBIT_DT   = $DebitDt
      AND G.RSLT_SEG_NUM = (SELECT MAX(G1.RSLT_SEG_NUM) FROM PS_GP_PAYMENT G1
                                  WHERE G1.CAL_RUN_ID = G.CAL_RUN_ID
                                    AND G1.PAY_ENTITY = G.PAY_ENTITY)
GROUP BY G.EMPLID, G.EMPL_RCD, G.PAYMENT_MTHD
END-SELECT
end-procedure ! Process-Main
!
!***********************************************************************
! Get-HK-ID
!***********************************************************************
Begin-Procedure Get-HK-ID
    Let $HK_ID = ''
Begin-SELECT
N.NATIONAL_ID
    Let $HK_ID = rtrim(&N.NATIONAL_ID,' ')
    if length($HK_ID) = 8
       Let $HK_ID = ' ' || $HK_ID
    End-if
   FROM PS_GPHK_EE_NID_VW N
  WHERE EMPLID = $Emplid
End-SELECT
end-procedure !
!
!***********************************************************************
! Get-Name
!***********************************************************************
Begin-Procedure Get-Name
Begin-SELECT
A.NAME
     Let $Name = &A.NAME
   FROM PS_NAMES A
  WHERE A.EMPLID = $Emplid
    AND A.NAME_TYPE = 'PRI'
End-SELECt   
End-Procedure
!
!***********************************************************************
! Print-Values
!***********************************************************************
Begin-procedure Print-Values
   if #LC > 50
      if #PageNo > 1
         new-page
      end-if
      Do Print-Heading
      Add 1 to #PageNo
   End-if
   if rtrim($HK_ID,' ') <> ''
      Print $HK_ID    (#LC,{Col1})  edit 'XXXXXXXX(X)'
   end-if
   Print $MemberID (#LC,{Col2})
   Print $Emplid   (#LC,{Col3})
   Print $EmplRcd  (#LC,{Col4})  edit '99'
   Print $Name     (#LC,{Col5})
   Print #Amount   (#LC,{Col6})
   Print $XlatLongName (#LC,{Col7})
   Let #LC = #LC + 1
End-Procedure
!
!***********************************************************************
! Delete-RunControl                                                    *
!***********************************************************************
begin-procedure Delete-RunControl
begin-SQL
DELETE FROM PS_GPHK_EFT_RUNCTL
    WHERE OPRID         = $PRCS_OPRID
        AND RUN_CNTL_ID = $PRCS_RUN_CNTL_ID;
end-SQL

begin-SQL
DELETE FROM PS_GPHK_RCP_RUNCTL
    WHERE OPRID         = $PRCS_OPRID
        AND RUN_CNTL_ID = $PRCS_RUN_CNTL_ID;
end-SQL
end-procedure ! Delete-RunControl
!
!***********************************************************************
! Get-Membership                                                       *
!***********************************************************************
Begin-Procedure Get-Membership
  Let $MemberID = ''
Begin-SELECT
VAR.GPHK_MEMBERSHIP_NO 
     Let $MemberID = rtrim(&VAR.GPHK_MEMBERSHIP_NO,' ')
  FROM PS_GPHK_MEMBERSHIP VAR 
  WHERE VAR.EMPLID       = $Emplid
    AND VAR.RECIPIENT_ID = $RecipientID
End-SELECT
End-Procedure
!
!********************************************************************
! Procedure :   Pay-Entity-Details
!********************************************************************
Begin-procedure Pay-Entity-Details
Begin-SELECT
PED.DESCR
    Let $PayEntityName = &PED.DESCR  
    FROM PS_GP_PYENT PED
     WHERE PED.PAY_ENTITY = $PayEntity
End-SELECT
End-Procedure   Pay-Entity-Details
!
!********************************************************************
! Procedure :   Recp-Member-Details
!********************************************************************
Begin-procedure Recp-Member-Details
    Let $RecpntRegNum = ''
    Let $CommType = ''
    Let $CommOptn = ''
    Let $RecpntRegNum   = ''
    Let $RecpntCommType = ''
    Let $RecpntCommOptn = ''
    Let #RecpntCommAmt  = 0
Begin-SELECT
RMD.GPHK_RCP_GROUP_ID
RMD.GPHK_RCP_COMM_TYPE
RMD.GPHK_RCP_COMM_OPTN
RMD.GPHK_RCP_COMM_AMT
    Let $RecpntRegNum   = &RMD.GPHK_RCP_GROUP_ID
    Let $RecpntCommType = &RMD.GPHK_RCP_COMM_TYPE
    Let $RecpntCommOptn = &RMD.GPHK_RCP_COMM_OPTN
    Let #RecpntCommAmt  = &RMD.GPHK_RCP_COMM_AMT

    Let $FieldName = 'GPHK_RCP_COMM_TYPE'
    Let $FieldValue = $RecpntCommType
    Do Read-Translate-Table
    Let $CommType = $XlatLongName
 
    Let $FieldName = 'GPHK_RCP_COMM_OPTN'
    Let $FieldValue = $RecpntCommOptn
    Do Read-Translate-Table
    Let $CommOptn = $XlatLongName

    FROM PS_GPHK_RCP_PYNT_S RMD
     WHERE RMD.RECIPIENT_ID = $RecipientID
       AND RMD.PAY_ENTITY   = $PayEntity
End-SELECT
End-Procedure   Recp-Member-Details
!
#include 'datemath.sqc'     ! Date conversion procedures
#include 'sqrtrans.sqc'     ! Translate SQR strings to given language
#include 'prcslng.sqc'      ! Replaces getrplng.sqc
#include 'readxlat.sqc'     ! Procedure to read values from xlattable
#include 'reset.sqc'        ! Reset printer procedure
#include 'stdapi.sqc'       ! routines for process scheduler manipulation.


