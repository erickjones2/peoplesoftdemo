!***********************************************************************
!  PAY720: Print contents of Garnishment Prorate Rule Table            *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:42:32                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'  !printer and page-size initialization

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  move 'PAY720' to $ReportID

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  move 'Garnishment Proration Rule Listing' to $ReportTitle

  display ' '
  display $ReportTitle
  display ' '

end-procedure

begin-HEADING 7

  #Include 'stdhdg01.sqc'

  print 'State'                  (+1,1)
  print 'Prorate Type'           (0,8)
  print 'Proration ID'           (0,21)
  print 'Proration Description'  (0,34)
  print 'Effective Date'         (0,107)
  print 'Maintenance Resp'       (0,123)

end-HEADING

!***********************************************************************
!  PROC PROCESS-MAIN
!         Main Procedure
!***********************************************************************

BEGIN-PROCEDURE Process-Main
BEGIN-SELECT

A.STATE_GARN_PRORATE
 if RTRIM(&A.State_Garn_Prorate, ' ') = '$U'
   print 'FED'                 (+2,1,5)
 else
   print &A.State_Garn_Prorate (+2,1,5)
 end-if
A.PRORATE_TYPE
B.DESCRLONG
A.GARN_PRORATE_RL_ID           (0,21,6)
A.DESCR
A.MAINT_RESPONSBLTY
A.EFFDT

  let $FieldName  = 'PRORATE_TYPE'
  let $FieldValue = &A.Prorate_Type
  let $AsOfDate   = &A.Effdt
  Do Read-Translate-Table
  print $XlatShortName          (0,8,10)

  let $FieldName  = 'MAINT_RESPONSBLTY'
  let $FieldValue = &A.Maint_Responsblty
  let $AsOfDate   = &A.Effdt
  Do Read-Translate-Table
  print $XlatLongName          (0,123,10)

  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                   (0,107,10)

  let $PRNT_DescrLong = &B.Descrlong

  while INSTR($PRNT_DescrLong,'  ',1)
    let #locnblank = INSTR($PRNT_DescrLong,'  ',1)
    let $PRNT_DescrLong = {ps-substr}($PRNT_DescrLong,1,#locnblank - 1)   ||
                    {ps-substr}($PRNT_DescrLong,#locnblank + 1,2100)
  end-while

  if RTRIM($PRNT_DescrLong,' ') <> ''
    print $PRNT_DescrLong      (0,34)      wrap 71 21
  end-if

FROM  PS_GARN_PRORATE_DF B, PS_GARN_PRORATE_RL A
WHERE B.GARN_PRORATE_RL_ID   = A.GARN_PRORATE_RL_ID
ORDER BY A.STATE_GARN_PRORATE, A.GARN_PRORATE_RL_ID, A.EFFDT DESC
END-SELECT
END-PROCEDURE

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
