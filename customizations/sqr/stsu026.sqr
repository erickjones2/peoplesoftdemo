!--------------------------------------------------------------------------------------
! Report Name: STSU026.SQR - Process Groups
!--------------------------------------------------------------------------------------
!
!
!                                                               
! This software and related documentation are provided under a  
! license agreement containing restrictions on use and          
! disclosure and are protected by intellectual property         
! laws. Except as expressly permitted in your license agreement 
! or allowed by law, you may not use, copy, reproduce,          
! translate, broadcast, modify, license, transmit, distribute,  
! exhibit, perform, publish or display any part, in any form or 
! by any means. Reverse engineering, disassembly, or            
! decompilation of this software, unless required by law for    
! interoperability, is prohibited.                              
! The information contained herein is subject to change without 
! notice and is not warranted to be error-free. If you find any 
! errors, please report them to us in writing.                  
!                                                               
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
! All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!
!          $Date:  2013/01/18:02:57:47                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!----------------------------------------------------------
! Program Descr: This report prints Process Groups.
!----------------------------------------------------------
! Tables:        ST_PROCESS_GRP, R_PRCS_GRP_DTL, COMPANY_TBL,
!                PAYGROUP_TBL
! Sorts:         By Process Group Name
!----------------------------------------------------------

#include 'setenv.sqc'

!--------------------------------------------------------------------
Begin-Setup
!--------------------------------------------------------------------

 #include 'ptpsl177.sqc'

End-Setup

!--------------------------------------------------------------------
! Procedure: Begin-Program
! Descr:     Main Report procedure.
!--------------------------------------------------------------------
Begin-Program

 do Init-Report
 do Main-Process
 do Reset
 do Successful-EOJ

End-Program

!--------------------------------------------------------------------
! Procedure: Init-Report
! Descr:     Report initialization procedure.  Set titles, parameters.
!            This is called from the Begin-Program procedure.
!--------------------------------------------------------------------
begin-procedure Init-Report

 do Init-DateTime
 do Init-Number
 do Get-Current-DateTime
 do Define-Prcs-Vars
 do Get-Run-Control-Parms
 let $ReportId = 'STSU026'

 do Init_Report_Translation ('STOCK', $language_cd)
 do Append_Report_Translation ($ReportID)

 let $AsofDate = $AsofToday

 do Get_Field_Information ('STOCK',    'COMPANY',       $HCompany,    #DW)
 do Get_Field_Information ('STOCK',    'PAYGROUP',      $HPaygroup,   #DW)
 do Get_Field_Information ('STOCK',    'CURRENCY_CD',   $HCurr,       #DW)
 do Get_Field_Information ('STOCK',    'PROCESS_GRP',   $ProcessGrp,  #DW)
 do Get_Field_Information ('STOCK',    'EFFDT',         $EffDtLbl,    #DW)

 do Get_Field_Information ('STSU026',  'DESCR',         $HDescr,      #DW)
 do Get_Field_Information ('STSU026',  'CONTACT',       $HContact,    #DW)
 do Get_Field_Information ('STSU026',  'PHONE',         $HPhone,      #DW)
 do Get_Field_Information ('STSU026',  'STSU026',       $ReportTitle, #DW)

 show   ' '
 show   $ReportTitle
 show   ' '

end-procedure

!*******************************************************************
begin-heading 4
!*******************************************************************

#include 'stdhdgtr.sqc'

end-heading


!----------------------------------------------------------
! Section: Main-Process
! Descr:   Main Select
!----------------------------------------------------------
begin-procedure Main-Process

begin-Select
A.PROCESS_GROUP
A.EFFDT
A.DESCR
A.CONTACT_EMPLID
A.PHONE
A.CURRENCY_CD
CUR.DESCR

 print $ProcessGrp                              (+1,1,13)
 print ':'                                      (,,1)
 print &a.process_group                         (,18,10)
 print &a.descr                                 (,+2,30)

 print $EffDtLbl                                (+1,1,8)
 print ':'                                      (,,1)

 do Format-DateTime (&a.effdt, $EffDt, {DEFDATE}, '', '')
 print $EffDt                                   (,18,10)

 let $Emplid = &a.contact_emplid

  do Get-Employee-Name

 print $HContact                                (+2,1,7)
 print ':'                                      (,,1)
 print $Name                                    (0,18,30)

 print $HPhone                                  (+1,1,5)
 print ':'                                      (,,1)
 print &a.phone                                 (0,18,15)

 print $HCurr                                   (+1,1,8)
 print ':'                                      (,,1)
 print &a.currency_cd                           (0,18,3)
 print &cur.descr                               (0,+2,30)

 print $HCompany                                (+2,1,7)
 print $HPayGroup                               (0,45,8)
 print '-'                                      (+1,1,80) fill

 do Get-Detail

 new-page

FROM PS_ST_PROCESS_GRP A,
     PS_CURRENCY_CD_TBL CUR
WHERE A.CURRENCY_CD = CUR.CURRENCY_CD
  AND CUR.EFFDT = (SELECT MAX(CUR2.EFFDT)
                   FROM PS_CURRENCY_CD_TBL CUR2
                   WHERE CUR2.CURRENCY_CD = CUR.CURRENCY_CD
                     AND CUR2.EFFDT <= $AsOfToday)
end-select

end-procedure Main-Process


!----------------------------------------------------------
! Section: Get-Detail
! Descr:   Retrieve and print company and paygroups
!----------------------------------------------------------
begin-procedure Get-Detail

begin-Select
B.COMPANY
B.PAYGROUP

 let $PayGroup = rtrim(&b.paygroup,' ')

 if $PayGroup = ''
    do Get-Wildcard
 else
    do Print-Detail
 end-if

FROM  PS_ST_PRCS_GRP_DTL B
WHERE B.PROCESS_GROUP = &A.PROCESS_GROUP
  AND B.EFFDT = &A.EFFDT
end-select

end-procedure Get-Detail


!----------------------------------------------------------
! Section: Get-Wildcard
! Descr:   Retrieve and print all paygroups for a company.
!----------------------------------------------------------
begin-procedure Get-Wildcard

begin-Select
E.PAYGROUP

 let $PayGroup = rtrim(&e.paygroup,' ')

 do Print-Detail

FROM  PS_PAYGROUP_TBL E
WHERE E.COMPANY = &B.COMPANY
  AND E.EFF_STATUS = 'A'
  AND E.EFFDT = (SELECT MAX(E2.EFFDT)
                   FROM PS_PAYGROUP_TBL E2
                   WHERE E2.PAYGROUP = E.PAYGROUP
                     AND E2.EFFDT <= &A.EFFDT)
end-select

end-procedure Get-Wildcard


!----------------------------------------------------------
! Section: Print-Detail
! Descr:   Print
!----------------------------------------------------------
begin-procedure Print-Detail

 do Get-Company-Descr
 do Get-PayGroup-Descr

 print &b.company               (+1,1,3)
 print $CoDescr                 (,6,30)
 print $PayGroup                (,45,3)
 print $PgDescr                 (,50,30)

end-procedure Print-Detail


!----------------------------------------------------------
! Section: Get-Company-Descr
! Descr:   Retrieves company description
!----------------------------------------------------------
begin-procedure Get-Company-Descr

begin-Select
C.DESCR

 let $CoDescr = &c.descr

FROM  PS_COMPANY_TBL C
WHERE &B.COMPANY = C.COMPANY
  AND C.EFFDT = (SELECT MAX(C2.EFFDT)
                   FROM PS_COMPANY_TBL C2
                   WHERE C2.COMPANY = C.COMPANY
                     AND C2.EFFDT <= &A.EFFDT)
end-select

end-procedure Get-Company-Descr


!----------------------------------------------------------
! Section: Get-Paygroup-Descr
! Descr:   Retrieves paygroup description
!----------------------------------------------------------
begin-procedure Get-Paygroup-Descr

begin-Select
D.DESCR

 let $PgDescr = &D.descr

FROM  PS_PAYGROUP_TBL D
WHERE $Paygroup = D.PAYGROUP
  AND D.EFFDT = (SELECT MAX(D2.EFFDT)
                   FROM PS_PAYGROUP_TBL D2
                   WHERE D2.COMPANY = D.COMPANY
                     AND D2.EFFDT <= &A.EFFDT)
end-select

end-procedure Get-Company-Descr

!-----------------------------------------------------------------------
! SQC Includes
!-----------------------------------------------------------------------
#include 'prcsapi.sqc'
#include 'prcsdef.sqc'    ! Update Process Open_Trans variable declaration
#include 'eoj.sqc'        ! End of Report Format
#include 'sqrtrans.sqc'   ! Access Strings Table
#include 'reset.sqc'      ! Reset printer procedure
#include 'curdttim.sqc'   ! Get-Current-DateTime procedure
                          ! Procedures: Get-Current-DateTime
#include 'datemath.sqc'
#include 'datetime.sqc'   ! Routines for date and time formatting
                          ! Procedures: Init-Datetime
                          ! Procedures: Format-DateTime
                          ! Procedures: Diff-Date
                          ! Procedures: many others.
#include 'number.sqc'     ! Procedures: Init-Number
#include 'getempnm.sqc'   ! Retrieves employee name
