!*********************************************************
!  GetErnNm:  Get Earnings Name from the Earnings Table
!*********************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:31:04                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!********************************
begin-procedure Get-Earnings-Name
!********************************

  !Keys passed in:  $ErnCd, $AsOfDate
  !Values returned: $EarningsName, $EarningsAbbrv

  move ''  to $EarningsName
  move ''  to $EarningsAbbrv

  if Rtrim($ErnCd,' ') = ''
    goto GetErnNmExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
ET.DESCR
ET.DESCRSHORT
ET.GL_EXPENSE
ET.SHIFT_DIFF_ELIG
ET.FACTOR_MULT
ET.FACTOR_RATE_ADJ
ET.FACTOR_HRS_ADJ
ET.FACTOR_ERN_ADJ

   let $EarningsName      = Rtrim(&ET.DESCR,' ')
   let $EarningsAbbrv     = Rtrim(&ET.DESCRSHORT,' ')
   let $ShiftDiffElig     = &ET.SHIFT_DIFF_ELIG
   let #FactorMult        = &ET.FACTOR_MULT
   let #FactorRateAdj     = &ET.FACTOR_RATE_ADJ
   let #FactorHrsAdj      = &ET.FACTOR_HRS_ADJ
   let #FactorErnAdj      = &ET.FACTOR_ERN_ADJ

FROM  PS_EARNINGS_TBL ET
WHERE ET.ERNCD  = $ErnCd
  AND ET.EFFDT <= $AsOfDate
  AND ET.EFFDT  =
      (SELECT MAX(EFFDT)
       FROM   PS_EARNINGS_TBL
       WHERE  ERNCD  = ET.ERNCD
         AND  EFFDT <= $AsOfDate)
end-SELECT

  do Get-Earnings-Name-Lang

GetErnNmExit:

end-procedure


!*************************************
begin-procedure Get-Earnings-Name-Lang
!*************************************

  !Keys passed in:  $ErnCd, $AsOfDate
  !Values returned: $EarningsName, $EarningsAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetErnNmLngExit
  end-if

  if Rtrim($ErnCd,' ') = ''
    goto GetErnNmLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
ETL.DESCR
ETL.DESCRSHORT

   if Rtrim(&ETL.DESCR,' ') <> ''
      let $EarningsName = Rtrim(&ETL.DESCR,' ')
   end-if
   if Rtrim(&ETL.DESCRSHORT,' ') <> ''
      let $EarningsAbbrv = Rtrim(&ETL.DESCRSHORT,' ')
   end-if

FROM  PS_EARNINGS_LANG ETL
WHERE ETL.ERNCD       = $ErnCd
  AND ETL.LANGUAGE_CD = $Curr_Language_Cd
  AND ETL.EFFDT      <= $AsOfDate
  AND ETL.EFFDT       =
      (SELECT MAX(EFFDT)
       FROM   PS_EARNINGS_LANG
       WHERE  ERNCD       = ETL.ERNCD
         AND  LANGUAGE_CD = ETL.LANGUAGE_CD
         AND  EFFDT      <= $AsOfDate)
end-SELECT

GetErnNmLngExit:

end-procedure

!***************************** End of Include File **************************

