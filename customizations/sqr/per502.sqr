!********************************************************
!  PER502: Update Department Entry Date on JOB record   *
!********************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:13:59:11                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
Begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup


begin-REPORT
  do Init-Number
  do Init-DateTime
  move 'Update Department Entry Date' to $update_date_message
  move 'Department Entry Date Changed' to $change_date_message
  move 'Change Employee              ' to $change_empl_message
  move 'SQL error on attempted update' to $SQL_error
  do Process-Main
  do Commit-Transaction
end-REPORT


!***********************************************************************
!  PROC Process-Main
!         Main Procedure
!***********************************************************************

begin-PROCEDURE Process-Main

begin-SELECT

A.EMPLID    () on-break print=never level=1 procedure=Reset-Empl-Dept
A.EMPL_RCD () on-break print=never level=2 procedure=Reset-Empl-Dept
A.EFFDT
A.EFFSEQ
A.DEPTID
  if &A.DeptID <> $prior_DeptID
    move &A.DeptID to $prior_DeptID
    move &A.Effdt to $Dept_Entry_Dt
    move $change_date_message to $message
  else
    move $update_date_message to $message
  end-if
  do Update-Dept-Entry-Date

FROM PS_JOB A

ORDER BY A.EMPLID, A.EMPL_RCD, A.EFFDT , A.EFFSEQ

end-SELECT
end-PROCEDURE

!***********************************************************************

begin-PROCEDURE  Reset-Empl-Dept

  if Rtrim(&A.EmplID, ' ') <> $prior_EmplID
    display 'Updating ' noline
    display &A.EmplID
    move &A.EmplID to $prior_EmplID
  end-if
  move &A.DeptID to $prior_DeptID
  move &A.Effdt to $Dept_Entry_Dt
  move $change_empl_message to $message
  do Update-Dept-Entry-Date

end-PROCEDURE


begin-procedure Update-Dept-Entry-Date
begin-SQL on-error=SQL-Error
UPDATE    PS_JOB
   SET    DEPT_ENTRY_DT    = $Dept_Entry_Dt
 WHERE    EMPLID           = &A.EmplID
   AND    EMPL_RCD        = &A.EMPL_RCD
   AND    EFFDT            = &A.Effdt
   AND    EFFSEQ           = &A.EffSeq
end-SQL
  do Print-Exception
end-procedure

!***********************************************************************

begin-procedure SQL-Error

  move $SQL_error to $message
  do Print-Exception

end-procedure

!***********************************************************************

begin-PROCEDURE  Print-Exception

  let $out = Rtrim(&A.EmplID, ' ')
  print $out                 (+1,1)
  print &A.EMPL_RCD         (+0,+2,10)
  do Format-DateTime(&A.EffDt, $out, {DEFDATE}, '', '')
  print $out                 (+0,+2,10)
  print &A.EffSeq            (+0,+2,5)
  print $message             (+0,+2,50)
end-PROCEDURE

#Include 'number.sqc'    !Routines to format numbers
#Include 'datetime.sqc'  !Routines to format dates and times
#Include 'tranctrl.sqc'  !Common Transaction Handling
