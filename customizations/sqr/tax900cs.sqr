!*************************************
!  TAX900CS: Year End Consent Report *
!*************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2016, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!----------------------------------------------------------------------
!
!       $Release:  HR92                                                !
!           $Bug:  23205071                                            !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc' !Set environment
#Include 'setup32.sqc'   !Printer and page-size initialization

#define col_Name               1
#define col_ID                50
#define col_Date              65
#define col_SeqNum            85
#define col_Consent_Status    95
#define col_Notify_R          95
#define col_Notify_A         112

begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-procedure Init-Report
  do Stdapi-Init
  move 'TAX900CS' to $ReportID

  move 'Electronic Year End Form Consent Change Report' to $ReportTitle
  display $ReportTitle

  do Init-DateTime
  do Init-Number

  do Get-Report-Parameters

end-procedure


begin-heading 8

  #Include 'stdhtr08.sqc'

  Evaluate $RC_US_YE.Consent_Proc_Flag

  when = 'C'
    move 'Consent Received'  to $Report_Header
    print $Report_Header                   () Center
    print 'Employee Name'                  (+3,{col_Name})
    print 'ID'                             (0,{col_ID})
    print 'Consent Date'                   (0,{col_Date})
    print 'Seq#'                           (0,{col_SeqNum})
    break
  when = 'R'
    move 'Consent Reset By Employer' to $Report_Header
    print $Report_Header                   () Center
    print 'Employee Name'                  (+3,{col_Name})
    print 'ID'                             (0,{col_ID})
    print 'Reset Date'                     (0,{col_Date})
    print 'Seq#'                           (0,{col_SeqNum})
    print 'Reset Email Sent'               (0,{col_Notify_R})
    break
  when = 'W'
    move 'Withdrawn Consent Received' to $Report_Header
    print $Report_Header                   () Center
    print 'Employee Name'                  (+3,{col_Name})
    print 'ID'                             (0,{col_ID})
    print 'Withdrawn Date'                 (0,{col_Date})
    print 'Seq#'                           (0,{col_SeqNum})
    break
  when = 'A'
    move 'Consent Summary' to $Report_Header
    print $Report_Header                   () Center
    print 'Employee Name'                  (+3,{col_Name})
    print 'ID'                             (0,{col_ID})
    print 'Effective Date'                 (0,{col_Date})
    print 'Seq#'                           (0,{col_SeqNum})
    print 'Consent Status'                 (0,{col_Consent_Status})
    print 'Reset Email Sent'               (0,{col_Notify_A})
    break
  when-other
    break
  end-evaluate

end-heading


begin-procedure Process-Main

  if $Proc_Summary = 'Y'
    do Process-Consent-Summary
  else
    do Process-Consent-Record
  end-if

  if #Employee_Count > 0
    print 'Total no. of employees: '        (+2,1)

    do Format-Number(#Employee_Count, $Employee_Count,  '999,999,999,999')
    print $Employee_Count                  (0, 23)

  else
    evaluate $RC_US_YE.Consent_Proc_Flag
    when = 'C'
      print '* No employees have provided consent *'                 (+2,5)
      break
    when = 'R'
      print '* No employees have reset consent *'                    (+2,5)
      break
    when = 'W'
      print '* No employees have withdrawn consent *'                (+2,5)
      break
    when = 'A'
      print '* No employees have provided/reset/withdrawn consent *' (+2,5)
      break
    when-other
      break
    end-evaluate
  end-if

end-procedure


begin-procedure Process-Consent-Record

  let #Employee_Count = 0

begin-select

B.EMPLID
C.NAME_PSFORMAT
B.EFFDT
B.EFFSEQ
B.YE_CONSENT_CURRENT

  let $EE_Name =  rtrim(&C.NAME_PSFORMAT, ' ')
  print $EE_Name                           (+1,{col_Name}) on-break level=1 skiplines=1

  let $EE_ID   =  rtrim(&B.EMPLID, ' ')
  print $EE_ID                             (0,{col_ID})    on-break level=1

  do Format-DateTime(&B.EFFDT, $out, {DEFDATE}, '', '')
  print $out                               (0,{col_Date})

  do Format-Number(&B.EFFSEQ, $out, '999')
  print $out                               (0,{col_SeqNum})

  if $RC_US_YE.Consent_Proc_Flag  = 'R'
     do Employee-Notified
     print $Notified                        (0,103)
  end-if

  if $RC_US_YE.Consent_Proc_Flag  = 'A'
    move &B.YE_CONSENT_CURRENT   to $FieldValue
    move 'YE_CONSENT_CURRENT'    to $FieldName
    do Read-Translate-Table
    move $XlatShortName to $ConsentStatus
    print $ConsentStatus                   (0,{col_Consent_Status})
    if &B.YE_CONSENT_CURRENT  = 'R'
       do Employee-Notified
       print $Notified                     (0,116)
    end-if
  end-if

  if $EE_ID <> $Prior_EE_ID
    add 1 to #Employee_Count
  end-if

  move $EE_ID to $Prior_EE_ID

FROM PS_PY_FORMSTAT_USA B,
     PS_PERSON_NAME C
WHERE B.YE_CONSENT_CURRENT = $RC_US_YE.Consent_Proc_Flag
 AND  B.EFFDT =
   (SELECT MAX(B1.EFFDT)
      FROM PS_PY_FORMSTAT_USA B1
      WHERE B1.EMPLID = B.EMPLID
        AND B1.EFFDT  >= $RC_US_YE.FROMDATE
        AND B1.EFFDT  <= $RC_US_YE.THRUDATE)
 AND  B.EFFSEQ =
   (SELECT MAX(B2.EFFSEQ)
      FROM PS_PY_FORMSTAT_USA B2
      WHERE B2.EMPLID = B.EMPLID
        AND B2.EFFDT  = B.EFFDT)
 AND  B.EMPLID = C.EMPLID
ORDER BY C.NAME_PSFORMAT ASC, B.EMPLID ASC
end-select
end-procedure

begin-procedure Process-Consent-Summary

  let #Employee_Count = 0

begin-select

S.EMPLID
N.NAME_PSFORMAT
S.EFFDT
S.EFFSEQ
S.YE_CONSENT_CURRENT

  let $EE_Name =  rtrim(&N.NAME_PSFORMAT, ' ')
  print $EE_Name                           (+1,{col_Name}) on-break level=1 skiplines=1

  let $EE_ID   =  rtrim(&S.EMPLID, ' ')
  print $EE_ID                             (0,{col_ID})    on-break level=1

  do Format-DateTime(&S.EFFDT, $out, {DEFDATE}, '', '')
  print $out                               (0,{col_Date})

  do Format-Number(&S.EFFSEQ, $out, '999')
  print $out                               (0,{col_SeqNum})

  if $RC_US_YE.Consent_Proc_Flag  = 'R'
     do Employee-Notified
     print $Notified                        (0,103)
  end-if

  if $RC_US_YE.Consent_Proc_Flag  = 'A'
    move &S.YE_CONSENT_CURRENT   to $FieldValue
    move 'YE_CONSENT_CURRENT'    to $FieldName
    do Read-Translate-Table
    move $XlatShortName to $ConsentStatus
    print $ConsentStatus                   (0,{col_Consent_Status})
    if &S.YE_CONSENT_CURRENT  = 'R'
       do Employee-Notified
       print $Notified                     (0,120)
    end-if
  end-if

  if $EE_ID <> $Prior_EE_ID
    add 1 to #Employee_Count
  end-if

  move $EE_ID to $Prior_EE_ID


FROM PS_PY_FORMSTAT_USA S,
     PS_PERSON_NAME N
WHERE S.EMPLID   =  N.EMPLID
 AND  S.EFFDT   >= $RC_US_YE.FromDate
 AND  S.EFFDT   <= $RC_US_YE.ThruDate
 AND  (S.YE_CONSENT_CURRENT = $Consent OR
       S.YE_CONSENT_CURRENT = $ResetConsent OR
       S.YE_CONSENT_CURRENT = $Withdrawn)
ORDER BY N.NAME_PSFORMAT ASC, S.EMPLID ASC, S.EFFDT DESC, S.EFFSEQ DESC

end-select
end-procedure


begin-procedure Get-Report-Parameters

  do Select-US-Year-End-Parameters

  let $FromDate          = $RC_US_YE.FromDate
  let $ThruDate          = $RC_US_YE.ThruDate

  if $RC_US_YE.Consent_Proc_Flag  = 'A'
    let $Consent      = 'C'
    let $ResetConsent = 'R'
    let $Withdrawn    = 'W'
    let $Proc_Summary = 'Y'
  end-if

end-procedure

begin-procedure Employee-Notified

  let $Notified = ' '

begin-select

X.YE_NOTIF
X.LASTUPDDTTM

  if &X.YE_NOTIF = 'N' 
     let $Notified = 'N'
  end-if  

FROM PS_PY_RES_USA_TA0 X
WHERE X.EMPLID = $EE_ID
  AND X.LASTUPDDTTM =
      (SELECT MAX(X1.LASTUPDDTTM)
       FROM PS_PY_RES_USA_TA0 X1
       WHERE X1.EMPLID = X.EMPLID)
end-select
end-procedure


#Include 'txrnctl2.sqc'  !US Year End Process Scheduler Run Controls
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'getftdts.sqc'  !Get-From-Thru-Dates procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
