!****************************************
!  NetInWds:  Convert Net Pay to Words  *
!****************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:03:59:40                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

Begin-Setup
DECLARE-VARIABLE
       INTEGER #Index #Index2
END-DECLARE
END-Setup

begin-procedure Net-Pay-In-Words-GPIN (#NetPay, :$NetInWords)

  create-array name=NumberLits-GPIN size=91 field=NumLit:char
  put 'One'       into NumberLits-GPIN(01)
  put 'Two'       into NumberLits-GPIN(02)
  put 'Three'     into NumberLits-GPIN(03)
  put 'Four'      into NumberLits-GPIN(04)
  put 'Five'      into NumberLits-GPIN(05)
  put 'Six'       into NumberLits-GPIN(06)
  put 'Seven'     into NumberLits-GPIN(07)
  put 'Eight'     into NumberLits-GPIN(08)
  put 'Nine'      into NumberLits-GPIN(09)
  put 'Ten'       into NumberLits-GPIN(10)
  put 'Eleven'    into NumberLits-GPIN(11)
  put 'Twelve'    into NumberLits-GPIN(12)
  put 'Thirteen'  into NumberLits-GPIN(13)
  put 'Fourteen'  into NumberLits-GPIN(14)
  put 'Fifteen'   into NumberLits-GPIN(15)
  put 'Sixteen'   into NumberLits-GPIN(16)
  put 'Seventeen' into NumberLits-GPIN(17)
  put 'Eighteen'  into NumberLits-GPIN(18)
  put 'Nineteen'  into NumberLits-GPIN(19)
  put 'Twenty'    into NumberLits-GPIN(20)
  put 'Thirty'    into NumberLits-GPIN(30)
  put 'Forty'     into NumberLits-GPIN(40)
  put 'Fifty'     into NumberLits-GPIN(50)
  put 'Sixty'     into NumberLits-GPIN(60)
  put 'Seventy'   into NumberLits-GPIN(70)
  put 'Eighty'    into NumberLits-GPIN(80)
  put 'Ninety'    into NumberLits-GPIN(90)

  move #NetPay to $NetPay 099999999.99
  LET $NetInWords = ''
  if #NetPay < 1.00
    move 'Nil' to $NetInWords
  else
    move '' to $NetInWords
    move $NetPay to $Input xxx        !isolate Millions
    if $Input <> '000'
      do Convert-Input-GPIN ($Input, $NetInWords)
      concat ' Million ' with $NetInWords
    end-if
    move $NetPay to $Input ~~~xxx     !isolate Thousands
    if $Input <> '000'
      do Convert-Input-GPIN ($Input, $NetInWords)
      concat ' Thousand ' with $NetInWords
    end-if
    move $NetPay to $Input ~~~~~~xxx  !isolate Hundreds
    do Convert-Input-GPIN ($Input, $NetInWords)
  end-if
  move $NetPay to $Input ~~~~~~~~~~xx  !isolate Paisa
  if $Input <> '00'
    let $SPaisa = ''
    do Append-Literal-GPIN ($Input, $SPaisa)
    concat ' and '  with $NetInWords
    concat $SPaisa  with $NetInWords
    concat ' Paise' with $NetInWords
  end-if
end-procedure

begin-procedure Convert-Input-GPIN ($Input, :$NetInWords)
  move $Input to $Index x~~  !isolate the "hundreds" position
  if $Index <> '0'
    do Append-Literal-GPIN ($Index, $NetInWords)
    concat ' Hundred ' with $NetInWords
  end-if
  move $Input to $Index ~xx  !isolate the "tens" and "units"
  do Append-Literal-GPIN ($Index, $NetInWords)
end-procedure

begin-procedure Append-Literal-GPIN ($Index, :$NetInWords)
  move $Index to #Index
  if #Index < 20
    get $NumLit from NumberLits-GPIN(#Index)
    concat $NumLit with $NetInWords
  else
    move $Index to $Index x  !isolate the "tens" position
    move $Index to #Index2
    multiply 10 times #Index2
    get $NumLit from NumberLits-GPIN(#Index2)
    concat $NumLit with $NetInWords
    if #Index > #Index2
      subtract #Index2 from #Index
      get $NumLit from NumberLits-GPIN(#Index)
      concat '-' with $NetInWords
      concat $NumLit with $NetInWords
    end-if
  end-if
end-procedure
