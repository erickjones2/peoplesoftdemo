!*****************************************
!  PER032:  Passport/Visa Expiration     *
!*****************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2017, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  26236670                                            !
!    Revision   :  09.22.2000  BSHAK-s79A8                             *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
begin-setup
#Include 'ptpsp125.sqc'   !Printer and page-size initialization
end-setup

begin-report

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


begin-heading 6
do Get_Field_Information ('PER032',     'REPORT_TITLE',         $REPORTTITLE,   #DW)
do Get_Field_Information ('PER032',     'EMPLID',               $EMPLID,        #DW)
  #Include 'stdhdgtr.sqc'

do Get_Field_Information ('HR',         'COUNTRY',              $COUNTRY,       #DW)
do Get_Field_Information ('HR',         'PASSPORT_NBR',         $PASSPORT,      #DW)
do Get_Field_Information ('HR',         'ISSUE_DT',             $ISSUE,         #DW)
do Get_Field_Information ('HR',         'EXPIRATN_DT',          $EXPIRE,        #DW)
do Get_Field_Information ('PER032',     'NAME',                 $NAME,          #DW)
do Get_Field_Information ('HR',         'DEPENDENT_ID',         $DEPID,         #DW)



PRINT $EMPLID                           (+2,1,10)
PRINT $DEPID                            (0,12,9)
PRINT $NAME                             (0,22,40)
PRINT $COUNTRY                          (0,63,7)
! PRINT $PASSPORT                         (0,71,0)
  print $Lbl_Nbr                    (0,71,15)
  print $Lbl_Type                   (0,87,4)

PRINT $ISSUE                            (0,92,8)
PRINT $EXPIRE                           (0,103)
end-heading

begin-procedure Init-Report

  move 'PER032' to $ReportID
  do Stdapi-Init
  let $language_cd = $PRCS_LANGUAGE_CD
  if $prcs_process_instance = ''
    !No Prompt
  else
    do Select-Parameters
  end-if
  do Init_Printer
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')

#ifdef ORACLE
#define TESTSUFFIX , 'YYYY-MM-DD'
#else
#define TESTSUFFIX
#end-if

end-procedure

begin-procedure Get-Values
let $language_cd = $PRCS_LANGUAGE_CD
end-procedure

begin-procedure Process-Main

  do Get_Field_Information ('HR',     'PASSPORT_NBR',         $PASS_NBR,   #DW)
  let $Lbl_Nbr = $PASS_NBR
  let $Lbl_Type = '    '
  do Passport-Expiration

  new-page

  do Get_Field_Information ('PER032',     'PERMIT_NBR',         $PERMIT_NBR,   #DW)
  do Get_Field_Information ('PER032',     'TYPE',        $TYPE,   #DW)
  let $Lbl_Nbr = $PERMIT_NBR
  let $Lbl_Type = $TYPE
  do Visa-Expiration

end-procedure


begin-procedure Visa-Expiration

begin-SELECT
A.EMPLID
A.DEPENDENT_ID
A.COUNTRY
A.VISA_WRKPMT_NBR
A.VISA_PERMIT_TYPE
A.DT_ISSUED
A.EXPIRATN_DT &expdate

  if not IsNULL(&expdate)
    do Convert-To-DTU-Date($AsOfToday, $CmpToday)
    do Convert-To-DTU-Date(&expdate , $expymd)
    do Dtu-Diff-Days($CmpToday, $expymd, #VisaDays)

    do Format-DateTime(&expdate, $expymd, {DEFDATE}, '', '')

    if #VisaDays <= 90 and #VisaDays > 0

      let $EmplID = &A.Emplid
      let $Dependent_ID = &A.Dependent_ID
      if RTRIM(&A.Dependent_ID, ' ') = '' !if null
        do Select-Employee
      else
        do Select-Dependent
      end-if

      if not IsNULL(&A.Dt_Issued)

        do Format-DateTime(&A.Dt_Issued, $Dt_Issued, {DEFDATE}, '', '')

      end-if

      print $Emplid                 (+1,1,10)
      print $Dependent_ID           (0,12,9)
      print $Name                   (0,22,40)
      print &A.Country              (0,63,7)
      print &A.Visa_WrkPmt_Nbr      (0,71,15)
      print &A.Visa_Permit_Type     (0,87,4)
      print $Dt_Issued              (0,92,10)
      print $expymd                 (0,103)
    end-if

  end-if
  
     
!Bug 22727242 11/22/2016 code begin 
FROM PS_JOB J,
PS_VISA_PMT_DATA A
WHERE J.EMPLID = A.EMPLID
AND J.JOB_INDICATOR = 'P'
AND J.EMPL_STATUS IN ('A', 'L', 'S', 'P', 'W') 
AND J.EFFDT =
               (SELECT MAX(J1.EFFDT)
                FROM PS_JOB J1
                WHERE J1.EMPLID  = J.EMPLID
                AND J1.EMPL_RCD = J.EMPL_RCD
                AND J1.EFFDT <= $AsofToday)

  AND J.EFFSEQ =
      (SELECT MAX(J2.EFFSEQ)
       FROM PS_JOB J2
       WHERE J2.EMPLID = J.EMPLID
         AND J2.EMPL_RCD = J.EMPL_RCD
         AND J2.EFFDT = J.EFFDT)
!Bug 22727242 11/22/2016 code end

ORDER BY A.EMPLID, A.DEPENDENT_ID, A.EXPIRATN_DT

end-SELECT

end-procedure


begin-procedure Passport-Expiration

begin-SELECT
B.EMPLID
B.DEPENDENT_ID
B.COUNTRY
B.PASSPORT_NBR
B.DT_ISSUED
B.EXPIRATN_DT &Expiratn_Dt


  if not IsNULL(&Expiratn_Dt)
    do Convert-To-DTU-Date($AsOfToday, $CmpToday)
    do Convert-To-DTU-Date(&Expiratn_Dt, $Expiratn_YMD)
    do Dtu-Diff-Days($CmpToday, $Expiratn_YMD, #PassportDays)
    do Format-DateTime(&Expiratn_Dt, $Expiratn_YMD, {DEFDATE}, '', '')
    if #PassportDays <= 90 and #PassportDays > 0
      let $EmplID = &B.Emplid
      let $Dependent_ID = &B.Dependent_ID
      if RTRIM(&B.Dependent_ID, ' ') = '' !if null
        do Select-Employee
      else
        do Select-Dependent
      end-if

      if not IsNULL(&B.Dt_Issued)
        do Format-DateTime(&B.Dt_Issued, $Dt_Issued, {DEFDATE}, '', '')
      end-if
  
      print $Emplid                 (+1,1,10)
      print $Dependent_ID           (0,12,9)
      print $Name                   (0,22,40)
      print &B.Country              (0,63,7)
      print &B.Passport_Nbr         (0,71,20)
      print $Dt_Issued              (0,92,10)
      print $Expiratn_YMD           (0,103)
    end-if

  end-if
  
!Bug 22727242 11/22/2016 code begin
FROM  PS_CITIZEN_PSSPRT B,
PS_JOB JJ
WHERE JJ.EMPLID = B.EMPLID
AND JJ.JOB_INDICATOR = 'P'
AND JJ.EMPL_STATUS IN ('A', 'L', 'S', 'P', 'W') 
AND JJ.EFFDT =
               (SELECT MAX(J11.EFFDT)
                FROM PS_JOB J11
                WHERE J11.EMPLID  = JJ.EMPLID
                AND J11.EMPL_RCD = JJ.EMPL_RCD
                AND J11.EFFDT <= $AsofToday)

  AND JJ.EFFSEQ =
      (SELECT MAX(J22.EFFSEQ)
       FROM PS_JOB J22
       WHERE J22.EMPLID = JJ.EMPLID
         AND J22.EMPL_RCD = JJ.EMPL_RCD
         AND J22.EFFDT = JJ.EFFDT)
!Bug 22727242 11/22/2016 code end

ORDER BY B.EMPLID, B.DEPENDENT_ID, B.EXPIRATN_DT

end-SELECT

end-procedure


begin-procedure Select-Employee

begin-SELECT
!*********Begin NAME DISPLAY CHANGE ********************
!C.NAME
C.NAME_DISPLAY
   !let $Name = &C.Name
   let $Name = &C.Name_Display
!*********End NAME DISPLAY CHANGE **********************
   

FROM PS_PERSON_NAME C
WHERE C.EMPLID=$EMPLID

end-SELECT

end-procedure


begin-procedure Select-Dependent

begin-SELECT
!*********Begin NAME DISPLAY CHANGE ********************
!D.NAME
D.NAME_DISPLAY
   !let $Name = &D.Name
   let $Name = &D.Name_Display
!*********End NAME DISPLAY CHANGE **********************

   

FROM PS_DEPENDENT_BENEF D
WHERE D.EMPLID=$EMPLID
  AND D.DEPENDENT_BENEF=$DEPENDENT_ID

end-SELECT

end-procedure


#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines
#include 'stdapi.sqc'    !Routines to update run status

#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'
#Include 'useprntr.sqc'
