!************************************************************************
!  STSU016:  Stock Plan
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:56:58                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup

   #include 'ptpsl177.sqc'  !Printer and page-size init (landscape)

end-setup

#define  col2    5
#define  col3    8
#define  col4    10
#define  col5    16
#define  col6    31
#define  col7    26
#define  col8    38
#define  col9    56
#define  col10   52
#define  col11   68
#define  col12   88
#define  col13   114
#define  col14   128
#define  col15   159

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let $ReportID = 'STSU016'

  do Get-Report-Language

  if $prcs_process_instance = ''
     do Prompts             !Prompt user

  else
     do Get-SQR-Parms
     do Get-Values
  end-if

  show    'FLOW: Init-Report'
  show    'Report Title: ' $REPORTTITLE

end-procedure

!******************************************************************************
begin-procedure Prompts
!******************************************************************************

!-----------------------------------------------------------------------------!
! Procedure: Prompts                                                          !
! Descr:     Prompts for all input parameters in the event the program is run !
!            directly from SQR.  In normal use, this should not happen.       !
!-----------------------------------------------------------------------------!

end-procedure

!************************************************************************
begin-procedure Get-Values
!************************************************************************

end-procedure

!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ('STOCK',$language_cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK','STOCK_PLAN_TYPE',   $STOCK_PLAN_TYPE,   #DW)
  do Get_Field_Information ('STOCK','STOCK_PLAN',        $STOCK_PLAN,        #DW)
  do Get_Field_Information ('STOCK','EXPIRATN_DT',       $EXPIRATN_DT,       #DW)
  do Get_Field_Information ('STOCK','SHARES_ALLOCATED',  $SHARES_ALLOCATED,  #DW)
  do Get_Field_Information ('STOCK','STOCK_ID',          $STOCK_ID,          #DW)

  do Get_Field_Information ('STSU016','STSU016',           $REPORTTITLE,       #DW)
  do Get_Field_Information ('STSU016','DESCR',             $DESCR,             #DW)
  do Get_Field_Information ('STSU016','APPROVAL_DT',       $APPROVAL_DT,       #DW)
  do Get_Field_Information ('STSU016','ALLOCATION_TYPE',   $ALLOCATION_TYPE,   #DW)
  do Get_Field_Information ('STSU016','ALLOCATION_DT',     $ALLOCATION_DT,     #DW)
  do Get_Field_Information ('STSU016','SEQ_NUM',           $SEQ_NUM,           #DW)
  do Get_Field_Information ('STSU016','TREASURY_ID',       $TREASURY_ID,       #DW)
  do Get_Field_Information ('STSU016','LAST_EMPLID',       $LAST_EMPLID,       #DW)
  do Get_Field_Information ('STSU016','LAST_DTTM_UPDATE',  $LAST_DTTM_UPDATE,  #DW)

end-procedure

!************************************************************************
begin-heading 3
!************************************************************************

#include 'stdhdgtr.sqc'

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

  let $Print = 'N'
  
begin-Select

A.STOCK_ID                () ON-BREAK Print=never before=Select-Dtl
A.STOCK_PLAN_TYPE
A.STOCK_PLAN
A.DESCRSHORT
A.APPRVL_DT
A.EXPIRATN_DT

  do Print-Info
  do Select-LowerDtl
  print ' '                    (+1,1,1)
  
  let $Print = 'Y'
  
 from     PS_STOCK_PLAN_TBL  A
order by  A.STOCK_ID

end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

B.STOCK_ID
B.DESCRSHORT

  if $Print = 'Y'
     new-page
  end-if
  
  do Print-data

 from  PS_STOCK_TBL B
where  B.STOCK_ID   =  &A.STOCK_ID

end-Select

end-procedure

!***********************************************************************
begin-procedure Select-LowerDtl
!***********************************************************************

begin-Select

F.ALLOCATION_DT

 from  PS_ST_PLAN_ALLOC   F
where  F.STOCK_ID         =  &A.STOCK_ID
  and  F.STOCK_PLAN_TYPE  =  &A.STOCK_PLAN_TYPE
  and  F.STOCK_PLAN       =  &A.STOCK_PLAN

end-Select

 if rtrim(&F.ALLOCATION_DT,' ') <> ''
    do Print-heading
 end-if


begin-Select

C.STOCK_ID
C.STOCK_PLAN_TYPE
C.STOCK_PLAN
C.ALLOCATION_DT
C.SEQ_NUM
C.ST_TREASURY_ID
C.APPRVL_DT
C.ST_ALLOCATION_TYPE
C.SHARES_ALLOCATED
C.LASTUPDOPRID
C.LASTUPDDTTM
C.DESCR
 
  do Print-LowerInfo

 from  PS_ST_PLAN_ALLOC    C
where  STOCK_ID         =  &A.STOCK_ID
  and  STOCK_PLAN_TYPE  =  &A.STOCK_PLAN_TYPE
  and  STOCK_PLAN       =  &A.STOCK_PLAN

end-Select

end-procedure

!************************************************************************
begin-procedure Print-heading
!************************************************************************

  print $ALLOCATION_DT         (+2,{col2},15)
  print $SEQ_NUM               (0,+5,07)
  print $TREASURY_ID           (0,+4,11)
  print $APPROVAL_DT           (0,+4,14)
  print $ALLOCATION_TYPE       (0,+4,15)
  print $SHARES_ALLOCATED      (0,+10,16)
  print $LAST_EMPLID           (0,+4,20)
  print $LAST_DTTM_UPDATE      (0,+4,26)
  print $DESCR                 (0,+2,20)
  print ' '                    (+1,1,1)

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  print $STOCK_ID              (+2,0,08)
  print ':'                    (,,1)
  print &A.STOCK_ID            (0,+2,10)
  print &B.DESCRSHORT          (0,+5,10)


end-procedure

!************************************************************************
begin-procedure Print-Info
!************************************************************************

  do Get-Data
  do Get-Descr
  
  next-listing no-advance need=3

  print $STOCK_PLAN_TYPE       (+1,{col2},15)
  print ':'                    (,,1)
  print $PlanType              (0,23,16)

  print $STOCK_PLAN            (+1,{col2},13)
  print ':'                    (,,1)
  print &A.STOCK_PLAN          (0,23,10)

  print $DESCR                 (+2,{col2},11)
  print ':'                    (,,1)
  print &A.DESCRSHORT          (0,23,10)

  do Format-DateTime (&A.APPRVL_DT,        $ApprvlDate, {DEFYMD}, '', '')
  do Format-DateTime (&A.EXPIRATN_DT,      $ExpDate,    {DEFYMD}, '', '')

  print $APPROVAL_DT           (+1,{col2},14)
  print ':'                    (,,1)
  print $ApprvlDate            (0,23,10)

  print $EXPIRATN_DT           (+1,{col2},15)
  print ':'                    (,,1)
  print $ExpDate               (0,23,10)

end-procedure

!***********************************************************************
begin-procedure Print-LowerInfo
!***********************************************************************

  do Format-DateTime(&C.ALLOCATION_DT,    $AllocDate,  {DEFYMD}, '', '')
  do Format-DateTime(&C.APPRVL_DT,        $ApprvlDate, {DEFYMD}, '', '')
  do Format-DateTime(&C.LASTUPDDTTM,      $LastUpDate, {DEFYMD}, '', '')

  print $AllocDate             (+1,{col2},10)
  print &C.SEQ_NUM             (0,{col7},15)

  do Get-Data
  do Get-Descr

  print &D.DESCRSHORT          (0,36,10)       !ST_TREASURY_ID
  print $ApprvlDate            (0,51,10)
  print $AllocType             (0,69,10)

  do Format-Number(&C.SHARES_ALLOCATED, $SHARES_ALLOC, '999,999,999,990.000000')

  print $SHARES_ALLOC          (0,{col12},22)
  print &C.LASTUPDOPRID        (0,+4,20)
  print $LastUpDate            (0,+4,26)
  print &C.DESCR               (0,+2,)
end-procedure

!************************************************************************
begin-procedure Get-Data
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Data                                              !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'STOCK_PLAN_TYPE'
  let $FieldValue = &A.STOCK_PLAN_TYPE

  do Read-Translate-Table

  let $PlanType = RTRIM($XlatShortName,' ')



  let $FieldName = 'ST_ALLOCATION_TYPE'
  let $FieldValue = &C.ST_ALLOCATION_TYPE

  do Read-Translate-Table

  let $AllocType = RTRIM($XlatShortName,' ')

end-procedure


!************************************************************************
begin-procedure Get-Descr
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Descr                                             !
! Descr:      This procedure gets the description of the specific   !
!             field from the prompt tables                          !
!-------------------------------------------------------------------!

begin-Select

D.DESCRSHORT                   !TREASURY ID DESCR

 from  PS_STOCK_TREASURY   D
where  D.STOCK_ID       =  &C.STOCK_ID
  and  D.ST_TREASURY_ID =  &C.ST_TREASURY_ID

end-Select

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************

#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
#include 'strunctl.sqc'  !Get run control parameter values
