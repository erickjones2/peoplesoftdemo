!************************************************************
!  GetDedNm:  Get Deduction Name from the Deductions Table
!************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:30:45                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!*********************************
begin-procedure Get-Deduction-Name
!*********************************

  !Keys passed in:  $PlanType, $DedCd, $AsOfDate
  !Values returned: $DeductionName, $DeductionAbbrv, $SpclProcess

  move ''  to $DeductionName
  move ''  to $DeductionAbbrv
  move ''  to $SpclProcess

  if Rtrim($PlanType,' ') = '' or Rtrim($DedCd,' ') = ''
    goto GetDedNmExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
DT.DESCR
DT.DESCRSHORT
DT.SPCL_PROCESS

  let $DeductionName  = Rtrim(&DT.DESCR,' ')
  let $DeductionAbbrv = Rtrim(&DT.DESCRSHORT,' ')
  let $SpclProcess    = Rtrim(&DT.SPCL_PROCESS,' ')

FROM  PS_DEDUCTION_TBL DT
WHERE DT.PLAN_TYPE = $PlanType
  AND DT.DEDCD     = $DedCd
  AND DT.EFFDT    <= $AsOfDate
  AND DT.EFFDT =
        (SELECT MAX(EFFDT)
         FROM  PS_DEDUCTION_TBL
         WHERE PLAN_TYPE = DT.PLAN_TYPE
           AND DEDCD     = DT.DEDCD
           AND EFFDT    <= $AsOfDate)
end-SELECT

  do Get-Deduction-Name-Lang

GetDedNmExit:

end-procedure

!**************************************
begin-procedure Get-Deduction-Name-Lang
!**************************************

  !Keys passed in:  $PlanType, $DedCd, $AsOfDate
  !Values returned: $DeductionName, $DeductionAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetDedNmLngExit
  end-if

  if Rtrim($PlanType,' ') = '' or Rtrim($DedCd,' ') = ''
    goto GetDedNmLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
DTL.DESCR
DTL.DESCRSHORT

  if Rtrim(&DTL.DESCR,' ') <> ''
    let $DeductionName  = Rtrim(&DTL.DESCR,' ')
  end-if
  if Rtrim(&DTL.DESCRSHORT,' ') <> ''
    let $DeductionAbbrv = Rtrim(&DTL.DESCRSHORT,' ')
  end-if

FROM  PS_DEDUCTION_LANG DTL
WHERE DTL.PLAN_TYPE   = $PlanType
  AND DTL.DEDCD       = $DedCd
  AND DTL.LANGUAGE_CD = $Curr_Language_Cd
  AND DTL.EFFDT      <= $AsOfDate
  AND DTL.EFFDT            =
        (SELECT MAX(EFFDT)
         FROM  PS_DEDUCTION_LANG
         WHERE PLAN_TYPE   = DTL.PLAN_TYPE
           AND DEDCD       = DTL.DEDCD
           AND LANGUAGE_CD = DTL.LANGUAGE_CD
           AND EFFDT      <= $AsOfDate)
end-SELECT

GetDedNmLngExit:

end-procedure

!***************************** End of Include File **************************
