!************************************************************************
!  STSU013:  Stock Split
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:56:44                                 
!       $Release:  HR92                                                
!      $Revision:  102                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
  #include 'ptpsp125.sqc'  !Printer and page-size init
end-setup

#define  col2    8
#define  col3    30
#define  col4    45

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let     $ReportID     =   'STSU013'

  do Get-Report-Language

  if $prcs_process_instance = ''
     do Prompts             !Prompt user

  else
     do Get-SQR-Parms
     do Get-Values
  end-if

  show    'FLOW: Init-Report'
  show    'Report Title: ' $REPORTTITLE

end-procedure

!******************************************************************************
begin-procedure Prompts
!******************************************************************************

!-----------------------------------------------------------------------------!
! Procedure: Prompts                                                          !
! Descr:     Prompts for all input parameters in the event the program is run !
!            directly from SQR.  In normal use, this should not happen.       !
!-----------------------------------------------------------------------------!

end-procedure

!************************************************************************
begin-procedure Get-Values
!************************************************************************

end-procedure

!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ($ReportID,$language_cd)
  do Append_Report_Translation ($ReportID)

  do Get_Field_Information ('STSU013','REPORT_TITLE',      $REPORTTITLE,       #DW)
  do Get_Field_Information ('STSU013','ST_SPLIT_ID',       $ST_SPLIT_ID,       #DW)
  do Get_Field_Information ('STSU013','RECORD_DT',         $RECORD_DT,         #DW)
  do Get_Field_Information ('STSU013','PAYABLE_DT',        $PAYABLE_DT,        #DW)
  do Get_Field_Information ('STSU013','HEADING_1',         $HEADING1,          #DW)
  do Get_Field_Information ('STSU013','HEADING_2',         $HEADING2,          #DW)
  do Get_Field_Information ('STSU013','HEADING_3',         $HEADING3,          #DW)
  do Get_Field_Information ('STSU013','SPLIT_CONVERSION',  $SPLIT_CONVERSION,  #DW)
  do Get_Field_Information ('STSU013','DECIMAL_PLACES',    $DECIMAL_PLACES,    #DW)
  do Get_Field_Information ('STSU013','ST_ROUNDING_RULE',  $ST_ROUNDING_RULE,  #DW)
  do Get_Field_Information ('STSU013','STATUS',            $STATUS,            #DW)
  do Get_Field_Information ('STSU013','STATUS_DATE',       $STATUS_DT,         #DW)
  do Get_Field_Information ('STSU013','STOCK_ID',          $STOCK_ID,          #DW)
  do Get_Field_Information ('STSU013','DESCRSHORT',        $DESCRSHORT,        #DW)

end-procedure

!************************************************************************
begin-heading 3
!************************************************************************

#include 'stdhdgtr.sqc'

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

begin-Select

A.STOCK_ID               ()  ON-BREAK  Print=never    before = Select-Dtl
A.ST_SPLIT_ID
A.DESCRSHORT
A.RECORD_DT
A.PAYABLE_DT
A.ST_SPLIT_SHARE_NEW
A.ST_SPLIT_SHARE_OLD
A.DEC_PLACES
A.ST_ROUNDING_RULE
A.ST_ROUNDING_RULE2
A.DEC_PLACES2
A.ST_SPLIT_STATUS
A.STATUS_DT
 
  do Print-Data

from     PS_ST_SPLIT_TBL     A
order by A.STOCK_ID

end-Select


end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

B.STOCK_ID
B.DESCRSHORT

  do Print-Info

 from   PS_STOCK_TBL   B
where   B.STOCK_ID     =   &A.STOCK_ID

end-Select

end-procedure

!************************************************************************
begin-procedure Print-Info
!************************************************************************

  print  $STOCK_ID            (+2,1,08)
  print  &A.STOCK_ID          (0,+2,10)
  print  &B.DESCRSHORT        (0,+5,10)

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  print  $ST_SPLIT_ID         (+2,{col2},09)
  print  &A.ST_SPLIT_ID       (0,+2,10)

  print  $DESCRSHORT          (+2,{col2},12)
  print  &A.DESCRSHORT        (0,26,10)

  do Format-DateTime(&A.RECORD_DT, $RecordDate, {DEFYMD}, '', '')
  do Format-DateTime(&A.PAYABLE_DT,$PayableDate, {DEFYMD}, '', '')

  print  $RECORD_DT           (+1,{col2},12)
  print  $RecordDate          (0,26,10)

  print $PAYABLE_DT           (+1,{col2},13)
  print $PayableDate          (0,26,10)

  print $SPLIT_CONVERSION     (+1,{col2},17)

  let $New = to_char(&a.st_split_share_new)
  let $Old = to_char(&a.st_split_share_old)
  let $Conv = ltrim($New,' ') || ' ' || $HEADING2 || ' ' || ltrim($Old,' ')
  
  print $Conv                 (0,26,15)

  print $HEADING1             (+2,{col2},23)
 
  print $HEADING3             (0,60,18)

  let $Dec1 = ltrim(to_char(&A.DEC_PLACES),' ')

  print $DECIMAL_PLACES       (+1,11,15)
  print $Dec1                 (0,+2,03)

  let $Dec2 = ltrim(to_char(&A.DEC_PLACES),' ')

  print $DECIMAL_PLACES       (0,63,15)
  print $Dec2                 (0,+2,03)

  do Get-XlatValue

  print $ST_ROUNDING_RULE     (+2,11,14)
  print $RoundRule            (0,+2,10)

  print $ST_ROUNDING_RULE     (0,63,14)
  print $RoundRule2            (0,+2,10)

  print  $STATUS              (+2,{col2},13)
  print  &A.ST_SPLIT_STATUS   (0,23,12)

  do Format-DateTime(&A.STATUS_DT, $StatusDate, {DEFYMD}, '', '')

  print $STATUS_DT            (+1,{col2},12) 
  print $StatusDate           (0,23,10)
  print ' '                   (+2,1,1)

end-procedure

!************************************************************************
begin-procedure Get-XlatValue
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-XlatValue                                             !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'ST_ROUNDING_RULE'
  let $FieldValue = &A.ST_ROUNDING_RULE

  do Read-Translate-Table

  let $RoundRule = RTRIM($XlatShortName,' ')

  let $FieldName = 'ST_ROUNDING_RULE2'
  let $FieldValue = &A.ST_ROUNDING_RULE2

  do Read-Translate-Table

  let $RoundRule2 = RTRIM($XlatShortName,' ')

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************

#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
#include 'strunctl.sqc'  !Get run control parameter values
