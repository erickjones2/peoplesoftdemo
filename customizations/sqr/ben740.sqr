!***********************************************************************
!  BEN740: Billing Calendar Table
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:12:25:52                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


#include 'setenv.sqc' !Set environment

Begin-Setup
  #Include 'ptpsp125.sqc'
End-Setup

#define col_sep            1   !Column Separator
#define col_col11         11   !positions
#define col_col12         12   !positions

!************
Begin-Program
!************

  do Init-Report
  do Process-Main

  do Reset
  do StdAPI-Term

End-Program


!***************************
Begin-Procedure Init-Report
!***************************

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime

  do StdAPI-Init

  move 'BEN740' to $ReportID
  do Get-Report-Language
  move $BEN740-REPORTTITLE TO $ReportTitle
  display $ReportID    NoLine
  display ': '         NoLine
  display $ReportTitle
  display ''

End-Procedure

!**********************************
! This procedure is used to read language-dependent text from the Strings table
!**********************************
Begin-Procedure Report-Translation
!**********************************
! Read from BEN740 program IDS
do Init_Report_Translation($ReportID, $Language_Cd)
do Append_Report_Translation('BEN1')

do Get_Field_Information ('BEN740', 'REPORT_TITLE',    $BEN740-REPORTTITLE,   #CW)
do Get_Field_Information ('BEN740', 'CALC',            $BEN740-CALC,          #CW)
do Get_Field_Information ('BEN740', 'STATEMTS',        $BEN740-STATEMTS,      #CW)
do Get_Field_Information ('BEN740', 'RUN',             $BEN740-RUN,           #CW)
do Get_Field_Information ('BEN740', 'PAYMENT_DUE',     $BEN740-PAYMENT-DUE,   #CW)
do Get_Field_Information ('BEN740', 'PRINTED',         $BEN740-PRINTED,       #CW)

do Get_Field_Information ('BEN1',   'BILLING',         $BEN1-BILLING,         #CW)
do Get_Field_Information ('BEN1',   'PERIOD',          $BEN1-PERIOD,          #CW)
do Get_Field_Information ('BEN1',   'PAYMENT',         $BEN1-PAYMENT,         #CW)
do Get_Field_Information ('BEN1',   'COBRA',           $BEN1-COBRA,           #CW)
do Get_Field_Information ('BEN1',   'BEGIN_DT2',       $BEN1-BEGIN-DT2,       #CW)
do Get_Field_Information ('BEN1',   'END_DATE',        $BEN1-END-DATE,        #CW)
do Get_Field_Information ('BEN1',   'DUE',             $BEN1-DUE,             #CW)

End-Procedure

!***************
Begin-Heading 7
!***************

  #Include 'stdhdgtr.sqc'

!----- Heading line 1 ------
  print $BEN1-BILLING             (+2,1,{col_col12})
  print $BEN1-PERIOD              (0,+{col_sep},{col_col12})
  print $BEN1-PERIOD              (0,+{col_sep},{col_col12})
  print $BEN1-PAYMENT             (0,+{col_sep},{col_col12})
  print $BEN1-COBRA               (0,+{col_sep},{col_col12})
  print $BEN740-CALC              (0,+{col_sep},{col_col12})
  print $BEN740-STATEMTS          (0,+{col_sep},{col_col11})

!----- Heading line 2 ------
  print $BEN1-PERIOD              (+1,1,{col_col12})
  print $BEN1-BEGIN-DT2           (0,+{col_sep},{col_col12})
  print $BEN1-END-DATE            (0,+{col_sep},{col_col12})
  print $BEN1-DUE                 (0,+{col_sep},{col_col12})
  print $BEN740-PAYMENT-DUE       (0,+{col_sep},{col_col12})
  print $BEN740-RUN               (0,+{col_sep},{col_col12})
  print $BEN740-PRINTED           (0,+{col_sep},{col_col11})

!----- Heading line 3 ------
  print '='                       (+1,1,{col_col12})          FILL
  print '='                       (0,+{col_sep},{col_col12})  FILL
  print '='                       (0,+{col_sep},{col_col12})  FILL
  print '='                       (0,+{col_sep},{col_col12})  FILL
  print '='                       (0,+{col_sep},{col_col12})  FILL
  print '='                       (0,+{col_sep},{col_col12})  FILL
  print '='                       (0,+{col_sep},{col_col11})  FILL

End-Heading

!****************************
Begin-Procedure Process-Main
!****************************

Begin-Select
BILL_PER_CD                     (+2,3,7)
PERIOD_BEGIN_DT
  do Format-DateTime(&Period_Begin_Dt, $out, {DEFDATE}, '', '')
  print $out                     (0,+4,10)

PERIOD_END_DT
  do Format-DateTime(&Period_End_Dt, $out, {DEFDATE}, '', '')
  print $out                     (0,+3,10)

BILL_DUE
  do Format-DateTime(&Bill_Due, $out, {DEFDATE}, '', '')
  print $out                     (0,+3,10)

COBRA_DUE
  do Format-DateTime(&Cobra_Due, $out, {DEFDATE}, '', '')
  print $out                     (0,+3,11)
BILL_CALC_RUN                    (0,+7,5)
BILL_STATEMENT_RUN               (0,+7,6)
COMMENTS                        (+1,1) wrap 120 10


From  PS_BEN_BI_CALENDAR

Order By PERIOD_END_DT DESC

End-Select

End-Procedure


!*****************************************************************************
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API

#Include 'sqrtrans.sqc'  !Translate SQR strings to given language
#Include 'getrplng.sqc'  !Get the report language
!******************************* End of Report *******************************
