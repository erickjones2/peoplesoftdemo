
!***********************************************************************
!  ReadMsgC:  Read the Message Catalog Table for the desired values.   *
!                                                                      *
!  Gets:                  #MessageSetNbr (Message Set Number)          *
!                         #MessageNbr    (Message Number)              *
!                                                                      *
!  Sets:                  #MessageText   (Message Text)                *
!                         #DescrLong     (Message Long Description)    *
!                         #MessageLangCd (Message Language Code)       *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:14:01:43                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
begin-procedure Read-MsgCatalog-Table

#ifdef debugp
  display 'Entering READMSGC.SQC: Read-MsgCatalog-Table'
#endif

#ifdef debugd
  display ' Input #MessageSetNbr: ' noline
  display #MessageSetNbr
  display ' Input #MessageNbr: ' noline
  display #MessageNbr
  display '   $curr_language_cd: ' noline
  display $curr_language_cd
#endif

  move '' to $MessageText
  move '' to $DescrLong

  if #MessageSetNbr > 0 and #MessageNbr > 0

    if IsBlank($curr_language_cd)
      do Get-Current-Language
    end-if

    if IsBlank($PSOptions_language_cd)
      do Get-PSOptions-Language ($PSOptions_language_cd)
    end-if

    if $PSOptions_language_cd = $curr_language_cd

      do Select_From_MsgCat (#MessageSetNbr, #MessageNbr,
                             $MessageText, $DescrLong)

      let $MessageLangCd = Rtrim($PSOptions_language_cd,' ')

    else

      do Select_From_MsgCatLang (#MessageSetNbr, #MessageNbr, $curr_language_cd,
                           $MessageText, $DescrLong)

      let $MessageLangCd = Rtrim($curr_language_cd,' ')

      if IsBlank($MessageText) and IsBlank($DescrLong)
      do Select_From_MsgCat (#MessageSetNbr, #MessageNbr,
                             $MessageText, $DescrLong)

      let $MessageLangCd = Rtrim($PSOptions_language_cd,' ')

      end-if
    end-if
  end-if

#ifdef debugp
  display 'Exiting READMSGC.SQC: Read-MsgCatalog-Table'
#endif

#ifdef debugd
  display ' Output $MessageText:   ' noline
  display $MessageText
  display ' Output $DescrLong:     ' noline
  display $DescrLong
  display ' Output $MessageLangCd: ' noline
  display $MessageLangCd
#endif
end-procedure


!***********************************************************************
! Select_From_MsgCat                                                   *
!                                                                      *
!  Retrieves MESSAGE_TEXT and DESCRLONG from PSMSGCATDEFN              *
!                                                                      *
!***********************************************************************
begin-procedure Select_From_MsgCat (#MsgSetNum, #MsgNum,
                                  :$MsgText, :$LongDescr)
#ifdef debugp
  display 'Entering READMSGC.SQC: Select_From_MsgCat'
#endif

#ifdef debugd
  display ' Input #MsgSetNum: ' noline
  display #MsgSetNum
  display ' Input #MsgNum: ' noline
  display #MsgNum
#endif

begin-SELECT
MSGCAT.MESSAGE_TEXT
MSGCAT.DESCRLONG

  let $MsgText        = Rtrim(&MSGCAT.MESSAGE_TEXT,' ')
  let $LongDescr      = Rtrim(&MSGCAT.DESCRLONG,' ')

FROM  PSMSGCATDEFN MSGCAT
WHERE MSGCAT.MESSAGE_SET_NBR = #MsgSetNum
  AND MSGCAT.MESSAGE_NBR     = #MsgNum
end-SELECT

#ifdef debugp
  display 'Exiting READMSGC.SQC: Select_From_MsgCat'
#endif

#ifdef debugp
  display ' Output $MsgText: ' noline
  display $MsgText
  display ' Output $LongDescr: ' noline
  display $LongDescr
#endif
end-procedure

!***********************************************************************
! Select_From_MsgCatLang                                               *
!                                                                      *
!  Retrieves MESSAGE_TEXT and DESCRLONG from PSMSGCATLANG              *
!  for the input language code.                                        *
!                                                                      *
!***********************************************************************
begin-procedure Select_From_MsgCatLang (#MsgSetNum, #MsgNum, $Language_cd,
                                      :$MsgText, :$LongDescr)
#ifdef debugp
  display 'Entering READMSGC.SQC: Select_From_MsgCatLang'
#endif

#ifdef debugd
  display ' Input $Language_cd: ' noline
  display $Language_cd
  display ' Input #MsgSetNum: ' noline
  display #MsgSetNum
  display ' Input #MsgNum: ' noline
  display #MsgNum
#endif

  let $Lcl_Language_Cd = rtrim($Language_cd, ' ')

begin-SELECT
MSGCAT.MESSAGE_TEXT
MSGCAT.DESCRLONG

  let $MsgText      = Rtrim(&MSGCAT.MESSAGE_TEXT,' ')
  let $LongDescr      = Rtrim(&MSGCAT.DESCRLONG,' ')

FROM  PSMSGCATLANG MSGCAT
WHERE MSGCAT.MESSAGE_SET_NBR = #MsgSetNum
  AND MSGCAT.MESSAGE_NBR     = #MsgNum
  AND MSGCAT.LANGUAGE_CD     = $Lcl_Language_Cd
end-SELECT

#ifdef debugp
  display 'Exiting READMSGC.SQC: Select_From_MsgCatLang'
#endif

#ifdef debugd
  display ' Output $MsgText: ' noline
  display $MsgText
  display ' Output $LongDescr: ' noline
  display $LongDescr
#endif
end-procedure
