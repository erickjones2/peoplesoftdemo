!****************************************************
!  FGHR019D:  Organizational Component Translations  *
!****************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:13:28:43                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!
! MODIFICATION:  N/A
!
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#include 'setup32.sqc'

 begin-setup
 declare-variable
    date $AsOfDate
    date $TempDate
 end-declare
 end-setup

begin-report

Do Init-DateTime
Do Init-Number
Do Get-Current-DateTime
Do Init-Report
Do Report
Do Reset
Do Stdapi-Term

end-report

begin-heading 8

#Include 'stdhdg01.sqc'
move 'Mon dd, YYYY' to $mask
print 'CPDF Organizational Component Translations for ' (0,58)
print $asofdate                                         (0,106) edit :$mask
print ' '                                               (+1)
print 'Agency'                                          (+1,40)
print 'Sub-Agency'                                      (0,48)
print 'Component       '                                (0,59)
print 'Description'                                     (0,77)

end-heading

begin-procedure Init-Report

move 'FGHR019D' to $ReportID
move 'Organizational Component Translations' to $ReportTitle
display $ReportTitle

let $spaces = '                                        '

Do Open
Do Stdapi-Init

if $prcs_process_instance = ''

  Do Ask-As-Of-Date

else

  Do Select-Parameters
  Do Get-Values
  Do Get-Default-Setid

end-if

end-procedure

begin-procedure Get-Values

Let $AsOfDate = &RUN_CNTL_FG.ASOFDATE
!Do Get-As-Of-Date

end-procedure

begin-procedure Open

!let  $Filename = '{IMPORTPREFIX}fghr019d.dat'
let  $Filename = '{IMPORTPREFIX}fghr019d'

open $FileName as 1 for-writing record=200:fixed status = #Status

if #Status = -1
  display 'OPEN error on file: ' noline
  display $FileName
  STOP
end-if

end-procedure

begin-procedure Report

begin-select
A.NODE_COUNT
B.EFFDT
B.TREE_NODE_NUM
B.TREE_NODE
B.TREE_LEVEL_NUM
B.PARENT_NODE_NUM
B.TREE_NODE_NUM_END
C.COMPANY
C.GVT_SUB_AGENCY
C.DEPTID
C.GVT_DESCR40

  let $indent        = substr($spaces,1,(&B.TREE_LEVEL_NUM * 2) - 2)
  !let $org-component = &C.GVT_SUB_AGENCY||&C.DEPTID
  let $org-component = &C.DEPTID
  let $org-descr     = $indent||&C.GVT_DESCR40

  print &C.COMPANY                      (+1,42)
  print &C.GVT_SUB_AGENCY               (0,52)
  print &C.DEPTID                       (0,59)
  print $org-descr                      (0,77)

  Do Write-Org-File

FROM PSTREEDEFN A, PSTREENODE B, PS_DEPT_TBL C
WHERE A.TREE_NAME  = 'DEPT_SECURITY'
AND   A.SETID      = $setid
AND   A.EFF_STATUS = 'A'
AND   A.EFFDT      = (SELECT MAX(A1.EFFDT)
                      FROM PSTREEDEFN A1
                      WHERE A1.TREE_NAME  = A.TREE_NAME
                      AND   A1.SETID      = A.SETID
                      AND   A1.EFF_STATUS = A.EFF_STATUS
                      AND   A1.EFFDT     <= &RUN_CNTL_FG.ASOFDATE)
!                      AND   A1.EFFDT     <= $AsOfDate)
AND   B.TREE_NAME  = A.TREE_NAME
AND   B.SETID      = A.SETID
AND   B.EFFDT      = A.EFFDT
AND   C.EFF_STATUS = 'A'
AND   C.DEPTID     = B.TREE_NODE
AND   C.EFFDT      = (SELECT MAX(C1.EFFDT)
                      FROM PS_DEPT_TBL C1
                      WHERE C1.DEPTID    = C.DEPTID
                      AND   C1.EFFDT    <= &RUN_CNTL_FG.ASOFDATE)
!                      AND   C1.EFFDT    <= $AsOfDate)
ORDER BY B.TREE_NODE_NUM
END-SELECT

end-procedure

begin-procedure Get-Default-Setid

 do get-operclass

begin-select
OPR_DEF.SETID
      Let $setid = &OPR_DEF.SETID

FROM PS_OPR_DEF_TBL_HR OPR_DEF
WHERE OPRCLASS = &OPRDFN.OPRCLASS
end-select

end-procedure

begin-procedure Get-Operclass
begin-select
OPRDFN.OPRCLASS

FROM PSOPRDEFN OPRDFN
WHERE OPRID = $Prcs_OprID
end-select

end-procedure

begin-procedure Write-Org-File

WRITE 1 FROM
  &C.COMPANY:2                  ! 1-2
  $org-component:18             ! 3-20
  $filler:1                     ! 21
  $org-descr:179                ! 22-200

end-procedure

#include 'fgrunctl.sqc'  !Get run control parameter values
#include 'fggetval.sqc'  !Get values mask routines
#include 'stdapi.sqc'    !Routines to update run status
#Include 'askaod.sqc'    !Input logic for user entered as of date
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
