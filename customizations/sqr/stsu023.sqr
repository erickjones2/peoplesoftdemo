!************************************************************************
!  STSU023:  Stock Purchase Information
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:57:32                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
   #include 'ptpsl177.sqc'  !Printer and page-size init

end-setup

#define  col2    5
#define  col3    19
#define  col4    34
#define  col5    48
#define  col6    61
#define  col7    75
#define  col8    92
#define  col9    100
#define  col10   125
#define  col11   150

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let     $ReportID     =   'STSU023'

  do Get-Report-Language

  if $prcs_process_instance = ''
     do Prompts             !Prompt user

  else
     do Get-SQR-Parms
     do Get-Values
  end-if

  show    'FLOW: Init-Report'
  show    'Report Title: ' $REPORTTITLE

end-procedure

!******************************************************************************
begin-procedure Prompts
!******************************************************************************

!-----------------------------------------------------------------------------!
! Procedure: Prompts                                                          !
! Descr:     Prompts for all input parameters in the event the program is run !
!            directly from SQR.  In normal use, this should not happen.       !
!-----------------------------------------------------------------------------!

end-procedure

!************************************************************************
begin-procedure Get-Values
!************************************************************************

end-procedure

!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ('STOCK',$language_cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK','STOCK_ID',         $STOCK_ID,         #DW)
  do Get_Field_Information ('STOCK','STOCK_PLAN_TYPE',  $STOCK_PLAN_TYPE,  #DW)
  do Get_Field_Information ('STOCK','STOCK_PLAN',       $STOCK_PLAN,       #DW)
  do Get_Field_Information ('STOCK','OFFERING_ID',      $OFFERING_ID,      #DW)
  do Get_Field_Information ('STOCK','GRANT_DT',         $GRANT_DT,         #DW)
  do Get_Field_Information ('STOCK','PURCHASE_FMV',     $PURCHASE_FMV,     #DW)
  do Get_Field_Information ('STOCK','PURCHASE_PRICE',   $PURCHASE_PRICE,   #DW)

  do Get_Field_Information ('STSU023','REPORT_TITLE',      $REPORTTITLE,      #DW)
  do Get_Field_Information ('STSU023','STATUS_DT',         $STATUS_DT,        #DW)
  do Get_Field_Information ('STSU023','OFFERING_STATUS',   $OFFERING_STATUS,  #DW)
  do Get_Field_Information ('STSU023','PERIOD_END_DT',     $PERIOD_END_DT,    #DW)
  do Get_Field_Information ('STSU023','PERIOD_BEGIN_DT',   $PERIOD_BEGIN_DT,  #DW)
  do Get_Field_Information ('STSU023','OFFERING_NEXT_ID',  $OFFERING_NEXT_ID, #DW)
  do Get_Field_Information ('STSU023','PURCHASE_PRC_CALC', $PURCHASE_PRC_CALC,#DW)
  do Get_Field_Information ('STSU023','ANTI_DILUTION_PRC', $ANTI_DILUTION_PRC,#DW)
  do Get_Field_Information ('STSU023','GRANT_FMV',         $GRANT_FMV,       #DW)
  do Get_Field_Information ('STSU023','PURCH_PRC_NO_DISC', $PURCH_PRC_NO_DISC,       #DW)
  do Get_Field_Information ('STSU023','DESCRSHORT',        $DESCRSHORT,       #DW)

end-procedure

!************************************************************************
begin-heading 3
!************************************************************************

#include 'stdhdgtr.sqc'

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

begin-Select

A.STOCK_ID                         () ON-BREAK Print=never before=Select-Descr
A.STOCK_PLAN_TYPE
A.STOCK_PLAN

  do Print-Info
  do Select-Detail
  print ' '                        (+1,1,1)

FROM PS_STOCK_PLAN_TBL  A
WHERE A.STOCK_PLAN_TYPE  = 'E'  !ESPP
ORDER BY A.STOCK_ID

end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Descr
!**************************************************************************

begin-Select

B.STOCK_ID
B.DESCRSHORT

  do Print-data

FROM  PS_STOCK_TBL B
WHERE B.STOCK_ID   =  &A.STOCK_ID

end-Select

end-procedure

!***********************************************************************
begin-procedure Select-Detail
!***********************************************************************

begin-Select
C.OFFERING_ID                      () ON-BREAK Print=never before=Print-DtlHeading
C.STOCK_ID
C.STOCK_PLAN_TYPE
C.STOCK_PLAN
C.DESCR
C.DESCRSHORT
C.STATUS_DT
C.OFFERING_STATUS
C.PERIOD_BEGIN_DT
C.PERIOD_END_DT
C.OFFERING_ID_PREV

  do Print-Detail
  do Select-LowerDetail

FROM  PS_ST_OFFERING_PD    C
WHERE C.STOCK_ID           =  &A.STOCK_ID
  AND C.STOCK_PLAN_TYPE    =  &A.STOCK_PLAN_TYPE
  AND C.STOCK_PLAN         =  &A.STOCK_PLAN

end-Select

end-procedure

!************************************************************************
begin-procedure Select-LowerDetail
!************************************************************************

begin-Select

D.STOCK_ID
D.STOCK_PLAN_TYPE
D.STOCK_PLAN
D.OFFERING_ID                      () ON-BREAK Print=never before=Print-LowerDtlHeading
D.PERIOD_END_DT
D.PERIOD_BEGIN_DT

  do Print-LowerDetail
  do Select-LowestDtl

FROM  PS_ST_PURCHASE_PD  D
WHERE D.STOCK_ID         =  &C.STOCK_ID
  AND D.STOCK_PLAN_TYPE  =  &C.STOCK_PLAN_TYPE
  AND D.STOCK_PLAN       =  &C.STOCK_PLAN
  AND D.OFFERING_ID      =  &C.OFFERING_ID

end-Select

end-procedure

!************************************************************************
begin-procedure Select-LowestDtl
!************************************************************************

begin-Select

E.STOCK_ID
E.STOCK_PLAN_TYPE
E.STOCK_PLAN
E.OFFERING_ID
E.PERIOD_END_DT                    () ON-BREAK Print=never before=Print-LowestDtlHeading
E.GRANT_DT
E.GRANT_FMV
E.PURCHASE_FMV
E.PURCHASE_PRC_CALC
E.PURCH_PRC_NO_DISC
E.PURCHASE_PRICE
E.ANTI_DILUTION_PRC

  do Print-LowestDtl

FROM  PS_ST_PURCHASE_PRC E
WHERE E.STOCK_ID         =  &D.STOCK_ID
  AND E.STOCK_PLAN_TYPE  =  &D.STOCK_PLAN_TYPE
  AND E.STOCK_PLAN       =  &D.STOCK_PLAN
  AND E.OFFERING_ID      =  &D.OFFERING_ID
  AND E.PERIOD_END_DT    =  &D.PERIOD_END_DT

end-Select

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  print $STOCK_ID              (+2,1,08)
  print ':'                    (,,1)
  print &A.STOCK_ID            (0,18,10)
  print &B.DESCRSHORT          (0,+5,10)

end-procedure

!************************************************************************
begin-procedure Print-Info
!************************************************************************

  print $STOCK_PLAN            (+1,1,13)
  print ':'                    (,,1)
  print &A.STOCK_PLAN          (0,18,10)

end-procedure

!***********************************************************************
begin-procedure Print-DtlHeading
!***********************************************************************

  print $OFFERING_ID           (+2,{col2},11)
  print $STATUS_DT             (0,{col3},11)
  print $OFFERING_STATUS       (0,{col4},)
  print $PERIOD_BEGIN_DT       (0,52,)
  print $PERIOD_END_DT         (0,72,)
  print $OFFERING_NEXT_ID      (0,89,)
  print $DESCRSHORT            (0,110,)

end-procedure

!***********************************************************************
begin-procedure Print-Detail
!***********************************************************************

  do Format-DateTime (&C.STATUS_DT,       $StatusDate,  {DEFYMD}, '', '')
  do Format-DateTime (&C.PERIOD_BEGIN_DT, $PeriodBgnDt, {DEFYMD}, '', '')
  do Format-DateTime (&C.PERIOD_END_DT,   $PeriodEndDt, {DEFYMD}, '', '')

  print &C.OFFERING_ID         (+2,{col2},10)
  print $StatusDate            (0,{col3},10)

  do Get-Data

  print $OfferingStatus        (0,{col4},10)
  print $PeriodBgnDt           (0,52,10)
  print $PeriodEndDt           (0,72,10)
  print &C.OFFERING_ID_PREV    (0,89,10)
  print &C.DESCRSHORT          (0,110,10)

end-procedure

!***********************************************************************
begin-procedure Print-LowerDtlHeading
!***********************************************************************

  print $PERIOD_END_DT         (+3,{col3},)
  print $PERIOD_BEGIN_DT       (0,36,)

end-procedure

!************************************************************************
begin-procedure Print-LowerDetail
!************************************************************************

  do Format-DateTime (&D.PERIOD_END_DT,   $PerdEndDate,     {DEFYMD}, '', '')
  do Format-DateTime (&D.PERIOD_BEGIN_DT, $PeriodBeginDate, {DEFYMD}, '', '')

  print $PerdEndDate           (+2,{col3},10)
  print $PeriodBeginDate       (0,36,10)

end-procedure

!***********************************************************************
begin-procedure Print-LowestDtlHeading
!***********************************************************************

  print  $GRANT_DT             (+2,{col4},)
  print  $GRANT_FMV            (0,52,)
  print  $PURCHASE_FMV         (0,70)
  print  $PURCH_PRC_NO_DISC    (0,88,)
  print  $PURCHASE_PRC_CALC    (0,119,)
  print  $ANTI_DILUTION_PRC    (0,141,)
  print  ' '                   (+1,1,)

end-procedure

!************************************************************************
begin-procedure Print-LowestDtl
!************************************************************************

  do Format-DateTime (&E.GRANT_DT, $GrantDate, {DEFYMD}, '', '')

  print $GrantDate             (+1,{col4},10)

  do Format-Number (&E.PURCHASE_FMV,     $PruchaseFMV,     '999,999,990.009999')
  do Format-Number (&E.GRANT_FMV,        $GrantFMV,        '999,999,990.009999')
  do Format-Number (&E.PURCHASE_PRC_CALC, $PurchasePrcCalc, '999,999,990.009999')
  do Format-Number (&E.PURCH_PRC_NO_DISC,   $PurchasePriceNoDisc, '999,999,990.009999')
  do Format-Number (&E.ANTI_DILUTION_PRC,$AntiDilutionPrc, '999,999,990.009999')

  print   $GrantFMV           (0,44,18)
  print   $PruchaseFMV        (0,64,18)
  print   $PurchasePriceNoDisc (0,91,18)
  print   $PurchasePrcCalc    (0,115,18)
  print   $AntiDilutionPrc    (0,142,18)

end-procedure

!************************************************************************
begin-procedure Get-Data
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Data                                              !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'OFFERING_STATUS'
  let $FieldValue = &C.OFFERING_STATUS

  do Read-Translate-Table

  let $OfferingStatus = RTRIM($XlatShortName,' ')

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************

#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
#include 'strunctl.sqc'  !Get run control parameter values
