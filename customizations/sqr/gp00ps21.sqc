!***********************************************************************
!  GP00PS21: Global Payroll Payslips - Dynamic Earnings for ALL Countries*
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:04:08:37                                 *
!       $Release:  HR92                                                *
!      $Revision:  103                                                 *
!                                                                      *
!***********************************************************************


#Define ET_DetailArray_EarnDynamic_Size 99
#Define EARN_DYNAMIC_TITLE_SIZE 3
!***********************************************************************
begin-procedure Init-Arrays_EarnDynamic
!***********************************************************************

Create-Array Name=ET_DetailArray_EarnDynamic Size = {ET_DetailArray_EarnDynamic_Size}
    Field=Name:char
    Field=CompRateCode:Number
    Field=CurrentHrs:Number
    Field=CurrentEarn:Number
    Field=YTDHrs:Number
    Field=YTDEarn:Number
    Field=Print_Blank:char

Create-Array Name=ET_TotalArray_EarnDynamic Size = 1
    Field=Name:char
    Field=CompRateCode:Number
    Field=CurrentHrs:Number
    Field=CurrentEarn:Number
    Field=YTDHrs:Number
    Field=YTDEarn:Number



let #i = 0

while (#i < {ET_DetailArray_EarnDynamic_Size} )


    Put ' ' into ET_DetailArray_EarnDynamic(#i) name
    Put 0 into ET_DetailArray_EarnDynamic(#i) CompRateCode
    Put 0 into ET_DetailArray_EarnDynamic(#i) CurrentHrs
    Put 0 into ET_DetailArray_EarnDynamic(#i) CurrentEarn
    Put 0 into ET_DetailArray_EarnDynamic(#i) YTDHrs
    Put 0 into ET_DetailArray_EarnDynamic(#i) YTDEarn
    Put ' ' into ET_DetailArray_EarnDynamic(#i) Print_Blank
    add 1 to #i

end-while

Put ' ' into ET_TotalArray_EarnDynamic(0) name
    Put 0 into ET_TotalArray_EarnDynamic(0) CompRateCode
    Put 0 into ET_TotalArray_EarnDynamic(0) CurrentHrs
    Put 0 into ET_TotalArray_EarnDynamic(0) CurrentEarn
    Put 0 into ET_TotalArray_EarnDynamic(0) YTDHrs
    Put 0 into ET_TotalArray_EarnDynamic(0) YTDEarn


end-procedure

!***********************************************************************
begin-procedure Init-Variables_EarnDynamic
!***********************************************************************

  let $space_left   = 'y'

let #curr_detail_line = 0

let #ytd_detail_line = 0


let $section_printed = 'n'
let #detail_line = 0

let #gbl_total_earn_hrs_cur_EarnDynamic = 0
let #gbl_total_earn_cur_EarnDynamic = 0
let #gbl_total_earn_hrs_ytd_EarnDynamic = 0
let #gbl_total_earn_ytd_EarnDynamic = 0

if upper($gbl_new_lang_flag) = 'Y'
             
    do Get_Field_Information ($ReportID, 'HRS_EARN_LBL'       , $HRS_EARN_LBL,         #DW)
    do Get_Field_Information ($ReportID, 'DESCR_LBL'   , $DESCR_LBL,       #DW)
    do Get_Field_Information ($ReportID, 'CUR_LBL'       , $CUR_LBL,        #DW)
    do Get_Field_Information ($ReportID, 'YTD_LBL'    , $YTD_LBL,        #DW)
    do Get_Field_Information ($ReportID, 'RATE_LBL'     , $RATE_LBL,           #DW)
    do Get_Field_Information ($ReportID, 'HOURS_LBL'           , $HOURS_LBL,           #DW)
    do Get_Field_Information ($ReportID, 'EARN_LBL'     , $EARN_LBL,       #DW)
    do Get_Field_Information ($ReportID, 'OTHER_LBL'     , $OTHER_LBL,       #DW)
    do Get_Field_Information ($ReportID, 'TOTAL_LBL'     , $TOTAL_LBL,           #DW)   
  end-if
  
end-procedure

!***********************************************************************



    
!**********************************************************************
!**********************************************************************
!      Hours_Earnings_EarnDynamic
!**********************************************************************
!**********************************************************************

begin-procedure Hours_Earnings_EarnDynamic
#debug do Pslp-Proc-Debug-Msg('ALL Earnings section GP00PS21.SQC ')
#debug do Pslp-Proc-Debug-Msg('ALL Earnings section: Procedure Hours_Earnings_EarnDynamic')
    
    do Init-Variables_EarnDynamic
    do Init-Arrays_EarnDynamic
    do Get-Current-DateTime

    while ($section_printed = 'n')
        do get_start_page_line_number(#current-line, #page-count, #start-section-line-number, #start-section-page-number)
         do evaluate_space_for_title(#start-section-line-number,{EARN_DYNAMIC_TITLE_SIZE},$space_left)
        if $space_left = 'n'
            if $gbl_Paycheck_To_Do = 'y'
                Do print_check_usa
                move 'n' to $check_collision 
                move 'n' to $gbl_Paycheck_To_Do
            else
                new-page
                Move 1 to #gbl_curr_Section_Start_Line
                Move 1 to #start-section-line-number
                do Hours_Earnings
                move 'y' to $section_printed
                break
            end-if
        end-if
        if $space_left = 'y'
            if $gbl_Paycheck_To_Do = 'y'
                do evaluate_bankdraft_collision(#start-section-line-number,$check_collision)
                if $check_collision = 'y'
                
                    Do print_check_usa
                    move 'n' to $check_collision 
                    move 'n' to $gbl_Paycheck_To_Do
                else
                    do Hours_Earnings
                    move 'y' to $section_printed
                    break
                end-if
            else
                do Hours_Earnings
                move 'y' to $section_printed
                break
            end-if
        end-if
    end-while
    
end-procedure



!**********************************************************************
!**********************************************************************
!      Hours_Earnings
!**********************************************************************
!**********************************************************************
begin-procedure Hours_Earnings
    
    do get_sum_earnings_elements_EarnDynamic
    Do get_sum_Earn_Accum_EarnDynamic
    do total_line_EarnDynamic
    do Print_Hours_Earnings_Title_EarnDynamic
    do Print_Hours_Earnings_Detail_Lines_EarnDynamic
    do Print_Total_Line_EarnDynamic
    
end-procedure Hours_Earnings_EarnDynamic

!**********************************************************************
!**********************************************************************
!      get_earnings_elements
!**********************************************************************
!**********************************************************************
begin-procedure get_earnings_elements_EarnDynamic
#debug do Pslp-Proc-Debug-Msg('ALL Earnings section: Procedure get_earnings_elements_EarnDynamic')
!select from the extract table

BEGIN-SELECT on-error=SQL-Error

A.RATE_RSLT_VAL        &A.RATE_RSLT_VAL_EARN_CUR_DYN
    Move &A.RATE_RSLT_VAL_EARN_CUR_DYN to #CompRateCode
SUM(A.UNIT_RSLT_VAL)    &A.UNIT_RSLT_VAL_EARN_CUR_DYN
    Move &A.UNIT_RSLT_VAL_EARN_CUR_DYN to #CurrentHrs
SUM(A.CALC_RSLT_VAL)    &A.CALC_RSLT_VAL_EARN_CUR_DYN
    Move &A.CALC_RSLT_VAL_EARN_CUR_DYN to #CurrentEarn 
A.PIN_NUM               &A.PIN_NUM_EARN_CUR_DYN
A.GPUS_PSLP_PRT_HRS     &A.GPUS_PSLP_PRT_HRS_EARN_CUR_DYN
    let $print_cur_earn_dyn_hrs = &A.GPUS_PSLP_PRT_HRS_EARN_CUR_DYN
A.GPUS_PSLP_PRT_RATE    &A.GPUS_PSLP_PRT_RATE_EARN_CUR_DYN
    let $print_cur_earn_dyn_rate = &A.GPUS_PSLP_PRT_RATE_EARN_CUR_DYN

    Do get_pin_name(&A.PIN_NUM_EARN_CUR_DYN, $pin_nm)

    Do curr_detail_line_EarnDynamic
        
A.GP_PSLP_ORDER              &A.GP_PSLP_ORDER_EARN_CUR_DYN
FROM PS_GPUS_PSLP_RSLT A
WHERE A.JOBINSTANCE = #gbl_job_instance AND
A.EMPLID = $gbl_tgt_Emplid AND
A.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
A.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
A.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
A.GP_ELN_SET        = $gbl_GP_ELN_SET AND
A.GPUS_PSLP_ELN_CAT = '10' AND
A.GPUS_PSLP_CURR_YTD = '10' AND A.SEQ_NUM = 20 GROUP BY A.PIN_NUM, A.RATE_RSLT_VAL,
A.GP_PSLP_ORDER,A.GPUS_PSLP_PRT_HRS, A.GPUS_PSLP_PRT_RATE ORDER BY A.GP_PSLP_ORDER

END-SELECT


end-procedure


!**********************************************************************
!**********************************************************************
!      get_Earn_Accum
!**********************************************************************
!**********************************************************************
begin-procedure get_Earn_Accum_EarnDynamic
#debug do Pslp-Proc-Debug-Msg('ALL Earnings section: Procedure get_Earn_Accum_EarnDynamic')
!select from the extract table
BEGIN-SELECT  on-error=SQL-Error

B.RATE_RSLT_VAL         &B.RATE_RSLT_VAL_EARN_YTD_DYN
    Move &B.RATE_RSLT_VAL_EARN_YTD_DYN to #CompRateCode
SUM(B.UNIT_RSLT_VAL)    &B.UNIT_RSLT_VAL_EARN_YTD_DYN
    Move &B.UNIT_RSLT_VAL_EARN_YTD_DYN to #YtdHrs
SUM(B.CALC_RSLT_VAL)    &B.CALC_RSLT_VAL_EARN_YTD_DYN
        Move &B.CALC_RSLT_VAL_EARN_YTD_DYN to #YtdEarn 
B.PIN_NUM               &B.PIN_NUM_EARN_YTD_DYN
B.GPUS_PSLP_PRT_HRS     &B.PRT_HRS_EARN_YTD_DYN
    let $print_ytd_earn_dyn_hrs = &B.PRT_HRS_EARN_YTD_DYN
B.GPUS_PSLP_PRT_RATE    &B.PRT_RATE_EARN_YTD_DYN
    let $print_ytd_earn_dyn_rate = &B.PRT_RATE_EARN_YTD_DYN
     
     Do get_pin_name(&B.PIN_NUM_EARN_YTD_DYN, $pin_nm)
     Do ytd_detail_line_EarnDynamic
B.GP_PSLP_ORDER              &B.GP_PSLP_ORDER_EARN_YTD_DYN
FROM PS_GPUS_PSLP_RSLT B
WHERE B.JOBINSTANCE = #gbl_job_instance AND
B.EMPLID = $gbl_tgt_Emplid AND
B.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
B.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
B.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
B.GP_ELN_SET        = $gbl_GP_ELN_SET AND
B.GPUS_PSLP_ELN_CAT = '10' AND
B.GPUS_PSLP_CURR_YTD = '20' AND 
B.SEQ_NUM = 10 AND
B.SLICE_END_DT = (SELECT MAX(X.SLICE_END_DT) 
FROM PS_GPUS_PSLP_RSLT X
WHERE X.JOBINSTANCE = #gbl_job_instance AND
X.EMPLID = $gbl_tgt_Emplid AND
X.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
X.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
X.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
X.GP_ELN_SET        = $gbl_GP_ELN_SET  AND
X.GPUS_PSLP_ELN_CAT = '10' AND
X.GPUS_PSLP_CURR_YTD = '20')
GROUP BY B.PIN_NUM, B.RATE_RSLT_VAL,B.GP_PSLP_ORDER, B.GPUS_PSLP_PRT_HRS, B.GPUS_PSLP_PRT_RATE  
ORDER BY B.GP_PSLP_ORDER

END-SELECT


end-procedure

!**********************************************************************
!**********************************************************************
!      get_sum_earnings_elements
!**********************************************************************
!**********************************************************************
begin-procedure get_sum_earnings_elements_EarnDynamic
#debug do Pslp-Proc-Debug-Msg('ALL Earnings section: Procedure get_sum_earnings_elements_EarnDynamic')
!select from the extract table

BEGIN-SELECT on-error=SQL-Error

C.RATE_RSLT_VAL        &C.RATE_RSLT_VAL_EARN_CUR_DYN
    Move &C.RATE_RSLT_VAL_EARN_CUR_DYN to #CompRateCode
C.UNIT_RSLT_SUM        &C.UNIT_RSLT_VAL_EARN_CUR_DYN
    Move &C.UNIT_RSLT_VAL_EARN_CUR_DYN to #CurrentHrs
C.CALC_RSLT_SUM        &C.CALC_RSLT_VAL_EARN_CUR_DYN
    Move &C.CALC_RSLT_VAL_EARN_CUR_DYN to #CurrentEarn 
C.PIN_NUM               &C.PIN_NUM_EARN_CUR_DYN
C.GPUS_PSLP_PRT_HRS     &C.GPUS_PSLP_PRT_HRS_EARN_CUR_DYN
    let $print_cur_earn_dyn_hrs = &C.GPUS_PSLP_PRT_HRS_EARN_CUR_DYN
C.GPUS_PSLP_PRT_RATE    &C.GPUS_PSLP_PRT_RATE_EARN_CUR_DYN
    let $print_cur_earn_dyn_rate = &C.GPUS_PSLP_PRT_RATE_EARN_CUR_DYN
    Do get_pin_name(&C.PIN_NUM_EARN_CUR_DYN, $pin_nm)
    let #pin_num = &C.PIN_NUM_EARN_CUR_DYN

    !if printing salary hours then find out if this is the salary hours element
    if $gbl_tgt_salary_hrs_flag = 'Y' and #CurrentHrs = 0   
       if (#pin_num <> #gbl_tgt_salary_pin) or #sal_hrs_pin = 0
         do fetch_salary_hrs_pin_from_array(&C.PIN_NUM_EARN_CUR_DYN, #sal_hrs_pin)
         if #sal_hrs_pin <> 0
            let #gbl_tgt_salary_hrs_pin = #sal_hrs_pin
            let #gbl_tgt_salary_pin = #pin_num
          end-if
       end-if
       let #sal_hrs = 0
       if #sal_hrs_pin <> 0 
           do get_salary_hours(#gbl_tgt_salary_hrs_pin, #sal_hrs)
           if #sal_hrs <> 0
             let #CurrentHrs = to_char(#sal_hrs)
           end-if
         end-if
    end-iF

    Do curr_detail_line_EarnDynamic
        
C.GP_PSLP_ORDER              &C.GP_PSLP_ORDER_EARN_CUR_DYN
FROM PS_GPUS_PSLP_RSUM C
WHERE C.JOBINSTANCE = #gbl_job_instance AND
C.EMPLID = $gbl_tgt_Emplid AND
C.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
C.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
C.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
C.GP_ELN_SET        = $gbl_GP_ELN_SET AND
C.GPUS_PSLP_ELN_CAT = '10' AND
C.GPUS_PSLP_CURR_YTD = '10' AND 
C.SEQ_NUM = 20 
ORDER BY C.PIN_NUM, C.RATE_RSLT_VAL, C.GP_PSLP_ORDER

END-SELECT


end-procedure


!**********************************************************************
!**********************************************************************
!      get_sum_Earn_Accum
!**********************************************************************
!**********************************************************************
begin-procedure get_sum_Earn_Accum_EarnDynamic
#debug do Pslp-Proc-Debug-Msg('ALL Earnings section: Procedure get_sum_Earn_Accum_EarnDynamic')
!select from the extract table
BEGIN-SELECT  on-error=SQL-Error

D.RATE_RSLT_VAL         &D.RATE_RSLT_VAL_EARN_YTD_DYN
    Move &D.RATE_RSLT_VAL_EARN_YTD_DYN to #CompRateCode
D.UNIT_RSLT_SUM         &D.UNIT_RSLT_VAL_EARN_YTD_DYN
    Move &D.UNIT_RSLT_VAL_EARN_YTD_DYN to #YtdHrs
D.CALC_RSLT_SUM         &D.CALC_RSLT_VAL_EARN_YTD_DYN
        Move &D.CALC_RSLT_VAL_EARN_YTD_DYN to #YtdEarn 
D.PIN_NUM               &D.PIN_NUM_EARN_YTD_DYN
D.GPUS_PSLP_PRT_HRS     &D.PRT_HRS_EARN_YTD_DYN
    let $print_ytd_earn_dyn_hrs = &D.PRT_HRS_EARN_YTD_DYN
D.GPUS_PSLP_PRT_RATE    &D.PRT_RATE_EARN_YTD_DYN
    let $print_ytd_earn_dyn_rate = &D.PRT_RATE_EARN_YTD_DYN
     
     Do get_pin_name(&D.PIN_NUM_EARN_YTD_DYN, $pin_nm)
     Do ytd_detail_line_EarnDynamic
D.GP_PSLP_ORDER              &D.GP_PSLP_ORDER_EARN_YTD_DYN
FROM PS_GPUS_PSLP_RSUM D
WHERE D.JOBINSTANCE = #gbl_job_instance AND
D.EMPLID = $gbl_tgt_Emplid AND
D.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
D.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
D.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
D.GP_ELN_SET        = $gbl_GP_ELN_SET AND
D.GPUS_PSLP_ELN_CAT = '10' AND
D.GPUS_PSLP_CURR_YTD = '20' AND 
D.SEQ_NUM = 10 AND 
D.SLICE_END_DT = (SELECT MAX(Y.SLICE_END_DT) 
FROM PS_GPUS_PSLP_RSUM Y
WHERE Y.JOBINSTANCE = #gbl_job_instance AND
Y.EMPLID = $gbl_tgt_Emplid AND
Y.PMT_GRP_ID = #gbl_tgt_Grp_ID AND
Y.CAL_RUN_ID        = $gbl_rc_Calendar_Group AND
Y.GP_CNTRY_PSLP_ELN = $gbl_GP_CNTRY_PSLP_ELN AND
Y.GP_ELN_SET        = $gbl_GP_ELN_SET  AND
Y.GPUS_PSLP_ELN_CAT = '10' AND
Y.GPUS_PSLP_CURR_YTD = '20') 
ORDER BY D.PIN_NUM, D.RATE_RSLT_VAL,D.GP_PSLP_ORDER

END-SELECT


end-procedure



!**********************************************************************
!**********************************************************************
!      curr_detail_line_EarnDynamic
!**********************************************************************
!**********************************************************************


begin-procedure curr_detail_line_EarnDynamic

#debug do Pslp-Proc-Debug-Msg('ALL Earnings section: Procedure curr_detail_line_EarnDynamic')

    Put $pin_nm into ET_DetailArray_EarnDynamic(#curr_detail_line) name
   if upper($print_cur_earn_dyn_rate) = 'Y'
    Put #CompRateCode into ET_DetailArray_EarnDynamic(#curr_detail_line) CompRateCode
   end-if
   if upper($print_cur_earn_dyn_hrs) = 'Y'
    Put #CurrentHrs into ET_DetailArray_EarnDynamic(#curr_detail_line) CurrentHrs
   end-if
    Put #CurrentEarn into ET_DetailArray_EarnDynamic(#curr_detail_line) CurrentEarn
    Add #CurrentHrs to #gbl_total_earn_hrs_cur_EarnDynamic
    Add #CurrentEarn to #gbl_total_earn_cur_EarnDynamic
    Add 1 to #curr_detail_line

end-procedure


!**********************************************************************
!**********************************************************************
!      ytd_detail_line_EarnDynamic
!**********************************************************************
!**********************************************************************


begin-procedure ytd_detail_line_EarnDynamic

    #debug do Pslp-Proc-Debug-Msg('ALL Earnings section: Procedure ytd_detail_line_EarnDynamic')
    let $found = 'n'
    let $update_array = 'n'
    do search_DetailArray_EarnDynamic($pin_nm, $found, #ind, $update_array)
    if $found = 'y'
     if $update_array = 'y'
      if upper($print_ytd_earn_dyn_hrs) = 'Y'
        Array-Add #YtdHrs to ET_DetailArray_EarnDynamic(#ind) YtdHrs
      end-if
        Array-Add #YtdEarn to ET_DetailArray_EarnDynamic(#ind) YtdEarn
      else
      end-if
    else

        Put $pin_nm into ET_DetailArray_EarnDynamic(#curr_detail_line) name
      if upper($print_ytd_earn_dyn_rate) = 'Y'
        Put #CompRateCode into ET_DetailArray_EarnDynamic(#curr_detail_line) CompRateCode
      end-if
      if upper($print_ytd_earn_dyn_hrs) = 'Y'
        Put #YtdHrs into ET_DetailArray_EarnDynamic(#curr_detail_line) YtdHrs
      end-if
        Put #YtdEarn into ET_DetailArray_EarnDynamic(#curr_detail_line) YtdEarn
      Add 1 to #curr_detail_line
    end-if
  
    Add #YtdHrs to #gbl_total_earn_hrs_ytd_EarnDynamic
    Add #YtdEarn to #gbl_total_earn_ytd_EarnDynamic
    
end-procedure

!**********************************************************************
!**********************************************************************
!      search_DetailArray_EarnDynamic
!**********************************************************************
!**********************************************************************
begin-procedure search_DetailArray_EarnDynamic($search_pin_nm, :$found, :#found_ind, :$update_earn_array)
#debug do Pslp-Proc-Debug-Msg('ALL Earnings section: Procedure search_DetailArray_EarnDynamic')

let #i = 0
let #found_ind = 0
let #num_pin_found = 0

while (#i = 0) or (not isblank($Pin_nm))
    get $Pin_nm from ET_DetailArray_EarnDynamic(#i) name 

    if $search_pin_nm = $Pin_nm
        #debug1 show '** ALL Earnings section:     VARS $pin_nm' $pin_nm
        let $found = 'y'
        let #found_ind = #i
        Add 1 to #num_pin_found
    end-if

    Add 1 to #i

end-while
If #num_pin_found = 1
     move 'y' to $update_earn_array
else
     move 'n' to $update_earn_array
     do Append_detail_dyn_array(#num_pin_found, $search_pin_nm)
end-if
end-procedure

!**********************************************************************
!**********************************************************************
!      Append_detail_dyn_array
!**********************************************************************
!**********************************************************************

begin-procedure Append_detail_dyn_array(#num_pin_found, $search_pin_nm)
let #i = 0
let #j = 0
while (#i < {ET_DetailArray_EarnDynamic_Size})
    get $Pin_nm from ET_DetailArray_EarnDynamic(#i) name 
    if $search_pin_nm = $Pin_nm
      Add 1 to #j
      if (#j = #num_pin_found)
        if upper($_print_ytd_earn_static_hrs) = 'Y'
          Array-Add #_YtdHrs to ET_DetailArray_EarnDynamic(#i) YtdHrs
        end-if
        Array-Add #_YtdEarn to ET_DetailArray_EarnDynamic(#i) YtdEarn
        Add #_YtdHrs to #_gbl_total_earn_hrs_ytd
        Add #_YtdEarn to #_gbl_total_earn_ytd
        
      else
        
        Put 'y' into ET_DetailArray_EarnDynamic(#i) Print_Blank 
      end-if 
     end-if

    Add 1 to #i

end-while

end-procedure


!**********************************************************************
!**********************************************************************
!      total_line
!**********************************************************************
!**********************************************************************


begin-procedure total_line_EarnDynamic

Put $TOTAL_LBL into ET_TotalArray_EarnDynamic(0) name
Put 0 into ET_TotalArray_EarnDynamic(0) CompRateCode
Put #gbl_total_earn_hrs_cur_EarnDynamic into ET_TotalArray_EarnDynamic(0) CurrentHrs
Put #gbl_total_earn_cur_EarnDynamic into ET_TotalArray_EarnDynamic(0) CurrentEarn
Put #gbl_total_earn_hrs_ytd_EarnDynamic into ET_TotalArray_EarnDynamic(0) YTDHrs
Put #gbl_total_earn_ytd_EarnDynamic into ET_TotalArray_EarnDynamic(0) YTDEarn

end-procedure


!**********************************************************************
!**********************************************************************
!      Read-totalArray
!**********************************************************************
!**********************************************************************


begin-procedure Read-TotalArray_EarnDynamic

    get $Pin_nm #CompRateCode #CurrentHrs #CurrentEarn #YTDHrs #YTDEarn from ET_TotalArray_EarnDynamic(0)
                name CompRateCode CurrentHrs CurrentEarn YTDHrs YTDEarn
    
end-procedure


!**********************************************************************
!**********************************************************************
!      Print_Hours_Earnings_Title_EarnDynamic
!**********************************************************************
!**********************************************************************
begin-procedure Print_Hours_Earnings_Title_EarnDynamic
#debug do Pslp-Proc-Debug-Msg('ALL Earnings section: Procedure Print_Hours_Earnings_Title_EarnDynamic')

let #row1 = #start-section-line-number
let #totalboxpos = #gbl_curr_Section_Dtl_Lines - 1


  columns 3 
  graphic (#row1,1,126) box  1 10 12
  graphic (#row1,,126)  box  1 10
  graphic (+1,,126)     box  2 10
  !graphic (+2,,126)     box #totalboxpos 10
  !graphic (+#totalboxpos,,126)    box  1 10
  

  use-column 0
  print $HRS_EARN_LBL    (#row1,53) bold
  print '*'                     (+1,38,14) fill
  print $CUR_LBL               (,52)
  print '*'                     (,59,15)   fill
  print '*'                     (,92,9)   fill
  print $YTD_LBL                   (,101)
  print '*'                     (,104,12)   fill
  print $DESCR_LBL           (+1,4)
  print $RATE_LBL                  (,38)
  print $HOURS_LBL                 (,51)
  print $EARN_LBL              (,66)
  print $HOURS_LBL                 (,92)
  print $EARN_LBL              (,108)
 

let #row1 = #row1 + 3



end-procedure
 

!**********************************************************************
!**********************************************************************
!      Print_Hours_Earnings_Detail_Lines_EarnDynamic
!**********************************************************************
!**********************************************************************
begin-procedure Print_Hours_Earnings_Detail_Lines_EarnDynamic


let #i = 0



!while (#i < {ET_DetailArray_EarnDynamic_Size} )
while (#i < #curr_detail_line)
    

columns 4 27 33 49 62 88 102
    get $Pin_nm #CompRateCode #CurrentHrs #CurrentEarn #YTDHrs #YTDEarn $print_blank from ET_DetailArray_EarnDynamic(#i) 
           name CompRateCode CurrentHrs CurrentEarn YTDHrs YTDEarn Print_Blank
   if (#CurrentEarn <> 0 or #YTDEarn <> 0)  
    
   ! if ($Pin_nm = ' ' )
   !     break
   ! else

     let #page-break-test =  #gbl_Page_lines - #current-line 

     if #page-break-test > 1

        print $Pin_nm             (#row1,1)
    
        next-column             !++
 
         next-column
        if #CompRateCode <> 0
              print #CompRateCode          () edit b999.9999mi ! change to 12,6 if needed
        end-if
        next-column
        print #CurrentHrs            () edit b999.99mi
        next-column
       
              print #CurrentEarn          () edit 9,999,990.99mi
      
        next-column
        if $print_blank <> 'y'     
           print #YTDHrs            () edit b9,999.99mi
        end-if
        next-column
        if $print_blank <> 'y'
          print #YTDEarn             () edit 999,999,990.99mi
        end-if

        next-column
          
        add 1 to #row1
        !add 1 to #i
            
            
    else
        new-page
        let #start-section-line-number = 1

        do Print_Hours_Earnings_Title_EarnDynamic
        
     end-if
        
    end-if
   add 1 to #i
    
end-while


end-procedure


!**********************************************************************
!**********************************************************************
!      Print_Total_Line_EarnDynamic
!**********************************************************************
!**********************************************************************
begin-procedure Print_Total_Line_EarnDynamic

do Read-TotalArray_EarnDynamic

columns 4 27 33 49 62 88 102

 let #page-break-test =  #gbl_Page_lines - #current-line 

 if #page-break-test <= 1
      new-page
        let #start-section-line-number = 1

        do Print_Hours_Earnings_Title_EarnDynamic
        columns 4 27 34 47 62 88 102
        print $Pin_nm             (#row1,1)
    next-column             !++
 
    next-column
    if #CompRateCode <> 0
      print #CompRateCode           () edit b999.999999mi ! change to 12,6 if needed
    end-if
    next-column
    print #CurrentHrs            () edit b999.99mi
    next-column
   
      print #CurrentEarn           () edit 9,999,990.99mi
  
    next-column
    print #YTDHrs             () edit b9,999.99mi
    next-column
   
      print #YTDEarn             () edit 999,999,990.99mi
   

    next-column
  Else

    print $Pin_nm             (#row1,1)
    next-column             !++
 
    next-column
    if #CompRateCode <> 0
      print #CompRateCode           () edit b999.999999mi ! change to 12,6 if needed
    end-if
    next-column
    print #CurrentHrs            () edit b999.99mi
    next-column
  
      print #CurrentEarn           () edit 9,999,990.99mi
   
    next-column
    print #YTDHrs             () edit b9,999.99mi
    next-column
   
      print #YTDEarn             () edit 999,999,990.99mi
  

    
    next-column
          
 end-if

end-procedure


!**********************************************************************
!**********************************************************************
!      dyn_fetch_hours_from_pin_array
!**********************************************************************
!**********************************************************************
begin-procedure dyn_fetch_hours_from_pin_array(#pin_num, $pin_descr, :#hours)

#debug1 show 'ALL Earnings section:   dyn_Fetch_hours_FROM_PIN_ARRAY: PIN = '#PIN_NM

let #mid = 0
let #start = 0
let #end = (#_gbl_pin_array_ctr - 1)
let $pin_found = 'N' 

while (#start <= #end) and $pin_found = 'N'
  let #mid = trunc((#start+#end)/2,0) 
  let #pin_array_pin = PIN.NUM(#mid)
  #debug1 show '**                 start = ' #start ' mid = ' #mid ' end = ' #end ' array_PIN = '#pin_array_pin
  if #Pin_num = #pin_array_pin
     let $Pin_Name = PIN.NAME(#mid)
     let $pin_found = 'Y'
     #debug1 show '**                PIN FOUND AT ELEMENT = '#MID
  else 
     if #Pin_num < #pin_array_pin
       let #end = #mid - 1
     else
       if #Pin_num > #pin_array_pin
         let #start = #mid + 1
       end-if
     end-if
  end-if
end-while

!loop again 

let #mid = 0
let #start = 0
let #end = (#_gbl_pin_array_ctr - 1)
let $pin_found = 'N' 

while (#start <= #end) and $pin_found = 'N'
  let $pin_array_custom1 = PIN.CUSTOM1(#mid)
  #debug1 show '**                 start = ' #start ' mid = ' #mid ' end = ' #end ' pin_array_custom1 = '$pin_array_custom1
  if $Pin_Name = $pin_array_custom1
     let #hours = PIN.CALC_RSLT_VAL(#mid)
     let $pin_found = 'Y'
     #debug1 show '**                PIN FOUND AT ELEMENT = '#MID ' HOURS = ' #hours
  else 
       let #mid = #mid + 1
  end-if
end-while


end-procedure



