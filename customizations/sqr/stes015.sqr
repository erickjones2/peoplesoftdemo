!------------------------------------------------------------------------
!  STES015: Grant Value Limit Detail
!------------------------------------------------------------------------
!
!
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!-----------------------------------------------------------------------
!
!          $Date:  2013/01/18:02:50:57                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!
!------------------------------------------------------------------------
! Program Descr:  Prints Grant Value Limit Detail
!------------------------------------------------------------------------
! Prompts:       Stock ID, Stock Plan, Offering ID, Calendar Year, Emplid
! Options:       Sort by Emplid or Name
! Tables:        RUN_CNTL_ST
!                STOCK_DTL
!                ST_ESPP_PURCH
!                ST_PERSON_NAME
!------------------------------------------------------------------------

  #include 'setenv.sqc'    !Set environment

Begin-setup

  #Include 'ptset02.sqc'   !Printer and page-size initialization

End-Setup

!------------------------------------------------------------------------
Begin-Report
!------------------------------------------------------------------------

  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Successful-EOJ

End-Report

!------------------------------------------------------------------------
! Procedure: Report-Translation
! Desc:      This procedure gets the report language and headers
!------------------------------------------------------------------------
Begin-Procedure Report-Translation

  do Init_Report_Translation ('STOCK', $language_cd)
  do Append_Report_Translation ($ReportID)

  do Get_Field_Information ('STOCK'  ,'GRANT',            $GrantLbl,           #DW)
  do Get_Field_Information ('STOCK'  ,'EMPLID',           $EmplidLbl,          #DW)
  do Get_Field_Information ('STOCK'  ,'STOCK_ID',         $StockIdLbl,         #DW)
  do Get_Field_Information ('STOCK'  ,'ASOFDATE',         $AsOfDateLbl,        #DW)
  do Get_Field_Information ('STOCK'  ,'NAME',             $NameLbl,            #DW)
  do Get_Field_Information ('STOCK'  ,'DATE',             $DateLbl,            #DW)
  do Get_Field_Information ('STOCK'  ,'PURCHASE',         $PurchaseLbl,        #DW)
  do Get_Field_Information ('STOCK'  ,'SHARES',           $SharesLbl,          #DW)
  do Get_Field_Information ('STOCK'  ,'PURCHASED',        $PurchasedLbl,       #DW)
  do Get_Field_Information ('STOCK'  ,'STOCK_PLAN',       $StockPlanLbl,       #DW)
  do Get_Field_Information ('STOCK'  ,'PROCESS_GRP',      $ProcessGroupLbl,    #DW)

  do Get_Field_Information ('STES015','STES015',          $Reporttitle,        #DW)
  do Get_Field_Information ('STES015','FMV',              $FmvLbl,             #DW)
  do Get_Field_Information ('STES015','SORT_BY',          $SortByLbl,          #DW)
  do Get_Field_Information ('STES015','EMPLOYEE',         $EmployeeLbl,        #DW)
  do Get_Field_Information ('STES015','YEAR',             $YearLbl,            #DW)
  do Get_Field_Information ('STES015','OFFERING',         $OfferingLbl,        #DW)
  do Get_Field_Information ('STES015','ID',               $IdLbl,              #DW)
  do Get_Field_Information ('STES015','BEGIN_DATE',       $BeginDateLbl,       #DW)
  do Get_Field_Information ('STES015','END_DATE',         $EndDateLbl,         #DW)
  do Get_Field_Information ('STES015','IRS_GRANT',        $IrsGrantLbl,        #DW)
  do Get_Field_Information ('STES015','BALANCE',          $BalanceLbl,         #DW)
  do Get_Field_Information ('STES015','STOCK',            $StockLbl,           #DW)
  do Get_Field_Information ('STES015','PLAN_ID',          $Plan_IdLbl,         #DW)
  do Get_Field_Information ('STES015','VALUE',            $ValueLbl,           #DW)
  do Get_Field_Information ('STES015','GRANT_VALUE_LIMIT',$GrantValueLimitLbl, #DW)
  do Get_Field_Information ('STES015','YRSOUT',           $HyrsoutLbl,         #DW)
  do Get_Field_Information ('STES015','CALYEAR',          $HcalyearLbl,        #DW)
  do Get_Field_Information ('STES015','CALYEAR2',         $Hcalyear2Lbl,       #DW)
  
End-Procedure

!------------------------------------------------------------------------
Begin-Procedure Init-Report
!------------------------------------------------------------------------

  move 'STES015' to $ReportID
  display $ReportTitle
  do Get-Report-Language
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Define-Prcs-Vars
  do Get-Run-Control-Parms

  if $prcs_process_instance = ''
  else
    do Get-SQR-Parms
  end-if

  let $StockId     = $RC_STOCK_ID
  let $EmplId      = $RC_EMPLID
  let $StockPlan   = $RC_STOCK_PLAN
  let $OfferingId  = $RC_OFFERING_ID
  let #Year        = #RC_YEAR
  let $SortBy      = $RC_SORT_BY
  let $SQLQuote    = ''''
  let $WhereSort   = ''
  let $WhereEmplid = ''
  let $WhereSelect   = ''
  do Format-Number(#Year, $Year, '9999')
  do Get-Purchase-Value-Rules
  let #Balance = #PurchaseValueLim

  if $PurchaseLimRule = 'C'
    #ifdef ORACLE
      let $WhereSelect = ' and TO_CHAR(A.PERIOD_END_DT,' || $SQLQuote || 'YYYY' || $SQLQuote || ') = ' || $Year
    #else
      #ifdef SYBASE
        let $WhereSelect = ' and DATEPART(YEAR,A.PERIOD_END_DT) = ' || $Year
      #else
        let $WhereSelect = ' and YEAR(A.PERIOD_END_DT) = ' || $Year
      #end-if
    #end-if
  else
    let $WhereSelect = ' and A.STOCK_PLAN = ' || $SQLQuote || $StockPlan  || $SQLQuote || ' and A.OFFERING_ID = ' || $SQLQuote || $OfferingId  || $SQLQuote
  end-if

  if $EmplId <> ''
    let $WhereEmplid = ' and A.EMPLID = ' || $SQLQuote || $EmplId || $SQLQuote
  end-if

  if $SortBy = 'E'
    let $WhereSort = 'ORDER BY A.EMPLID,A.PERIOD_END_DT,A.OFFERING_ID,A.GRANT_DT'
  else
    let $WhereSort = 'ORDER BY B.NAME,A.PERIOD_END_DT,A.OFFERING_ID,A.GRANT_DT'
  end-if

End-Procedure

!------------------------------------------------------------------------
! Procedure:  Get-Purchase-Value-Rules
!------------------------------------------------------------------------
Begin-Procedure Get-Purchase-Value-Rules

Begin-Select

A1.PURCHASE_VALUE_LIM
A1.PURCHASE_LIM_RULE

  let $PurchaseLimRule  = rtrim(&a1.purchase_lim_rule, ' ')
  let #PurchaseValueLim =  &a1.purchase_value_lim

 from PS_STOCK_DTL A1
where A1.STOCK_ID = $StockId
  and A1.EFFDT    = (SELECT MAX(A2.EFFDT)
                       from PS_STOCK_DTL A2
                      where A2.STOCK_ID = $RC_STOCK_ID
                        and A2.EFFDT   <= $AsOfToday)

End-Select

End-Procedure

!------------------------------------------------------------------------
Begin-Heading 09
!------------------------------------------------------------------------

  #include 'stdhdg01.sqc'

  print $StockIdLbl                                        (03,001,009)
  print $StockId                                           (  ,022,010)

  if $PurchaseLimRule = 'Y'
    print $HYrsOutLbl                                      (03,   ,043) center
    print $OfferingLbl                                     (04,001,008)
    print $OfferingId                                      (  ,022,010)
    print $StockPlanLbl                                    (05,001,010)
    print $StockPlan                                       (  ,022,010)
    print $GrantValueLimitLbl                              (06,001,018)
    print #PurchaseValueLim                                (  ,019,013) edit $$,$$9,999.99
  else
    print $HCalYearLbl                                     (03,   ,030) center
    print $HCalYear2Lbl                                    (04,001,013)
    print $Year                                            (  ,022,004)
    print $GrantValueLimitLbl                              (05,001,018)
    print #PurchaseValueLim                                (  ,019,013) edit $$,$$9,999.99
  end-if

  print  $GrantLbl                                         (08,076,005)
  print  $PurchaseLbl                                      (  ,088,018)
  print  $GrantLbl                                         (  ,105,005)
  print  $SharesLbl                                        (  ,128,006)
  print  $IrsGrantLbl                                      (  ,161,011)
  Print  $NameLbl                                          (09,001,004)
  print  $EmplidLbl                                        (  ,032,006)
  print  $YearLbl                                          (  ,045,004)
  Print  $StockPlanLbl                                     (  ,051,010)
  print  $OfferingLbl                                      (  ,063,010)
  print  $DateLbl                                          (  ,076,004)
  print  $DateLbl                                          (  ,088,004)
  print  $FmvLbl                                           (  ,107,003)
  print  $PurchasedLbl                                     (  ,125,009)
  print  $IrsGrantLbl                                      (  ,142,011)
  print  $BalanceLbl                                       (  ,157,015)
  print '-'                                                (10,001,172) fill

End-Heading

!------------------------------------------------------------------------
! Procedure:  Process-Main
! Descr:      This is where all required data is selected
!------------------------------------------------------------------------
Begin-Procedure Process-Main

Begin-Select

A.EMPLID                                                   () On-Break print=never level=1 procedure=Reset-Begin-Balance
#ifdef ORACLE
TO_CHAR(A.PERIOD_END_DT,'YYYY') &OfferYear
#else
#ifdef SYBASE
DATEPART(YEAR,A.PERIOD_END_DT)  &OfferYear
#else
YEAR(A.PERIOD_END_DT)           &OfferYear
#end-if
#end-if

A.STOCK_PLAN
A.OFFERING_ID
A.PERIOD_END_DT
A.GRANT_DT
A.GRANT_FMV
A.PURCH_RECEIPT_DT
A.SHARES_PURCHASED
B.NAME

  let $Emplid          = rtrim(&a.emplid,' ')
  let $StockPlan       = rtrim(&a.stock_plan,' ')
  let $OfferingId      = rtrim(&a.offering_id,' ')
  let $PeriodEndDt     = rtrim(&a.period_end_dt,' ')
  let $GrantDt         = rtrim(&a.grant_dt,' ')
  let #GrantFmv        = &a.grant_fmv
  let $PurchReceiptDt  = rtrim(&a.purch_receipt_dt,' ')
  let #SharesPurchased = &a.shares_purchased
  let $Name            = rtrim(&b.name,' ')
  let #GrantValue      = round(#SharesPurchased * #GrantFmv,2)
  let #Rows            = #Rows + 1

  do Print-Details

from PS_ST_ESPP_PURCH  A,
     PS_ST_PERSON_NAME B
where A.STOCK_ID = $StockId
  and A.EMPLID   = B.EMPLID
[$WhereEmplid]
[$WhereSelect]
[$WhereSort]

End-Select

End-Procedure

!------------------------------------------------------------------------
Begin-Procedure Print-Details
!------------------------------------------------------------------------

  if #Rows = 1
    let $EmplidOld = $Emplid
    print $Name                                            (+1,001,030)
    print $Emplid                                          (  ,032,011)
  else
    if $Emplid <> $EmplidOld
      let $EmplidOld = $Emplid
      print $Name                                          (+2,001,030)
      print $Emplid                                        (  ,032,011)
    else
      print ''                                             (+1,001,001)
    end-if
  end-if

  let #Balance = (#Balance - #GrantValue)
  print &OfferYear                                         (  ,045,004)
  print $StockPlan                                         (  ,051,010)
  print $OfferingId                                        (  ,063,011)
  do Format-DateTime($GrantDt, $out, {DEFDATE}, '', '')
  print $out                                               (  ,076,010)
  do Format-DateTime($PeriodEndDt, $out, {DEFDATE}, '', '')
  print $out                                               (  ,088,010)
  print #GrantFmv                                          (  ,100,010) edit 999.999999
  print #SharesPurchased                                   (  ,112,022) edit 999,999,999,999.999999
  print #GrantValue                                        (  ,136,017) edit $9,999,999,999.99
  print #Balance                                           (  ,155,017) edit $9,999,999,999.99

End-Procedure Print_Details

!------------------------------------------------------------------------
Begin-Procedure Reset-Begin-Balance
!------------------------------------------------------------------------

  let #Balance = #PurchaseValueLim

End-Procedure

!------------------------------------------------------------------------
!  SQC Include Files
!------------------------------------------------------------------------
#include 'strunctl.sqc'   ! Stock Admin's Run Control Record
#include 'prcsapi.sqc'
#include 'prcsdef.sqc'    ! Update Process Open_Trans variable declaration
#include 'eoj.sqc'        ! End of Report Format
#include 'sqrtrans.sqc'   ! Access Strings Table
#include 'reset.sqc'      ! Reset printer procedure
#include 'curdttim.sqc'   ! Get-Current-DateTime procedure
                          ! Procedures: Get-Current-DateTime
#include 'datemath.sqc'
#include 'datetime.sqc'   ! Routines for date and time formatting
                          ! Procedures: Init-Datetime
                          ! Procedures: Format-DateTime
                          ! Procedures: Diff-Date
                          ! Procedures: many others.
#include 'number.sqc'     ! Procedures: Init-Number
#include 'getrplng.sqc'   ! Get the report language
