!************************************************************************
!  STSU008:  STOCK EXCHAGE
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:56:19                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-Setup
  #include 'ptpsp125.sqc'  !Printer and page-size init
end-Setup

#define  col2    16
#define  col3    20

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let     $ReportID     =   'STSU008'

  do Get-Report-Language

  if $prcs_process_instance = ''
     do Prompts             !Prompt user

  else
     do Get-SQR-Parms
     do Get-Values
  end-if

  show    'FLOW: Init-Report'
  show    'Report Title: ' $ReportTitle

end-procedure

!******************************************************************************
begin-procedure Prompts
!******************************************************************************

!-----------------------------------------------------------------------------!
! Procedure: Prompts                                                          !
! Descr:     Prompts for all input parameters in the event the program is run !
!            directly from SQR.  In normal use, this should not happen.       !
!-----------------------------------------------------------------------------!

end-procedure

!************************************************************************
begin-procedure Get-Values
!************************************************************************

end-procedure


!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ('STOCK',$language_cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK','CURRENCY_CD',    $CurrencyCd,       #DW)
  do Get_Field_Information ('STOCK','NAME',           $Name,             #DW)
  do Get_Field_Information ('STOCK','COMPANY',        $Company,          #DW)
  do Get_Field_Information ('STOCK','STOCK_ID',       $StockID,          #DW)

  do Get_Field_Information ('STSU008','STSU008',        $ReportTitle,      #DW)
  do Get_Field_Information ('STSU008','EFF_STATUS',     $EffStatus,        #DW)
  do Get_Field_Information ('STSU008','EXCHANGE_ID',    $ExchangeID,       #DW)
  do Get_Field_Information ('STSU008','PRICE_SOURCE',   $PriceSource,      #DW)
  do Get_Field_Information ('STSU008','ST_TICKER_SYMBL',$StTickerSymbol,   #DW)
  do Get_Field_Information ('STSU008','EXCHANGE',       $Exchange,         #DW)
  do Get_Field_Information ('STSU008','CONTACT',        $Contact,          #DW)
  do Get_Field_Information ('STSU008','ADDRESS',        $Address,          #DW)
  do Get_Field_Information ('STSU008','PHONE',          $Phone,            #DW)
  do Get_Field_Information ('STSU008','EMAIL_ADDR',     $EmailAddr,        #DW)
  do Get_Field_Information ('STSU008','FAX_NBR',        $FaxNbr,           #DW)
  do Get_Field_Information ('STSU008','DESCR',          $Descr,            #DW)

end-procedure

!************************************************************************
begin-heading 3
!************************************************************************

#include 'stdhdgtr.sqc'

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'


begin-Select

A.STOCK_ID         () ON-BREAK Print=never  before = Select-Dtl
A.EFFDT
A.EFF_STATUS
A.EXCHANGE_ID
A.PRICE_SOURCE
A.ST_TICKER_SYMBL
A.CURRENCY_CD
A.CONTACT_NAME
A.EMAIL_ADDR
A.FAX
A.ADDRESS1
A.HOUSE_TYPE
A.COUNTY
A.GEO_CODE
A.IN_CITY_LIMIT
A.CITY
A.STATE
A.COUNTRY
A.POSTAL
A.PHONE

 do Print-Data

    from  PS_STOCK_EXCHANGE  A
order by  A.STOCK_ID,A.EFFDT desc

end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

B.STOCK_ID
B.DESCRSHORT

  do Print-Info

 from  PS_STOCK_TBL  B
where  B.STOCK_ID    = &A.STOCK_ID

end-Select

end-procedure

!************************************************************************
begin-procedure Print-Info
!************************************************************************

  print $StockID             (+2,1,08)
  print ':'                  (,,1)
  print &B.STOCK_ID          (0,+2,10)
  print &B.DESCRSHORT        (0,+5,10)

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  do Format-DateTime(&A.EFFDT, $EFFDT1, {DEFYMD}, '', '')

  print $EFFDT1              (+2,1,10)

  do Get-Data
  do Get-Descr

  print $Exchange            (+1,{col2},08)

  print $ExchangeID          (+1,{col3},11)
  print ':'                  (,,1)
  print &A.EXCHANGE_ID       (0,+2,10)

  print $Descr               (+1,{col3},05)
  print ':'                  (,,1)
  print &D.DESCR             (0,+2,30)

  print $EffStatus           (+1,{col3},06)
  print ':'                  (,,1)
  print $STATUS              (0,+2,30)

  print $StTickerSymbol      (+1,{col3},14)
  print ':'                  (,,1)
  print &A.ST_TICKER_SYMBL   (0,+2,05)

  print $CurrencyCd          (+1,{col3},08)
  print ':'                  (,,1)
  print &E.DESCRSHORT        (0,+2,10)        !CURRENCY CD

  print $PriceSource         (+1,{col3},13)
  print ':'                  (,,1)
  print &A.PRICE_SOURCE      (0,+2,30)

  print $Contact             (+2,{col2},07)

  print $Name                (+1,{col3},04)
  print ':'                  (,,1)
  print &A.CONTACT_NAME      (0,+3,50)

  print $Phone               (+1,{col3},05)
  print ':'                  (,,1)
  print &A.PHONE             (0,+2,24)

  print $EmailAddr           (+1,{col3},05)
  print ':'                  (,,1)
  print &A.EMAIL_ADDR        (0,+2,70)

  print $FaxNbr              (+1,{col3},06)
  print ':'                  (,,1)
  print &A.FAX               (0,+1,24)

  print $Address             (+2,{col2},07)

  print &A.ADDRESS1          (+1,{col3},35)

   let $TempCity = rtrim(&A.CITY,' ') || ', ' || rtrim(&A.STATE,' ') || ' ' || &A.POSTAL

  print   $TempCity          (+1,{col3},51)

  let $TempCounty = rtrim(&A.COUNTY,' ') || ' ' || &A.COUNTRY

  print   $TempCounty        (+1,{col3},34)
  print   ' '                (+1,1,1)

end-procedure

!************************************************************************
begin-procedure Get-Data
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Data                                             !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'EFF_STATUS'
  let $FieldValue = &A.EFF_STATUS

  do Read-Translate-Table

  let $STATUS = RTRIM($XlatLongName,' ')

end-procedure

!************************************************************************
begin-procedure Get-Descr
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Descr                                             !
! Descr:      This procedure gets the description of the specific   !
!             field from the prompt tables                          !
!-------------------------------------------------------------------!

begin-Select

D.DESCRSHORT
D.DESCR

 from   PS_ST_EXCHANGE_TBL D
where   EXCHANGE_ID  =    &A.EXCHANGE_ID
  and   EFFDT        =    (Select  MAX(EFFDT)
                             from  PS_ST_EXCHANGE_TBL
                            where  EXCHANGE_ID  =   D.EXCHANGE_ID
                              and  EFFDT        <=  &A.EFFDT)

end-Select


begin-Select

E.DESCRSHORT

from   PS_CURRENCY_CD_TBL E
where  E.CURRENCY_CD  =   &A.CURRENCY_CD
  and  EFFDT          =   (Select MAX(EFFDT)
                           from   PS_CURRENCY_CD_TBL
                          where   CURRENCY_CD  =  E.CURRENCY_CD
                            and   EFFDT        <= &A.EFFDT)

end-Select

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************

#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
#include 'strunctl.sqc'  !Get run control parameter values
