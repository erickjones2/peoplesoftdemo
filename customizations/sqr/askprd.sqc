!***********************************************************************
! ASKPRD.SQC:  Input logic for user enterd period begin/end dates         *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:12:19:58                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!----------------------------------------------------------------------!
! Procedure:    Ask-Period-Begin-End-Dates                             !
! Description:  Loops until user enters a valid Period Begin-End Dates !
!----------------------------------------------------------------------!

Begin-Procedure Ask-Period-Begin-End-Dates
  display ''
  display 'You may restrict this report to a specified period begin and end dates.'
  display 'Choose the value from the options below or press enter to accept defaults:'
  display ''
  display '   A = All Dates (default)'
  display '   S = Specify a date range'
  display ''

  input $Answer maxlen=1 'Enter your Period Begin/End date range selection (A/S)'
  uppercase $Answer

  do Get-Period-Date-Values

End-Procedure

!----------------------------------------------------------------------!
! Procedure:    Get-Period-Date-Values                                 !
! Description:  Loops until user enters a valid Begin-End set of dates !
!----------------------------------------------------------------------!

Begin-Procedure Get-Period-Date-Values

  do Get-Century-Begin-Date

  if $Answer = 'A' or $Answer = ''
     move $Century_Begin_Dt to $PeriodBegin
     move $AsOfToday to $PeriodEnd
     goto Finish
  else
     do Get-Period-Dates

     While $DateOK <> 'Y'
       do Get-Period-Dates
     End-While
   end-if

  Finish:

  do Format-DateTime($PeriodBegin, $out1, {DEFDATE}, '', '')
  do Format-DateTime($PeriodEnd, $out2, {DEFDATE}, '', '')

  display 'Report Value:  ' noline
  display $out1 noline
  display ' thru ' noline
  display $out2

End-Procedure

!----------------------------------------------------------------------!
! Procedure:    Get-Period-Dates                                       !
! Description:  Controls prompting for user entered From-Thru dates    !
!----------------------------------------------------------------------!

Begin-Procedure Get-Period-Dates

  move 'Y' to $DateOK

  if $FromDateOK <> 'Y'
     display ''
     display 'Enter the Period ''Begin'' Date ({Prompt-Date}) for this report or press'

     input $PeriodBegin 'enter to default the ''Begin'' date to 01/01/2000'

     if $PeriodBegin = ''
        move $Century_Begin_Dt to $PeriodBegin
     else
        move {PDelimiter} to $DDelimiter
        do Format-DateTime($PeriodBegin,$PeriodBegin,{Prompt-Mask},'','native')
     end-if

     do Validate-Native-Date($PeriodBegin,$DateOK)

     if $DateOK = 'N'
         display '**** This is an invalid date, please reenter ****'
     end-if
  end-if

  if $DateOK = 'Y'
     move 'Y' to $FromDateOK
     display ''
     display 'Enter the Period ''End'' Date ({Prompt-Date}) for this report or press'

     input $PeriodEnd 'enter to default the ''End'' Date to today''s date'

     if $PeriodEnd = ''
        move $AsOfToday to $PeriodEnd
     else
        move {PDelimiter} to $DDelimiter
        do Format-DateTime($PeriodEnd,$PeriodEnd,{Prompt-Mask},'','native')
     end-if

     do Validate-Native-Date($PeriodEnd,$DateOK)

     if $DateOK = 'N'
         display '**** This is an invalid date, please reenter ****'
     end-if

     if $DateOK = 'Y'
        do Check-Period-Dates
     end-if
  end-if
End-Procedure

!----------------------------------------------------------------------!
! Procedure:    Check-Period-Dates                                            !
! Description:  Validates that the thru date is > than the from date   !
!----------------------------------------------------------------------!

Begin-Procedure Check-Period-Dates

  do Format-DateTime($PeriodBegin,$out1,{DEFCMP},'','')
  do Format-DateTime($PeriodEnd,$out2,{DEFCMP},'','')
  if $out1 > $out2
    display ''
    display 'Begin Date can not be greater than End Date'
    move 'N' to $DateOK
    move 'N' to $FromDateOK
  end-if
End-Procedure

!----------------------------------------------------------------------!
! Procedure:    Get-Century-Begin-Date                                 !
! Description:  Retrieves the century begin date from the installation !
!               table.                                                 !
!----------------------------------------------------------------------!

Begin-Procedure Get-Century-Begin-Date

  do Format-DateTime('20000101',$Century_Begin_Dt,{DEFCMP},'','native')

End-Procedure

#include 'validdt.sqc'          ! Validate date routine
