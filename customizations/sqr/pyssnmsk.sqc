!***********************************************************************
!  PYSSNMSK: Common procedures for SSN masking on year end forms       *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  22508392                                            !
!                                                                      *
!***********************************************************************
!
!***********************************************************************

begin-procedure Get-SSN-Masking-for-XMLP($ProcessingCopyType,:$W2_Masking)

   let $W2_Mask_EE    = ''
   let $W2_Mask_CO    = ''
   let $W2_Mask_GVT   = ''

begin-select

MASK.PY_W2_MASK_SSN_EE
MASK.PY_W2_MASK_SSN_CO
MASK.PY_W2_MASK_SSN_GVT

   if RTRIM(&MASK.PY_W2_MASK_SSN_EE, ' ') = ''
      let $W2_Mask_EE   = 'N'
   else
      let $W2_Mask_EE   = RTRIM(&MASK.PY_W2_MASK_SSN_EE, ' ')
   end-if

   if RTRIM(&MASK.PY_W2_MASK_SSN_CO, ' ') = ''
      let $W2_Mask_CO   = 'N'
   else
      let $W2_Mask_CO   = RTRIM(&MASK.PY_W2_MASK_SSN_CO, ' ')
   end-if

   if RTRIM(&MASK.PY_W2_MASK_SSN_GVT, ' ') = ''
      let $W2_Mask_GVT  = 'N'
   else
      let $W2_Mask_GVT  = RTRIM(&MASK.PY_W2_MASK_SSN_GVT, ' ')
   end-if

   evaluate $ProcessingCopyType
    when = '1'
      let $W2_Masking   = $W2_Mask_GVT
      break
    when = '2'
      let $W2_Masking   = $W2_Mask_EE
      break
    when = '4'
      let $W2_Masking   = $W2_Mask_CO
      break
    end-evaluate

FROM  PS_PY_YE_MASK_SSN MASK

end-select

end-procedure !Get-SSN-Masking-for-XMLP



begin-procedure Mask-SSN($FullSSN,:$MaskedSSN)

   let $MaskedSSN = ''
   let $FullSSN   = rtrim($FullSSN, ' ')

   let #SSNLen    = length($FullSSN)
   let $Last_Four = Substr($FullSSN, #SSNLen - 3, 4)

   let #Otherdigit_Len = #SSNLen - 4
   let #j = 1

   While #j <= #Otherdigit_Len
      if Substr($FullSSN, #j, 1) = ' ' or
         Substr($FullSSN, #j, 1) = '-'
         let $MaskedSSN = $MaskedSSN || Substr($FullSSN, #j, 1)
      else
         let $MaskedSSN = $MaskedSSN || 'X'
      end-If

      let #j = #j + 1
   End-While

   if #SSNLen > 4
      let $MaskedSSN = $MaskedSSN || $Last_Four
   else
      let $MaskedSSN = $FullSSN
   end-if

end-procedure !Mask-SSN



begin-procedure Reset-Mask-SSN ($UpdRecord)
begin-SQL

UPDATE [$UpdRecord]
SET PY_W2_MASK_SSN = 'N'
WHERE OPRID       = $_Prcs_OprID
  AND RUN_CNTL_ID = $_Prcs_Run_Cntl_ID

end-SQL
end-procedure !Reset-Mask-SSN
