!***********************************************************************
!  GPMXAA04:  Accreditable Subsidy                                     *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:03:49:51                                 *
!       $Release:  HR92                                                *
!      $Revision:  101                                                 *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set environment
#Include 'setup02.sqc'   !Printer and page-size initialization

begin-report
   move '1' to $ReportDateType
   move '1' to $ReportYear4
   do Init-DateTime
   do Init-Number
   do Stdapi-Init
   do Get-Current-DateTime
   do Init-Report
   do Stdapi-Term
   do Reset
end-report


begin-procedure Init-Report
   move 'GPMXAA04' to $ReportID
   move 'Subsidio Acreditable Promedio' to $ReportTitle
   display $ReportID
   display $ReportTitle
   date-time () hh:mi:ss &timeBegan
   display 'Report Began: ' noline
   display &timeBegan

   do Select-Parameters
   do Build-Query-Company
   do Select-Company-Data
   date-time () hh:mi:ss &timeEnded
   display 'Report Ended: ' noline
   display &timeEnded
end-procedure Init-Report


begin-heading 11
    uppercase $ReportTitle
    print $ReportTitle           (2,) center
    print 'PeopleSoft'           (1,) center
    print 'Reporte : '           (4,1)
    print $ReportID              ()
    print 'Compañía: '           (5,1)
    print $Company               ()
    print $Company_Name          (0,+5)
    print 'Año del Ejercicio: '  (6,1)
    print $Bal_Year              ()
    page-number                  (4,157) 'Página No. '
    print 'Hora : '              (5,157)
    date-time () HH:MM:SS  &ReportTime
    print &ReportTime            ()
    print 'Fecha: '              (6,157)
    print $ReportDate            ()

    print '='                    (+2,1,175)fill
    print 'Cve.'                 (+1,78)
    print 'R.F.C.'               ( ,84)
    print 'Subsidio Acred'       ( ,98)
    print 'Subs No Acred.'       ( ,114)
    print 'Subsidio'             ( ,130)
    print 'Subsidio'             ( ,146)
    print 'Subsidio'             ( ,163)

    print 'C.Empleado'           (+1,1)
    print 'Nombre del Empleado'  ( ,14)
    print 'F. Ingreso'           ( ,54)
    print 'Fecha Baja'           ( ,66)
    print 'Cía'                  ( ,78)
    print 'Compañía'             ( ,84)
    print 'Otros Patrones'       ( ,98)
    print 'Otros Patrones'       ( ,114)
    print 'Acreditable'          ( ,130)
    print 'No Acreditable'       ( ,146)
    print 'Promedio'             ( ,163)
    print '='                    (+1,1,175)fill
end-heading


begin-procedure Select-Company-Data
   let $BegDtDTU = $Bal_Year || '-01-01'
   do Convert-From-DTU-Date($BegDtDTU, $BegDt)
   let $EndDtDTU = $Bal_Year || '-12-31'
   do Convert-From-DTU-Date($EndDtDTU, $EndDt)
begin-select
CC.COMPANY
CC.DESCR
CM.RFC_ER_ID_MEX
   let #Empl_Count_Comp = 0
   let $Company      = rtrim(&CC.COMPANY,' ')
   let $Company_Name = rtrim(&CC.DESCR,' ')
   let $Company_RFC  = rtrim(&CM.RFC_ER_ID_MEX,' ')
   do Get-Emplid
   if #Empl_Count_Comp > 0
      do Print-Totals
      new-page
   end-if

FROM PS_COMPANY_TBL     CC,
     PS_COMPANY_TBL_MEX CM

WHERE CC.EFFDT = (SELECT MAX(CC1.EFFDT) FROM PS_COMPANY_TBL CC1
                  WHERE CC1.COMPANY = CC.COMPANY
                  AND   CC1.EFFDT <= $EndDt)
  AND CC.COMPANY = CM.COMPANY
  AND CM.EFFDT   = CC.EFFDT
[$Query_Company]
ORDER BY CC.COMPANY
end-select
end-procedure Select-Company-Data


begin-procedure Get-Emplid
begin-select
OTH.EMPLID
OTH.EMPL_RCD
OTH.RFC_ER_ID_MEX
OTH.GPMX_BALANCE_YEAR
OTH.START_DT
OTH.END_DT
PER.NAME
   let $Emplid     = rtrim(&OTH.EMPLID,' ')
   let #Empl_Rcd   = &OTH.EMPL_RCD
   let $Comp_RFC   = rtrim(&OTH.RFC_ER_ID_MEX,' ')
   let $Begin_Date = rtrim(&OTH.START_DT,' ')
   let $End_Date   = rtrim(&OTH.END_DT,' ')
   let $Name       = rtrim(&PER.NAME,' ')

   do Convert-To-DTU-Date($Begin_Date,$Begin_Date1)
   do Convert-To-DTU-Date($End_Date,$End_Date1)
   let $Begin_DtP = substr($Begin_Date1,9,2) || '-' || substr($Begin_Date1,6,2) || '-' || substr($Begin_Date1,1,4)
   let $End_DtP = substr($End_Date1,9,2) || '-' || substr($End_Date1,6,2) || '-' || substr($End_Date1,1,4)

   print $Emplid                 (+1,1)     on-break print=always level=1 save=$Old_Emplid after=Get-Act-Values
   print $Name                   ( ,14,37)  on-break print=always level=1 save=$Old_Name
   print $Begin_DtP              ( ,54)
   print $End_DtP                ( ,66)
   print $Comp_RFC               ( ,84)
   print $Company_Id             ( ,78)

   do Get-Detail-Values

   print #Acred_Others           ( ,98) edit 999,999,999.99
   print #NoAcred_Others         ( ,114) edit 999,999,999.99

FROM PS_GPMX_OTH_ER OTH,
     PS_PERSON_NAME PER,
     PS_JOB J
WHERE OTH.EMPLID = PER.EMPLID
AND   OTH.GPMX_BALANCE_YEAR = #Bal_Year
AND   OTH.EMPLID = J.EMPLID
AND   J.COMPANY = $Company
AND   J.EFFDT = (SELECT MAX(J1.EFFDT) FROM PS_JOB J1
                 WHERE J1.EMPLID = J.EMPLID
                 AND   J1.EMPL_RCD = J.EMPL_RCD
                 AND   J1.EFFDT <= $EndDt)
AND   J.EFFSEQ = (SELECT MAX(J2.EFFSEQ) FROM PS_JOB J2
                  WHERE J2.EMPLID = J.EMPLID
                  AND   J2.EMPL_RCD = J.EMPL_RCD
                  AND   J2.EFFDT = J.EFFDT)
ORDER BY OTH.EMPLID, OTH.END_DT
end-select
end-procedure Get-Emplid


begin-procedure Get-Detail-Values
  let #Acred_Others = 0
  let #NoAcred_Others = 0
begin-select
O.GPMX_ERNDED_CD
O.AMOUNT
  evaluate &O.GPMX_ERNDED_CD
    when = '0070'
      let #Acred_Others = &O.AMOUNT
      let #Acum_Acred_Others = #Acum_Acred_Others + &O.AMOUNT
      break
    when = '0080'
      let #NoAcred_Others = &O.AMOUNT
      let #Acum_NoAcred_Others = #Acum_NoAcred_Others + &O.AMOUNT
      break
  end-evaluate
  do Get-Company-Id

FROM PS_GPMX_OTH_ER_DET O
WHERE O.EMPLID = $Emplid
AND   O.EMPL_RCD = #Empl_Rcd
AND   O.RFC_ER_ID_MEX = $Comp_RFC
AND   O.GPMX_BALANCE_YEAR = #Bal_Year
AND   O.START_DT = $Begin_Date
AND   O.END_DT = $End_Date
AND   O.GPMX_ERNDED_CD IN ('0070','0080')
end-select
end-procedure Get-Detail-Values


begin-procedure Get-Act-Values
  let #Acred = 0
  let #NoAcred = 0
  let #Promedio = 0
begin-select
F.GPMX_BALANCE_YEAR
F.COMPANY
F.EMPLID
MAX(F.GPMX_SUB_RET) &GPMX_SUB_RET
MAX(F.GPMX_SUB_NO_ACRE) &GPMX_SUB_NO_ACRE

  let #Acred = &GPMX_SUB_RET
  let #NoAcred = &GPMX_SUB_NO_ACRE

FROM PS_GPMX_FORM37_WA F
WHERE F.GPMX_BALANCE_YEAR = #Bal_Year
AND   F.COMPANY = $Company
AND   F.EMPLID  = $Old_Emplid
GROUP BY F.GPMX_BALANCE_YEAR, F.COMPANY, F.EMPLID
ORDER BY F.GPMX_BALANCE_YEAR, F.COMPANY, F.EMPLID
end-select

begin-select
  let $Term_Date = ''
  let $Service_Date = ''
E.TERMINATION_DT
E.SERVICE_DT
  let $Term_Date = &E.TERMINATION_DT
  let $Service_Date = &E.SERVICE_DT
  do Convert-To-DTU-Date($Term_Date,$Term_Date1)
  do Convert-To-DTU-Date($Service_Date,$Service_Date1)
  if rtrim($Term_Date,' ') <> ''
     let $Term_DateP = substr($Term_Date1,9,2) || '-' || substr($Term_Date1,6,2) || '-' || substr($Term_Date1,1,4)
  else
     let $Term_DateP = ''
  end-if
  if rtrim($Service_Date,' ') <> ''
     let $Service_DateP = substr($Service_Date1,9,2) || '-' || substr($Service_Date1,6,2) || '-' || substr($Service_Date1,1,4)
  else
     let $Service_DateP = ''
  end-if

FROM PS_PER_ORG_ASGN_VW E
WHERE EMPLID = $Old_Emplid
end-select

  let #subs1 = #Acred + #Acum_Acred_Others
  let #Subs2 = #Subs1 + #NoAcred + #Acum_NoAcred_Others
  if #Subs2 > 0
     let #Promedio = #Subs1 / #Subs2
  end-if

  let #Empl_Count = #Empl_Count + 1
  let #Empl_Count_Comp = #Empl_Count_Comp + 1
  let #Acum_Acred_Others = 0
  let #Acum_NoAcred_Others = 0

  print $Old_Emplid         (+1,1)
  print $Old_Name           ( ,14,37)
  print $Service_DateP      ( ,54)
  print $Term_DateP         ( ,66)
  print $Company            ( ,78)
  print $Company_RFC        ( ,84)
  print #Acred              ( ,130) edit 999,999,999.99
  print #NoAcred            ( ,146) edit 999,999,999.99
  print #Promedio           ( ,163) edit 88.88888888
end-procedure Get-Act-Values


begin-procedure Print-Totals
  print 'Total de Empleados: '    (+3,1)
  print #Empl_Count               ( ,+5)  edit 888888
end-procedure Print-Totals


begin-procedure Select-Parameters
begin-select
P.COMPANY
P.GPMX_BALANCE_YEAR

  let $Company      = rtrim(&P.COMPANY,' ')
  let #Bal_Year     = &P.GPMX_BALANCE_YEAR
  let $Bal_Year     = to_char(&P.GPMX_BALANCE_YEAR)

FROM PS_GPMX_RC_ANNLADJ P
WHERE P.OPRID = $prcs_oprid
AND   P.RUN_CNTL_ID = $prcs_run_cntl_id
end-select
end-procedure Select-Parameters


begin-procedure Get-Company-Id
   let $Company_Id = ''
begin-select
CD.COMPANY
   let $Company_Id = &CD.COMPANY

FROM PS_COMPANY_TBL_MEX CD
WHERE CD.RFC_ER_ID_MEX = $Comp_RFC
AND   CD.EFFDT = (SELECT MAX(CC1.EFFDT)
                  FROM PS_COMPANY_TBL_MEX CC1
                  WHERE CC1.COMPANY = CD.COMPANY
                  AND   CC1.EFFDT <= $EndDt)
end-select
end-procedure Get-Company-Id


begin-procedure Build-Query-Company
  let $Query_Company = ' '
  if $Company <> ''
     let $Query_Company = $Query_Company || 'AND CC.COMPANY = ' || chr(39) || $Company || chr(39)
  end-if
end-procedure Build-Query-Company


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Datemath Routines
