!************************************************************************
!  STSU024:  Stock Allocation
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:57:37                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
   #include 'ptpsl177.sqc'  !Printer and page-size init (landscape)
end-setup

#define  col2    5
#define  col3    8
#define  col4    10
#define  col5    16
#define  col6    31
#define  col7    26
#define  col8    38
#define  col9    51
#define  col10   71
#define  col11   96
#define  col12   110
#define  col13   135

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let     $ReportID     =   'STSU024'

  do Get-Report-Language

  if $prcs_process_instance = ''
     do Prompts             !Prompt user

  else
     do Get-SQR-Parms
     do Get-Values
  end-if

  show    'FLOW: Init-Report'
  show    'Report Title: ' $REPORTTITLE

end-procedure

!******************************************************************************
begin-procedure Prompts
!******************************************************************************

!-----------------------------------------------------------------------------!
! Procedure: Prompts                                                          !
! Descr:     Prompts for all input parameters in the event the program is run !
!            directly from SQR.  In normal use, this should not happen.       !
!-----------------------------------------------------------------------------!

end-procedure

!************************************************************************
begin-procedure Get-Values
!************************************************************************

end-procedure

!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ('STOCK',$language_cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK','STOCK_ID',          $STOCK_ID,           #DW)
  do Get_Field_Information ('STOCK','FROM',              $FROM,               #DW)
  do Get_Field_Information ('STOCK','SHARES_ALLOCATED',  $SHARES_ALLOCATED,   #DW)

  do Get_Field_Information ('STSU024','STSU024',           $REPORTTITLE,        #DW)
  do Get_Field_Information ('STSU024','STOCK_CLASS_TYPE',  $STOCK_CLASS_TYPE,   #DW)
  do Get_Field_Information ('STSU024','STOCK_CLASS',       $STOCK_CLASS,        #DW)
  do Get_Field_Information ('STSU024','CONVERSN_RULES',    $CONVERSN_RULES,     #DW)
  do Get_Field_Information ('STSU024','FRACTION_SHARES',   $FRACTION_SHARES,    #DW)
  do Get_Field_Information ('STSU024','RATIO',             $RATIO,              #DW)
  do Get_Field_Information ('STSU024','TO',                $TO,                 #DW)
  do Get_Field_Information ('STSU024','ROUNDING_RULE',     $ROUNDING_RULE,      #DW)
  do Get_Field_Information ('STSU024','DEC_PLACES',        $DEC_PLACES,         #DW)
  do Get_Field_Information ('STSU024','UNIT_OF_OWNERSHIP', $UNIT_OF_OWNERSHIP,  #DW)
  do Get_Field_Information ('STSU024','IMPLEMENTATION_DT', $IMPLEMENTATION_DT,  #DW)
  do Get_Field_Information ('STSU024','ALLOCATION_DT',     $ALLOCATION_DT,      #DW)
  do Get_Field_Information ('STSU024','SEQ_NUM',           $SEQ_NUM,            #DW)
  do Get_Field_Information ('STSU024','TREASURY_ID',       $TREASURY_ID,        #DW)
  do Get_Field_Information ('STSU024','ALLOCATION_TYPE',   $ALLOCATION_TYPE,    #DW)
  do Get_Field_Information ('STSU024','LAST_EMPLID',       $LAST_EMPLID,        #DW)
  do Get_Field_Information ('STSU024','LAST_DTTM_UPDATE',  $LAST_DTTM_UPDATE,   #DW)
  do Get_Field_Information ('STSU024','DESCR',             $STDESCR,            #DW)

end-procedure

!************************************************************************
begin-heading 3
!************************************************************************

#include 'stdhdgtr.sqc'

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

begin-Select

A.STOCK_ID                () ON-BREAK Print=never before=Select-Dtl
A.STOCK_CLASS_TYPE
A.STOCK_CLASS
A.DESCRSHORT
A.DESCR

  do Print-Info
  do Select-LowerDtl
  print ' '                    (+1,1,1)

 from     PS_STOCK_CLASS_TBL  A
order by  A.STOCK_ID

end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

B.STOCK_ID
B.DESCRSHORT

  do Print-data

 from  PS_STOCK_TBL B
where  B.STOCK_ID   =  &A.STOCK_ID

end-Select

end-procedure

!***********************************************************************
begin-procedure Select-LowerDtl
!***********************************************************************

begin-Select

F.STOCK_ID
F.STOCK_CLASS_TYPE
F.ALLOCATION_DT
F.SEQ_NUM

 from  PS_ST_CLASS_ALLOC  F
where  F.STOCK_ID         =  &A.STOCK_ID
  and  F.STOCK_CLASS_TYPE =  &A.STOCK_CLASS_TYPE
  and  F.STOCK_CLASS      =  &A.STOCK_CLASS

end-Select

 if rtrim(&F.ALLOCATION_DT,' ') != ''
    do Print-heading
 end-if


begin-Select

C.STOCK_ID
C.STOCK_CLASS_TYPE
C.STOCK_CLASS
C.ALLOCATION_DT
C.SEQ_NUM
C.ST_TREASURY_ID
C.DESCR
C.ST_ALLOCATION_TYPE
C.SHARES_ALLOCATED
C.LASTUPDOPRID
C.LASTUPDDTTM

  do Print-LowerInfo

 from  PS_ST_CLASS_ALLOC C
where  STOCK_ID           =  &A.STOCK_ID
  and  STOCK_CLASS_TYPE   =  &A.STOCK_CLASS_TYPE
  and  STOCK_CLASS        =  &A.STOCK_CLASS

end-Select

end-procedure

!************************************************************************
begin-procedure Print-heading
!************************************************************************

  print $ALLOCATION_DT         (+2,{col2},15)
  print $SEQ_NUM               (0,25,07)
  print $TREASURY_ID           (0,37,11)
  print $ALLOCATION_TYPE       (0,51,15)
  print $SHARES_ALLOCATED      (0,77,16)
  print $LAST_EMPLID           (0,+4,20)
  print $LAST_DTTM_UPDATE      (0,+4,26)
  print $STDESCR               (0,+4,20)
  print ' '                    (+1,1,1)

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  print $STOCK_ID              (+2,0,08)
  print ':'                    (,,1)
  print &A.STOCK_ID            (0,+2,10)
  print &B.DESCRSHORT          (0,+5,10)


end-procedure

!************************************************************************
begin-procedure Print-Info
!************************************************************************

  do Get-Data
  do Get-Descr

  print $STOCK_CLASS_TYPE      (+2,{col2},17)
  print ':'                    (,,1)
  print $ClassType             (0,+2,10)

  print $STOCK_CLASS           (+1,{col2},15)
  print ':'                    (,,1)
  print &A.STOCK_CLASS         (0,+2,10)

  print $STDESCR              (+2,{col2},05)
  print ':'                    (,,1)
  print &A.DESCRSHORT          (0,+2,10)

end-procedure

!***********************************************************************
begin-procedure Print-LowerInfo
!***********************************************************************

  do Format-DateTime(&C.ALLOCATION_DT,    $AllocDate,  {DEFYMD}, '', '')
  do Format-DateTime(&C.LASTUPDDTTM,      $LastUpDate, {DEFYMD}, '', '')

  print $AllocDate             (+1,{col2},10)
  print &C.SEQ_NUM             (0,{col7},15)

  do Get-Data
  do Get-Descr

  print &D.DESCRSHORT          (0,{col8},10)       !ST_TREASURY_ID
  print $AllocType             (0,{col9},10)

  do Format-Number(&C.SHARES_ALLOCATED, $SHARES_ALLOC, '999,999,999,990.000000')

  print $SHARES_ALLOC          (0,{col10},22)
  print &C.LASTUPDOPRID        (0,+4,20)
  print $LastUpDate            (0,+4,26)
  print &C.DESCR               (0,+4,20)

end-procedure

!************************************************************************
begin-procedure Get-Data
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Data                                              !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'STOCK_CLASS_TYPE'
  let $FieldValue = &A.STOCK_CLASS_TYPE

  do Read-Translate-Table

  let $ClassType = RTRIM($XlatShortName,' ')


  let $FieldName = 'ST_ALLOCATION_TYPE'
  let $FieldValue = &C.ST_ALLOCATION_TYPE

  do Read-Translate-Table

  let $AllocType = RTRIM($XlatShortName,' ')

end-procedure


!************************************************************************
begin-procedure Get-Descr
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Descr                                             !
! Descr:      This procedure gets the description of the specific   !
!             field from the prompt tables                          !
!-------------------------------------------------------------------!

begin-Select

D.DESCRSHORT                   !TREASURY ID DESCR

 from  PS_STOCK_TREASURY   D
where  D.STOCK_ID       =  &C.STOCK_ID
  and  D.ST_TREASURY_ID =  &C.ST_TREASURY_ID

end-Select

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************

#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
#include 'strunctl.sqc'  !Get run control parameter values
