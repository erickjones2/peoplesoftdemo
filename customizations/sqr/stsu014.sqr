!************************************************************************
!  STSU014:  STOCK BLACKOUT
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!
!          $Date:  2013/01/18:02:56:48                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
  #include 'ptpsl177.sqc'  !Printer and page-size init (landscape)
end-setup

#define  col2    16
#define  col3    28
#define  col4    47
#define  col5    65
#define  col6    84
#define  col7    102
#define  col8    115
#define  col9    123

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let     $ReportID     =   'STSU014'

  do Get-Report-Language

  if $prcs_process_instance = ''
     do Prompts             !Prompt user

  else
     do Get-SQR-Parms
     do Get-Values
  end-if

  show    'FLOW: Init-Report'
  show    'Report Title: ' $REPORTTITLE

end-procedure

!******************************************************************************
begin-procedure Prompts
!******************************************************************************

!-----------------------------------------------------------------------------!
! Procedure: Prompts                                                          !
! Descr:     Prompts for all input parameters in the event the program is run !
!            directly from SQR.  In normal use, this should not happen.       !
!-----------------------------------------------------------------------------!

end-procedure

!************************************************************************
begin-procedure Get-Values
!************************************************************************

end-procedure


!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ('STOCK',$language_cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK','EMPLID',            $EMPLID,            #DW)
  do Get_Field_Information ('STOCK','NAME',              $NAME,              #DW)
  do Get_Field_Information ('STOCK','STOCK_ID',          $STOCK_ID,          #DW)
  do Get_Field_Information ('STOCK','EMPL_RCD',          $EMPL_RCD,          #DW)


  do Get_Field_Information ('STSU014','STSU014',           $REPORTTITLE,       #DW)
  do Get_Field_Information ('STSU014','BLACKOUT_BEGIN_DT', $BLACKOUT_BEGIN_DT, #DW)
  do Get_Field_Information ('STSU014','BLACKOUT_END_DT',   $BLACKOUT_END_DT,   #DW)
  do Get_Field_Information ('STSU014','BLACKOUT_ALL',      $BLACKOUT_ALL,      #DW)
  do Get_Field_Information ('STSU014','COMPANY_INSIDER',   $COMPANY_INSIDER,   #DW)
  do Get_Field_Information ('STSU014','REPORTING_PERSON',  $REPORTING_PERSON,  #DW)
  do Get_Field_Information ('STSU014','DIRECTOR',          $DIRECTOR,          #DW)
  do Get_Field_Information ('STSU014','OFFICER',           $OFFICER,           #DW)
  do Get_Field_Information ('STSU014','ADDTNL_PARTCPNTS',  $ADDTNL_PARTCPNTS,  #DW)
  do Get_Field_Information ('STSU014','DESCR',             $DESCR,             #DW)

end-procedure

!************************************************************************
begin-heading 3
!************************************************************************

#include 'stdhdgtr.sqc'

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

begin-Select

A.STOCK_ID             () ON-BREAK Print=never before=Select-Dtl
A.BLACKOUT_BEGIN_DT
A.BLACKOUT_END_DT
A.DESCR
A.BLACKOUT_ALL
A.COMPANY_INSIDER
A.REPORTING_PERSON
A.ST_DIRECTOR
A.ST_OFFICER

   do Print-Info
   do Select-LowerDtl

 from    PS_ST_BLACKOUT_TBL A
order by A.STOCK_ID

end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

B.STOCK_ID
B.DESCRSHORT

  do Print-data

 from  PS_STOCK_TBL B
where  B.STOCK_ID   =  &A.STOCK_ID

end-Select

end-procedure

!***********************************************************************
begin-procedure Select-LowerDtl
!***********************************************************************

begin-Select

F.STOCK_ID
F.EMPLID

from  PS_ST_BLACKOUT_LST   F
where  F.STOCK_ID = &B.STOCK_ID
  and  F.BLACKOUT_BEGIN_DT =  &A.BLACKOUT_BEGIN_DT
  and  F.BLACKOUT_END_DT =  &A.BLACKOUT_END_DT

end-Select

 if rtrim(&F.EMPLID,' ') != ''
    do Print-heading
 end-if


begin-Select

C.STOCK_ID
C.BLACKOUT_BEGIN_DT
C.BLACKOUT_END_DT
C.EMPLID

  do GET-GRANT-NBR
  do Print-LowerInfo

from   PS_ST_BLACKOUT_LST   C
where   C.STOCK_ID           =  &A.STOCK_ID
  and   C.BLACKOUT_BEGIN_DT =  &A.BLACKOUT_BEGIN_DT
  and   C.BLACKOUT_END_DT =  &A.BLACKOUT_END_DT

end-Select

  print   ' '                     (+1,1,1)

end-procedure

!**************************************************************************
begin-procedure GET-GRANT-NBR
!**************************************************************************

begin-Select

SED.GRANT_NBR

  DO GET-EMPL-RCD

from   PS_ST_EXER_DISPOSN SED
where SED.EMPLID = &C.EMPLID
  AND SED.STOCK_ID  =  &A.STOCK_ID
  and SED.DISPOSITION_DT >=  &A.BLACKOUT_BEGIN_DT
  and SED.DISPOSITION_DT <= &A.BLACKOUT_END_DT

end-Select


begin-Select

SER.GRANT_NBR

  DO GET-EMPL-RCD
from   PS_ST_EXER_RELEASE SER
where SER.EMPLID = &C.EMPLID
  AND SER.STOCK_ID  =  &A.STOCK_ID
  and SER.RELEASE_DT >=  &A.BLACKOUT_BEGIN_DT
  and SER.RELEASE_DT <= &A.BLACKOUT_END_DT

end-Select

end-procedure

!**************************************************************************
begin-procedure GET-EMPL-RCD
!**************************************************************************

begin-Select

ST.EMPL_RCD

       Let #Empl_RCD = &ST.EMPL_RCD
 from  PS_ST_GRANT ST
where  ST.EMPLID = $EMPLID
   AND ST.STOCK_ID   =  &A.STOCK_ID
   AND ST.GRANT_NBR = &SED.GRANT_NBR

end-Select

end-procedure



!************************************************************************
begin-procedure Print-heading
!************************************************************************

    print    $ADDTNL_PARTCPNTS    (+2,{col2},23)
    print    $EMPLID              (+2,{col2},06)
    print    $EMPL_RCD            (0,+2,08)
    print    $NAME                (0,+6,04)

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  print $STOCK_ID                 (+2,1,08)
  print ':'                       (,,1)
  print &A.STOCK_ID               (0,+2,10)
  print &B.DESCRSHORT             (0,+5,10)

  print  $BLACKOUT_BEGIN_DT       (+2,{col2},10)
  print  $BLACKOUT_END_DT         (0,+2,08)
  print  $BLACKOUT_ALL            (0,+6,12)
  print  $COMPANY_INSIDER         (0,+5,15)
  print  $REPORTING_PERSON        (0,+5,14)
  print  $DIRECTOR                (0,+5,08)
  print  $OFFICER                 (0,+5,07)
  print  $DESCR                   (0,+5,05)
  print  ' '                      (+1,1,1)

end-procedure

!************************************************************************
begin-procedure Print-Info
!************************************************************************


  do Format-DateTime(&A.BLACKOUT_BEGIN_DT, $BeginDate, {DEFYMD}, '', '')
  do Format-DateTime(&A.BLACKOUT_END_DT,   $EndDate,   {DEFYMD}, '', '')

  print $BeginDate                (+1,{col2},10)
  print $EndDate                  (0,{col3},10)
  print &A.BLACKOUT_ALL           (0,{col4},01)
  print &A.COMPANY_INSIDER        (0,{col5},01)
  print &A.REPORTING_PERSON       (0,{col6},01)
  print &A.ST_DIRECTOR            (0,{col7},01)
  print &A.ST_OFFICER             (0,{col8},01)
  print &A.DESCR                  (0,{col9},30)

end-procedure

!***********************************************************************
begin-procedure Print-LowerInfo
!***********************************************************************

  print &C.EMPLID                 (+1,{col2},11)
  print &ST.EMPL_RCD              (0,{col3},10)
  do Get-Descr

  print &D.NAME                   (0,{col4},40)

end-procedure

!************************************************************************
begin-procedure Get-Descr
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Descr                                             !
! Descr:      This procedure gets the description of the specific   !
!             field from the prompt tables                          !
!-------------------------------------------------------------------!

begin-Select

D.NAME
D.EMPLID

from   PS_PERSONAL_VW D
where  EMPLID  =    &C.EMPLID

end-Select

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************

#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
#include 'strunctl.sqc'  !Get run control parameter values
