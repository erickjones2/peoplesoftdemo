!***********************************************************************
! Report Name: ASKCAR.SQC - Input logic for P11D reports               *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2013/01/17:12:56:09                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
! This program asks the user to enter parameters if the calling report *
! is run directly via the SQRW icon.                                   *
!***********************************************************************
!                                                                      *
! Tables Referenced:                    Read  Delete  Update  Insert   *
!                                       ----  ------  ------  ------   *
!                    PS_PERSON_NAME       x                            *
!                                                                      *
! Related programs:  called by CAR001.SQR, CAR002.SQR                  *
!                                                                      *
!***********************************************************************

begin-procedure Ask-CAR001

  do Ask-Tax-Year-End
  do Ask-Emplid
  do Ask-EmplRcdNum

end-procedure

!-----------------------------------------------------------------------
! Get UK Tax Year End Date. Input as 4-digit year.
!-----------------------------------------------------------------------
begin-procedure Ask-Tax-Year-End

  display ''

  while 1 = 1

    input $Answer maxlen=4 'Enter the Tax Year End (YYYY) for this report, or press Enter for previous tax year' type=integer

    if $Answer = ''
      move 0 to #tax_year_end
      let $tax_year_end_uk = RTRIM(to_char(#tax_year_end), ' ')
      break
    else
      move $Answer to #tax_year_end
      let $tax_year_end_uk = RTRIM(to_char(#tax_year_end), ' ')
      do Validate-Tax-Year-End
      if $OK = 'Y'
        break
      else
        Display 'Invalid Tax Year End. ' noline
      end-if
    end-if

  end-while


end-procedure Ask-Tax-Year-End

!-----------------------------------------------------------------------
! Validate Tax Year End
!-----------------------------------------------------------------------
begin-procedure Validate-Tax-Year-End

  let $OK = ''

begin-SELECT

X.XLATLONGNAME
  let $OK = 'Y'

from PSXLATITEM X
where X.EFF_STATUS  = $active
  and X.FIELDNAME   = 'TAX_YEAR_END_UK'
  and X.FIELDVALUE  = $tax_year_end_uk
  and X.EFFDT       = (SELECT MAX(Y.EFFDT) from PSXLATITEM Y
                         where Y.FIELDNAME   = 'TAX_YEAR_END_UK'
                           and Y.FIELDVALUE  = $tax_year_end_uk
                           and Y.EFFDT <= $AsOfDate)

end-SELECT

end-procedure Validate-Tax-Year-End

!-----------------------------------------------------------------------
! Get Employee ID
!-----------------------------------------------------------------------
begin-procedure Ask-Emplid

  while 1 = 1

    input $Emplid maxlen=11 'Enter the employee ID, or press Enter for all employees'
    let $Emplid = rtrim($Emplid, ' ')
    if $Emplid = ''
      break
    else
      do Validate-Emplid
      if $OK = 'Y'
        let $emplid   = RTRIM($emplid, ' ')
        let #empl_rcd = 0
        break
      else
        Display 'Invalid Employee ID. ' noline
      end-if
    end-if

  end-while

end-procedure Ask-Emplid

!-----------------------------------------------------------------------
! Validate Employee ID
!-----------------------------------------------------------------------
begin-procedure Validate-Emplid

  let $OK = ''

begin-SELECT
E.NAME
  let $OK = 'Y'
FROM PS_PERSON_NAME E
WHERE E.EMPLID = $Emplid
end-SELECT

end-Procedure Validate-Emplid

!-----------------------------------------------------------------------
! Get Employee Record Number
!-----------------------------------------------------------------------
begin-procedure Ask-EmplRcdNum

  input $Answer maxlen=1 'Enter the Employee Record Number, or press Enter for all records' type=integer

  if $Answer = ''
    move -1 to #empl_rcd
  else
    move $Answer to #empl_rcd
  end-if

end-Procedure Ask-EmplRcdNum

