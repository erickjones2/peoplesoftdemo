
! ***********************************************************************
!   SETIDUTL:  PROCEDURES TO ADD SETID AND SETCNTRLVAL                  *
! ***********************************************************************
! ***********************************************************************
!                                                                       *
!                                                                       *
!                This software and related documentation are provided under a  *
!                license agreement containing restrictions on use and          *
!                disclosure and are protected by intellectual property         *
!                laws. Except as expressly permitted in your license agreement *
!                or allowed by law, you may not use, copy, reproduce,          *
!                translate, broadcast, modify, license, transmit, distribute,  *
!                exhibit, perform, publish or display any part, in any form or *
!                by any means. Reverse engineering, disassembly, or            *
!                decompilation of this software, unless required by law for    *
!                interoperability, is prohibited.                              *
!                The information contained herein is subject to change without *
!                notice and is not warranted to be error-free. If you find any *
!                errors, please report them to us in writing.                  *
!                                                                              *
! ***********************************************************************
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                       *
!          $Date:  2013/01/17:15:47:22                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                       *
! ***********************************************************************




! *****************************************************************
!  Procedure To Insert values into SETID Table                    *
! *****************************************************************

Begin-Procedure Add-Setid($SETID, $DESCR, $DESCRSHORT)

do SetidCnt($SETID, #CNT_SETID)

if #Cnt_Setid = 0
  do Insert-Setid(($SETID, $DESCR, $DESCRSHORT)
else
  display 'Setid: ' noline
  display $setid noline
  display ' already exists'
  display 'Setid not added'
end-if

End-Procedure


Begin-Procedure Insert-Setid($SETID, $DESCR, $DESCRSHORT)

Begin-SQL  ON-ERROR=SQL-Error
Insert into PS_SETID_TBL
(SETID, DESCR, DESCRSHORT, PARTIAL_SHARING)
Values ($SETID, $DESCR, $DESCRSHORT, 'N' )
End-SQL

End-Procedure


Begin-Procedure Create-Setcntrl-Data($SETCNTRLVALUE, $DEFAULTSETID)

do CntrlValCnt($SETCNTRLVALUE, #CNT_CNTRLVAL)
if #CNT_CNTRLVAL = 0
  do Insert-SetCntrlValue($SETCNTRLVALUE, $DEFAULTSETID)
else
  display 'Setcntrlvalue: ' noline
  display $Setcntrlvalue noline
  display ' already exists'
  display 'Setcntrlvalue was not created.'
  display 'If necessary change the SETID for the required Record Group'
end-if

end-procedure


! *****************************************************************
!  Procedure To Insert values into SETCONTROLVALUE Tables         *
! *****************************************************************
Begin-Procedure Insert-SetCntrlValue($SETCNTRLVALUE, $DEFAULTSETID)

  do GetVersionPPC(#VERSION)
  do Insert-CntrlTbl($SETCNTRLVALUE, $DEFAULTSETID, #VERSION)
  do Insert-CntrlGrp($SETCNTRLVALUE)
  do Insert-CntrlRec($SETCNTRLVALUE)

End-Procedure




Begin-Procedure SetidCnt($DEFAULTSETID, :#CNT_SETID)
Begin-Select  ON-ERROR=SQL-Error
COUNT(*)  &CNT_SETID
  Move &CNT_SETID to #CNT_SETID

FROM PS_SETID_TBL
WHERE SETID = $DEFAULTSETID
End-Select
End-Procedure


Begin-Procedure CntrlValCnt($SETCNTRLVALUE, :#CNT_CNTRLVAL)
Begin-Select  ON-ERROR=SQL-Error
COUNT(*)  &CNT_CNTRLVAL
 Move &CNT_CNTRLVAL TO #CNT_CNTRLVAL

FROM PS_SET_CNTRL_TBL
WHERE SETCNTRLVALUE = $SETCNTRLVALUE
End-Select
End-Procedure



! *****************************************************************
! Procedure For PeopleCode Version                                *
! *****************************************************************
Begin-Procedure GetVersionPPC(:#VERSION)
Begin-Select  ON-ERROR=SQL-Error

VERSIONPPC

     Move &VERSIONPPC to #Version
     Let #Version = #Version + 1

     Do VersionPPC-Update(#Version)

FROM PSLOCK
End-Select
End-Procedure


Begin-Procedure VersionPPC-Update(#Version)
Begin-SQL  ON-ERROR=SQL-Error
UPDATE PSLOCK
SET VERSIONPPC = #Version, VERSION = VERSION + 1
End-SQL
End-Procedure


! *****************************************************************
!  Insert values into PS_SET_CNTRL_TBL Table                      *
! *****************************************************************
Begin-Procedure Insert-CntrlTbl($SETCNTRLVALUE, $SETID, #VERSION)
Begin-SQL  On-Error=SQL-Error
Insert Into PS_SET_CNTRL_TBL
(SETCNTRLVALUE, SETID, VERSION)
Values
($SETCNTRLVALUE, $SETID, #VERSION)
End-SQL
End-Procedure


! *****************************************************************
!  Insert values into PS_SET_CNTRL_GROUP Table                    *
! *****************************************************************
Begin-Procedure Insert-CntrlGrp($SETCNTRLVALUE)
Begin-SQL  On-Error=SQL-Error
Insert Into PS_SET_CNTRL_GROUP
(SETCNTRLVALUE, REC_GROUP_ID, SETID)
SELECT A.SETCNTRLVALUE, B.REC_GROUP_ID, A.SETID
FROM PS_SET_CNTRL_TBL A, PS_REC_GROUP_TBL B
WHERE A.SETCNTRLVALUE = $SETCNTRLVALUE

End-SQL
End-Procedure


! *****************************************************************
!  Insert values into PS_SET_CNTRL_REC Table                      *
! *****************************************************************
Begin-Procedure Insert-CntrlRec($SETCNTRLVALUE)

Begin-SQL  On-Error=SQL-Error
INSERT INTO PS_SET_CNTRL_REC
(SETCNTRLVALUE, REC_GROUP_ID, SETID, RECNAME)
SELECT A.SETCNTRLVALUE, B.REC_GROUP_ID, A.SETID, B.RECNAME
FROM PS_SET_CNTRL_TBL A, PS_REC_GROUP_REC B
WHERE A.SETCNTRLVALUE = $SETCNTRLVALUE
End-SQL

End-Procedure


