!*************************************************************
!  Getsupjp:  Get Supervisor Level Name from the supervisor level ID Table
!*************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!          $Date:  2013/01/17:13:41:18                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************
!
!**********************************
begin-procedure Get-Supvlvl-Name
!**********************************

  !Keys passed in:  $SETID_DEPT, $SUPV_LVL_ID, $AsOfDate
  !Values returned: $SupvlvlName, $SupvlvlNameAbbrv

  move '' to $SupvlvlName
  move '' to $SupvlvlNameAbbrv

  if Rtrim($SETID_DEPT,' ') = ''
    goto GetSupvExit
  end-if

  if Rtrim($SUPV_LVL_ID,' ') = ''
    goto GetSupvExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
XX.DESCR
XX.DESCRSHORT

  let $SupvlvlName      = Rtrim(&XX.DESCR,' ')
  let $SupvlvlNameAbbrv = Rtrim(&XX.DESCRSHORT,' ')

FROM  PS_SUPVSR_LVL_TBL XX
WHERE XX.SETID = $SETID_DEPT AND XX.SUPV_LVL_ID = $SUPV_LVL_ID
  AND XX.EFFDT  =
       (SELECT MAX(EFFDT)
        FROM   PS_SUPVSR_LVL_TBL
        WHERE  SETID = XX.SETID AND SUPV_LVL_ID = XX.SUPV_LVL_ID
          AND  EFFDT <= $AsOfDate)
end-SELECT

  do Get-Supvlvl-Name-Lang

GetSupvExit:

end-procedure

!***************************************
begin-procedure Get-Supvlvl-Name-Lang
!***************************************

  !Keys passed in:  $SETID_DEPT, $SUPV_LVL_ID, $AsOfDate
  !Values returned: $SupvlvlName, $SupvlvlNameAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetSupvLngExit
  end-if

  if Rtrim($SETID_DEPT,' ') = ''
    goto GetSupvLngExit
  end-if

  if Rtrim($SUPV_LVL_ID,' ') = ''
    goto GetSupvLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
XXL.DESCR
XXL.DESCRSHORT

  if Rtrim(&XXL.DESCR,' ') <> ''
    let $SupvlvlName = Rtrim(&XXL.DESCR,' ')
  end-if
  if Rtrim(&XXL.DESCRSHORT,' ') <> ''
    let $SupvlvlNameAbbrv = Rtrim(&XXL.DESCRSHORT,' ')
  end-if

FROM  PS_SUPVSR_LVL_LANG XXL
WHERE XXL.SETID      = $SETID_DEPT AND XXL.SUPV_LVL_ID      = $SUPV_LVL_ID
  AND XXL.LANGUAGE_CD = $Curr_Language_Cd
  AND XXL.EFFDT =
        (SELECT MAX(EFFDT)
         FROM  PS_SUPVSR_LVL_LANG
         WHERE SETID = XXL.SETID
           AND SUPV_LVL_ID = XXL.SUPV_LVL_ID
           AND LANGUAGE_CD = XXL.LANGUAGE_CD
           AND EFFDT      <= $AsOfDate)
end-SELECT

GetSupvLngExit:

end-procedure
!***************************** End of Include File **************************
