!***********************************************************************
!  GPDEUT05 :  Gets the Emplid Name, Address, Phone, Email             *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!----------------------------------------------------------------------
!
!          $Date:  2013/01/18:03:37:51                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

begin-procedure Get-Names($IN_EMPLID,$IN_NAME_TYPE,:$Full_Name,:$NAME_TITLE,:$LastName,:$FirstName)

Begin-Select
NM.NAME
NM.NAME_PREFIX
NM.NAME_TITLE
NM.NAME_ROYAL_PREFIX
NM.NAME_ROYAL_SUFFIX
FROM PS_NAMES NM , PS_PERSON NM1
WHERE NM.EMPLID    = $IN_EMPLID
AND   NM.NAME_TYPE = $IN_NAME_TYPE
AND   NM.EMPLID    = NM1.EMPLID
AND   NM.EFFDT     =(SELECT MAX(NM2.EFFDT) FROM PS_NAMES NM2 WHERE
                     NM2.EMPLID    = NM.EMPLID AND
                     NM2.NAME_TYPE = NM.NAME_TYPE AND
                     NM2.EFFDT    <= NM.EFFDT)
end-SELECT


  move '' to $Full_Name
  move '' to $Letter_Name


  let $Name              = rtrim(&NM.NAME,' ')
  let $NAME_PREFIX       = rtrim(&NM.NAME_PREFIX,' ')
  let $NAME_TITLE        = rtrim(&NM.NAME_TITLE, ' ')
  let $NAME_ROYAL_PREFIX = rtrim(&NM.NAME_ROYAL_PREFIX, ' ')
  let $NAME_ROYAL_SUFFIX = rtrim(&NM.NAME_ROYAL_SUFFIX, ' ')

  !find last name
  let $field = rtrim($Name,' ')
  find ',' in $Name 0 #locn
  extract $LastName from $Name 0 #locn

  !find first name
  add 2 to #locn
  let $Name = substr($Name,#locn,50 - #locn)
  find ',' in $Name 0 #locn
  if #locn <> -1
    extract $FirstName from $Name 0 #locn
    add 1 to #locn
    extract $PrefixName from $Name #locn 50
  else
    let $Firstname = $Name
    let $PrefixName = ''
  end-if

  let $Full_Name = rtrim ($FirstName, ' ')

  if rtrim ($NAME_ROYAL_SUFFIX, ' ') <> ''
    concat ' ' with $Full_Name
    concat $NAME_ROYAL_SUFFIX with $Full_Name
  end-if

  if rtrim ($NAME_ROYAL_PREFIX, ' ') <> ''
    concat ' ' with $Full_Name
    concat $NAME_ROYAL_PREFIX with $Full_Name
  end-if

  concat ' ' with $Full_Name
  concat $LastName with $Full_Name

end-procedure

!*************************************************************
begin-procedure Get-Address ($IN_EMPLID,$IN_ADD_TYPE,:$Address1,:$Address2,:$Postal,:$City,:$State,:$Country,:$EuroZipCity)

Begin-Select
AD.ADDRESS1
AD.ADDRESS2
AD.POSTAL
AD.CITY
AD.STATE
AD.COUNTRY

FROM PS_ADDRESSES AD , PS_PERSON AD1
WHERE AD.EMPLID    = $IN_EMPLID
AND   AD.ADDRESS_TYPE = $IN_ADD_TYPE
AND   AD.EMPLID    = AD1.EMPLID
AND   AD.EFFDT     =(SELECT MAX(AD2.EFFDT) FROM PS_ADDRESSES AD2 WHERE
                     AD2.EMPLID       = AD.EMPLID AND
                     AD2.ADDRESS_TYPE = AD.ADDRESS_TYPE AND
                     AD2.EFFDT       <= $_AsOfToday)
end-SELECT

    move '' to $Address1
    move '' to $Address2
    move '' to $Postal
    move '' to $City
    move '' to $State
    move '' to $Country
    move '' to $EuroZipCity


    let $Address1 = rtrim(&AD.ADDRESS1,' ')
    let $Address2 = rtrim(&AD.ADDRESS2,' ')
    let $Postal   = rtrim(&AD.POSTAL,' ')
    let $City     = rtrim(&AD.CITY,' ')
    let $State    = rtrim(&AD.STATE,' ')
    let $Country  = rtrim(&AD.COUNTRY,' ')

    let $EuroZipCity = $Postal
    concat ' ' with $EuroZipCity
    concat $City with $EuroZipCity

end-procedure
!*************************************************************

begin-procedure Get-Phone ($IN_EMPLID,$IN_PHO_TYPE,:$Phone)

 move '' to $Phone

Begin-Select
PN.PHONE
FROM PS_PERSONAL_PHONE PN , PS_PERSON PN1
WHERE PN.EMPLID     = $IN_EMPLID
AND   PN.PHONE_TYPE = $IN_PHO_TYPE
AND   PN.EMPLID     = PN1.EMPLID
end-SELECT

  let $Phone = rtrim(&PN.PHONE,' ')

end-procedure
!*************************************************************

begin-procedure Get-Email ($IN_EMPLID,$IN_EMAIL_TYPE,:$Email)

 move '' to $Email

Begin-Select
EM.EMAIL_ADDR
FROM  PS_EMAIL_ADDRESSES EM , PS_PERSON EM1
WHERE EM.EMPLID      = $IN_EMPLID
AND   EM.E_ADDR_TYPE = $IN_EMAIL_TYPE
AND   EM.EMPLID      = EM1.EMPLID

end-SELECT

  let $Email = rtrim(&EM.EMAIL_ADDR,' ')

end-procedure
!*************************************************************
