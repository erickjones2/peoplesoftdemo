!*************************************************************
!  GetRtCls:  Get Rate Code Class Description
!*************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:13:40:58                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!****************************************
begin-procedure Get-Rate-Code-Class-Descr
!****************************************

  !Keys passed in:  $Rate_Code_Class, $AsOfDate
  !Values returned: $Class_Descr50, $Class_Descr15

  move '' to $Class_Descr50
  move '' to $Class_Descr15

  if Rtrim($Rate_Code_Class,' ') = ''
    goto GetRtClsExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
{DATEOUT-PREFIX}X.EFFDT{DATEOUT-SUFFIX} &X.EFFDT
X.DESCR50
X.DESCR15

  let $Class_Effdt = &X.EFFDT
  let $Class_Descr50 = Rtrim(&X.DESCR50,' ')
  let $Class_Descr15 = Rtrim(&X.DESCR15,' ')

FROM  PS_RATECD_CLS_TBL X
WHERE X.RATE_CODE_CLASS = $Rate_Code_Class
  AND X.EFFDT  =
       (SELECT MAX(EFFDT)
        FROM   PS_RATECD_CLS_TBL
        WHERE  RATE_CODE_CLASS = X.RATE_CODE_CLASS
          AND  EFFDT <= {DATEIN-PREFIX}$AsOfDate{DATEIN-SUFFIX})
  AND X.EFF_STATUS = 'A'
end-SELECT

  do Get-Rate-Code-Class-Lang

GetRtClsExit:

end-procedure

!***************************************
begin-procedure Get-Rate-Code-Class-Lang
!***************************************

  !Keys passed in:  $Rate_Code_Class, $AsOfDate
  !Values returned: $Class_Descr50, $Class_Descr15

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetRtClsLngExit
  end-if

  if Rtrim($Rate_Code_Class,' ') = ''
    goto GetRtClsLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
XL.DESCR50
XL.DESCR15

  if Rtrim(&XL.DESCR50,' ') <> ''
    let $Class_Descr50 = Rtrim(&XL.DESCR50,' ')
  end-if
  if Rtrim(&XL.DESCR15,' ') <> ''
    let $Class_Descr15 = Rtrim(&XL.DESCR15,' ')
  end-if

FROM  PS_RATECD_CLS_LNG XL
WHERE XL.RATE_CODE_CLASS = $Rate_Code_Class
  AND XL.LANGUAGE_CD     = $Curr_Language_Cd
  AND XL.EFFDT           = {DATEIN-PREFIX}$Class_Effdt{DATEIN-SUFFIX}
end-SELECT

GetRtClsLngExit:

end-procedure
