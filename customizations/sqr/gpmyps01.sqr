!***********************************************************************
! GPMYPS01.SQR        GLOBAL PAYROLL MALAYSIA PAYSLIP PRINT PROGRAM       *
!                                                                      *
!  Description:                                                        *
!                  This program will take the results from the main GP *
!                  result tables and based on a payslp template ID     *
!                  attached to a paygroup, extract, format and print   *
!                  the results.                                        *
!                                                                      *
!                                                                      *
!***********************************************************************

!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
!            Copyright (C) 1988, 2017, Oracle and/or its affiliates.       *
!            All Rights Reserved.                                          *
!***********************************************************************
!       $Release:  HR92                                                *
!           $Bug:  25873288                                            *
!***********************************************************************


#include 'setenv.sqc'   !Set environment


Begin-Setup

#include 'setupdb.sqc'
  page-size 85 130

  declare printer
        type          = HPLASERJET
        orientation   = portrait
        top-margin    = .27
        left-margin   = 0.10
        font          = 3
        point-size    = 8
        
End-Setup


!List of constants for program
#define $payee_list     '10'
#define box_shade        40
#define colA 2
#define colCO 4
#define colB 8
#define colC 16
#define colD 54
#define colE 32
#define colF 40
#define colG 72
#define colH 56
#define colI 72
#define colII 84
#define colI2 87
#define colI3 89
#define colJ 72
#define colK 90
#define colL 88
#define colM 96
#define colMM 101
#define colN 108
#define colO 112
#define colP 114

!Earnings and Deductions Heading Settings
#define ColHD3  26
#define ColHD5  33
#define ColHD6  46
#define ColHD7  55
#define ColHD8  64
#define ColHD9  74
#define ColHD10 84
#define ColHD11 96
#define ColHD12 105
#define ColHD13 115
#define ColHD14 124

!Earnings and deductions column settings
#define ColED3  22
#define ColED5  33
#define ColED6  41
#define ColED7  49
#define ColED8  59
#define ColED9  71
#define ColED10 81
#define ColED11 91
#define ColED12 101
#define ColED13 111
#define ColED14 121

!Accumulators and its heading col settings
#define ColAC1  36
#define ColAC2  51
#define ColAC3  66
#define ColAC4  81
#define ColAC5  96
#define ColAC6  111

!#DEFINE DEBUGA

begin-report


  do Init-DateTime
  Do Get-Current-DateTime
  do Init-Report
  do Process-Main
!ePay Implementation Changes begins here
  do GP-ePay-Control ! if ePay installed have a control row inserted.
!ePay Implementation Changes ends here

  Do Delete-RunControl

  do Stdapi-Term
end-report

begin-heading 23


   graphic (0,1,130) horz-line


   do Print_Company

   Print $Header1 (8, {colII})   Bold

   Print $Header3 (9, {colII})   Bold
    Print $Header4a (10, {colII})  Bold    !Empl Name
    Print $Header13a  (11, {colII})   Bold !Empl Rcd#
   Print $Header12a  (12, {colII})   Bold
   Print $Header5a (13,{colII})   Bold
   Print $Header6a (14,{colII})     Bold
   Print $Header6b  (15, {colII})   Bold
   Print $Header8a  (16,{colII})    Bold

If $Del_opt = '50' or $Del_opt = '60'  !HOME, MAIL

    If ltrim(rtrim($NM,' '),' ') = ''
      let $NM = $EmployeeName
    End-if
    Print $NM (15, 20)   Bold  !Replacing header 6

    If ltrim(rtrim($ADDLINE2,' '),' ') <> $Country
       print $ADD2 (16, 20)   Bold  !Replacing Header 7
    End-if

    If ltrim(rtrim($ADD3,' '),' ') <> $Country
       Print $ADD3 (17, 20)   Bold  !Replacing Header 7
    end-if

    Print $ADD31 (18, 20)  Bold  !Replacing header 9
    Print $ADD4  (19, 20)  Bold
    Print $ADDCITY  (20, 20)  Bold


End-if

if $Del_opt = '20'   ! print Location address,  when deL OPT = 20 OR 30
      Do Print_Location
End-if

if $Del_opt = '30'   ! print Location address,  WHEN DEL_OPT = 20 OR 30
          Do Print_Location
End-if

Print $Header9a   (17, {colII})   Bold
Print $Header10a  (18, {colII})   Bold
Print $Header11a  (19, {colII})   Bold     !PRINTS TAX REF NUMBER


end-heading

begin-footing 2
   graphic (1,1,130) horz-line
end-footing

!**************************************************************************
! Procedure: Init-Report
! Description: Initialise header and run control variables.
!**************************************************************************
Begin-Procedure Init-Report
  do Stdapi-Init
#IFDEF DEBUGA

#ENDIF
   if $prcs_process_instance = ''

  else
    do Select-Parameters
  end-if

!ePay Implementation Changes begins here
  do GP-ePay-Init ! Initialize ePay variables
!ePay Implementation Changes ends here
  do Define-Values

End-Procedure

!**************************************************************************
!
! Procedure: Get_Company
!
! Description:  Get the Company Details
!
!**************************************************************************
Begin-Procedure Get_Company

Begin-Select on-error=SQL-Error

A.COMPANY
B.DESCR
B.ADDRESS1
B.ADDRESS2
B.ADDRESS3
B.ADDRESS4
B.CITY
B.STATE
B.POSTAL
B.COUNTRY
        Let $Name       = &B.DESCR
        Let $address1   = &B.ADDRESS1
        Let $address2   = &B.ADDRESS2
        Let $address3   = &B.ADDRESS3
        Let $address4   = &B.ADDRESS4
        Let $City       = &B.CITY
        Let $State      = &B.STATE
        Let $Postal     = &B.POSTAL
        Let $Country = &B.COUNTRY

        do adformat
        do Compress-Address

        Let $COMPANY_NM     = $ADDLINE1
        Let $COADD1             = $ADDLINE2
        Let $COADD2             = $ADDLINE3
        Let $COADD3             = $ADDLINE31
        Let $COADD4             = $ADDLINE4
        Let $COCITY             = $ADDLINE5
        Let $COSTATE            = $ADDLINE6
  FROM PS_JOB A,  PS_COMPANY_TBL B
  WHERE A.COMPANY = B.COMPANY
        AND A.EMPLID = $Emplid
        AND  A.EFFDT = (SELECT MAX(A1.EFFDT) FROM PS_JOB A1
                         WHERE A1.EMPLID = A.EMPLID
                        AND A1.EMPL_RCD = A.EMPL_RCD)
        AND A.EFFSEQ = (SELECT MAX(A2.EFFSEQ) FROM PS_JOB A2
                        WHERE A2.EMPLID = A.EMPLID
                        AND A2.EMPL_RCD = A.EMPL_RCD
                        AND A2.EFFDT = A.EFFDT)
        AND B.EFFDT =   (SELECT MAX(B1.EFFDT) FROM PS_COMPANY_TBL B1
                         WHERE B1.COMPANY = B.COMPANY)

End-Select
End-Procedure

!**************************************************************************
! Procedure: Print_Company
! Description:  Get the Company Details
!**************************************************************************
Begin-Procedure Print_Company

        print $COMPANY_NM (4,4,30) BOLD
        print $COADD1 (+1,4,30) BOLD
        print $COADD2 (+1,4,30) BOLD
        print $COADD3 (+1,4,30) BOLD
        print $COADD4 (+1,4,30) BOLD
End-Procedure

!**************************************************************************
!   Procedure: Define-Values
!   Description: Define general SQL Dynamic Query Variables.
!**************************************************************************
Begin-Procedure Define-Values

Let $EmplList = 'N'
Let $DeptList = 'N'
Let $LocnList = 'N'
Let $PyenList = 'N'
Let $PyGPList = 'N'
Let $No_List  = 'Y'

  If $sort_key1 <> '00'
     evaluate $sort_key1
            when = '10'
                Let $Sort_Order1  = 'ORDER BY PRC.PAY_ENTITY '
          when = '20'
                Let $Sort_Order1  ='ORDER BY PER.LOCATION '
          when = '30'
                Let $Sort_Order1  ='ORDER BY PER.DEPTID '
          when = '40'
                Let $Sort_Order1  ='ORDER BY PER.NAME '
          When = '50'
                Let $Sort_Order1  ='ORDER BY PER.GP_PAYGROUP '
          when-other
                break
     end-evaluate
  End-if
  If $sort_key2 <> '00'
     evaluate $sort_key2
            when = '10'
                Let $Sort_Order2  = ', PRC.PAY_ENTITY '
          when = '20'
                Let $Sort_Order2  = ', PER.LOCATION '
          when = '30'
                Let $Sort_Order2  = ', PER.DEPTID '
          when = '40'
                Let $Sort_Order2  = ', PER.NAME '
          When = '50'
                Let $Sort_Order2  = ', PER.GP_PAYGROUP '
          when-other
                break
     end-evaluate
  End-if
  If $sort_key3 <> '00'
     evaluate $sort_key3
            when = '10'
                Let $Sort_Order3  = ', PRC.PAY_ENTITY '
          when = '20'
                Let $Sort_Order3  = ', PER.LOCATION '
          when = '30'
                Let $Sort_Order3  = ', PER.DEPTID '
          when = '40'
                Let $Sort_Order3  = ', PER.NAME '
          When = '50'
                Let $Sort_Order3  = ', PER.GP_PAYGROUP '
          when-other
                break
     end-evaluate
  End-if
     string $Sort_order1 $Sort_order2 $Sort_order3 by ' ' into $sort_order
  if $Sort_Key1 = '00'
     Let $Sort_order = 'ORDER BY PER.NAME '
  End-If
  if ($Sort_Key1 = '20' or $Sort_Key1 = '30') or
     ($Sort_Key2 = '20' or $Sort_Key2 = '30') or
     ($Sort_Key3 = '20' or $Sort_Key3 = '30')
#define colI 72
#define colI2 87
#define colI3 89
  End-If
End-Procedure
!**************************************************************************
! Procedure: Process-Main
! Description: Main processing section
!**************************************************************************
Begin-Procedure Process-Main

!Mobile Payslip - Start
    do check-mob-custom-enabled
    If( $custEnabled = 'Y')
        Do init-mpslp($Cal_Run_ID)
        LET $GPwhere_clause = ' '
        Do clean_mpslp_records ($Cal_Run_ID, $GPwhere_clause)
    End-If

!Mobile Payslip -  End
  
  Do Get-GP-Paygroup
  Do Process-Empl-List
  Do Process-Dept-List
  Do Process-Locn-List
  Do Process-Pay-Entity-List
  Do Process-PayGroup-List

  Let #J = 1

  If $No_List = 'Y'

     While #J <= #I

        Get $GP_Pygrp FROM Gp_Paygroups(#J)
        If #j = 1
           Let $WhereTemp = ' AND (PRC.GP_PAYGROUP = ' || '''' || $GP_Pygrp || ''''
        Else
           if rtrim($GP_Pygrp,' ') <> ''
              Let $TempString = ' OR PRC.GP_PAYGROUP = ' || '''' || $GP_Pygrp || ''''
              Let $WhereTemp = $WhereTemp || $TempString
           End-if
        End-If
        Let #J = #J + 1
     End-While
     if rtrim($WhereTemp,' ') <> ''
        Let $WhereTemp = $WhereTemp || ')'
        Let $WhereClause = $WhereTemp
        Do Process-Payee
     End-if
  End-If
  
!Mobile Payslip Update statistics
    If( $custEnabled = 'Y')
      
      DO update_stats

    End-If  

End-Procedure
!
!**************************************************************************
! Procedure: Process-Payee
! Description: Payee processing section
!**************************************************************************
Begin-Procedure Process-Payee
   Let $First_Empl = 'Y'
   Let #No_payroll_emp = 0

Begin-Select on-error=SQL-Error
PER.EMPLID
PER.EMPL_RCD
PER.NAME
PER.SETID_DEPT
PER.BUSINESS_UNIT
PER.DEPTID
PER.SETID_LOCATION
PER.LOCATION
PER.COMPANY
PER.GP_PAYGROUP
PER.COMP_FREQUENCY
PER.JOBTITLE
PRC.PAY_ENTITY
PRC.PYMT_DT
SEG.SEG_BGN_DT
SEG.SEG_END_DT
SEG.CAL_RUN_ID
SEG.CAL_ID
SEG.RSLT_SEG_NUM
PYGRP.DESCR

        Let $Emplid         = &PER.EMPLID
        Let #No_payroll_emp = #No_payroll_emp + 1
        do Get_Company
        do clear-address-vars

        Let #Empl_Rcd       = &PER.EMPL_RCD
        Let $Empl_Rcd       = to_char( #Empl_Rcd)
        Let $Empl_Rcd =  edit($Empl_Rcd, '999')
        Let $Empl_Rcd =  ltrim($Empl_Rcd,' ')

        Let $EmployeeName   = &PER.NAME
        Let $SetIDDept      = &PER.SETID_DEPT
        Let $Deptid         = &PER.DEPTID
        Let $SetIDLocn      = &PER.SETID_LOCATION
        Let $Location       = &PER.LOCATION
        Let $Company        = &PER.COMPANY
        Let $Comp_Freq      = &PER.COMP_FREQUENCY
        Let $Jobtitle       = &PER.JOBTITLE
        Let $Pay_Ent        = &PRC.PAY_ENTITY
        Let $Pymt_Dt        = &PRC.PYMT_DT
        Let $Pay_Pd_From_Dt = &SEG.SEG_BGN_DT
        Let $Pay_Pd_To_Dt   = &SEG.SEG_END_DT
        Let $Cal_ID         = &SEG.CAL_ID
        Let $GP_Paygroup    = &PER.GP_PAYGROUP
        Let $GP_Paygroup_Descr    = &PYGRP.DESCR
        Let #Rslt_Seg_num   = &SEG.RSLT_SEG_NUM

!ePay Implementation Changes begins here
        Let $SegBgnDt      = &SEG.SEG_BGN_DT
!ePay Implementation Changes ends here

!Check that the payee is not in an excluded location or dept


        Let $locn_excl             = 'N'
        Let $dept_excl             = 'N'
        do Get_Payslip_ID


        do Check_Excl_Dept
        do Check_Excl_Loctn

        If $locn_excl = 'N' and $dept_excl = 'N'

         do Get_Payslip_Labels
               If $Del_opt = '50'
                  Let $addr_Type = 'HOME'
                  do Get_Address
                  Let $Name = $EmployeeName
                  do adformat
                  do Compress-Address
               End-if

           do Get-PayEntity
           do Get_dept
           DO Get_BASEPAY
           do Get_location
           do GET-SOCSO-NUMBER
           do GET-TAXREF-NUMBER
           do Get_EPF_Number
           do clear-address-vars
           do Get_Frequency
           do Print_Empl_Details
           do Print_Heading
           do Get_ED_Sections
           do Get_Leave
           do Get_Disburs_Banked
           do Get_Disburs_NonBanked
           do Get_Messages
           do Print_Summ_Lbls
           do Get_OffCycle_Ind
           do Get_Payment_Summ
           
!ePay Implementation Changes begins here
          Let #EndPageNumber = #page-count
!ePay Implementation Changes ends here


!ePay Implementation Changes begins here
          do GP-ePay-Guide  ! if ePay installed write Guide data for each payslip
!ePay Implementation Changes ends here

         Let $End_Msg = '***** End Of Payslip *****'
         print $End_msg (+2, 10)   Bold Center
         NEW-PAGE

         do clear-address-vars
        End-If
FROM PS_GPMY_SS_PSLP_VW PER
, PS_GP_PYE_PRC_STAT PRC
, PS_GP_PYE_SEG_STAT SEG
, PS_GP_PYGRP PYGRP
  WHERE PER.EMPLID      = PRC.EMPLID
    AND PER.EMPL_RCD    = PRC.EMPL_RCD
    AND PER.CAL_RUN_ID  = PRC.CAL_RUN_ID
    AND PER.CAL_ID      = PRC.CAL_ID
    AND PER.GP_PAYGROUP = PRC.GP_PAYGROUP

    AND PER.GP_PAYGROUP = PYGRP.GP_PAYGROUP
    AND PRC.GP_PAYGROUP = PYGRP.GP_PAYGROUP

    AND PER.EMPLID      = SEG.EMPLID
    AND PER.EMPL_RCD    = SEG.EMPL_RCD
    AND PRC.CAL_RUN_ID  = SEG.CAL_RUN_ID
    AND PRC.EMPLID      = SEG.EMPLID
    AND PRC.EMPL_RCD    = SEG.EMPL_RCD
    AND PRC.GP_PAYGROUP = SEG.GP_PAYGROUP
    AND PRC.CAL_ID      = SEG.CAL_ID
    AND PRC.PRD_TYPE   <> 'H'
    AND PRC.CAL_RUN_ID  = $Cal_Run_ID
    AND SEG.PYE_CALC_STAT IN ('60','65','70','75')
    AND SEG.ORIG_CAL_RUN_ID = SEG.CAL_RUN_ID
    AND SEG.RSLT_SEG_NUM = PER.RSLT_SEG_NUM
      [$whereClause]
      [$sort_order]
End-Select

End-Procedure

!**************************************************************************
! Procedure :   Clear-address-vars
! Description  :Clear the variables set previously
!**************************************************************************
begin-procedure clear-address-vars

  Let $address1   = ''
  Let $address2   = ''
  Let $address3   = ''
  Let $address4   = ''
  Let $City       = ''
  Let $State      = ''
  Let $Postal     = ''
  let $ADDLINE1 = ''
  let $ADDLINE2 = ''
  let $ADDLINE3 = ''
  let $ADDLINE31 = ''
  let $ADDLINE4 = ''
  let $ADDLINE5 = ''
  let $ADDLINE6 = ''
end-procedure

!**************************************************************************
! Procedure:    Get_Payslip_Labels
! Description:  Get the payslip labels on delivery option for the payees
!               based on the paygroup
!**************************************************************************
Begin-Procedure Get_Payslip_Labels


Begin-Select on-error=SQL-Error

PSLP.GPMY_DEL_OPT
PSLP.GPMY_LBL_ACUM1
PSLP.GPMY_LBL_ACUM2
PSLP.GPMY_LBL_ACUM3
PSLP.GPMY_LBL_ACUM4
PSLP.GPMY_LBL_ACUM5
PSLP.GPMY_LBL_ACUM6


!Check if an override for the delivery address exits
        Let $Del_Opt               = &PSLP.GPMY_DEL_OPT

        do Check_Override_Add
!If the deleivery option is secondary get the details

        If $Del_opt = '60' !then get the secondary address
              Let $addr_Type = 'MAIL'
              do Get_Address
        End-If

        Let $Acum_Lbl1      = &PSLP.GPMY_LBL_ACUM1
        Let $Acum_Lbl2      = &PSLP.GPMY_LBL_ACUM2
        Let $Acum_Lbl3      = &PSLP.GPMY_LBL_ACUM3
        Let $Acum_Lbl4      = &PSLP.GPMY_LBL_ACUM4
        Let $Acum_Lbl5      = &PSLP.GPMY_LBL_ACUM5
        Let $Acum_Lbl6      = &PSLP.GPMY_LBL_ACUM6

FROM PS_GPMY_PSLP PSLP,
PS_GP_PG_DTL_SGPMY SGPMY
WHERE PSLP.EFFDT = (SELECT MAX(PSLP1.EFFDT)
                 FROM PS_GPMY_PSLP PSLP1
                 WHERE PSLP1.EFFDT <= PSLP.EFFDT)
AND PSLP.GPMY_PAYSLIP_ID = SGPMY.GPMY_PAYSLIP_ID
AND GP_PAYGROUP = $GP_Paygroup
End-Select

End-Procedure
!**************************************************************************
!
! Procedure:  Print_Summ_Lbls
!
! Description: Print the summary headings/labels
!**************************************************************************
Begin-Procedure Print_Summ_Lbls
!ePay Implementation Changes begins here
   Let #BeginPageNumber = #page-count
!ePay Implementation Changes ends here

        print 'Pay Summary' (+4) Center Bold Underline

        Do Find-Start-Pos ($Acum_Lbl1, {colAC1},#ReturnCol)
        print $Acum_Lbl1 (+1, #ReturnCol) Bold

        Do Find-Start-Pos ($Acum_Lbl2, {colAC2},#ReturnCol)
        print $Acum_Lbl2 (0, #ReturnCol) Bold

        Do Find-Start-Pos ($Acum_Lbl3, {colAC3},#ReturnCol)
        print $Acum_Lbl3 (0, #ReturnCol) Bold

        Do Find-Start-Pos ($Acum_Lbl4, {colAC4},#ReturnCol)
        print $Acum_Lbl4 (0, #ReturnCol) Bold

        Do Find-Start-Pos ($Acum_Lbl5, {colAC5},#ReturnCol)
        print $Acum_Lbl5 (0, #ReturnCol) Bold

        Do Find-Start-Pos ($Acum_Lbl6, {colAC6},#ReturnCol)
        print $Acum_Lbl6 (0, #ReturnCol) Bold

End-Procedure
!
!**************************************************************************
! Procedure:  Find-Start-Pos
! Description: Control the positioning  print the headings
!**************************************************************************
Begin-Procedure Find-Start-Pos($Acum_Lbl, #StartCol,:#ReturnCol)
     Let $Acum_lbl = rtrim($Acum_lbl,' ')
     Let #Len = length($Acum_lbl)
     Let #ReturnCol = (#StartCol + 10) - #Len
     if #ReturnCol <= 0
        Let #ReturnCol = #StartCol
     End-if
End-Procedure

!**************************************************************************
! Procedure:  Pad-String
! Description: Control the positioning  of the values
!**************************************************************************
Begin-Procedure Pad-String($Label, $Value,:$ReturnVal)
     Let #MaxLblLen = 17
     Let #Vallen  = length($Label)
     if #Vallen < #MaxLbllen
        Let #DiffLen = #MaxLblLen - #Vallen
        Let $PadVar = Lpad('',#Difflen,' ')
     else
        Let $PadVar = ''
     end-if
     Let $ReturnVal = $Label || $PadVar || ': ' || $Value
End-Procedure


!**************************************************************************
! Procedure: Get_BASEPAY
! Description: Get the BASEPAY of the Employee
!**************************************************************************
Begin-Procedure Get_BASEPAY
Let #BASEPAY = 0

Begin-Select on-error=SQL-Error
A.CALC_RSLT_VAL

   Let #BASEPAY       = &A.CALC_RSLT_VAL + #BASEPAY

    FROM  PS_GP_RSLT_PIN A
            , PS_GP_PIN_NM_VW B
    WHERE  A.PIN_NUM = B.PIN_NUM
    AND    B.PIN_NM IN ('INSAL', 'INHRLY')
    AND    A.CAL_RUN_ID = $Cal_Run_ID
    AND    A.EMPLID = $Emplid

End-Select
End-Procedure

!**************************************************************************
! Procedure: Get_EPF_Number
! Description: Get the EPF Number of the Employee
!**************************************************************************
Begin-Procedure Get_EPF_Number
    Let $EPF_MBR_NBR = ''
Begin-Select on-error=SQL-Error
EPF.GPMY_MMBRSHIP_NBR
    LET  $EPF_MBR_NBR = &EPF.GPMY_MMBRSHIP_NBR

    FROM PS_GPMY_EPF_PYE EPF
    WHERE EPF.EMPLID = $Emplid
    AND ( EPF.EFFDT = (SELECT MAX(A1.EFFDT) FROM PS_GPMY_EPF_PYE A1
        WHERE EPF.EMPLID = A1.EMPLID)
        AND EPF.EFF_STATUS = 'A' )
End-Select
End-Procedure


!**************************************************************************
! Procedure: GET-SOCSO-NUMBER
! Description: Get the SOCSO Number of the Employee
!**************************************************************************
Begin-Procedure GET-SOCSO-NUMBER

Let $SOCSO_MBR_NBR = ''
Begin-Select on-error=SQL-Error
A.GPMY_MMBRSHIP_NBR
A.EMPL_RCD

        Let $SOCSO_MBR_NBR = &A.GPMY_MMBRSHIP_NBR

    FROM PS_GPMY_SOC_PYE A
    WHERE A.EMPLID = $Emplid
     AND ( A.EFFDT = (SELECT MAX(A1.EFFDT) FROM PS_GPMY_SOC_PYE A1
        WHERE A.EMPLID = A1.EMPLID
     AND A1.EFFDT <= $Pymt_Dt  )
           AND A.EFF_STATUS = 'A' )


End-Select
End-Procedure


!**************************************************************************
! Procedure: GET-TAXREF-NUMBER
! Description: Get the TAX REFERENCE Number of the Employee
!**************************************************************************
Begin-Procedure GET-TAXREF-NUMBER
Begin-Select on-error=SQL-Error
A.GPMY_EMPL_TAX_NBR

    LET $TAXREF_NBR = &A.GPMY_EMPL_TAX_NBR
    FROM PS_GPMY_TAX_PYE A
    WHERE A.EMPLID = $Emplid
    AND ( A.EFFDT = (SELECT MAX(A1.EFFDT) FROM PS_GPMY_TAX_PYE A1
        WHERE A.EMPLID = A1.EMPLID)
        AND A.EFF_STATUS = 'A' )
End-Select
End-Procedure

!**************************************************************************
! Procedure:  Print_Empl_Details
! Description: Print the headings
!**************************************************************************
Begin-Procedure Print_Empl_Details
 ! The date formatting routines below have been commented because date format for printing should be done using
 ! the setting in setenv.sqc

 ! let $Pymt_Dt= edit ($Pymt_Dt,'DD/MM/YYYY')
 ! let $Pay_Pd_From_Dt= edit ($Pay_Pd_From_Dt,'DD/MM/YYYY')
 ! let $Pay_Pd_To_Dt= edit ($Pay_Pd_To_Dt,'DD/MM/YYYY')

  do Format-DateTime($Pay_Pd_From_Dt, $Pay_Dt_From_Conv, {DEFDATE}, '', '')
  do Format-DateTime($Pay_Pd_To_Dt, $Pay_Dt_To_Conv, {DEFDATE}, '', '')
  do Format-DateTime($Pymt_Dt, $Pymt_Dt_Conv, {DEFDATE}, '', '')

  Let $Header1 = 'Pay Period'

  Let $Header2 = 'Pay Period To    ' || $Pay_Dt_To_Conv

  Let $PayDates = $Pay_Dt_From_Conv || ' To ' || $Pay_Dt_To_Conv
  Do Pad-String ($Header1,$PayDates,$Header1)

  Let $Header3 = 'Payment Date'
  Do Pad-String ($Header3,$Pymt_Dt_Conv,$Header3)

  Let $Header4a = 'Employee Number'
  Do Pad-String ($Header4A,$Emplid,$Header4A)

  Let $Header5 = 'Name:            ' || $Name

  Let $Header5a = 'Pay Entity'
  Do Pad-String ($Header5A,$PayEntityDescr,$Header5A)

  if $Sort_key1 = '30' or $Sort_key2 = '30' or $Sort_key3 = '30'
        Let $SortDeptid = rtrim($DeptID,' ')
        Let $Sort_Dept = '(' || $SortDeptID || ')' || $Dept_Name
  else
        Let $Sort_Dept = $Dept_Name
  end-if

  Do Pad-String ($Header6A,$Sort_Dept,$Header6A)


  if $Sort_key1 = '20' or $Sort_key2 = '20' or $Sort_key3 = '20'
        Let $SortLocation = rtrim($Location,' ')
        Let $Sort_Location = '(' || $SortLocation || ')' || $lctnDesc
  else
        Let $Sort_Location = $lctnDesc
  end-if

  Let $Header6a = 'Department'

  Do Pad-String ($Header6A,$dept_name,$Header6A)

  Let $Header6b = 'Location'

  Do Pad-String ($Header6b,$lctnDesc,$Header6b)

  Let $Comprate = to_char(#BASEPAY)
  Let $Comprate = edit($Comprate, '999,999.99')
  Let $Compensation = ltrim($comprate,' ')
  Let $Header8a = 'Job Description'
  Do Pad-String ($Header8a,$Jobtitle,$Header8a)

  Let $Header9a = 'EPF Number'
  Do Pad-String ($Header9A,$EPF_MBR_NBR,$Header9A)

  Let $Header10a = 'SOCSO Number'
  Do Pad-String ($Header10A,$SOCSO_MBR_NBR,$Header10A)

  Let $Header11a = 'Tax Ref Number'
  Do Pad-String ($Header11A,$TAXREF_NBR,$Header11A)

  Let $Header12a = 'Pay Group'

  Do Pad-String ($Header12A,$GP_Paygroup_Descr,$Header12A)


  Let $Header13a = 'Empl Rcd Nbr'
  Do Pad-String ($Header13A,$Empl_Rcd ,$Header13A)


End-Procedure

!**************************************************************************
! Procedure: Print_Heading
! Description:  Print the headings breakup accross page
!**************************************************************************
Begin-Procedure Print_Heading
        print 'Payment Details' (+2,) Center Bold Underline
        print '|' (+2,{ColA}) Bold
        graphic (0,3,25) horz-line
        graphic (0,44,27) horz-line
        print 'Current Values' (0,28) bold
        print '|Payback|'(0,71) Bold
        graphic (0,81,8) horz-line
        print 'Adjustments' (0,89) bold
        graphic (0,101,8) horz-line
        print '|' (0,109)
        graphic (0,110,4) horz-line
        print 'YTD Values' (0,114) bold
        graphic (0,125,4) horz-line
        print '|' (0,129)

End-Procedure

!**************************************************************************
! Procedure: Get_ED_Sections
! Description:  Get the sections applicable based on the payslip id
!**************************************************************************
Begin-Procedure Get_ED_Sections


  Let $sql-statement = 'Get_Section'
Begin-Select on-error=SQL-Error
SECT.DESCR
SECT.SEQ_NUM
        Let $Sect_Hdg           = rtrim(&SECT.DESCR, ' ')
        Let #Sect_Seq_Num       = &SECT.SEQ_NUM
        do Print_ED_Headings
        do Get_ErnDed_Results

FROM PS_GPMY_PSLP_SECT SECT
WHERE SECT.GPMY_PAYSLIP_ID = $Payslip_ID
      AND SECT.EFFDT = (SELECT MAX(SECT1.EFFDT) FROM PS_GPMY_PSLP_SECT SECT1
                                WHERE SECT1.GPMY_PAYSLIP_ID = SECT.GPMY_PAYSLIP_ID
                                  AND SECT1.EFFDT <= $Pymt_Dt)
ORDER BY SECT.SEQ_NUM

End-Select
End-Procedure


!**************************************************************************
! Procedure: Get_ErnDed_Results
! Description: Get and print the earnings and deduction details for the payee.
!**************************************************************************
Begin-Procedure Get_ErnDed_Results

        Let $TenCharVar = '1234567890'
        Let #PinCounter      = 0
        Let #LastPinNum = 0
        Let #InstCounter = 0
        Let $AcmString = ''
        Let #LastEmplRcd = 0
Begin-Select on-error=SQL-Error
PIN.PIN_NUM
ED.DESCR
PIN.DESCR
PIN.PIN_NM
PIN.PIN_TYPE
ED.GPMY_DESC_TYPE
RSLT.SLICE_BGN_DT
RSLT.SLICE_END_DT
RSLT.CALC_RSLT_VAL
RSLT.UNIT_RSLT_VAL
RSLT.RATE_RSLT_VAL
RSLT.BASE_RSLT_VAL
RSLT.PCT_RSLT_VAL
RSLT.RSLT_PAYBK
RSLT.CALC_ADJ_VAL
RSLT.BASE_ADJ_VAL
RSLT.UNIT_ADJ_VAL
RSLT.INSTANCE
ED.GPMY_YTD_AC_AMT
ED.GPMY_YTD_AC_UNITS
ED.GPMY_BSE_COMP_IND
ED.GPMY_PCT_COMP_IND
ED.GPMY_RTE_COMP_IND
ED.GPMY_UNT_COMP_IND
ED.GPMY_YTD_ACAMT_IND
ED.GPMY_YTD_ACUNT_IND
ED.SEQ_NUM5
PRC.PRD_BGN_DT                   
PRC.PRD_END_DT                    
PRC.ORIG_CAL_RUN_ID              
PRC.RUN_TYPE

       Let $PrdBgnDt     = &PRC.PRD_BGN_DT
       Let $PrdEndDt     = &PRC.PRD_END_DT
       Let $Orig_Cal_Run_ID = &PRC.ORIG_CAL_RUN_ID
       Let $Run_Type = &PRC.RUN_TYPE
       Let #Instance = &RSLT.INSTANCE
       Let #Seq_Num5 = &ED.SEQ_NUM5

!Get the accumulated value of the hours , if entered at setup
       if &ED.GPMY_YTD_ACUNT_IND = 'Y'
        if &ED.GPMY_YTD_AC_UNITS > 0
           Let #Pin_Num = &ED.GPMY_YTD_AC_UNITS
           do Get_Accum_Values
           Let #Acum_Rslt_Hrs         = #Calc_Rslt_Val
        end-if
       end-if
!Get the accumulated value of the amount, if entered at setup
       if &ED.GPMY_YTD_ACAMT_IND = 'Y'
       !    do get-Acum-Pin
        !   Let #Pin_Num = #acum_pin_num
        Let #Pin_Num = &ED.GPMY_YTD_AC_AMT
          do Get_Accum_Values
           Let #Acum_Rslt_Amt         = #Calc_Rslt_Val
           
        end-if

        Let $ED_Descr                 = &ED.DESCR
        Let $Pin_Descr                = &PIN.DESCR
        Let $Descr_Type               = &ED.GPMY_DESC_TYPE
        Let $Pin_Nm                   = &PIN.PIN_NM
        Let #ED_PinNum                = &PIN.PIN_NUM
        Let $PinType                  = $PIN.PIN_TYPE
        Let #Calc_Rslt_Val            = &RSLT.CALC_RSLT_VAL

        Let #Unit_Rslt_Val            = &RSLT.UNIT_RSLT_VAL
        Let #Rate_Rslt_Val            = &RSLT.RATE_RSLT_VAL
        Let #Base_Rslt_Val            = &RSLT.BASE_RSLT_VAL
        Let #Pct_Rslt_Val             = &RSLT.PCT_RSLT_VAL
        Let #Rslt_Paybk               = &RSLT.RSLT_PAYBK
        Let #Calc_Adj_Val             = &RSLT.CALC_ADJ_VAL
        Let #Base_Adj_Val             = &RSLT.BASE_ADJ_VAL
        Let #Unit_Adj_Val             = &RSLT.UNIT_ADJ_VAL        
        
        Let #Acum_PinNum              = &ED.GPMY_YTD_AC_AMT
        Let #Acum_UnitPin             = &ED.GPMY_YTD_AC_UNITS   
    


        Let $Print_Base_YN        = &ED.GPMY_BSE_COMP_IND
        Let $Print_Pct_YN        = &ED.GPMY_PCT_COMP_IND
        Let $Print_Rte_YN        = &ED.GPMY_RTE_COMP_IND
        Let $Print_Unt_YN        = &ED.GPMY_UNT_COMP_IND

        Let #total_curr_amt  = #total_curr_amt + #Calc_Rslt_Val
        Let #total_pybck_amt = #total_pybck_amt + #Rslt_Paybk
        Let #total_adj_amt   = #total_adj_amt + #Calc_Adj_Val

        Let $PinSec = '00'
        evaluate $Pin_Type 
            when = 'ER'
                Let $PinSec = '10'
            when = 'DD'
                Let $PinSec = '20'
            when = 'AC'
                Let $PinSec = '40'
        end-evaluate

        evaluate $Descr_Type
                when = '10'
                        Let $Descr_Temp = $PIN_Descr
                when = '20'
                        Let $Descr_Temp = $ED_Descr
                when = '30'
                        Let $Descr_Temp = $Pin_Nm
        end-evaluate

        Let $Descr = substr($Descr_Temp,1 , 16)

     If #Current-line >  80
             NEW-PAGE
             do Print_Heading
             do Print_ED_Headings
     else
             print $Descr         (+1, {ColA})

        If $Print_Base_YN = 'Y'
           if #Base_Rslt_VAl <> 0
              Do Find-Start-Pos ($TenCharVar, {colED3},#ReturnCol)
              Let #ReturnCol = #ReturnCol - 3
              print #Base_Rslt_Val (0, #ReturnCol) edit '9999999.99'
           end-if
        End-If
        If $Print_Pct_YN = 'Y'
           if #Pct_Rslt_Val <> 0
              Do Find-Start-Pos ($TenCharVar, {colED5},#ReturnCol)
              Let #ReturnCol = #ReturnCol - 3
              print #Pct_Rslt_Val  (0, #ReturnCol) edit '9999999.99'
           end-if
        End-If
        If $Print_Rte_YN = 'Y'
           IF #Rate_Rslt_Val <> 0
              Do Find-Start-Pos ($TenCharVar, {colED6},#ReturnCol)
              Let #ReturnCol = #ReturnCol - 3
              print #Rate_Rslt_Val (0, #ReturnCol) edit '9999999.99'
           end-if
        End-If
        If $Print_Unt_YN = 'Y'
           if #Unit_Rslt_Val <> 0
              Do Find-Start-Pos ($TenCharVar, {colED7},#ReturnCol)
              Let #ReturnCol = #ReturnCol - 3
              print #Unit_Rslt_Val (0, #ReturnCol) edit '9999999.99'
           end-if
        End-If
        if #Calc_Rslt_Val <> 0
           Do Find-Start-Pos ($TenCharVar, {colED8},#ReturnCol)
           Let #ReturnCol = #ReturnCol - 3
           print #Calc_Rslt_Val (0, #ReturnCol)   edit '9999999.99'
        End-If
        If #Rslt_Paybk > 0
           Do Find-Start-Pos ($TenCharVar, {colED9},#ReturnCol)
           Let #ReturnCol = #ReturnCol - 3
           print #Rslt_Paybk    (0, #ReturnCol)   edit '9999999.99'
        end-if
        if #Calc_Adj_Val <> 0
           Do Find-Start-Pos ($TenCharVar, {colED10},#ReturnCol)
           Let #ReturnCol = #ReturnCol - 3
           print #Calc_Adj_Val  (0, #ReturnCol)  edit '9999999.99'
        end-if
        if #Base_Adj_Val <> 0
           Do Find-Start-Pos ($TenCharVar, {colED11},#ReturnCol)
           Let #ReturnCol = #ReturnCol - 3
           print #Base_Adj_Val  (0, #ReturnCol)  edit '9999999.99'
        end-if
        if #Unit_Adj_Val <> 0
           Do Find-Start-Pos ($TenCharVar, {colED12},#ReturnCol)
           Let #ReturnCol = #ReturnCol - 3
           print #Unit_Adj_Val  (0, #ReturnCol)  edit '9999999.99'
        end-if
        if #Acum_Rslt_Hrs <> 0
           Do Find-Start-Pos ($TenCharVar, {colED13},#ReturnCol)
           Let #ReturnCol = #ReturnCol - 3
           print #Acum_Rslt_Hrs (0, #ReturnCol)  edit '9999999.99'
        end-if
        if #Acum_Rslt_Amt <> 0
           Do Find-Start-Pos ($TenCharVar, {colED14},#ReturnCol)
           Let #ReturnCol = #ReturnCol - 3
           print #Acum_Rslt_Amt (0, #ReturnCol)  edit '9999999.99'
           Let #Acum_Rslt_Amt_TTL = #Acum_Rslt_Amt + #Acum_Rslt_Amt_TTL
        end-if

        If( $custEnabled = 'Y')
           do Get-Element-Group
           do Get-Section_Number
                     
               
           If (#ED_PinNum = #LastPinNum OR #LastEmplRcd <> #Empl_Rcd)
           
                     
           Let #Instance = #Instance + 1
           
           End-If
           
           If ( #LastEmplRcd <> #Empl_Rcd )
           
           Let $AcmString =''
           
           End-If
           
        
        Let #Pos = 0
        Let $AcmChar =to_char(#Acum_PinNum)
        
        Let #Pos = instr($AcmString, $AcmChar, 0)
        
        !**********Populate Mobile Payslip Earnings/Deductions into staging tables**************
           
          
           LET MPSLP_ED.EMPLID(0) = $Emplid
           LET MPSLP_ED.CAL_RUN_ID(0) = $Cal_Run_Id
           LET MPSLP_ED.EMPL_RCD(0) = #Empl_Rcd
           LET MPSLP_ED.GP_PAYGROUP(0) = $GP_Paygroup
           LET MPSLP_ED.CAL_ID(0) = $Cal_ID 
           LET MPSLP_ED.ORIG_CAL_RUN_ID(0) = $orig_Cal_Run_ID 
           LET MPSLP_ED.RSLT_SEG_NUM(0) = #Rslt_Seg_num
           LET MPSLP_ED.GP_PSLP_SRCPRODUCT(0) = 'GPMYS'
           LET MPSLP_ED.GP_PSLP_ID(0) = to_char(#Empl_Rcd ) || ' ' || Edit($Pay_Pd_To_Dt,'MM/DD/YYYY')
           LET MPSLP_ED.INSTANCE(0) = #Instance
           LET MPSLP_ED.PIN_NUM(0) = #ED_PinNum
           LET MPSLP_ED.SLICE_BGN_DT(0) = $Slice_Bgn_Dt
           LET MPSLP_ED.SLICE_END_DT(0) = $Slice_End_Dt
           LET MPSLP_ED.PIN_ELEM_GRP_NUM(0) = #PARENT_PIN
           LET MPSLP_ED.ED_ASSIGN_INSTANCE(0) = 0
           LET MPSLP_ED.PI_INSTANCE(0) = 0
           LET MPSLP_ED.PRD_BGN_DT(0) = $Pay_Pd_From_Dt
           LET MPSLP_ED.PRD_END_DT(0) = $Pay_Pd_To_Dt 
           LET MPSLP_ED.SEG_BGN_DT(0) = $Pay_Pd_From_Dt
           LET MPSLP_ED.SEG_END_DT(0) = $Pay_Pd_To_Dt
           LET MPSLP_ED.PYMT_DT(0) =  $Pymt_Dt
           LET MPSLP_ED.RUN_TYPE(0) = rtrim($Run_Type, ' ')
           LET MPSLP_ED.CALC_RSLT_VAL(0) = #Calc_Rslt_Val
           LET MPSLP_ED.GP_MPSLP_SECTION(0) = $GP_section_nm
           LET MPSLP_ED.CALC_DELTA_VAL(0) =    #Calc_Adj_Val         
           LET MPSLP_ED.BASE_RSLT_VAL(0) =     #Base_Rslt_Val      
           LET MPSLP_ED.BASE_ADJ_VAL(0) =     #Base_Adj_Val       
           LET MPSLP_ED.RATE_RSLT_VAL(0) =     #Rate_Rslt_Val        
           LET MPSLP_ED.UNIT_RSLT_VAL(0) =     #Unit_Rslt_Val       
           LET MPSLP_ED.UNIT_ADJ_VAL(0) =      #Unit_Adj_Val
           LET MPSLP_ED.PCT_RSLT_VAL(0) =    #Pct_Rslt_Val 
                          
           LET MPSLP_ED.GP_MPSLP_SPRNT_ORD(0) =   $PinSec        
           LET MPSLP_ED.GP_MPSLP_SUBSECTN(0) =    $Sect_Hdg   
           LET MPSLP_ED.GP_MPSLP_PRNT_ORD(0) =    #SeqNum5
           LET MPSLP_ED.GP_MPSLP_PIN_DESCR(0) =   $Descr_Temp
           
           
           
           If ( #Calc_Rslt_Val <> 0)
           
           Do insert_mpslp_ernded_row
           
                   
           End-If
           
        
            If (#Pos = 0)
           
            LET MPSLP_ACUM.EMPLID(0) = $Emplid
            LET MPSLP_ACUM.CAL_RUN_ID(0) = $Cal_Run_ID
            LET MPSLP_ACUM.EMPL_RCD(0) =    #Empl_Rcd                
            LET MPSLP_ACUM.GP_PAYGROUP(0) =    $GP_Paygroup             
            LET MPSLP_ACUM.CAL_ID(0) =          $Cal_ID    
            LET MPSLP_ACUM.ORIG_CAL_RUN_ID(0) =  $orig_Cal_Run_ID
            LET MPSLP_ACUM.GP_PSLP_SRCPRODUCT(0) =   'GPMYS'         
            LET MPSLP_ACUM.GP_PSLP_ID(0) =    to_char(#Empl_Rcd ) || ' ' || Edit($Pay_Pd_To_Dt,'MM/DD/YYYY')   
            LET MPSLP_ACUM.RSLT_SEG_NUM(0) =    #Rslt_Seg_num      
            LET MPSLP_ACUM.PIN_NUM(0) =        #Acum_PinNum         
            LET MPSLP_ACUM.EMPL_RCD_ACUM(0) = #Empl_Rcd
            LET MPSLP_ACUM.ACM_FROM_DT(0) =  $Acm_from_Dt
            LET MPSLP_ACUM.ACM_THRU_DT(0) = $Acm_Thru_Dt
            LET MPSLP_ACUM.SEQ_NUM8(0) = #Seq_Num8
            LET MPSLP_ACUM.PIN_ELEM_GRP_NUM(0) = #PARENT_PIN_Y
            LET MPSLP_ACUM.SLICE_BGN_DT(0) = $Slice_Bgn_Dt            
            LET MPSLP_ACUM.SLICE_END_DT(0) = $Slice_End_Dt
            LET MPSLP_ACUM.SEG_BGN_DT(0) = $Pay_Pd_From_Dt
            LET MPSLP_ACUM.SEG_END_DT(0) = $Pay_Pd_To_Dt
            LET MPSLP_ACUM.PRD_BGN_DT(0) = $Pay_Pd_From_Dt
            LET MPSLP_ACUM.PRD_END_DT(0) = $Pay_Pd_To_Dt
            LET MPSLP_ACUM.PYMT_DT(0) = $Pymt_Dt
            LET MPSLP_ACUM.RUN_TYPE(0) = rtrim($Run_Type, ' ')           
            LET MPSLP_ACUM.COUNTRY(0) = 'MYS'
            LET MPSLP_ACUM.CALC_RSLT_VAL(0) = #Acum_Rslt_Amt
            LET MPSLP_ACUM.CALC_DELTA_VAL(0) = 0
            LET MPSLP_ACUM.CALC_VAL(0) = #CalcVal          
            LET MPSLP_ACUM.GP_MPSLP_SECTION(0) =    $GP_section_nm_Y         
            LET MPSLP_ACUM.GP_MPSLP_SPRNT_ORD(0) =  $PinSec
            LET MPSLP_ACUM.GP_MPSLP_SUBSECTN(0) =    $Sect_Hdg   
            LET MPSLP_ACUM.GP_MPSLP_PRNT_ORD(0) =    #SeqNum5
            LET MPSLP_ACUM.GP_MPSLP_PIN_DESCR(0) =   $Descr_Temp 
       
       
           If ( (&ED.GPMY_YTD_ACUNT_IND = 'Y'  AND &ED.GPMY_YTD_AC_UNITS > 0 ) OR (&ED.GPMY_YTD_ACAMT_IND = 'Y' AND &ED.GPMY_YTD_AC_AMT > 0)  )
           
               If ( #Acum_Rslt_Amt <> 0 )
               
                do insert_mpslp_acum_row
                
                Let $AcmString = $AcmString || ' ' || to_char(#Acum_PinNum)
            
               End-If
            End-If   
            
         
        End-If
        
        End-If
          
        !**********Populate Mobile Payslip Ends***************  
        
        Let #Calc_Rslt_Val = 0
        Let #Acum_Rslt_Hrs = 0
        Let #Acum_Rslt_Amt = 0
          
        end-if     

    Let #LastPinNum = #ED_PinNum
    Let #LastEmplRcd = #Empl_Rcd
    
    
FROM PS_GPMY_PSLP_ED ED
, PS_GP_RSLT_ERN_DED RSLT
, PS_GP_PIN PIN
, PS_GP_PYE_PRC_STAT PRC


WHERE ED.PIN_NUM         = RSLT.PIN_NUM

  AND ED.PIN_NUM         = PIN.PIN_NUM

  AND ED.GPMY_PAYSLIP_ID = $Payslip_ID
  AND ED.SEQ_NUM         = #Sect_Seq_Num
  AND PRC.EMPLID         = $Emplid
  AND PRC.EMPL_RCD       = #Empl_Rcd
  AND RSLT.EMPLID        = PRC.EMPLID
  AND RSLT.EMPL_RCD      = PRC.EMPL_RCD
  AND RSLT.CAL_RUN_ID    = PRC.CAL_RUN_ID
  AND RSLT.GP_PAYGROUP   = PRC.GP_PAYGROUP
  AND RSLT.CAL_ID        = PRC.CAL_ID
  AND PRC.CAL_RUN_ID     = $Cal_Run_ID
  AND PRC.CAL_ID         = $Cal_ID
  AND PRC.GP_PAYGROUP    = $GP_Paygroup
  AND RSLT.RSLT_SEG_NUM  = #Rslt_Seg_num
  AND ED.EFFDT = (SELECT MAX(ED1.EFFDT) FROM PS_GPMY_PSLP_ED ED1
                    WHERE ED1.GPMY_PAYSLIP_ID = ED.GPMY_PAYSLIP_ID
                    AND ED1.SEQ_NUM = ED.SEQ_NUM )
  GROUP BY PIN.PIN_NUM, ED.DESCR, PIN.DESCR, PIN.PIN_NM, PIN.PIN_TYPE, ED.GPMY_DESC_TYPE, RSLT.SLICE_BGN_DT, RSLT.SLICE_END_DT, RSLT.CALC_RSLT_VAL, RSLT.UNIT_RSLT_VAL, RSLT.RATE_RSLT_VAL,
  RSLT.BASE_RSLT_VAL, RSLT.PCT_RSLT_VAL, RSLT.RSLT_PAYBK, RSLT.CALC_ADJ_VAL, RSLT.BASE_ADJ_VAL, RSLT.UNIT_ADJ_VAL, RSLT.INSTANCE, ED.GPMY_YTD_AC_AMT, ED.GPMY_YTD_AC_UNITS,
  ED.GPMY_BSE_COMP_IND, ED.GPMY_PCT_COMP_IND, ED.GPMY_RTE_COMP_IND, ED.GPMY_UNT_COMP_IND, ED.GPMY_YTD_ACAMT_IND, ED.GPMY_YTD_ACUNT_IND, ED.SEQ_NUM5, PRC.PRD_BGN_DT,  PRC.PRD_END_DT,
  PRC.ORIG_CAL_RUN_ID, PRC.RUN_TYPE
                
  ORDER BY ED.SEQ_NUM5
End-Select

        if #total_curr_amt <> 0
           Do Find-Start-Pos ($TenCharVar, {colED8},#ReturnCol)
           Let #ReturnCol = #ReturnCol - 3
           Let $Sec_Total = 'Total ' || $Sect_Hdg
           print $Sec_Total (+2, {ColA}) Bold
           print #total_curr_amt (0, #ReturnCol)   edit '9999999.99'
        End-If

        if #total_pybck_amt <> 0
            Do Find-Start-Pos ($TenCharVar, {colED9},#ReturnCol)
            Let #ReturnCol = #ReturnCol - 4
            print #total_pybck_amt   (0, #ReturnCol)  edit '99999999.99'

       end-if

       if  #total_adj_amt <> 0
            Do Find-Start-Pos ($TenCharVar, {colED10},#ReturnCol)
            Let #ReturnCol = #ReturnCol - 4
            print #total_adj_amt   (0, #ReturnCol)  edit '99999999.99'
        end-if

        if #Acum_Rslt_Amt_TTL <> 0
            Do Find-Start-Pos ($TenCharVar, {colED14},#ReturnCol)
            Let #ReturnCol = #ReturnCol - 4
            print #Acum_Rslt_Amt_TTL (0, #ReturnCol)  edit '99999999.99'
        end-if

        Let $New_Page_ED = 'N'
        Let #total_curr_amt = 0
        Let #total_adj_amt = 0
        Let #total_pybck_amt = 0
        Let #Acum_Rslt_Amt_TTL = 0
        
End-Procedure

!**************************************************************************
! Procedure: Get-Acum-Pin
! Description: Get and print the FYTDA Accumulators
!**************************************************************************
!Begin-procedure get-Acum-Pin
 ! let #acum_pin_num = 0
  !let $acum_pin_name = ''
  !let $GP_PIN-PIN_NM = ''
!#define GP_PIN
!begin-select
!AC1.PIN_NM

 ! Let $GP_PIN-PIN_NM = &AC1.PIN_NM
  !Do Get_Related_GP_PIN(&PIN.PIN_NUM)

!FROM PS_GP_PIN AC1
!WHERE AC1.PIN_NUM = &PIN.PIN_NUM
!end-select
! let $acum_pin_name = ltrim(rtrim($GP_PIN-PIN_NM,' '),' ') || '_CYTDA'

!begin-select
!AC2.PIN_NUM

 !          let #acum_pin_num = &AC2.PIN_NUM

!FROM PS_GP_PIN AC2
!WHERE AC2.PIN_NM = $acum_pin_name
!end-select

 ! If $curr_language_cd <> $Psoptions_Language_Cd
!begin-select
!AC2L.PIN_NUM

 ! let #acum_pin_num = &AC2L.PIN_NUM

!FROM PS_GP_PIN_LANG AC2L
!WHERE AC2L.PIN_NM = $acum_pin_name
 ! AND AC2L.LANGUAGE_CD = $Curr_language_Cd
!end-select
 ! End-If

!end-procedure



!**************************************************************************
! Procedure: Get_Leave
! Description: Get and print the leave balances for the payee.
!**************************************************************************
Begin-Procedure Get_Leave
        Let $First_Abs = 'Y'

Begin-Select
ABTMPL.SEQ_NUM
ABTMPL.GPMY_PIN_ENTL_NUM      !GPMY_ENTL_PIN_NUM
ABTMPL.PIN_NUM           &PIN1
ABTMPL.GPMY_DESC_TYPE
ABTMPL.DESCR
ABTMPL.GPMY_PIN_AC1_NUM  &PIN2
ABTMPL.GPMY_PIN_AC2_NUM  &PIN3
ABTMPL.GPMY_PIN_AC3_NUM  &PIN4

        Let #Pin1 = &PIN1  ! Entitlement
        Let #Pin2 = &PIN2  ! Balance
        Let #Pin3 = &PIN3  ! Adjustment
        Let #Pin4 = &PIN4  ! Take

        Let $AB_Descr = &ABTMPL.DESCR
        Let $Descr_Type = &ABTMPL.GPMY_DESC_TYPE

        Do Get-Leave-Data

        if #Pin1Val <> 0 or #Pin2Val <> 0 or #Pin3Val <> 0 or #Pin4Val <> 0
         If #Current-line >  80
             NEW-PAGE
             do Print_Heading
         Else
           If $First_Abs = 'Y'
              do Print_Abs_headings
              Let $First_Abs = 'N'
           end-if

           print $Descr        (+1, {ColA})

           if #Pin1Val <> 0
              print #Pin1Val  (0, {ColD})   edit '9999.999999'
           end-if
           if #Pin2Val <> 0
              print #Pin2Val  (0, {ColN})  edit '9999.999999'
           end-if
           if #Pin3Val <> 0
              print #Pin3Val  (0, {ColG})  edit '9999.999999'
           end-if

           if #Pin4Val <> 0
              print #Pin4Val  (0, {ColK})  edit '9999.999999'
           end-if
         end-if
        end-if

FROM PS_GPMY_PSLP_ABS ABTMPL
WHERE ABTMPL.GPMY_PAYSLIP_ID = $Payslip_Id
AND ABTMPL.EFFDT  = (SELECT MAX(ABTMPL1.EFFDT)
                            FROM PS_GPMY_PSLP_ABS ABTMPL1
                            WHERE ABTMPL1.GPMY_PAYSLIP_ID = ABTMPL.GPMY_PAYSLIP_ID
                            AND ABTMPL1.EFFDT <= $Pymt_Dt)
ORDER BY ABTMPL.SEQ_NUM
End-Select

          Let $New_Page_Abs = 'N'

End-Procedure

!**************************************************************************
! Procedure: Get-Leave-Data
! Description: Get and print the leave balances for the payee.
!**************************************************************************
Begin-Procedure Get-Leave-Data
   Let #Pin1Val = 0
   Let #Pin2Val = 0
   Let #Pin3Val = 0
   Let #Pin4Val = 0

Begin-Select on-error=SQL-Error
D.PIN_NUM
D.PIN_NM
ABS.CALC_RSLT_VAL
D.DESCR

        Let $Pin_Descr                 = &D.DESCR
        Let $Pin_Nm                = &D.PIN_NM

           evaluate $Descr_Type
           when = '10'
                Let $Descr = $Ab_Descr
           when = '20'
                Let $Descr = $Pin_Descr
           when = '30'
                Let $Descr = $Pin_Nm
        end-evaluate

        if &D.PIN_NUM = #PIN1
           Let #Pin1Val = &ABS.CALC_RSLT_VAL
        end-if
        if &D.PIN_NUM = #PIN2
           Let #Pin2Val = &ABS.CALC_RSLT_VAL
        end-if
        if &D.PIN_NUM = #PIN3
           Let #Pin3Val = &ABS.CALC_RSLT_VAL
        end-if
        if &D.PIN_NUM = #PIN4
           Let #Pin4Val = &ABS.CALC_RSLT_VAL
        end-if
FROM PS_GP_PYE_PRC_STAT A
   , PS_GP_PYE_SEG_STAT B
   , PS_GP_PIN D
   , PS_GP_RSLT_ACUM ABS
   , PS_GP_RUN_TYPE DR
   , PS_GP_CAL_PRD H
  WHERE A.CAL_RUN_ID         = B.CAL_RUN_ID
    AND A.EMPLID             = B.EMPLID
    AND A.EMPL_RCD           = B.EMPL_RCD
    AND A.GP_PAYGROUP        = B.GP_PAYGROUP
    AND A.CAL_ID             = B.CAL_ID
    AND DR.RUN_TYPE           = A.RUN_TYPE
    AND DR.CALC_TYPE          = 'A'
    AND A.CAL_PRD_ID         = H.CAL_PRD_ID
    AND A.PRD_BGN_DT         = H.PRD_BGN_DT
    AND A.PRD_END_DT         = H.PRD_END_DT
    AND B.EMPLID             = ABS.EMPLID
    AND B.CAL_RUN_ID         = ABS.CAL_RUN_ID
    AND B.EMPL_RCD           = ABS.EMPL_RCD
    AND ABS.EMPL_RCD         = ABS.EMPL_RCD_ACUM
    AND B.CAL_ID             = ABS.CAL_ID
    AND B.GP_PAYGROUP        = ABS.GP_PAYGROUP
    AND ABS.PIN_NUM          = D.PIN_NUM
    AND (ABS.PIN_NUM = #PIN1 or ABS.PIN_NUM = #PIN2 or ABS.PIN_NUM = #PIN3 or ABS.PIN_NUM = #PIN4)
    AND (ABS.ACM_THRU_DT IS NULL
    OR
       ((ABS.ACM_THRU_DT IS NOT NULL) AND
    ABS.ACM_THRU_DT   = (SELECT MAX(ABS1.ACM_THRU_DT)
                                FROM PS_GP_RSLT_ACUM ABS1
                                WHERE ABS.EMPLID        = ABS1.EMPLID
                                AND ABS.CAL_RUN_ID      = ABS1.CAL_RUN_ID
                                AND ABS.EMPL_RCD        = ABS1.EMPL_RCD
                                AND ABS.GP_PAYGROUP     = ABS1.GP_PAYGROUP
                                AND ABS.CAL_ID          = ABS1.CAL_ID
                                AND ABS.ORIG_CAL_RUN_ID = ABS1.ORIG_CAL_RUN_ID
                                AND ABS.RSLT_SEG_NUM    = ABS1.RSLT_SEG_NUM
                                AND ABS.PIN_NUM         = ABS1.PIN_NUM
                                AND ABS.EMPL_RCD_ACUM   = ABS1.EMPL_RCD_ACUM
                          )
        )AND ABS.ACM_THRU_DT > ABS.SLICE_END_DT)
    AND A.EMPLID             = $Emplid
    AND A.EMPL_RCD           = #Empl_Rcd
    AND A.CAL_RUN_ID         = $Cal_Run_Id
    AND A.GP_PAYGROUP        = $GP_Paygroup
    AND A.PRC_ORD_TS = (SELECT MAX(A21.PRC_ORD_TS) FROM PS_GP_PYE_PRC_STAT A21, PS_GP_CAL_PRD AG21
                                         WHERE A21.EMPLID     = A.EMPLID
                                           AND A21.EMPL_RCD   = A.EMPL_RCD
                                           AND A21.CAL_RUN_ID = A.CAL_RUN_ID
                                           AND A21.GP_PAYGROUP = A.GP_PAYGROUP
                                           AND A21.CAL_ID = A.CAL_ID
                                           AND A21.ORIG_CAL_RUN_ID = A.ORIG_CAL_RUN_ID
                                           AND A21.CAL_PRD_ID = AG21.CAL_PRD_ID
                                           AND A21.PRD_BGN_DT = H.PRD_BGN_DT
                                           AND A21.PRD_END_DT = H.PRD_END_DT )

End-Select
End-Procedure

!**************************************************************************
! Procedure: Get_Disburs_Banked
! Description: Get and print the banking details for the payee.
!**************************************************************************
Begin-Procedure Get_Disburs_Banked

        Let $First_Disb = 'Y'

Begin-Select on-error=SQL-Error
B.GP_PMT_AMT
D.BRANCH_EC_CD
D.ACCOUNT_EC_ID
        Let $MACNO = &D.ACCOUNT_EC_ID

E.BANK_NM
    If #Current-line >  80
        NEW-PAGE
        do Print_Heading
    Else
        If $First_Disb = 'Y'
                do Print_Disb_headings
                Let $First_Disb = 'N'
        end-if

        Let $Bank_Name = &E.BANK_NM
        print $Bank_Name (+1, {ColA})
        Let $BSB_Char = edit(&D.BRANCH_EC_CD,'XXX-XXX-XXXXXXXX')
        print $BSB_char (0, {ColD})

        Let $Acct_Nbr = &D.ACCOUNT_EC_ID

        print $Acct_Nbr (0, {ColG})

        Let #Paymnt_Amt = &B.GP_PMT_AMT
        print #Paymnt_Amt (0, {ColN}) edit '999999999.99'
    End-If

FROM PS_GP_CALENDAR A
, PS_GP_PAYMENT B
, PS_GP_NET_DIST_DTL C
, PS_PYE_BANKACCT D
, PS_BANK_EC_TBL E
WHERE   B.CAL_ID      = A.CAL_ID
    AND B.GP_PAYGROUP = A.GP_PAYGROUP
    AND A. RUN_TYPE   = C.RUN_TYPE
    AND B.EMPLID      = C.EMPLID
    AND C.EMPLID      = D.EMPLID
    AND B.EMPL_RCD    = C.EMPL_RCD
    AND B.INSTANCE    = C.INSTANCE
    AND C.ACCOUNT_ID  = D.ACCOUNT_ID
    AND D.BANK_CD     = E.BANK_CD
    AND D.COUNTRY_CD  = E.COUNTRY_CD
    AND B.PMT_TYPE    = '01'
    AND C.EFFDT =(SELECT MAX(C1.EFFDT) FROM PS_GP_NET_DIST_DTL C1 WHERE C1.EMPLID = C.EMPLID
                      AND C1.EFFDT <= $Pymt_Dt
                      AND C1.EMPL_RCD = C.EMPL_RCD
                      AND C1.RUN_TYPE = C.RUN_TYPE
                      AND C1.INSTANCE = C.INSTANCE)
    AND B.EMPLID        = $Emplid
    AND B.EMPL_RCD      = #Empl_Rcd
    AND B.CAL_RUN_ID    = $Cal_Run_ID
    AND B.GP_PAYGROUP   = $GP_Paygroup
    AND B.CAL_ID        = $Cal_ID

End-Select

End-Procedure
!**************************************************************************
! Procedure: Get_Disburs_Nonbanked
! Description: Get and print the non banking disburment details for the payee.
!**************************************************************************
Begin-Procedure Get_Disburs_Nonbanked

Begin-Select on-error=SQL-Error
PMNT.PAYMENT_MTHD    &PMNT_Mthd
SUM(PMNT.GP_PMT_AMT) &NetPayAmt

    If #Current-line >  80
        NEW-PAGE
        do Print_Heading
    Else
        If $First_Disb = 'Y'
           do Print_Non_Disb_headings
           Let $First_Disb = 'N'
        end-if

        Let #Paymnt_Amt = &NetPayAmt
        if #Paymnt_Amt <> 0
           Let $FieldName  = 'PAYMENT_MTHD'
           Let $FieldValue = &PMNT_Mthd

           Let $curr_language_cd = 'ENG'

           Do Read-Translate-Table
           Print $XlatLongName (+1,{ColA})
           print #Paymnt_Amt (0, {ColN}) edit '999999999.99'
        end-if
     End-If
FROM PS_GP_PAYMENT PMNT
    WHERE PMNT.EMPLID      = $Emplid
      AND PMNT.EMPL_RCD    = #Empl_Rcd
      AND PMNT.CAL_RUN_ID  = $Cal_Run_ID
      AND PMNT.CAL_ID      = $Cal_ID
      AND PMNT.GP_PAYGROUP = $GP_Paygroup
      AND PMNT.PAYMENT_MTHD <> 'T'
      AND PMNT.PMT_TYPE    = '01'
GROUP BY PMNT.PAYMENT_MTHD
End-Select

        Let $New_Page_Dis = 'N'

End-Procedure

!**************************************************************************
! Procedure: Get_Messages
! Description: Get and print the messages for the payee.
!**************************************************************************
Begin-Procedure Get_Messages

        Let $First_Msg = 'Y'
        Let #Msg_Nbr = 0
Begin-Select on-error=SQL-Error
B.GPMY_MSG_NBR
A.GPMY_MESSAGE

        If $First_Msg = 'Y'
            print 'Messages' (+2, {ColB}) Center Bold Underline
           Let $First_Msg = 'N'
        end-if

        Let #Msg_Nbr = &B.GPMY_MSG_NBR
        Let $Message = &A.GPMY_MESSAGE

        if #Msg_nbr <> 0
           print #Msg_Nbr (+1, {ColC}) EDIT '9999'
           print $Message (0, 25) Wrap {colK} 3
        end-if

FROM PS_GPMY_PSLP_MSG A
, PS_GPMY_PSLP_RSLT B
WHERE A.CAL_RUN_ID = B.CAL_RUN_ID
    AND A.GPMY_MSG_NBR = B.GPMY_MSG_NBR
    AND B.EMPLID        = $Emplid
    AND B.EMPL_RCD      = #Empl_Rcd
    AND B.CAL_RUN_ID    = $Cal_Run_ID
    AND B.GP_PAYGROUP   = $GP_Paygroup
    AND B.CAL_ID        = $Cal_ID
End-Select
End-Procedure

!**************************************************************************
! Procedure: Print_ED_Headings
! Description: Prints the section headings
!**************************************************************************
Begin-Procedure Print_ED_Headings
        print $Sect_Hdg (+3,) Center Bold Underline

        print 'Description' (+1, {ColA}) Bold

        Do Find-Start-Pos ('Base', {colED3},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Base' (0, #ReturnCol) Bold

        Do Find-Start-Pos ('Percent', {colED5},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Percent' (0, #ReturnCol) Bold

        Do Find-Start-Pos ('Rate', {colED6},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Rate' (0, #ReturnCol) Bold


        Do Find-Start-Pos ('Units', {colED7},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Units' (0, #ReturnCol) Bold

        Do Find-Start-Pos ('Amount', {colED8},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Amount' (0, #ReturnCol) Bold

        Do Find-Start-Pos ('Amount', {colED9},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Amount' (0, #ReturnCol) Bold

        Do Find-Start-Pos ('Amount', {colED10},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Amount' (0, #ReturnCol) Bold

        Do Find-Start-Pos ('Base', {colED11},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Base ' (0, #ReturnCol) Bold


        Do Find-Start-Pos ('Unit', {colED12},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Unit' (0, #ReturnCol) Bold

        Do Find-Start-Pos ('Units', {colED13},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Units' (0, #ReturnCol) Bold

        Do Find-Start-Pos ('Amount', {colED14},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Amount' (0, #ReturnCol) Bold

        Let $New_Page_ED = 'Y'

End-Procedure

!**************************************************************************
! Procedure: Print_Abs_headings
! Description: Print the absence data headings
!**************************************************************************

Begin-Procedure Print_Abs_headings

        print 'Leave Balances' (+3, {ColG}) Center Bold Underline
        print 'Description' (+1, {ColA}) Bold
        print 'Entitlement' (0, {ColD}) Bold
        print 'Adjustment'  (0, {ColG}) Bold
        print 'Taken'       (0, {ColM}) Bold
        print 'End Balance' (0, {ColN}) Bold

        Let $New_Page_Abs = 'Y'
End-Procedure

!**************************************************************************
! Procedure: Print_Disb_headings
! Description: Print the disbusment data headings
!**************************************************************************

Begin-Procedure Print_Disb_headings
        print 'Disbursement  Details' (+3, {ColG}) Center Bold Underline
        print 'Bank' (+1, {ColA}) Bold
        print 'B.S.B' (0, {ColD}) Bold
        print 'Account #'  (0, {ColG}) Bold

        Do Find-Start-Pos ('Amount', {colO},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Amount' (0, #ReturnCol) Bold

        Let $New_Page_Dis = 'Y'

End-Procedure


!**************************************************************************
! Procedure: Print_Non_Disb_headings
! Description: Print the Non disbusment data headings
!**************************************************************************
Begin-Procedure Print_Non_Disb_headings
        print 'Disbursment Details' (+3, {ColG}) Center Bold Underline

        print 'Bank' (+1, {ColA}) Bold

        Do Find-Start-Pos ('Amount', {colO},#ReturnCol)
        Let #ReturnCol = #ReturnCol - 3
        print 'Amount' (0, #ReturnCol) Bold

        Let $New_Page_Dis = 'Y'

End-Procedure



!
!**************************************************************************
! Procedure: Get_dept
! Description: Get the dept name of the payee
!**************************************************************************
Begin-Procedure Get_dept
      Let $dept_name = ''
Begin-Select on-error=SQL-Error

DEPT.DESCR

      Let $dept_name = rtrim(&DEPT.DESCR,' ')

   FROM PS_DEPT_TBL DEPT
    WHERE DEPT.SETID  = $SetIDDept
      AND DEPT.DEPTID = $Deptid
      AND DEPT.EFFDT = (SELECT MAX(DEPT1.EFFDT) FROM PS_DEPT_TBL DEPT1
                            WHERE DEPT1.SETID  = DEPT.SETID
                              AND DEPT1.DEPTID = DEPT.DEPTID)
End-Select
End-Procedure
!
!
!**************************************************************************
! Procedure: Get_location
! Description: Get the location of the payee
!**************************************************************************
Begin-Procedure Get_location
Begin-Select on-error=SQL-Error
A.DESCR
A.ADDRESS1
A.ADDRESS2
A.ADDRESS3
A.ADDRESS4
A.CITY
A.STATE
A.POSTAL
A.COUNTY
A.COUNTRY

        if $Del_Opt = '30'   ! Location

              Let $Name     = $EmployeeName
              Let $address1 = &A.ADDRESS1
              Let $address2 = &A.ADDRESS2
              Let $address3 = &A.ADDRESS3
              Let $address4 = &A.ADDRESS4
              Let $City     = rtrim(&A.CITY, ' ')
              Let $State    = &A.STATE
              Let $Countrycode  = &A.COUNTRY
              Let $Postal   = &A.POSTAL

         do adformat
         do Compress-Address

                 Let $LOC_NM         = $ADDLINE1
                 Let $LOCADD1             = $ADDLINE2
                 Let $LOCADD2             = $ADDLINE3
                 Let $LOCADD3             = $ADDLINE31
                 Let $LOCADD4             = $ADDLINE4
                 Let $LOCCITY             = $ADDLINE5
                 Let $LOCSTATE           = $ADDLINE6



       end-if
       Let $LctnDesc = rtrim(&A.DESCR,' ')

FROM PS_LOCATION_TBL A
WHERE A.LOCATION = $Location
End-Select
End-Procedure

 !**************************************************************************
 !
 ! Procedure: Print_Company
 !
 ! Description:  Get the Company Details
 !
 !**************************************************************************
 Begin-Procedure Print_LOCATION

         print $LOC_NM  (10,15,30) BOLD

         print $LOCADD1 (+1,15,30) BOLD

         print $LOCADD2 (+1,15,30) BOLD

         print $LOCADD3 (+1,15,30) BOLD

         print $LOCADD4 (+1,15,30) BOLD
          print $LOCCITY (+1,15,30) BOLD
           print $LOCSTATE (+1,15,30) BOLD


 End-Procedure


!**************************************************************************
! Procedure: Get-PayEntity
! Description: Get the pay entity details
!**************************************************************************
Begin-Procedure Get-PayEntity
  Let $PayEntityDescr = ''
Begin-Select on-error=SQL-Error
PYE.DESCR

  Let $PayEntityDescr = rtrim(&PYE.DESCR,' ')

FROM PS_GP_PYENT PYE
WHERE PYE.PAY_ENTITY = $Pay_Ent
End-Select
End-Procedure

!**************************************************************************
! Procedure: Get_Frequency
! Description: Get the frequency of the payee
!**************************************************************************
Begin-Procedure Get_Frequency

Begin-Select on-error=SQL-Error
FREQ.DESCRSHORT

        Let $freq_descr = &FREQ.DESCRSHORT
FROM
PS_FREQUENCY_TBL FREQ
WHERE FREQ.FREQUENCY_ID = $Comp_Freq
AND FREQ.EFFDT = (SELECT MAX(FREQ1.EFFDT)
                FROM PS_FREQUENCY_TBL FREQ1
                WHERE FREQ.FREQUENCY_ID = FREQ1.FREQUENCY_ID)
End-Select
End-Procedure

!**************************************************************************
! Procedure     : Check_Excl_Dept
! Description   : Check if the payee's department has been excluded
!**************************************************************************
Begin-Procedure Check_Excl_Dept
Begin-Select on-error=SQL-Error
!DEPT.SETID
DEPT.DEPTID
        Let $dept_excl = 'Y'
   FROM PS_GPMY_PSLP_DEPT DEPT
   WHERE DEPT.DEPTID = $Deptid
    AND DEPT.GPMY_PAYSLIP_ID = $Payslip_Id
    AND DEPT.EFFDT = (SELECT MAX(DEPT1.EFFDT) FROM PS_GPMY_PSLP_DEPT DEPT1
                      WHERE DEPT1.DEPTID          = DEPT.DEPTID
                      AND DEPT1.GPMY_PAYSLIP_ID = $Payslip_ID )
End-Select
End-Procedure

!**************************************************************************
! Procedure     : Check_Excl_Loctn
! Description   : Check if the payee's location has been excluded
!**************************************************************************
Begin-Procedure Check_Excl_Loctn
Begin-Select on-error=SQL-Error
!LOCN.SETID
LOCN.LOCATION
        Let $locn_excl = 'Y'

   FROM PS_GPMY_PSLP_LOCTN LOCN
   WHERE LOCN.LOCATION = $Location
     AND LOCN.GPMY_PAYSLIP_ID = $Payslip_Id
     AND LOCN.EFFDT = (SELECT MAX(LOCN1.EFFDT) FROM PS_GPMY_PSLP_LOCTN LOCN1
                      WHERE LOCN1.LOCATION     = LOCN.LOCATION
                      AND LOCN1.GPMY_PAYSLIP_ID = $Payslip_ID )
End-Select
End-Procedure


!**************************************************************************
! Procedure     : Get_Address
! Description   : Get the payee's address details
!**************************************************************************
Begin-Procedure Get_Address
  Let $Adr2Found = 'N'
  Let $NM       = ''
! Initialize the Address Line variables, to avoid wrong information being printed
  Let $ADDLINE1 = ''
  Let $ADDLINE2 = ''
  Let $ADDLINE3 = ''
  Let $ADDLINE31 = ''
  Let $ADDLINE4 = ''
  Let $ADDLINE5 = ''
  Let $ADDLINE6 = ''

Begin-Select on-error=SQL-Error
ADR.ADDRESS1
ADR.ADDRESS2
ADR.ADDRESS3
ADR.ADDRESS4
ADR.CITY
ADR.STATE
ADR.POSTAL
ADR.COUNTY
ADR.COUNTRY
        Let $Adr2Found = 'Y'

        Let $Name     = $EmployeeName
        Let $address1 = &ADR.ADDRESS1
        Let $address2 = &ADR.ADDRESS2
        Let $address3 = &ADR.ADDRESS3
        Let $address4 = &ADR.ADDRESS4
        Let $City       = rtrim(&ADR.CITY, ' ')
        Let $State     = &ADR.STATE
        Let $Country  = &ADR.COUNTRY
        Let $Postal    = &ADR.POSTAL

           do adformat
           do Compress-Address

           Let $NM         = $ADDLINE1
           Let $ADD2      = $ADDLINE2
           Let $ADD3      = $ADDLINE3
           Let $ADD31     = $ADDLINE31
           Let $ADD4       = $ADDLINE4
           Let $ADDCITY   = $ADDLINE5
           Let $ADDSTATE = $ADDLINE6

    FROM PS_ADDRESSES ADR
    WHERE ADR.EMPLID = $Emplid
      AND ADR.ADDRESS_TYPE = $Addr_Type
      AND ADR.EFFDT = (SELECT MAX(ADR1.EFFDT) FROM PS_ADDRESSES ADR1
                          WHERE ADR1.EMPLID = ADR.EMPLID
                            AND ADR1.ADDRESS_TYPE = ADR.ADDRESS_TYPE)
End-Select
! If Address Entry wasn't found, populate $ADDLINE1 with EmployeeName.
! 599239 - Initialize vars when row not found
   If $Adr2Found = 'N'
      Let $ADDLINE1 = $EmployeeName
      Let $ADD2     = 'No Address Found'
      Let $ADD3     = ''
      Let $ADD31    = ''
      Let $ADD4     = ''
      Let $ADDCITY  = ''
      Let $ADDSTATE = ''
   End-if

End-Procedure
!**************************************************************************
! Procedure     : Check_Override_Add
! Description   : Check the existance of an override
!**************************************************************************
Begin-Procedure Check_Override_Add

Begin-Select on-error=SQL-Error

PAY.GPMY_DEL_OPT

        Let $Del_Opt = &PAY.GPMY_DEL_OPT

FROM PS_GPMY_PAYEE_TBL PAY
WHERE PAY.EMPLID = $Emplid
AND PAY.EMPL_RCD = #Empl_Rcd
AND PAY.EFFDT = (SELECT MAX(PAY1.EFFDT)
                FROM PS_GPMY_PAYEE_TBL PAY1
                WHERE PAY1.EMPLID = PAY.EMPLID
                AND PAY1.EMPL_RCD = PAY.EMPL_RCD)
AND PAY.EFF_STATUS = 'A'

End-Select
End-Procedure
!
!**************************************************************************
! Procedure     : Get_Payslip_Id
! Description   : Get the payslip id based on the paygroup of payee
!**************************************************************************
Begin-Procedure Get_Payslip_Id

Begin-Select on-error=SQL-Error
ID.GPMY_PAYSLIP_ID

        Let $Payslip_Id = &ID.GPMY_PAYSLIP_ID

FROM PS_GP_PG_DTL_SGPMY ID,PS_GP_CALENDAR GPCAL
WHERE ID.GP_PAYGROUP = GPCAL.GP_PAYGROUP
AND ID.GPMY_REG_RUN_TYPE = GPCAL.RUN_TYPE
AND ID.GP_PAYGROUP = $GP_Paygroup
AND ID.EFFDT = (SELECT MAX(F1.EFFDT)
                FROM PS_GP_PG_DTL_SGPMY F1
                WHERE F1.GP_PAYGROUP = ID.GP_PAYGROUP)
End-Select
End-Procedure

!**************************************************************************
! Procedure     : Get_Accum_Values
! Description   : Get the accumulated values for the payee
!**************************************************************************
Begin-Procedure Get_Accum_Values
Begin-Select on-error=SQL-Error
ACC.CALC_RSLT_VAL
ACC.CALC_VAL
ACC.SLICE_BGN_DT
ACC.SLICE_END_DT
ACC.EMPL_RCD_ACUM
ACC.ACM_FROM_DT
ACC.ACM_THRU_DT
ACC.SEQ_NUM8
ACC.PIN_NUM
ACC.USER_KEY1         &UsrKey1
ACC.USER_KEY2         &UsrKey2
ACC.USER_KEY3         &UsrKey3
ACC.USER_KEY4         &UsrKey4
ACC.USER_KEY5         &UsrKey5
ACC.USER_KEY6         &UsrKey6



        Let #Calc_Rslt_Val = &ACC.CALC_RSLT_VAL
        Let #CalcVal = &ACC.CALC_VAL
        Let $Slice_Bgn_Dt = &ACC.SLICE_BGN_DT
        Let $Slice_End_Dt = &ACC.SLICE_END_DT
        Let #Empl_Rcd_acum = &ACC.EMPL_RCD_ACUM
        Let $Acm_from_Dt = &ACC.ACM_FROM_DT
        Let $Acm_Thru_Dt = &ACC.ACM_THRU_DT
        Let #Seq_Num8    = &ACC.SEQ_NUM8
        Let #PIN_NUM_YTD = &ACC.PIN_NUM
        
        
FROM PS_GP_RSLT_ACUM ACC
  WHERE ACC.EMPLID        = $Emplid
    AND ACC.EMPL_RCD      = #Empl_Rcd
    AND ACC.CAL_RUN_ID    = $Cal_Run_ID
    AND ACC.GP_PAYGROUP   = $GP_Paygroup
    AND ACC.CAL_ID        = $Cal_ID
    AND ACC.RSLT_SEG_NUM  = #Rslt_Seg_num
    AND ACC.PIN_NUM       = #Pin_Num
    
End-Select
End-Procedure

!**************************************************************************
!Procedure: Get_Payment_Summ
!Description: Get and print the pay summary details for the payee.
!**************************************************************************
Begin-Procedure Get_Payment_Summ

Begin-Select on-error=SQL-Error
DISTINCT(C.SEQ_NUM)  &C.SEQ_NUM
C.GPMY_PIN_AC1_NUM                &GPMY_PIN_AC1_NUM
C.GPMY_PIN_AC2_NUM                &GPMY_PIN_AC2_NUM
C.GPMY_PIN_AC3_NUM                &GPMY_PIN_AC3_NUM
C.GPMY_PIN_AC4_NUM                &GPMY_PIN_AC4_NUM
C.GPMY_PIN_AC5_NUM                 &GPMY_PIN_AC5_NUM
C.GPMY_PIN_AC6_NUM                &GPMY_PIN_AC6_NUM
C.DESCR                          &C.DESCR

        Let #Calc_Rslt_Val = 0
        Let #Seq_No1 = &C.SEQ_NUM

         Let #Pin_Num = &GPMY_PIN_AC1_NUM
        do Get_Accum_Values
        Let #Calc_Acum_Val1 = #Calc_Rslt_Val

        Let #Calc_Rslt_Val = 0

         Let #Pin_Num = &GPMY_PIN_AC2_NUM
        do Get_Accum_Values
        Let #Calc_Acum_Val2 = #Calc_Rslt_Val

        Let #Calc_Rslt_Val = 0

         Let #Pin_Num = &GPMY_PIN_AC3_NUM
        do Get_Accum_Values
        Let #Calc_Acum_Val3 = #Calc_Rslt_Val

        Let #Calc_Rslt_Val = 0

         Let #Pin_Num = &GPMY_PIN_AC4_NUM
        do Get_Accum_Values
        Let #Calc_Acum_Val4 = #Calc_Rslt_Val

        Let #Calc_Rslt_Val = 0

         Let #Pin_Num = &GPMY_PIN_AC5_NUM
        do Get_Accum_Values
        Let #Calc_Acum_Val5 = #Calc_Rslt_Val

        Let #Calc_Rslt_Val = 0

         Let #Pin_Num = &GPMY_PIN_AC6_NUM
        do Get_Accum_Values
        Let #Calc_Acum_Val6 = #Calc_Rslt_Val

         Let $Pay_Summ_Desc  = &C.DESCR

        if #Calc_Acum_Val1 <> 0 or
              #Calc_Acum_Val2 <> 0 or
              #Calc_Acum_Val3 <> 0 or
              #Calc_Acum_Val4 <> 0 or
              #Calc_Acum_Val5 <> 0 or
              #Calc_Acum_Val6 <> 0

          print $Pay_Summ_Desc (+1, {colA}) Bold

          if  #Calc_Acum_Val1 <> 0
            print #Calc_Acum_Val1 (0, {colAC1})  edit 9999999.99
          end-if

          if  #Calc_Acum_Val2 <> 0
            print #Calc_Acum_Val2 (0, {colAC2})      edit 9999999.99
          end-if

          if  #Calc_Acum_Val3 <> 0
            print #Calc_Acum_Val3 (0, {colAC3})  edit 9999999.99
          end-if

          if  #Calc_Acum_Val4 <> 0
            print #Calc_Acum_Val4 (0, {colAC4})  edit 9999999.99
          end-if

          if  #Calc_Acum_Val5 <> 0
            print #Calc_Acum_Val5 (0, {colAC5})  edit 9999999.99
          end-if

          if  #Calc_Acum_Val6 <> 0
            print #Calc_Acum_Val6 (0, {colAC6})  edit 9999999.99
          end-if
        end-if

         
    !**********Populate Mobile Payslip Header info into staging tables***************
    
    If( $custEnabled = 'Y')
    
   !Get the current accumulator values 
     
   Let #CurrentAccum = &GPMY_PIN_AC1_NUM
   Do Check_CurrentAccum
       
    If ( $CurrentAccumEnabled = 'Y' ) 
    
        LET $PSLP_DT = Edit($Pay_Pd_To_Dt,'MM/DD/YYYY')  
        LET MPSLP_HDR.EMPLID(0) = $Emplid
        LET MPSLP_HDR.CAL_RUN_ID(0) = $Cal_Run_Id
        LET MPSLP_HDR.EMPL_RCD(0) = #Empl_Rcd
        LET MPSLP_HDR.GP_PAYGROUP(0) = $GP_Paygroup
        LET MPSLP_HDR.CAL_ID(0) = $Cal_ID
        LET MPSLP_HDR.ORIG_CAL_RUN_ID(0) = $orig_Cal_Run_ID
        LET MPSLP_HDR.RSLT_SEG_NUM(0) = #Rslt_Seg_num
        LET MPSLP_HDR.GP_PSLP_SRCPRODUCT(0) = 'GPMYS'
        LET MPSLP_HDR.GP_PSLP_ID(0) = to_char(#Empl_Rcd) || ' ' || $PSLP_DT
        LET MPSLP_HDR.SEG_BGN_DT(0) = $Pay_Pd_From_Dt
        LET MPSLP_HDR.SEG_END_DT(0) = $Pay_Pd_To_Dt
        LET MPSLP_HDR.PRD_BGN_DT(0) = $Pay_Pd_From_Dt
        LET MPSLP_HDR.PRD_END_DT(0) = $Pay_Pd_To_Dt
        LET MPSLP_HDR.PYMT_DT(0) = $Pymt_Dt
        LET MPSLP_HDR.GP_MPSLP_GROSS(0) = #Calc_Acum_Val1
        LET MPSLP_HDR.CALC_VAL(0) = #Calc_Acum_Val2
        LET MPSLP_HDR.GP_MPSLP_NET(0) = #Calc_Acum_Val5
        LET MPSLP_HDR.GP_COMPANY(0) = $Company
        LET MPSLP_HDR.RUN_TYPE(0)= rtrim($Run_Type,' ')

 
        DO insert_mpslp_hdr_row
        
    End-If  
            
                    
    End-If
    
    
    FROM PS_GPMY_PSLP_ACUM C,
    PS_GP_CALENDAR GPCAL,
    PS_GP_PG_DTL_SGPMY D,
    PS_GP_PYE_SEG_STAT B,
    PS_GP_PYE_PRC_STAT A
WHERE C.GPMY_PAYSLIP_ID = D.GPMY_PAYSLIP_ID
AND D.GP_PAYGROUP = B.GP_PAYGROUP
AND D.GPMY_PAYSLIP_ID = D.GPMY_PAYSLIP_ID
AND B.CAL_RUN_ID      = A.CAL_RUN_ID
AND B.EMPLID          = A.EMPLID
AND B.EMPL_RCD        = A.EMPL_RCD
AND B.CAL_ID          = A.CAL_ID
AND B.GP_PAYGROUP     = A.GP_PAYGROUP
AND ( ( ( GPCAL.GP_PAYGROUP = B.GP_PAYGROUP )
      AND ( GPCAL.CAL_ID = B.CAL_ID  )
      AND ( D.GPMY_REG_RUN_TYPE = GPCAL.RUN_TYPE ) )
      OR ( $OffCycle_Enabled ='Y'))
AND A.EMPLID = $Emplid
AND A.EMPL_RCD = #Empl_Rcd
AND A.CAL_RUN_ID     = $Cal_Run_ID
AND A.CAL_ID         = $Cal_ID
AND A.GP_PAYGROUP    = B.GP_PAYGROUP
AND B.RSLT_SEG_NUM = (SELECT MAX (SEG2.RSLT_SEG_NUM)
                                 FROM PS_GP_PYE_SEG_STAT SEG2
                                  WHERE B.EMPLID       = SEG2.EMPLID
                                    AND B.CAL_RUN_ID   = SEG2.CAL_RUN_ID
                                    AND B.EMPL_RCD     = SEG2.EMPL_RCD
                                    AND B.GP_PAYGROUP  = SEG2.GP_PAYGROUP
                                    AND B.CAL_ID       = SEG2.CAL_ID)
AND B.RSLT_REV_NUM = (SELECT MAX (SEG1.RSLT_REV_NUM)
                                 FROM PS_GP_PYE_SEG_STAT SEG1
                                  WHERE B.EMPLID       = SEG1.EMPLID
                                    AND B.CAL_RUN_ID   = SEG1.CAL_RUN_ID
                                    AND B.EMPL_RCD     = SEG1.EMPL_RCD
                                    AND B.GP_PAYGROUP  = SEG1.GP_PAYGROUP
                                    AND B.CAL_ID       = SEG1.CAL_ID
                                    AND B.RSLT_SEG_NUM = SEG1.RSLT_SEG_NUM)
AND C.EFFDT = (SELECT MAX(C1.EFFDT) FROM PS_GPMY_PSLP_ACUM C1
                              WHERE C1.GPMY_PAYSLIP_ID = C.GPMY_PAYSLIP_ID
                                AND C1.EFFDT <=$Pymt_Dt)
ORDER BY C.SEQ_NUM

End-Select


End-Procedure

!**************************************************************************
! Procedure: Delete-RunControl
! Description: Delete run control when report comes to successful end.
!**************************************************************************
Begin-Procedure Delete-RunControl
#ifdef debugy

#end-if
begin-SQL
DELETE FROM PS_GPMY_PSLP_RC
    WHERE OPRID         = $PRCS_OPRID
        AND RUN_CNTL_ID = $PRCS_RUN_CNTL_ID;

        #ifdef ORACLE
           COMMIT;
        #endif
end-SQL

begin-SQL
DELETE FROM PS_GPMY_PSLP_DP_RC
    WHERE OPRID         = $PRCS_OPRID
        AND RUN_CNTL_ID = $PRCS_RUN_CNTL_ID;

        #ifdef ORACLE
           COMMIT;
        #endif
end-SQL

begin-SQL
DELETE FROM PS_GPMY_PSLP_LC_RC
    WHERE OPRID         = $PRCS_OPRID
        AND RUN_CNTL_ID = $PRCS_RUN_CNTL_ID;

        #ifdef ORACLE
           COMMIT;
        #endif
end-SQL

begin-SQL
DELETE FROM PS_GPMY_PSLP_PE_RC
    WHERE OPRID         = $PRCS_OPRID
        AND RUN_CNTL_ID = $PRCS_RUN_CNTL_ID;

        #ifdef ORACLE
           COMMIT;
        #endif
end-SQL
begin-SQL
DELETE FROM PS_GPMY_PSLP_PG_RC
    WHERE OPRID         = $PRCS_OPRID
        AND RUN_CNTL_ID = $PRCS_RUN_CNTL_ID;

        #ifdef ORACLE
           COMMIT;
        #endif
end-SQL
begin-SQL
DELETE FROM PS_GPMY_PYE_RC
    WHERE OPRID         = $PRCS_OPRID
        AND RUN_CNTL_ID = $PRCS_RUN_CNTL_ID;

        #ifdef ORACLE
           COMMIT;
        #endif
end-SQL
End-Procedure
!
!**************************************************************************
! Procedure:    Process-Empl-List
! Description : Process Employees selected on Run Control Record
!**************************************************************************
Begin-Procedure Process-Empl-List
Begin-Select
APY1.EMPLID    &Emplid
APY1.EMPL_RCD  &EmplRcd
   If rtrim(&Emplid, ' ') <> ''
     Let $No_List = 'N'
     Let $WhereClause = ''
     Let $EmplRcd = edit(&EmplRcd,'999')
     Let $WhereClause = ' AND PER.EMPLID = ' || '''' || &Emplid || '''' || ' AND PER.EMPL_RCD = ' || $EmplRcd

     Do Process-Payee

  End-If
  FROM PS_GPMY_PYE_RC APY1
    WHERE APY1.OPRID          = $Prcs_OprID
    AND   APY1.RUN_CNTL_ID    = $Prcs_Run_Cntl_ID
End-Select
End-Procedure
!
!**************************************************************************
! Procedure     : Process-Dept-List
! Description   : Process Departments selected on Run Control Record
!**************************************************************************
Begin-Procedure Process-Dept-List
Begin-Select
!BPY1.SETID   &Setid
BPY1.BUSINESS_UNIT &Bus_Unit
BPY1.DEPTID  &Deptid
   If rtrim(&Deptid, ' ') <> ''
     Let $No_List = 'N'
     Let $WhereClause = ''
     Let $WhereClause = ' AND PER.BUSINESS_UNIT = ' || '''' || &Bus_Unit || '''' || ' AND PER.DEPTID = ' || '''' || &Deptid || ''''
 !    Let $WhereClause = ' AND PER.SETID_DEPT = ' || '''' || &Setid || '''' || ' AND PER.DEPTID = ' || '''' || &Deptid || ''''

     Do Process-Payee
   End-If
    FROM PS_GPMY_PSLP_DP_RC BPY1

    WHERE BPY1.OPRID          = $Prcs_OprID
    AND   BPY1.RUN_CNTL_ID    = $Prcs_Run_Cntl_ID
End-Select
End-Procedure
!
!**************************************************************************
! Procedure     : Process-Locn-List
! Description   : Process Locations selected on Run Control Record
!**************************************************************************
Begin-Procedure Process-Locn-List
Begin-Select
!CPY1.SETID    &SetId1
CPY1.BUSINESS_UNIT &loc_Bus_Unit
CPY1.LOCATION &Location
   If rtrim(&Location, ' ') <> ''
     Let $No_List = 'N'
     Let $WhereClause = ''
     Let $WhereClause = ' AND PER.BUSINESS_UNIT = ' || '''' || &loc_Bus_Unit || '''' || ' AND PER.LOCATION = ' || '''' || &Location || ''''

     Do Process-Payee

   End-If
    FROM PS_GPMY_PSLP_LC_RC CPY1
    WHERE CPY1.OPRID          = $Prcs_OprID
    AND   CPY1.RUN_CNTL_ID    = $Prcs_Run_Cntl_ID
End-Select
End-Procedure
!
!**************************************************************************
! Procedure     : Process-Pay Entity List
! Description   : Process Pay Entity selected on Run Control Record
!**************************************************************************
Begin-Procedure Process-Pay-Entity-List

Begin-Select

DPY1.PAY_ENTITY &PayEntity

   Let $WhereClause = ''

   If rtrim(&PayEntity, ' ') <> ''
     Let $No_List = 'N'
     Let $WhereClause = ' AND PRC.PAY_ENTITY = ' || '''' || &PayEntity || ''''

     Do Process-Payee
   End-If
    FROM PS_GPMY_PSLP_PE_RC DPY1
    WHERE DPY1.OPRID          = $Prcs_OprID
    AND   DPY1.RUN_CNTL_ID    = $Prcs_Run_Cntl_ID
End-Select
End-Procedure
!
!**************************************************************************
! Procedure     : Process-Paygroup List
! Description   : Process Paygroup selected on Run Control Record
!**************************************************************************
Begin-Procedure Process-PayGroup-List
Begin-Select
EPY1.GP_PAYGROUP &PayGrp

   If rtrim(&PayGrp, ' ') <> ''
     Let $No_List = 'N'
     Let $WhereClause = ''
     Let $WhereClause = ' AND PRC.GP_PAYGROUP = ' || '''' || &PayGrp || ''''

     Do Process-Payee
    End-If
    FROM PS_GPMY_PSLP_PG_RC EPY1
    WHERE EPY1.OPRID          = $Prcs_OprID
    AND   EPY1.RUN_CNTL_ID    = $Prcs_Run_Cntl_ID
End-Select
End-Procedure
!
!**************************************************************************
! Procedure:    Get-Country-Name
! Description : Get-Country Name
!**************************************************************************
Begin-Procedure Get-Country-Name
  Let $Country = ''
Begin-Select
CTY.DESCR
  Let $Country = &CTY.DESCR
  FROM PS_COUNTRY_TBL CTY
 WHERE CTY.COUNTRY = $Countrycode
End-Select
End-Procedure
!
!**************************************************************************
! Procedure     : Get-GP-Paygroup
! Description   : Get the GP Paygroup if none of the lists has been chosen.
!**************************************************************************
Begin-Procedure Get-GP-Paygroup

 Create-Array Name=Gp_Paygroups Size=10
 Field=Gp_Paygroup:Char
 Let #I = 1

Begin-Select
DTL.GP_PAYGROUP
DTL.CAL_ID

  Let $GP_Pygrp = ''
  Let $Payroll_CalID = ''
  Let $Payroll_CalID = &DTL.CAL_ID
  Let $GP_Pygrp   = &DTL.GP_PAYGROUP

  Put $GP_Pygrp INTO Gp_Paygroups(#I)
  Let #I = #I + 1

FROM PS_GP_CAL_RUN_DTL DTL
WHERE DTL.CAL_RUN_ID = $Cal_Run_ID
AND DTL.CALC_TYPE = 'P'
End-Select
Begin-SELECT
DTLA.GP_PAYGROUP

  Let $GP_Pygrp   = &DTLA.GP_PAYGROUP

  Put $GP_Pygrp INTO Gp_Paygroups(#I)
  Let #I = #I + 1


FROM PS_GP_CAL_RUN_OFF DTLA
WHERE DTLA.CAL_RUN_ID = $Cal_Run_ID
End-SELECT


End-Procedure
!
!************************************************************************
!************************************************************************
! Routines and code below have been added to enable ePay Implementation *
!************************************************************************
!************************************************************************
!
begin-Procedure Get-NetAmountValue
   Let #NetPayAccumulatorValue = 0
Begin-SELECT !on-error=SQL-Error
SUM(ENP.PIN_NET_VAL) &NetPayVal

    Let #NetPayAccumulatorValue = &NetPayVal

  FROM PS_GP_PYE_SEG_STAT ENP
   WHERE ENP.EMPLID         = $Emplid
     AND ENP.EMPL_RCD       = #Empl_Rcd
     AND ENP.CAL_RUN_ID     = $Cal_Run_ID
     AND ENP.CAL_ID         = $Cal_ID
     AND ENP.GP_PAYGROUP    = $GP_Paygroup
End-SELECT
End-Procedure
!
!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Init                                             *
! Initialize variables that we'll use for ePay processing              *
!***********************************************************************
begin-procedure GP-ePay-Init
   let $sql-statement = 'GPMYPS01.sqr, GP-ePay-Init '

  Do Check-ePay-installed ($ePay_Installed)

    If $ePay_Installed = 'Y'
     Move 'GPMYPS01' to $ReportID

      let #eV4 =  To_number($prcs_process_instance)

      !* have the Output Working Directory from the Process parameters table
      do Get-Output-Wrk-Directory(#eV4, $PRCSOUTPUTDIR, $PRCSNAME)

      !* have the URL id from the Payslip option table
      do Get-ePay-URLid ('MYS', $eV18)

      !* Global ePay variable settings used in GP-ePay procedures in this SQR
      let $eV1 = $prcs_oprid
      let $eV2 = $prcs_run_cntl_id
      let $eV3 = $ReportID          !used for proc name instaed of prcsname from output wrk dir

      ! Open the file for writing epay control data
      ! Let $GP_PSLP_CTLFILE   = $eV3 || '.txt'
      ! Let $FILELAYOUT = 'GP_SS_PSLP_TMP'
      ! Do Open-ePay-Guide-DataFile($PRCSOUTPUTDIR,$GP_PSLP_CTLFILE)

      ! when we do not pass a control file
       Let $GP_PSLP_CTLFILE = ' '
       Let $FILELAYOUT = ' '

    End-If

end-procedure !GP-ePay-Init

!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Guide                                            *
! Insert one row per payslip into the temporary guide table for ePay   *
!***********************************************************************

begin-procedure GP-ePay-Guide

   let $sql-statement = 'GPMYPS01.sqr,GP-ePay-Guide'

 If $ePay_Installed = 'Y'

   ! MYS Payslip does not exclude by run type. We can ignore this procedure

   !do Get-RUN-TYPE  ! not an ePay procedure

   Let $strEmplRcd = to_char(#Empl_Rcd)
   let $strEmplRcd  = rtrim($strEmplRcd,' ')
   let $strEmplRcd  = ltrim($strEmplRcd,' ')

   let $eV5  = rtrim($Emplid, ' ')
   let $eV5  = ltrim($eV5, ' ')
   let $eV6  = rtrim($Cal_Run_ID,' ')
   let $eV6  = ltrim($eV6,' ')
   let $eV7  = 'GPMYS'
   let $eV8  = $strEmplRcd || ' ' || $Pay_Dt_To_Conv  ! gp epay payslip id
   !let $eV8  = rtrim($CAL_run_ID, ' ') || '_' || $eV5 || '_' || $strEmplRcd        ! gp epay payslip id
   let $eV9  = $pymt_dt
   let $eV10 = $Pay_Pd_To_Dt
   let $eV11 = $SegBgnDt
   ! Perform routine to retrieve the NetPay amount based on Net pay pin number
   Do Get-NetAmountValue
   let #eV12 = #NetPayAccumulatorValue ! net pay

   let $eV13 = $PayEntityDescr   ! MYS will populate the Pay Entity Name in the Description column.

   let $eV14 = rtrim($RUN_TYPE, ' ')
   !let $eV14 = ' ' ! we are not populating the run type
   let $eV15 = 'ORIG' ! payslip status ORIGINAL
   let $eV16 = $eV5 || '_' || $eV6 || '_' ||$eV7 || '_' ||$eV8 || '.pdf'   ! sysfilename of the payslip pdf
   let $eV17 = $eV16                                                           !userfilename  - what the payee sees filename as
   let #eV19 = #BeginPageNumber                                                !begin page number of payslip in output report
   let #eV20 = #EndPageNumber                                                  !end page number of payslip in output report

   !Input:OPRID, RUN_CNTL_ID, PROCNAME, DATAINST, EMPLID, CAL_RUN_ID, SRCPRODUCT,
   !GP_PSLP_ID,PYMT_DT,PRD_END_DT,PRD_BGN_DT,#NET_PAY,Payslip DESCR,RUN_TYPE,
   !PSLP_STATUS,ATTACHSYSFILENAME, ATTACHUSERFILE,FILEURLID, BGNPGNBR,ENDPGNBR

    do Insert-ePay-Guide-Data($eV1,$eV2,$eV3,#eV4,$eV5,$eV6,$eV7,$eV8,$eV9,$eV10,$eV11,#eV12,$eV13,$eV14,$eV15,$eV16,$eV17,$eV18,#eV19,#eV20)

!   do Write-ePay-Guide-Data($eV1,$eV2,$eV3,#eV4,$eV5,$eV6,$eV7,$eV8,$eV9,$eV10,$eV11,#eV12,$eV13,$eV14,$eV15,$eV16,$eV17,$eV18,#eV19,#eV20)

 End-If

end-procedure ! GP-ePay-Guide

!***********************************************************************
! For ePay Purposes: Online Payslip                                    *
! Procedure : GP-ePay-Control                                          *
! Insert one row per report into the epay run control table            *
!***********************************************************************
begin-procedure GP-ePay-Control

let $sql-statement = 'GPMYPS01.sqr,GP-ePay-Control '

 If $ePay_Installed = 'Y'
   Let $rptid = lower($ReportID)
   let $eCV7  = $rptid || '_' || $prcs_process_instance || '.PDF'
   let $eCV8  = rtrim($PRCSOUTPUTDIR, ' ')

   ! Input:OPRID,RUN_CNTL_ID,PROCNAME,SPLIT_IND,ATCH_IND,CTLFILE,SOURCEFILE,
   ! SOURCELOC,WORKINGLOC,FILELAYOUT,DATAINST,FILEURLID,CLEANUP

do Insert-ePay-RunControl($eV1,$eV2,$eV3,'Y', 'Y', $GP_PSLP_CTLFILE, $eCV7,$eCV8,' ',$FILELAYOUT,#eV4,$eV18,'Y')

 End-If

end-procedure !GP-ePay-Control
!******************************************************************
!***    fetching element group
!******************************************************************
begin-procedure Get-Element-Group

begin-SELECT DISTINCT
A.PIN_NUM   &PIN_NM1

    LET #PARENT_PIN = &PIN_NM1  

FROM PS_GP_ELEM_GRP_MBR A
WHERE  A.EFFDT =
        (SELECT MAX(A_ED.EFFDT) FROM PS_GP_ELEM_GRP_MBR A_ED
        WHERE A.PIN_NUM = A_ED.PIN_NUM
          AND A_ED.EFFDT <= $AsOfToday)
and A.PIN_ELEM_NUM = #ED_PinNum
end-SELECT


begin-SELECT DISTINCT
A.PIN_NUM   &PIN_NM_Y

    LET #PARENT_PIN_Y = &PIN_NM_Y
    

FROM PS_GP_ELEM_GRP_MBR A
WHERE  A.EFFDT =
        (SELECT MAX(A_ED.EFFDT) FROM PS_GP_ELEM_GRP_MBR A_ED
        WHERE A.PIN_NUM = A_ED.PIN_NUM
          AND A_ED.EFFDT <= $AsOfToday)
and A.PIN_ELEM_NUM = #Acum_PinNum
end-SELECT

end-procedure
!******************************************************************
!***    Get section number
!******************************************************************
begin-procedure Get-Section_Number

begin-SELECT DISTINCT
A.PIN_TYPE  &PIN_TYP

    IF &PIN_TYP = 'ER'
        LET $GP_section_nm = '10'
    ELSE
    IF &PIN_TYP = 'DD'
        LET $GP_section_nm = '20'
    END-IF
        END-IF

FROM PS_GP_PIN A
WHERE   A.PIN_NUM = #ED_PinNum
end-SELECT

begin-SELECT DISTINCT
A.PIN_TYPE  &PIN_TYP_Y

    IF &PIN_TYP_Y = 'AC'
        LET $GP_section_nm_Y = '40'
    END-IF
        

FROM PS_GP_PIN A
WHERE   A.PIN_NUM = #PIN_NUM_YTD
end-SELECT

end-procedure Get-Section_Number
!
!***************************************************************************
!For Mobile Payslip Purposes: Mobile Payslip                               *
!Procedure : check-mob-custom-enabled                                      *
!Check if Mobile Payslip is enabled for Malaysia                              *
!***************************************************************************
begin-procedure check-mob-custom-enabled

begin-select
GP_SS_MPSLP_DATA    &MbPslpOpt

FROM PS_GP_SS_PSLP_OPT
WHERE COUNTRY = 'MYS'
AND GP_PSLP_HDCPYOPTN = 'Y'
AND GP_SS_MPSLP_ENABLE = 'Y'
end-select

   if &MbPslpOpt = 'CUST'
     let $custEnabled = 'Y'
     #debug6 show '** GPMYPS01 : Custom mode enabled for Mobile Payslip'
   else
     let $custEnabled = 'N'
   end-if

end-procedure !check-mob-custom-enabled
!Mobile Payslip - End
!***************************************************************************
!For Mobile Payslip Purposes: Mobile Payslip                               *
!Procedure : Check_CurrentAccum                                             *
!Check if returned Accumulator is Current Accumulator                      *
!***************************************************************************
begin-procedure Check_CurrentAccum
begin-select
PIN_NM  &AcmPinNm
PIN_CODE &AcmPinCode

FROM PS_GP_PIN
WHERE COUNTRY = 'MYS'
AND PIN_TYPE = 'AC'
AND PIN_NUM = #CurrentAccum
end-select

   if ( &AcmPinNm = 'GROSS SALARY' and &AcmPinCode ='GROSS SALARY MYS' )
     let $CurrentAccumEnabled = 'Y'
   else
     let $CurrentAccumEnabled = 'N'
   end-if

end-procedure !Check_CurrentAccum
!Mobile Payslip - End
!***************************************************************************
!For Off cycle Payslip Purpose
!Procedure : Get_OffCycle_Ind                                              *
!Check if current calendar is Off Cycle Calendar                           *
!***************************************************************************
begin-procedure Get_OffCycle_Ind
Let $OffCycle_Enabled =''
begin-select
GPRUN.OFF_CYCLE  &OffCyc

FROM PS_GP_CAL_RUN GPRUN
WHERE GPRUN.OFF_CYCLE = 'Y'
AND GPRUN.CAL_RUN_ID = $CAL_RUN_ID
end-select

Let $OffCycle_Enabled = &OffCyc

end-procedure !Get_OffCycle_Ind
!***************************************************************************
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date arithmetic
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'number.sqc'    !Routines to format numbers
#Include 'adformat.sqc'  !Routines to format address
#Include 'gpmyrc01.sqc'  !Select GP Malaysia Runcontrol Parameters procedure
#Include 'readxlat.sqc'  !Routines to read Translate table
!ePay Implementation Changes begins here
#Include 'gpsspslp.sqc'     ! ePay SQC with ePay procedures
!ePay Implementation Changes here
#include 'rellang.sqc'
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure