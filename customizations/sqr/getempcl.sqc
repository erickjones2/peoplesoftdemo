!*************************************************
!  GetEmpCl:  Get the Employee Class Name
!*************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!----------------------------------------------------------------------
!                                                                      
!          $Date:  2013/01/17:13:39:25                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

!*************************************
begin-procedure Get-Empl-Class-Name
!*************************************

  !Keys passed in:  $Reg_Region, $Empl_Class, $AsOfDate
  !Values returned: $Empl_Class_Name, $Empl_Class_Abbrev

  move '' to $Empl_Class_Name
  move '' to $Empl_Class_Abbrev

  if Rtrim($Reg_Region, ' ') = ''
    move 'STD' to $Empl_Class_Setid
  else
    do Get-Empl-Class-Setid
  end-if

  if Rtrim($Empl_Class,' ') = ''
    goto GetEmpClExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
EC.DESCR
EC.DESCRSHORT

 let $Empl_Class_Name   = Rtrim(&EC.DESCR,' ')
 let $Empl_Class_Abbrev = Rtrim(&EC.DESCRSHORT,' ')

FROM PS_EMPL_CLASS_TBL EC
WHERE EC.SETID      = $Empl_Class_Setid
  AND EC.EMPL_CLASS = $Empl_Class
  AND EC.EFFDT     <= $AsOfDate
  AND EC.EFFDT =
        (SELECT MAX(EFFDT)
         FROM   PS_EMPL_CLASS_TBL
         WHERE  SETID       = EC.SETID
           AND  EMPL_CLASS  = EC.EMPL_CLASS
           AND  EFFDT      <= $AsOfDate)
end-SELECT

  do Get-Empl-Class-Name-Lang

GetEmpClExit:

end-procedure


!***********************************************************************
begin-procedure Get-Empl-Class-Setid
!***********************************************************************

  !Keys passed in:  $Reg_Region
  !Values returned: $Empl_Class_Setid

BEGIN-SELECT
SCG.SETID

  let $Empl_Class_Setid = Rtrim(&SCG.SETID,' ')

FROM PS_SET_CNTRL_GROUP SCG, PS_REC_GROUP_REC RGR
WHERE RGR.RECNAME = 'EMPL_CLASS_TBL'
  AND SCG.REC_GROUP_ID = RGR.REC_GROUP_ID
  AND SCG.SETCNTRLVALUE = $Reg_Region
END-Select

end-procedure


!******************************************
begin-procedure Get-Empl-Class-Name-Lang
!******************************************

  !Keys passed in:  $Empl_Class_Setid, $Empl_Class, $AsOfDate
  !Values returned: $Empl_Class_Name, $Empl_Class_Abbrev

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetEmpClLngExit
  end-if

  if Rtrim($Empl_Class_Setid, ' ') = ''
    move 'STD' to $Empl_Class_Setid
  end-if

  if Rtrim($Empl_Class,' ') = ''
    goto GetEmpClLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
ECL.DESCR
ECL.DESCRSHORT

  if Rtrim(&ECL.DESCR,' ') <> ''
    let $Empl_Class_Name = Rtrim(&ECL.DESCR,' ')
  end-if
  if Rtrim(&ECL.DESCRSHORT,' ') <> ''
    let $Empl_Class_Abbrev = Rtrim(&ECL.DESCRSHORT,' ')
  end-if

FROM PS_EMPL_CL_TBL_LNG ECL
WHERE ECL.SETID        = $Empl_Class_Setid
  AND ECL.EMPL_CLASS   = $Empl_Class
  AND ECL.LANGUAGE_CD  = $Curr_Language_Cd
  AND ECL.EFFDT       <= $AsOfDate
  AND ECL.EFFDT =
         (SELECT MAX(EFFDT)
          FROM  PS_EMPL_CL_TBL_LNG
          WHERE SETID        = ECL.SETID
            AND EMPL_CLASS   = ECL.EMPL_CLASS
            AND LANGUAGE_CD  = ECL.LANGUAGE_CD
            AND EFFDT       <= $AsOfDate)
end-SELECT

GetEmpClLngExit:

end-procedure

!***************************** End of Include File **************************
