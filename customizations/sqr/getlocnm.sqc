!*********************************************************
!  GetLocNm:  Get Location Name from the Location Table  *
!*********************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:31:42                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************

begin-procedure Get-Location-Name
  LET $Location = RTRIM($Location, ' ')
  if rtrim($Location,' ') = ''
    move '' to $LocationName
    move '' to $LocationAbbrv
    goto GetLocNmExit
  end-if
  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today
    move $AsOfToday to $AsOfDate
  end-if
begin-SELECT
ZZ.DESCR,
ZZ.DESCRSHORT,
ZZ.BUILDING,
ZZ.ADDRESS1,
ZZ.ADDRESS2,
ZZ.ADDRESS3,
ZZ.ADDRESS4,
ZZ.CITY,
ZZ.COUNTY,
ZZ.STATE,
ZZ.POSTAL,
ZZ.COUNTRY,
ZZ.ESTABID,
ZZ.LOCALITY,
ZZ.MESSAGE_TEXT2,
ZZ.SETID
FROM  PS_LOCATION_TBL ZZ
WHERE ZZ.LOCATION = $Location AND ZZ.SETID = $Setid
  AND ZZ.EFFDT =
      (SELECT MAX(EFFDT)
       FROM   PS_LOCATION_TBL
       WHERE  LOCATION = ZZ.LOCATION
       AND    SETID = ZZ.SETID
       AND  EFFDT   <= $AsOfDate)
end-SELECT
  let $LocationName     = rtrim(&ZZ.Descr,' ')
  let $LocationAbbrv    = rtrim(&ZZ.DescrShort,' ')
  let $LocationStreet1  = rtrim(&ZZ.Address1,' ')
  let $LocationStreet2  = rtrim(&ZZ.Address2,' ')
  let $LocationStreet3  = rtrim(&ZZ.Address3,' ')
  let $LocationStreet4  = rtrim(&ZZ.Address4,' ')
  let $LocationCity     = rtrim(&ZZ.City,' ')
  let $LocationCounty   = rtrim(&ZZ.County,' ')
  let $LocationLocality = rtrim(&ZZ.Locality,' ')
  let $LocationChkCashLoc = rtrim(&ZZ.Message_Text2,' ')
  LET $LocationState = RTRIM(&ZZ.State, ' ')
  move &ZZ.Postal                  to $LocationZip

  move $LocationZip                to $ZipExtsn ~~~~~~xxxx

  if instr($LocationZip,'-',6) = 0 and $ZipExtsn <> '    '
    let $LocationZip = substr($LocationZip,1,5) || '-' || substr($LocationZip,6,4)
  end-if

  move $LocationZip                to $ZipExtsn ~~~~~xxxxx

  move &ZZ.Country                 to $LocationCountry

  let $SetIDLang = Rtrim(&ZZ.SETID,' ')
  do Get-Location-Name-Lang

GetLocNmExit:
end-procedure

!*************************************
begin-procedure Get-Location-Name-Lang
!*************************************

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetLocNmLngExit
  end-if

  if Rtrim($location,' ') = '' or Rtrim ($SetIDLang,' ') = ''
    goto GetLocNmLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
ZZL.DESCR,
ZZL.DESCRSHORT

  if rtrim(&ZZL.Descr,' ') <> ''
    let $LocationName     = rtrim(&ZZL.Descr,' ')
  end-if

  if rtrim(&ZZL.DescrShort,' ') <> ''
    let $LocationAbbrv    = rtrim(&ZZL.DescrShort,' ')
  end-if

FROM  PS_LOCATION_LANG ZZL
WHERE ZZL.LOCATION      = $Location
  AND ZZL.SETID         = $SetIDLang
  AND ZZL.LANGUAGE_CD   = $Curr_Language_Cd
  AND ZZL.EFFDT        <= $AsOfDate
  AND ZZL.EFFDT         =
      (SELECT MAX(EFFDT)
       FROM   PS_LOCATION_LANG
       WHERE  LOCATION          = $Location
         AND  SETID             = ZZL.SETID
         AND  LANGUAGE_CD       = ZZL.LANGUAGE_CD
         AND  EFFDT            <= $AsOfDate)
end-SELECT

GetLocNmLngExit:

end-procedure

