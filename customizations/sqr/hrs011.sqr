!***********************************************************************
!  HRS011:  Applicant Summary By Source/Sub Source within Requisition  *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:12:49:13                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
Begin-Setup
#Include 'ptpsl177.sqc'  !Printer and page-size initialization
End-Setup

begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

begin-Heading 8
  do Get_Field_information ('HRS011',   'REPORT_TITLE', $REPORTTITLE,   #DW)
  #Include 'stdhdgtr.sqc'
  do Get_Field_information ('HRS011', 'REQ_NBR',$REQ_NBR,               #DW)
  do Get_Field_information ('HRS011', 'SOURCE',$SOURCE,                 #DW)
  do Get_Field_information ('HRS011', 'EMPL_NAME',$EMPL_NAME,           #DW)
  do Get_Field_information ('HRS011', 'SPEC_SOURCE', $SPEC_SOURCE,      #DW)
  do Get_Field_information ('HRS011', 'APPLICANT_TOT', $APPLICANT_TOT,  #DW)
  do Get_Field_information ('HRS011', 'APPL_SPC_SRC', $APPL_SPC_SRC,    #DW)
  do Get_Field_information ('HRS011', 'APPL_PERC', $APPL_PERC,          #DW)

  print $REQ_NBR                (+3, 1,11)
  print $SOURCE                 (  ,13,30)
  print $EMPL_NAME              (  ,44,51)
  print $SPEC_SOURCE            (,  95,30)
  print $APPLICANT_TOT          (,  126,27)
  print $APPL_PERC              (,  154)
  print $APPL_SPC_SRC           (+1,126,27)
  print $APPL_SPC_SRC           (  ,154)

end-Heading

begin-procedure Init-Report

  move 'HRS011' to $ReportID
  move $BaseCurr to $ReportCurr
  move $BaseRtType to $ReportRtType
  move 'W' to $ReportMsgType  !Warning

  do Stdapi-Init

  if $prcs_process_instance = ''
    !No Prompt
  else
    do Select-Parameters
  end-if
  do Init_Printer
  do Init_Report_Translation ($ReportID, $language_cd)
  do Append_Report_Translation ('HR')

end-procedure

begin-procedure Get-Values
let $language_cd = $PRCS_LANGUAGE_CD
end-procedure

!***********************************************************************
!  PROC Process-Main
!         Main Procedure
!***********************************************************************

Begin-Procedure Process-Main
Begin-SELECT
A.HRS_JOB_OPENING_ID
    print &A.HRS_JOB_OPENING_ID            (1,1,11)
D.HRS_SOURCE_ID
    do Get-Src-Descr
    print &S.HRS_SOURCE_NAME               ( ,13,30)
D.HRS_SUBSOURCE_ID
    do Get-Sub-Cat-Descr
    print &SS.HRS_SUBSOURCE_DESC           ( ,95,34)
COUNT(*)        &APPL_SPCSRC

    do Get-APPL-REQSOURCE
    Let #TotalApplSrc = &TotalApplSrc

    if &D.HRS_SOURCE_ID <> 12
        Let #APPL_SPCSRC = &APPL_SPCSRC
        Do Format-Number(#APPL_SPCSRC, $out, '99999999')
        print $out (0,128)

        Do Print-Percentage (&APPL_SPCSRC, #TotalApplSrc)
    else
        Do Get-EmployeeSource-Details
    end-if

  NEXT-LISTING SKIPLINES=1 NEED=8

FROM PS_HRS_RCMNT_I A,
PS_HRS_APP_PROF_I B,
PS_HRS_AL_LOG C,
PS_HRS_AL_LOG_SRCE D
WHERE A.HRS_PERSON_ID = B.HRS_PERSON_ID
AND A.HRS_PROFILE_SEQ = B.HRS_PROFILE_SEQ
AND B.HRS_AL_LOG_ID = C.HRS_AL_LOG_ID
AND B.HRS_AL_LOG_ID = D.HRS_AL_LOG_ID
AND A.HRS_JOB_OPENING_ID > 0
GROUP BY A.HRS_JOB_OPENING_ID, D.HRS_SOURCE_ID, D.HRS_SUBSOURCE_ID
Order BY A.HRS_JOB_OPENING_ID
END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC Get-APPL-REQSOURCE
!***********************************************************************

BEGIN-PROCEDURE Get-APPL-REQSOURCE

BEGIN-SELECT
COUNT(*) &TotalApplSrc

FROM PS_HRS_RCMNT_I A1,
PS_HRS_APP_PROF_I B1,
PS_HRS_AL_LOG C1,
PS_HRS_AL_LOG_SRCE D1
WHERE A1.HRS_JOB_OPENING_ID = &A.HRS_JOB_OPENING_ID
AND A1.HRS_PERSON_ID = B1.HRS_PERSON_ID
AND A1.HRS_PROFILE_SEQ = B1.HRS_PROFILE_SEQ
AND B1.HRS_AL_LOG_ID = C1.HRS_AL_LOG_ID
AND B1.HRS_AL_LOG_ID = D1.HRS_AL_LOG_ID
AND D1.HRS_SOURCE_ID = &D.HRS_SOURCE_ID
END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC Get-Sub-Cat-Descr
!***********************************************************************

BEGIN-PROCEDURE Get-Sub-Cat-Descr

BEGIN-SELECT
SS.HRS_SUBSOURCE_DESC
FROM PS_HRS_SUBSOURCE SS
WHERE SS.HRS_SOURCE_ID = &D.HRS_SOURCE_ID
AND   SS.HRS_SUBSOURCE_ID = &D.HRS_SUBSOURCE_ID
AND  SS.EFFDT = (SELECT MAX(EFFDT)
               FROM PS_HRS_SUBSOURCE SS1
               WHERE SS1.HRS_SOURCE_ID = SS.HRS_SOURCE_ID
               AND SS1.HRS_SUBSOURCE_ID = SS.HRS_SUBSOURCE_ID
               AND SS1.EFFDT <= $AsOfToday)
END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC Get-Src-Descr
!***********************************************************************
BEGIN-PROCEDURE Get-Src-Descr
BEGIN-SELECT
S.HRS_SOURCE_NAME
FROM PS_HRS_SOURCE S
WHERE S.HRS_SOURCE_ID = &D.HRS_SOURCE_ID
AND S.EFFDT = (SELECT MAX(EFFDT)
               FROM PS_HRS_SOURCE S1
               WHERE S1.HRS_SOURCE_ID = S.HRS_SOURCE_ID
               AND S1.EFFDT <= $AsOfToday)
END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC Get-EmployeeSource-Details
!***********************************************************************
BEGIN-PROCEDURE Get-EmployeeSource-Details
BEGIN-SELECT
E1.EMPLID
    Let $EMPLID = RTRIM(&E1.EMPLID, ' ')
    DO Get-Employee-Name
    PRINT $Name                            ( ,44,50) on-break skiplines=1
COUNT(*)    &APPL_EMPSRC
    LET #APPL_EMPSRC = &APPL_EMPSRC
    Do Format-Number(#APPL_EMPSRC, $out, '99999999')
    print $out (0,128)

    Do Print-Percentage (#APPL_EMPSRC, #TotalApplSrc)

FROM PS_HRS_RCMNT_I R,
PS_HRS_APP_PROF_I P,
PS_HRS_AL_LOG AL,
PS_HRS_AL_LOG_SRCE ALS,
PS_HRS_EE_REFERRAL E1
WHERE R.HRS_PERSON_ID = P.HRS_PERSON_ID
AND R.HRS_PROFILE_SEQ = P.HRS_PROFILE_SEQ
AND R.HRS_JOB_OPENING_ID = &A.HRS_JOB_OPENING_ID
AND P.HRS_AL_LOG_ID = AL.HRS_AL_LOG_ID
AND P.HRS_AL_LOG_ID = ALS.HRS_AL_LOG_ID
and ALS.HRS_SOURCE_ID = &D.HRS_SOURCE_ID
AND E1.HRS_PERSON_ID = R.HRS_PERSON_ID
AND E1.HRS_PROFILE_SEQ = R.HRS_PROFILE_SEQ
AND E1.HRS_AL_LOG_SRC_SEQ = ALS.HRS_AL_LOG_SRC_SEQ
GROUP BY E1.EMPLID
END-SELECT
END-PROCEDURE

!***********************************************************************
!  PROC Print-Percentage
!***********************************************************************
BEGIN-PROCEDURE Print-Percentage (#Sbsrce_Count, #TotalApplSrc)
    if #TotalApplSrc <> 0
            LET #PercApplSrc = (#Sbsrce_Count / #TotalApplSrc)
            LET #PercApplSrc =  #PercApplSrc * 100
            Do Format-Number(#PercApplSrc, $out, '999.99')
            print $out (0,160)
            LET $Percent =  '%'
            print $Percent                           (0,166)
    end-if
END-PROCEDURE


#include 'hrrnctl1.sqc'  !Get run control parameter values
#include 'hrgetval.sqc'  !Get values mask routines

#Include 'getempnm.sqc'  !Get-Employee-Name and Get-Applicant-Name
#Include 'readxlat.sqc'  !Read-Translate-Table procedure
#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'  !Routines to Translate SQR
#Include 'useprntr.sqc'
