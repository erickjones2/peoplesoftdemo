!***********************************************************************
!  TAX108 : W4 - EMPLOYEE WITHHOLDING ALLOWANCE PRINT REPORT.          *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  30710097                                            !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'    !Set Database Environment
#include 'setup31.sqc'   !Printer and page-size initialization

begin-report
  do Stdapi-Init
  move 'TAX108' to $ReportID
  move 'EMPLOYEE W4 PRINT REPORT' to $ReportTitle
  display $ReportTitle
  display ''

  do Get-Run-ID

  do Get-From-Thru-Date

  move ' ' to $SelectedDates
  if $FromDate <> ' '
     move 'AND W4.LAST_UPDATE_DATE BETWEEN ' to $SelectedDates
     let $SelectedDates = $SelectedDates || $FromDate || ' AND ' || $ThruDate
  end-if
  do Init-DateTime
  do Get-Current-DateTime
  do Format-DateTime($AsOfToday, $out, {DEFMDY},'','')
  move $out to $ReportDate
  display 'Report Began: ' noline
  display $AsOfNow
  display ''

  do Format-DateTime($Fromdate, $FmDate, {DEFMDY},'','')
  do Format-DateTime($ThruDate, $TuDate, {DEFMDY},'','')

  print 'W4(s) updated via the web from ' (+1,1)
  print $FmDate ()
  print '  to  '  ()
  print $TuDate ()
  do Check-Processing
  do Get-W4Print

  do Get-Current-DateTime
  display ''
  display 'Report Ended: ' noline
  display $AsOfNow
  do Stdapi-Term
end-report

begin-procedure Get-Run-ID
  move ' ' to $FromDate
  move ' ' to $ThruDate
  if $Prcs_Process_Instance = ''
     do Prompt
  else

    do Select-Parameters
  end-if
end-procedure


Begin-Procedure Prompt

while 1 = 1
    input $RunID maxlen=3 'Enter Valid Run ID' type=char

    if RTRIM($RunID, ' ') = ''
      display 'Exiting....'
      Stop
    end-if

    do edit-Run

    if $Break_YN = 'Y'
      Break
    End-If
End-While

End-Procedure


begin-procedure Edit-Run

  move 0 to #Found

begin-select

Z.FROMDATE
Z.THRUDATE


  add 1 to #Found

FROM PS_RC_PAY Z
where Z.RUN_CNTL_ID = $RunID
end-select
  if #Found = 0
    display 'Error --  Not a Valid Run ID.'
    move 'N' to $Break_YN
  else

    if #Found = 1
    move 'Y' to $Break_YN

         Let $RC_PAY.FROMDATE = &Z.FROMDATE
         Let $RC_PAY.THRUDATE = &Z.THRUDATE

    end-if

  end-if
end-procedure


begin-procedure Get-W4Print
  display 'Generating report . . . '
  
begin-SELECT
W4.EMPLID
W4.FWT_MAR_STATUS
W4.FWT_ADDL_AMT
W4.SPECIAL_FWT_STATUS
W4.COMPANY
W4.EFFDT
W4.FWT_MULTI_JOBS
W4.FWT_TTL_DEP_AMT
W4.FWT_OTH_INCOME
W4.FWT_DED_ADJ
PERS.LAST_NAME
PERS.FIRST_NAME
PERS.MIDDLE_NAME
ADDR.ADDRESS1
ADDR.ADDRESS2
ADDR.ADDRESS3
ADDR.ADDRESS4
ADDR.CITY
ADDR.STATE
ADDR.POSTAL
NID.NATIONAL_ID

  do Get-Employer-Data
  move &W4.EMPLID              to $Emplid
  move &W4.FWT_ADDL_AMT        to #Fwt_Addl_Amt
  move &W4.EFFDT               to $Effdt
  
  If &W4.FWT_MAR_STATUS = 'S'
     let $FilingStatus = 'Single or Married filing separately'
  End-if

  If &W4.FWT_MAR_STATUS = 'M'
     let $FilingStatus = 'Married filing jointly (or Qualifying widow(er))'
  End-if

  If &W4.FWT_MAR_STATUS = 'H'
     let $FilingStatus = 'Head of Household'
  End-if

  If &W4.FWT_MULTI_JOBS = 'Y'
     let $MultiJobs     = 'X'
  Else
     let $MultiJobs     = ' '
  End-if
  
  let #TotalDepAmt      = &W4.FWT_TTL_DEP_AMT

  let #OtherIncome      = &W4.FWT_OTH_INCOME

  let #Deductions       = &W4.FWT_DED_ADJ

  let #ExtraWthld       = &W4.FWT_ADDL_AMT

  if rtrim(&W4.SPECIAL_FWT_STATUS, ' ') = 'G'
    let $Exempt         = 'X'
  else
    let $Exempt         = ' '
  end-if
  
  let $MiddleInit = substr(&PERS.MIDDLE_NAME, 1,1)
  if $MiddleInit = ' '
    let $Name = rtrim(&PERS.FIRST_NAME,' ') || ' ' || rtrim (&PERS.LAST_NAME, ' ')
  else  
    let $Name = rtrim(&PERS.FIRST_NAME,' ') || ' ' || $MiddleInit || ' ' || rtrim (&PERS.LAST_NAME, ' ')
  end-if    
  let $Address1 = rtrim(&ADDR.ADDRESS1, ' ')
  let $Address2 = rtrim(&ADDR.ADDRESS2, ' ')
  let $Address3 = rtrim(&ADDR.ADDRESS3, ' ')
  let $Address4 = rtrim(&ADDR.ADDRESS4, ' ')
  let $CityStateZip = Rtrim(&ADDR.CITY, ' ') ||', '|| Rtrim(&ADDR.STATE, ' ') || '  ' || Rtrim(&ADDR.POSTAL, ' ')
  let $SSN = &NID.NATIONAL_ID
  let $AsOfDate = &W4.EFFDT
  do Get-Date-Codes

  do Print-W4Print

FROM PS_FED_TAX_DATA  W4, PS_PERSON_NAME PERS, PS_PERS_NID NID, PS_PERSON_ADDRESS ADDR
WHERE W4.EMPLID = PERS.EMPLID
  [$SelectedEEs]
  AND W4.LAST_UPDATE_DATE BETWEEN $FromDate AND $ThruDate
  AND W4.EMPLID = ADDR.EMPLID
  AND ADDR.ADDRESS_TYPE = 'HOME'
  AND ADDR.ASOFDATE = (SELECT MAX(ADDR2.ASOFDATE) 
                              FROM PS_PERSON_ADDRESS ADDR2
                              WHERE ADDR.EMPLID = ADDR2.EMPLID
                                AND ADDR.ADDRESS_TYPE = ADDR2.ADDRESS_TYPE)
  AND W4.LAST_ACTION = 'W'
  AND W4.EMPLID=NID.EMPLID


end-SELECT
  display 'Done'
  do Delete-Sel-Employees-Table
end-procedure

Begin-procedure Get-Employer-Data
Begin-Select
A.W2_REPORTING_CO
A.DESCR
A.FEDERAL_EIN
A.ADDRESS1
A.ADDRESS2
A.CITY
A.STATE
A.POSTAL

  let $Company = &A.W2_Reporting_Co
  let $CompanyName = &A.Descr
  let $FederalEIN = &A.Federal_EIN
  let $ERAddress1 = rtrim(&A.Address1, ' ')
  let $ERAddress2 = rtrim(&A.Address2, ' ')
  let $ERCity = &A.City
  let $ERState = &A.State
  let $ERPostal = &A.Postal

  let $ERCityStateZip = Rtrim($ERCity, ' ') ||', '|| Rtrim($ERState, ' ') || '  ' || Rtrim($ERPostal, ' ')
  
FROM  PS_W2_COMPANY A
WHERE A.W2_REPORTING_CO = A.COMPANY
  AND A.COMPANY = &W4.COMPANY
  AND A.W2_REPORTING_CO <> ' '
  AND A.TAXFORM_ID      =  'W'
  AND A.CALENDAR_YEAR   = (SELECT MAX(CALENDAR_YEAR)
                              FROM PS_W2_COMPANY A1
                              WHERE A.COMPANY=A1.COMPANY
                                AND A.W2_REPORTING_CO = A1.W2_REPORTING_CO)
end-select

End-procedure

Begin-Procedure Print-W4Print

  new-page

  print 'EMPLID '                                     (+1,10)
  print $Emplid                                       (0,20)
  print 'FORM W-4'                                    (+5,10)
  print 'Employee''s Withholding Certificate '        (0,35)
  print 'Year '                                       (0,85)
  print $AsOfYYYY                                     (0,92)
  print 'Step 1: Personal Information '               (+3,10) 
  print 'Name '                                       (+2,10)
  print $Name                                         (+1,10)
  print 'Address '                                    (+2,10)
  print $Address1                                     (+1,10)
  if $Address2 > ''
    print $Address2                                   (+1,10)
  end-if
  if $Address3 > ''
    print $Address3                                   (+1,10)
  end-if
  if $Address4 > ''
    print $Address4                                   (+1,10)
  end-if
  print 'City, State ZIP Code '                       (+2,10)
  print $CityStateZip                                 (+1,10)
  print 'Filing Status '                              (+2,10)
  print 'Social Security Number '                     (0,85)
  print $FilingStatus                                 (+1,10) 
  print $SSN                                          (0,85)   edit xxx-xx-xxxx
  
  print 'Step 2: Multiple Jobs or Spouse Works '      (+3,10)     
  graphic                                             (0,85,2) box 1
  print $MultiJobs                                    (0,86)

  print 'Step 3: Claim Dependents '                   (+3,10)
  print 'Total Dependent Amount '                     (+1,62)  
  print #TotalDepAmt                                  (0,85)   edit 99999999.99 

  print 'Step 4: Other Adjustments '                  (+3,10)
  print '(a) Other Income '                           (+1,68)
  print #OtherIncome                                  (0,85)   edit 99999999.99   
  print '(b) Deductions '                             (+1,70)
  print #Deductions                                   (0,85)   edit 99999999.99   
  print '(c) Extra Withholding '                      (+1,63)
  print #ExtraWthld                                   (0,85)   edit 99999999.99     
  
  print 'Claim Exemption from Withholding '           (+3,10)
  print 'I claim exemption from withholding for the year ' (+1,10)
  print $AsOfYYYY                                     (0,58)
  print 'and I certify that I meet BOTH of the following' (0,63)
  print 'conditions for exemption from withholding: ' (+1,10)
  print '*'                                           (+2,19) 
  print 'Last year I owed no federal income tax. '    (0,22)
  print '*'                                           (+1,19)  
  print 'This year I expect to owe no federal income tax. ' (0,22)
  graphic                                             (+2,19,2) box 1
  print $Exempt                                       (0,20)
  print 'Check this box if you meet both conditions to claim exemption ' (0,22)
  print 'from tax withholding '                       (+1,22)
  print 'Under penalties of perjury, I declare that this certificate to the best ' (+3,10)
  print 'of my knowledge and belief, is '             (0,82)
  print 'true, correct and complete. '                (+1,10)
  print 'Employee Signature - Updated via employee self-service '  (+3,10)
  print 'Employers name and address '                 (+3,10)
  print 'Employer Identification Number (EIN) '       (0,85)
  print $CompanyName                                  (+1,10)
  print $FederalEIN                                   (0,85) edit 999999999
  print $ERAddress1                                   (+1,10)
  if $ERAddress2 > ''
     print $ERAddress2                                (+1,10)
  end-if
  print $ERCityStateZip                               (+1,10)

End-Procedure

Begin-Procedure Check-Processing

begin-SELECT

RC_W4A.REG_SELECT_PROC

FROM  PS_RC_TAX108A RC_W4A
WHERE RC_W4A.OPRID               = $Prcs_OprID
AND   RC_W4A.RUN_CNTL_ID         = $Prcs_Run_Cntl_ID

end-SELECT

  let $Reg_Select_Proc     = RTRIM(&RC_W4A.Reg_Select_Proc, ' ')

  if $Reg_Select_Proc = 'S'                                     !Load W-4 data for selected employees
    do Get-Selected-EEs
  end-if


End-Procedure

Begin-Procedure Get-Selected-EEs

move 'AND W4.EMPLID in (''' to $SelectedEEs
move ' ' to $SelectedEmplID

begin-SELECT
X.SELECTED_EE

  move &X.Selected_EE     to $SelectedEmplID

  let $SelectedEEs = $SelectedEEs || $SelectedEmplID || ''','''
  move 'Y' to $EE_Selected

FROM  PS_RC_TAX108B X
WHERE X.OPRID         = $Prcs_OprID
  AND X.RUN_CNTL_ID   = $Prcs_Run_Cntl_ID
end-SELECT

let $SelectedEEs = SUBSTR($SelectedEEs,1,LENGTH($SelectedEEs) - 2)
let $SelectedEEs = $SelectedEEs || ')'

End-Procedure

Begin-Procedure Delete-Sel-Employees-Table
begin-SQL on-error=Ignore
  DELETE FROM PS_RC_TAX108B
  WHERE OPRID       = $Prcs_OprID
    AND RUN_CNTL_ID = $Prcs_Run_Cntl_ID

end-SQL
End-Procedure

begin-procedure Ignore
! dummy procedure to avoid SQL error message on delete of empty table
end-procedure
#include 'getdatcd.sqc'  !Get Year from As of date
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Update Process API
#include 'payrctl2.sqc'  !Select Run Control Parameters
#include 'pygetval.sqc'  !To Get as Of date
#include 'readxlat.sqc'  !Read-Translate-Table procedure
#include 'number.sqc'    !Format numbers
