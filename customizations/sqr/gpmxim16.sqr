!***********************************************************************
!  GPMXIM16: Factor de Riesgo de Trabajo                               *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2020, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  31781263                                            !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   ! set enviroment
#include 'setup02.sqc'  ! landscape orientation and printer initialize

#define c98 158
#define c99 167


!***********************************************************************
! Setup Section
!***********************************************************************
begin-setup
 PAGE-SIZE 60 180
end-setup


!***********************************************************************
! Main of Program
begin-procedure Init-Report
  date-time () hh:mi:ss &timeBegan
  display 'Report Began: ' noline
  display &timeBegan
  move 'GPMXIM16'to $ReportID
  move 'Relacion de Casos de Riesgos de Trabajos Terminados' to $ReportTitle
  display $ReportTitle
  show 'Ver.2020 PUM37.01'
end-procedure Init-Report
begin-report
  move '1' to $ReportDateType
  move '1' to $ReportYear4
  do Init-DateTime
  do Init-Number
  do Stdapi-Init
  do Get-Current-DateTime
  do Init-Report

  do Select-Parameters
  do GET_PIN_NUM_ENF
  do GET_PIN_NUM_RIESGO
  do Get-Report-Data
  close 1

  date-time () hh:mi:ss &timeEnded
  display 'Report Ended: ' noline
  display &timeEnded
  do Stdapi-Term
end-report


!***********************************************************************
begin-heading 08
  print     'PeopleSoft'        (1,1)  center
  uppercase $ReportTitle
  print     $ReportTitle        (2,1)  center
  print     'Reporte:'          (3,1)
  print     $ReportID           (,20)
  print     'Página:'           (,{c98})
  page-number                   (,{c99})
 
  if $RC_Company = 'Y'
     print 'Compañía:'          (4,1)
     print $Company             (,20)
     print $CompanyName         (,+1)
  end-if
  if $RC_Estabid = 'Y'
     print 'Establecimiento:'   (5,1)
     print $Estabid_RC          (,20) edit XXXXXXXXXX-X
  end-if
  print 'Fecha:'                (4,{c98})
 ! print $ReportDate             ( ,{c99})
  do Format-DateTime($AsOfToday, $out, {DEFDMY},'','')  
  print $out                           ( ,{c99})
  
  print 'Hora:'                 (5,{c98})
 print $ReportTime             ( ,{c99})
  
  
  print '='                       (+1,1,176) fill
  print 'Registro'                (+1,1)
  print 'Empleado'                (,11)
  print 'Nombre del Empleado'     (,24)
  print 'Cía.'                    (,56)
  print 'Gpo. Pago'               (,62)
  print 'Afiliac. IMSS'           (,74)
  print 'Reg. Patronal'           (,90)
  print 'F. Inicio'               (,105)
  print 'Fecha Fin'               (,117)
  print 'Días Subs'               (,129)
  print '%IPP'                    (,140)
  print 'Tipo Ausent.'            (,148)
  print 'Muerte'                  (,164)
  print '='                       (+1,1,176) fill
end-heading


!***********************************************************************
Begin-procedure Select-Parameters
Begin-Select
A.YEARCD
  let #Year=&A.YEARCD
  let $Year=to_char(#Year)
A.COMPANY
  let $Company = rtrim(&A.COMPANY,' ')
A.ESTABID
  let $EstabID = rtrim(&A.ESTABID,' ')
  let $EstabID_RC = rtrim(&A.ESTABID,' ')
A.GPMX_ESTABID_OPTN
  Let $Estab_Opt = &A.GPMX_ESTABID_OPTN
FROM PS_GPMX_RC_ABSENCE A
WHERE A.OPRID = $Prcs_OprID
AND A.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
End-Select
  let $BegDtDTU = $Year || '-01-01'
  do Convert-From-DTU-Date($BegDtDTU, $BegDt)
  let $EndDtDTU = $Year || '-12-31'
  do Convert-From-DTU-Date($EndDtDTU, $EndDt)
  let $BGNDATE=$BegDt
  let $ENDDATE=$EndDt

  if $Company <> ''
     LET $Where_Company = 'AND C.COMPANY = ' || '''' || $COMPANY || ''''
     let $RC_Company = 'Y'
     do Get-Company-Data-Mex
     let $Company_Old = $Company
   else
     LET $Where_Company = ''
  end-if

  if $EstabID <> ''
     If $Estab_Opt = 'J'
       LET $Where_Estabid = 'AND C.ESTABID = ' || '''' || $EstabID || ''''
     Else
       LET $Where_Estabid = 'AND C.LOCATION IN (SELECT LOC.LOCATION FROM PS_LOCATION_TBL LOC
           WHERE LOC.ESTABID = ''' || $EstabID || '''
           AND LOC.EFFDT = (SELECT MAX(EFFDT) FROM PS_LOCATION_TBL
                            WHERE LOCATION = LOC.LOCATION
                            AND EFFDT <= ''' || $EndDt || '''))'
     End-If                            
     let $RC_Estabid = 'Y'
    else
     LET $Where_Estabid = ''
  end-if

  let $FileName = '{FILEPREFIX}' || 'RIESGOTR.TXT' || '{FILESUFFIX}'
  show 'Filename: ' $FileName

  open $FileName as 1 for-writing record=300:vary status=#filestat

  if #filestat < 0
     show 'Error opening output file. Program ending'
     stop quiet
  end-if
End-procedure Select-Parameters


!***********************************************************************
Begin-procedure GET_PIN_NUM_ENF
Begin-Select
B.PIN_NUM
  let #Pin_Num_Enf = &B.PIN_NUM

FROM PS_GP_PIN B

WHERE B.COUNTRY = 'MEX'
AND   B.PIN_TYPE = 'AT'
AND   B.PIN_CODE LIKE 'INC ENF TR%'
End-Select
End-procedure GET_PIN_NUM_ENF


Begin-procedure GET_PIN_NUM_RIESGO
Begin-Select
B2.PIN_NUM
  let #Pin_Num_Riesgo = &B2.PIN_NUM

FROM PS_GP_PIN B2

WHERE B2.COUNTRY = 'MEX'
AND B2.PIN_TYPE = 'AT'
AND B2.PIN_CODE LIKE 'INC RIESGO%'
End-Select
End-procedure GET_PIN_NUM_RIESGO

!************************************************ GET REPORT DATA *********
Begin-procedure Get-Report-Data
  let #Cons = 0
#Debug show '$Where_Company: ' $Where_Company
#Debug show '$Where_Estabid: ' $Where_Estabid  
Begin-Select
A.EMPLID
  LET $Emplid = &A.EMPLID
A.BGN_DT
A.PIN_TAKE_NUM
  let #Pin_Take_Num = &A.PIN_TAKE_NUM
A.END_DT
C.COMPANY
C.GP_PAYGROUP
C.ESTABID
  let $EstabID = &C.ESTABID
C.LOCATION
  let $Location = rtrim(&C.LOCATION,' ')
  If $Estab_Opt = 'L'
    do Get-Estabid-Location
  End-if  
D.NAME
  let $Emplid = &A.EMPLID
  let $Company = &C.COMPANY
  if $Company <> $Company_Old
     do Get-Company-Data-Mex
     let $Company_Old = $Company
  end-if
  let $GP_Paygroup = &C.GP_PAYGROUP
  let $Bgn_Dt = &A.BGN_DT
  let $End_Dt = &A.END_DT
  let $Name = &D.NAME
  do convert-to-dtu-date ($Bgn_Dt, $Bgn_Dt1)
  do convert-to-dtu-date ($End_Dt, $End_Dt1)
  do dtu-diff-days($Bgn_Dt1, $End_Dt1, #Duration)
  Let #Duration = #Duration + 1
  let $Data = 'Y'
  add 1 to #Cons
  !do Get-Name
  do Get-Imss_Id
  do GetAbsEventData
  do Print-Data

FROM PS_GP_ABS_EVENT A,
!     PS_GPMX_ABS_OPTNS B,
     PS_JOB C,
     PS_NAMES D

WHERE
! A.EMPLID = B.EMPLID  AND
A.EMPLID = C.EMPLID
! AND A.EMPL_RCD = B.EMPL_RCD
  AND A.EMPL_RCD = C.EMPL_RCD
  AND A.EMPLID = D.EMPLID
  AND D.NAME_TYPE = 'PRI'
  AND D.EFFDT = (SELECT MAX(EFFDT) FROM PS_NAMES
                 WHERE EMPLID = D.EMPLID
                 AND NAME_TYPE = D.NAME_TYPE
                 AND EFFDT <= A.END_DT)
  AND C.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOB
                 WHERE EMPLID = C.EMPLID
                 AND EMPL_RCD = C.EMPL_RCD
                 AND EFFDT <= A.END_DT)
  AND C.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB
                  WHERE EMPLID = C.EMPLID
                  AND EMPL_RCD = C.EMPL_RCD
                  AND EFFDT = C.EFFDT)
!  AND A.BGN_DT = B.BGN_DT
!  AND A.PIN_TAKE_NUM = B.PIN_TAKE_NUM
!  AND A.END_DT = B.END_DT
AND A.PIN_TAKE_NUM IN (SELECT GPP.PIN_NUM FROM PS_GP_PIN GPP
                         WHERE GPP.COUNTRY = 'MEX'
                          AND GPP.PIN_TYPE = 'AT'
                          AND GPP.PIN_CODE LIKE 'INC RIESGO%'
                         OR  GPP.PIN_CODE LIKE 'INC ENF TR%')
[$Where_Company]
[$Where_Estabid]
End-Select
End-Procedure Get-Report-Data

begin-procedure GetAbsEventData
begin-SELECT
ABS.PIN_TAKE_NUM
ABS.EVT_CONFIG1
  let $FolioNum = rtrim(&ABS.EVT_CONFIG1,' ')
ABS.EVT_CONFIG2
  let $IllPct = rtrim(&ABS.EVT_CONFIG2,' ')
ABS.EVT_CONFIG3
  let $Death = rtrim(&ABS.EVT_CONFIG3,' ')
  if $Death = 'S'
     Let $DeathInd = 'D'
  else
     Let $DeathInd = ''
  end-if
ABS.EVT_CONFIG4
  let $Illness_Type = rtrim(&ABS.EVT_CONFIG4,' ')

FROM PS_GP_ABS_EVENT ABS
WHERE ABS.EMPLID =$Emplid
AND PIN_TAKE_NUM=#Pin_Take_Num
end-select
end-procedure


begin-procedure Get-Estabid-Location
begin-select
S.ESTABID
  let $EstabID = &S.ESTABID

FROM PS_LOCATION_TBL S
WHERE S.LOCATION = $Location
end-select
end-procedure Get-Estabid-Location

!begin-procedure Get-Name
!begin-select
!N.NAME
!  let $Name = &N.NAME
!
!FROM PS_NAMES N
!WHERE N.EMPLID = $EMPLID
!AND N.COUNTRY_NM_FORMAT = 'MEX'
!AND N.NAME_TYPE = 'PRI'
!end-select
!end-procedure Get-Name


Begin-procedure Get-Imss_Id
  let $Imss_Id=''
begin-select
I.NATIONAL_ID
  let $Imss_Id = &I.NATIONAL_ID

from PS_PERS_NID I
WHERE I.EMPLID = $Emplid
AND I.COUNTRY = 'MEX'
AND I.NATIONAL_ID_TYPE = 'IMSS'
end-select
end-procedure Get-Imss_Id


Begin-Procedure Print-Data
  do Format-DateTime($Bgn_Dt, $Bgn_Dt3, {DEFDMY}, '', '')
  do Format-DateTime($End_Dt, $End_Dt3, {DEFDMY}, '', '')
  move #cons to $Cons 09999999
  move #Duration to $Duration 09
  if $Data = 'Y'
     print $cons                    (+1,1) !edit 09999999
     print $Emplid                  (,11)
     print $Name                    (,24,30)
     print $Company                 (,56)
     print $GP_Paygroup             (,62)
     print $Imss_Id                 (,74)
     if $RC_Estabid = 'Y'
        print $Estabid_RC           (,90)
     else
        print $EstabID              (,90)
     end-if
     print $Bgn_Dt3                 (,105)
     print $End_Dt3                 (,117)
     print #Duration                (,132) edit 999
     print $IllPct                  (,140) edit B99
     print $Illness_Type            (,148)     
     print $DeathInd                (,166)
  end-if
  do Write-Data
End-Procedure Print-Data


Begin-Procedure Write-Data
  let $Company_Address = $Address1 || ' ' || $Address2 || ' ' || $Address3 || ' ' || $Address4   -
  || ' ' || $City || ' ' || $State || ' ' || $County || ' ' || $Country || ' ' || $Zip

!  let $Illness_Pct = edit($Illness_Pct, 'B99')
!  if $Illness_Type <> ''
!     let $Illness = '*'
!  end-if

!  if $Death = 'Y'
!     let $Death_D = 'D'
!  else
!     let $Death_D = ' '
!  End-If

  if #Pin_Take_Num = #Pin_Num_Enf
     Let $risktype = '3'
  end-if

  if #Pin_Take_Num = #Pin_Num_Riesgo
      Let $risktype = '1'
  end-if

  if $RC_Estabid = 'Y'
     let $EstabID = $Estabid_RC
  end-if

WRITE 1 FROM $Cons:8               -
             '|':1                 -
             $Company:3            -
             '|':1                 -
             $CompanyName:25       -
             '|':1                 -
             $Company_Address:70   -
             '|':1                 -
             $Year:4               -
             '|':1                 -
             $EstabID:11           -
             '|':1                 -
             $Imss_Id:12           -
             '|':1                 -
             $Name:30              -
             '|':1                 -
             $Illness:1            -
             '|':1                 -
             $Bgn_Dt3:10           -
             '|':1                 -
             $risktype:1           -
             '|':1                 -
             $Duration:2           -
             '|':1                 -
!             $Illness_Pct:3        -
!             '|':1                 -
!             $Death_D:1            -
!             '|':1                 -
             $End_Dt3:10
end-Procedure Write-Data


#Include 'reset.sqc'    !Reset printer procedure
#Include 'curdttim.sqc' !Get-Current-DateTime procedure
#Include 'datetime.sqc' !Routines for date and time formatting
#Include 'number.sqc'   !Routines to format numbers
#Include 'stdapi.sqc'   !Update Process API
#Include 'datemath.sqc' !Routines for date math
#include 'getcomex.sqc' !Get-Company-data-Mex
#include 'gpmxnotf.sqc'  !Get-Run-Control procedure
