!-----------------------------------------------------------------------
!  STES017:  Disposition Reversals to Payroll
!-----------------------------------------------------------------------
!
!
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!----------------------------------------------------------------------
!
!          $Date:  2013/01/18:02:51:07                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!
!-----------------------------------------------------------------------
! Program Descr:  Summary report of Payroll Refund Reversals
! Prompts:  Stock ID, Stock Plan, From Date, Thru Date, PFrocess Group,
!           Emplid, Reprint
!-----------------------------------------------------------------------

  #include 'setenv.sqc' !Set environment

Begin-Setup

  #include 'ptpsl177.sqc'

End-Setup

!-----------------------------------------------------------------------
Begin-Program
!-----------------------------------------------------------------------

  do Init-Report
  do Process-Main
  do Reset
  do Successful-EOJ

  let $CurrentTime = datenow()
  show 'Process ended at:   ' $SysDateTime      !standard output info to sqr.log

End-Program

!-----------------------------------------------------------------------
! Print Report Header
!-----------------------------------------------------------------------

Begin-Heading 09

#include 'stdhdg01.sqc'

  print $StockIdLbl                                        (03,001,008)
  print ':'                                                (  ,   ,001)
  print $RC_Stock_ID                                       (  ,018,010)

  print $FromLbl                                           (  ,073,004)
  print ':'                                                (  ,   ,001)
  print $FromDate                                          (  , +1,010)

  print $ThruLbl                                           (  , +2,004)
  print ':'                                                (  ,   ,001)
  print $ThruDate                                          (  , +1,010)

  print $StockPlanLbl                                      (04,001,010)
  print ':'                                                (  ,   ,001)
  print $RC_Stock_Plan                                     (  ,018,010)

  print $PrcsGrpLbl                                        (05,001,013)
  print ':'                                                (  ,   ,001)
  print $PrcsGrp                                           (  ,018,010)

  print $ReprintLbl                                        (06,001,007)
  print ':'                                                (  ,   ,001)
  print $RC_Reprint                                        (  ,018,001)

  print $DisposnLbl                                        (08,041,011)
  print $PurchaseLbl                                       (  ,055,008)
  print $PurchaseLbl                                       (  ,077,008)
  print $DisposnLbl                                        (  ,087,011)

  print $NameLbl                                           (09,001,004)
  print $EmplidLbl                                         (  ,032,006)
  print $NumberLbl                                         (  ,046,006)
  print $DateLbl                                           (  ,055,004)
  print $PriceLbl                                          (  ,080,005)
  print $DateLbl                                           (  ,087,004)
  print $DispPriceLbl                                      (  ,101,017)
  print $ShareDispLbl                                      (  ,127,015)
  print $OrdIncLbl                                         (  ,151,015)
  print '-'                                                (10,001,165) fill

End-Heading

!-----------------------------------------------------------------------
! Function:    Init-Report
! Description: Prompt for run-time parameters
!-----------------------------------------------------------------------
Begin-Procedure Init-Report

  let $ReportID      = 'STES017'
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Define-Prcs-Vars
  do Get-Run-Control-Parms
  do Get-Report-Language
  let $AsOfDate = $AsOfToday

  if $prcs_process_instance = ''
  else
    do Get-SQR-Parms                   ! Get Run Control Values
  end-if

  let $RC_Reprint     = rtrim($RC_Reprint_Prompt,' ')

  show 'Reprint:       ' $RC_Reprint_Prompt
  show 'Stock ID:      ' $RC_Stock_ID
  show 'From Date:     ' $RC_FromDate
  show 'Thru Date:     ' $RC_ThruDate

  if $RC_Stock_Plan <> ''
    show 'Stock Plan:    ' $RC_Stock_Plan
  end-if

  if $RC_PrcsGrp <> ''
    show 'Prcs Group:    ' $RC_PrcsGrp
  end-if

  if $RC_Emplid <> ''
    show 'Emplid:        ' $RC_Emplid
  end-if

  do Format-DateTime ($RC_FromDate, $FromDate, {DEFDATE}, '', '')
  do Format-DateTime ($RC_ThruDate, $ThruDate, {DEFDATE}, '', '')

  show ' '
  let $CurrentTime = datenow()
  show 'Process started at: ' $SysDateTime      !standard output info to sqr.log

  do Build-Where-Clause

End-Procedure

!-----------------------------------------------------------------------
Begin-Procedure Report-Translation
!-----------------------------------------------------------------------

  do Init_Report_Translation ('STOCK', $language_cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK',   'STOCK_ID',           $StockIdLbl,     #DW)
  do Get_Field_Information ('STOCK',   'GRAND_TOTAL',        $GrndTotLbl,     #DW)
  do Get_Field_Information ('STOCK',   'STOCK_PLAN',         $StockPlanLbl,   #DW)
  do Get_Field_Information ('STOCK',   'NUMBER',             $NumberLbl,      #DW)
  do Get_Field_Information ('STOCK',   'PROCESS_GRP',        $PrcsGrpLbl,     #DW)
  do Get_Field_Information ('STOCK',   'NAME',               $NameLbl,        #DW)
  do Get_Field_Information ('STOCK',   'DATE',               $DateLbl,        #DW)
  do Get_Field_Information ('STOCK',   'DISPOSITION',        $DisposnLbl,     #DW)
  do Get_Field_Information ('STOCK',   'EMPLID',             $EmplidLbl,      #DW)
  do Get_Field_Information ('STOCK',   'SHARES_SOLD',        $SharesSoldLbl,  #DW)
  do Get_Field_Information ('STOCK',   'ST_COST_SALE_PRICE', $SalePriceLbl,   #DW)
  do Get_Field_Information ('STOCK',   'TOTAL',              $TotalLbl,       #DW)
  do Get_Field_Information ('STOCK',   'PURCHASE',           $PurchaseLbl,    #DW)
  do Get_Field_Information ('STOCK',   'FROM',               $FromLbl,        #DW)
  do Get_Field_Information ('STOCK',   'THRU',               $ThruLbl,        #DW)
  do Get_Field_Information ('STOCK',   'DISPOSITION_PRICE',  $DispPriceLbl,   #DW)
  do Get_Field_Information ('STOCK',   'DISPOSITION_DT',     $DispDtLbl,      #DW)
  do Get_Field_Information ('STOCK',   'SHARES_DISPOSED',    $ShareDispLbl,   #DW)
  do Get_Field_Information ('STOCK',   'ST_ORD_INCOME',      $OrdIncLbl,      #DW)
  do Get_Field_Information ('STOCK',   'PRICE',              $PriceLbl,       #DW)

  do Get_Field_Information ('STES017', 'REPORT_TITLE',       $ReportTitle,    #DW)
  do Get_Field_Information ('STES017', 'REPRINT',            $ReprintLbl,     #DW)

End-Procedure

!-----------------------------------------------------------------------
! Section: Build-Where-Clause
! Descr:   Construct portions of where clause, to be used in the main select,
!          based on report parameters choosen.
!-----------------------------------------------------------------------
Begin-Procedure Build-Where-Clause

  let $SQLQuote      = ''''
  let $WhereReprint  = ''
  let $WhereNew      = ''

  if $RC_Stock_Plan <> ''
    let $WhereStockPlan  = ' and A.STOCK_PLAN  = '   || $SQLQuote || $RC_Stock_Plan || $SQLQuote
  else
    let $WhereStockPlan  = ''
  end-if

  if $RC_Emplid <> ''
    let $WhereEmplid     = ' and A.EMPLID = '        || $SQLQuote || $RC_Emplid     || $SQLQuote
  else
    let $WhereEmplid     = ''
  end-if

  if $RC_PrcsGrp <> '' 
    let $WherePrcsGrp    = ' and A.PROCESS_GRP2  = '  || $SQLQuote || $RC_PrcsGrp    || $SQLQuote
  else
    let $WherePrcsGrp    = ''
  end-if

  if $RC_Reprint = 'Y'
    if $RC_FromDate <> ''
       let $WhereFromDate   = ' and A.REVERSAL_DT  >= '    || $SQLQuote || $RC_FromDate   || $SQLQuote
    else
       let $WhereFromDate   = ''
    end-if
    if $RC_ThruDate <> ''
       let $WhereThruDate   = ' and A.REVERSAL_DT  <= '    || $SQLQuote || $RC_ThruDate   || $SQLQuote
    else
       let $WhereThruDate   = ''
    end-if
    let $WhereReprint       = ' and A.REVERSAL_DT IS NOT NULL'
  else
    if $RC_FromDate <> ''
       let $WhereFromDate   = ' and A.DISPOSITION_DT  >= ' || $SQLQuote || $RC_FromDate || $SQLQuote
    else
       let $WhereFromDate   = ''
    end-if
    if $RC_ThruDate <> ''
       let $WhereThruDate   = ' and A.DISPOSITION_DT  <= ' || $SQLQuote || $RC_ThruDate || $SQLQuote
    else
       let $WhereThruDate   = ''
    end-if
    let $WhereNew          = ' and A.REVERSAL_DT IS NULL'
  end-if

End-Procedure

!-----------------------------------------------------------------------
! Function:    Process-Main
! Description: This is the main report procedurc.  Selects and prints
!              share disposition by employec.
!-----------------------------------------------------------------------
Begin-Procedure Process-Main

Begin-Select

A.PROCESS_GRP2                                             () on-break print=never level=1 procedure=PrcsGrp-Change
C.NAME
A.EMPLID
A.DISPOSITION_NBR
A.PERIOD_END_DT
A.DISPOSITION_DT
A.DISPOSITION_PRICE
A.SHARES_DISPOSED
A.ST_ORD_INCOME
B.PURCHASE_PRICE
B.OFFERING_ID
B.STOCK_PLAN
B.PROCESS_GROUP

  let $PrcsGrp        = rtrim(&a.process_grp2,' ')
  let $Print          = 'Y'
  let $Name           = rtrim(&c.name,' ')
  let $Emplid         = rtrim(&a.emplid,' ')
  let #DispositionNbr = &a.disposition_nbr
  let $PeriodEndDt    = rtrim(&a.period_end_dt,' ')
  let #PurchasePrice  = &b.purchase_price
  let $DispositionDt  = rtrim(&a.disposition_dt,' ')
  let #DispositionPrc = &a.disposition_price
  let #SharesDisposed = &a.shares_disposed * -1
  let #OrdIncome      = &a.st_ord_income * -1
  let #ShrTotal       = #ShrTotal + #SharesDisposed
  let #OrdTotal       = #OrdTotal + #OrdIncome
  do Print-Detail

  let #PrcsShares     = #PrcsShares     + #SharesDisposed
  let #PrcsOrdIncome  = #PrcsOrdIncome  + #OrdIncome

  if $RC_Reprint = 'N'
    do Update-Espp-Disposn
  end-if

 FROM PS_ST_ESPP_DISPOSN A,
      PS_ST_ESPP_PURCH   B,
      PS_ST_PERSON_NAME  C
where A.STOCK_ID           = $RC_Stock_Id
  and A.STOCK_PLAN_TYPE    = 'E'
  and A.STOCK_ID           = B.STOCK_ID
  and A.STOCK_PLAN_TYPE    = B.STOCK_PLAN_TYPE
  and A.STOCK_PLAN         = B.STOCK_PLAN
  and A.OFFERING_ID        = B.OFFERING_ID
  and A.PERIOD_END_DT      = B.PERIOD_END_DT
  and A.PROCESS_GROUP      = B.PROCESS_GROUP
  and A.EMPLID             = B.EMPLID
  and A.DISPOSITION_STATUS = 'V'
  and A.DISQUAL_DISPOSN    = 'Y'
  and A.ST_PR_DT          is not null
  and A.EMPLID             = C.EMPLID
  [$WhereFromDate]
  [$WhereThruDate]
  [$WhereStockPlan]
  [$WhereEmplid]
  [$WherePrcsGrp]
  [$WhereReprint]
  [$WhereNew]

End-Select

  if $Print = 'Y'
    do PrcsGrp-Change
    do Print-Totals
  end-if

End-Procedure

!-----------------------------------------------------------------------
! Section: Print-Detail
! Descr:   Prints the report
!-----------------------------------------------------------------------
Begin-Procedure Print-Detail

  print $Name                                              (+1,001,030)
  print $Emplid                                            (  ,032,011)
  print #DispositionNbr                                    (  ,045,007) edit 9999999
  do Format-DateTime ($PeriodEndDt,$out, {DEFDATE}, '', '')
  print $out                                               (  ,055,010)
  print #PurchasePrice                                     (  ,067,018) edit 999,999,999.999999
  do Format-DateTime ($DispositionDt,$out, {DEFDATE}, '', '')
  print $out                                               (  ,087,010)
  print #DispositionPrc                                    (  ,100,018) edit 999,999,999.999999
  print #SharesDisposed                                    (  ,120,022) edit 999,999,999,999.999999
  print #OrdIncome                                         (  ,144,022) edit 999,999,999,999.999999

End-Procedure

!-----------------------------------------------------------------------
! Section: Update-Espp-Disposn
! Descr:   Updates the sent to payroll date to the date the report is run
!-----------------------------------------------------------------------
Begin-Procedure Update-Espp-Disposn

 let $Procedure = 'Update-Espp-Disposn'
 let $SQL-Statement = 'Procedure: ' || $Procedure
  
Begin-Sql                           on-error = SQL-Error
update PS_ST_ESPP_DISPOSN
   set REVERSAL_DT   = $AsOfToday
 where STOCK_ID        = $RC_Stock_ID
   and STOCK_PLAN_TYPE = 'E'
   and STOCK_PLAN      = &b.stock_plan
   and EMPLID          = $Emplid
   and OFFERING_ID     = &b.offering_id
   and PERIOD_END_DT   = $PeriodEndDt
   and PROCESS_GROUP   = &b.process_group
End-Sql

End-Procedure

!-----------------------------------------------------------------------
! Procedure: PrcsGrp-Change
! Descr:     Prints Totals for each process group and advances a new page
!-----------------------------------------------------------------------
Begin-Procedure PrcsGrp-Change

  next-listing need=4
  let $ChangeLbl = $PrcsGrpLbl || ' ' || $TotalLbl || ':'
  print '-'                                                (+2,001,165) fill
  print $ChangeLbl                                         (+1,032,   )
  print #PrcsShares                                        (  ,120,022) edit 999,999,999,999.999999
  print #PrcsOrdIncome                                     (  ,144,022) edit 999,999,999,999.999999
  print '-'                                                (+1,001,165) fill
  let #TotalShares    = #TotalShares     + #PrcsShares
  let #TotalOrdIncome = #TotalOrdIncome  + #PrcsOrdIncome
  let #PrcsShares    = 0
  let #PrcsOrdIncome = 0
  new-page

End-Procedure

!-----------------------------------------------------------------------
! Print Totals
!-----------------------------------------------------------------------
Begin-Procedure Print-Totals

  next-listing need=4
  print '-'                                                (+2,001,165) fill
  print $GrndTotLbl                                        (+1,032,   )
  print ':'                                                (  ,   ,001)
  print #TotalShares                                       (  ,120,022) edit 999,999,999,999.999999
  print #TotalOrdIncome                                    (  ,144,022) edit 999,999,999,999.999999
  print '-'                                                (+1,001,165) fill

End-Procedure

!-----------------------------------------------------------------------
! SQC Files for called procedures
!-----------------------------------------------------------------------
#include 'prcsapi.sqc'   !Update Process Request API
#include 'prcsdef.sqc'   !Update Process Request variable declare
#include 'askftd.sqc'    !Routines to accept from - thru dates
#include 'eoj.sqc'       !End of Job procedure
#include 'sqrtrans.sqc'  !Access Strings Table
#include 'getrplng.sqc'  !Gets the report language
#include 'reset.sqc'     !End of Program
#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'number.sqc'    !Routines to format numbers
#include 'datemath.sqc'  !Routines for date arithmetic
#include 'strunctl.sqc'  !Stock Admin controls
#include 'getprimj.sqc'  !Get primary job
