!***********************************************************************
! GPJPSC06.sqr:  Social Insurance Bonus Payment Report                 *
!                (Summary)                                             *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:03:48:00                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#include 'setup32.sqc'  !page layout and printer definition

begin-setup
#Include 'setupdb.sqc'

end-setup


begin-report


        do Init-Processing

        do Build-select

        do Process-Main

        do Reset

        do Update-Prcs-Run-Status

end-report


!***********************************************************************
!                                                                      *
! Init-Processing                                                      *
!                                                                      *
!                                                                      *
! Called By:    begin-report                                           *
!                                                                      *
! Calls:        Init-Datetime           - datetime.sqc                 *
!               Get-Current-Datetime    - prcsapi.sqc (curdttim.sqc)   *
!               Get-Run-Control-Parms   - prcsapi.sqc                  *
!               Get-Language-Codes      - prcslng.sqc                  *
!               Get-GPJPSC06-runctl                                    *
!               Init-Report                                            *
!                                                                      *
! Description:  Standard PeopleSoft initialization processing.         *
!                                                                      *
!***********************************************************************
begin-procedure Init-Processing


        do Init-Datetime

        do Get-Current-Datetime

        do Define-Prcs-Vars

        do Get-Run-Control-Parms

        do Get-Language-Codes

        do Get-GPJPSC06-runctl

        do Init-Report


end-procedure ! Init-Processing


!***********************************************************************
!                                                                      *
! Get-GPJPSC06-runctl                                                  *
!                                                                      *
!                                                                      *
! Called By:    Init-Processing                                        *
!                                                                      *
! Description:  Get data from GPJPSC06 runcontrol record.              *
!                                                                      *
!***********************************************************************
begin-procedure Get-GPJPSC06-runctl


begin-select

RC.GPJP_TAX_ESTAB
RC.GPJP_ACTL_PMNT_DT

FROM  PS_GPJP_RC_SC_BNS RC
WHERE RC.OPRID          = $Prcs_OprID
AND   RC.RUN_CNTL_ID    = $Prcs_Run_Cntl_ID

end-select

        let $Rc_Gpjp_tax_estab = RTRIM(&RC.Gpjp_tax_estab, ' ')
        let $Gpjp_actl_pmnt_dt = &RC.GPJP_ACTL_PMNT_DT

end-procedure !Get-GPJPSC06-runctl


!***********************************************************************
!                                                                      *
! Init-Report                                                          *
!                                                                      *
!                                                                      *
! Called By:    Init-Processing                                        *
!                                                                      *
! Calls:        Init_Report_Translation    - sqrtrans.sqc              *
!               Append_Report_Translation  - sqrtrans.sqc              *
!               Get_Field_Information                                  *
!                                                                      *
! Description:  Initializes variables, defines values for variables    *
!               used in report header & throughout report.  Performs   *
!               Strings Table translation.  #Defines variables for     *
!               use throughout report in printing columns.             *
!                                                                      *
!***********************************************************************
begin-procedure Init-Report


!***********************************************************************
!                                                                      *
! Translations procedures & Strings Table retrievals.                  *
!                                                                      *
!***********************************************************************

    do Init_Report_Translation ($ReportID, $curr_language_cd)

    do Append_Report_Translation ('GPJPSC06')

    do Get_Field_Information ('GPJPSC06','TITLE',             $GPJPSC06_Title,                  #dummy_width)
    do Get_Field_Information ('GPJPSC06','ESTAB',             $GPJPSC06_Estab,                  #dummy_width)
    do Get_Field_Information ('GPJPSC06','ESTAB_NAME',        $GPJPSC06_Estab_name,             #dummy_width)
    do Get_Field_Information ('GPJPSC06','PYMT_DT',           $GPJPSC06_Pymt_dt,                #dummy_width)
    do Get_Field_Information ('GPJPSC06','TOT_BNS_PYE',       $GPJPSC06_Tot_bns_pye,            #dummy_width)
    do Get_Field_Information ('GPJPSC06','TOT_BNS_AMT',       $GPJPSC06_Tot_bns_amt,            #dummy_width)
    do Get_Field_Information ('GPJPSC06','TOT_INSURED',       $GPJPSC06_Tot_insured,            #dummy_width)

!***********************************************************************
!                                                                      *
! Report variables.                                                    *
!                                                                      *
!***********************************************************************

    let $ReportID              = 'GPJPSC06'          ! Printed in header
    let $ReportTitle           = $GPJPSC06_Title     ! Printed in header
    let $ReportDate            = $AsOfToday          ! Printed in header
    let $ReportTime            = $AsOfNow            ! Printed in header
    let $Report_Language       = $curr_language_cd   ! curr_language_cd resolved in prcslng.sqc


    !do Security-Param                   ! SQR Security

end-procedure ! Init-Report


!***********************************************************************
!                                                                      *
! Build-select                                                         *
!                                                                      *
!                                                                      *
! Called By:    Begin-Report                                           *
!                                                                      *
! Description:  Build dynamic select statement for main procedure      *
!                                                                      *
!***********************************************************************
begin-procedure Build-select

     move '' to $SlctSC_Estblm

     if $Rc_Gpjp_tax_estab <> ''
        concat 'AND A.GPJP_TAX_ESTAB = ' with  $SlctSC_Estblm
        concat   '''' with $SlctSC_Estblm
        concat  $Rc_Gpjp_tax_estab with  $SlctSC_Estblm
        concat   ''' ' with $SlctSC_Estblm
     end-if

end-procedure !Build-select


!***************************
begin-heading 7
!***************************

#include 'stdhdgtr.sqc'

end-heading


!***********************************************************************
!                                                                      *
! Process-Main                                                         *
!                                                                      *
!                                                                      *
! Called By:    Begin-Report                                           *
!                                                                      *
! Description:  Main processing                                        *
!                                                                      *
!***********************************************************************
begin-procedure Process-Main

let $Tax_estab_save = ' '
let #Tot_bns_pye  = 0
let #Tot_bns_amt  = 0
let #Tot_insured  = 0
let $No_records = 'Y'

begin-select
A.GPJP_TAX_ESTAB
!A.GPJP_TOT_BNS_ERN
A.GPJP_MTH_CASH_RMN
A.GPJP_MTH_MAT_RMN
A.GPJP_SUM_MTH_RMN

  if $Tax_estab_save = ' '
     move &A.Gpjp_tax_estab  to $Tax_estab_save
     let $Estab  = &A.Gpjp_tax_estab
     let $EstabType = 'SC'
     do Get-Estab-Name
  end-if

  if &A.Gpjp_tax_estab <>  $Tax_estab_save
     do TaxEstab-Change
     move &A.Gpjp_tax_estab  to $Tax_estab_save
  end-if

  let $Estab  = &A.Gpjp_tax_estab
  let $EstabType = 'SC'
  let $No_records    = 'N'

!  move &A.Gpjp_tot_bns_ern to #Gpjp_tot_bns_ern
 move &A.Gpjp_sum_mth_rmn to #Gpjp_tot_bns_ern

 add 1                 to #Tot_bns_pye
  add #Gpjp_tot_bns_ern to #Tot_bns_amt

FROM  PS_GPJP_SC_BNS_RPT A

Where A.JOBINSTANCE = #prcs_job_instance
[$SlctSC_Estblm]
ORDER BY A.GPJP_TAX_ESTAB

end-select

   if $No_records <> 'Y'
        let $End = 'Y'
        do TaxEstab-Change
   end-if

end-procedure ! Process-Main


!***********************************************************************
!                                                                      *
! TaxEstab-Change                                                      *
!                                                                      *
!                                                                      *
! Called By:    Process-Main                                           *
!                                                                      *
! Description:  This procedure is executed whenever a change is        *
!               detected in Tax Establishment number.                  *
!                                                                      *
!***********************************************************************
begin-procedure TaxEstab-Change

   do Get-Estab-Name
   do Get-Tot-Insured
   do Print-Details
let #Tot_bns_pye  = 0
let #Tot_bns_amt  = 0
let #Tot_insured  = 0
   do page_break

end-procedure !TaxEstab-Change


!***********************************************************************
!                                                                      *
! Get-Tot-Insured                                                      *
!                                                                      *
!                                                                      *
! Called By:    TaxEstab-Change                                        *
!                                                                      *
! Description:  This procedure counts up the number of the eligibles   *
!               for Social Insurance by Social Insurance Establishment *
!                                                                      *
!***********************************************************************
begin-procedure Get-Tot-Insured

let #Tot_insured  = 0

do convert-to-DTU-Date($Gpjp_actl_pmnt_dt,$date1)

let #month = to_number(substr($date1,6,2))
let #year = to_number(substr($date1,1,4))

let #first_day = 01
  if #month = 12
        let #next_month =  1
        let #next_year  = #year + 1
    else
        let #next_month =  #month + 1
        let #next_year  =  #year
    end-if


     do Format-Number(#next_month,$next_month1,'99')
        do Format-Number(#next_year,$next_year,'9999')

        let $next_month = RTRIM(LTRIM($next_month1, ' '), ' ')

     if #next_month < 10
  let $next_month = '0' || $next_month
  end-if

  do dtu-format-date(#next_year ,#next_month , #first_day , $date11)
  do dtu-add-days($date11,-1,$month_end_date)
  do convert-From-DTU-Date($month_end_date,$month_end_date)


begin-select
PYE.EMPLID
PYE.EMPL_RCD

  add 1 to #Tot_insured

FROM  PS_GPJP_SC_PYE_DTA PYE
WHERE PYE.GPJP_TAX_ESTAB = $Tax_estab_save
AND   PYE.GPJP_CHG_FLG NOT IN ('03','04','05','09')
AND   PYE.EFFDT = (SELECT MAX(PYE1.EFFDT)
                   FROM PS_GPJP_SC_PYE_DTA PYE1
                   WHERE PYE1.EMPLID = PYE.EMPLID AND PYE1.EMPL_RCD = PYE.EMPL_RCD
                   AND PYE1.EFFDT <= $month_end_date)
end-select

end-procedure ! Get-Tot-Insured


!***********************************************************************
!                                                                      *
! Print-Details                                                        *
!                                                                      *
!                                                                      *
! Called By:    TaxEstab-Change                                        *
!                                                                      *
! Description:  Prints the detail lines                                *
!                                                                      *
!***********************************************************************
begin-procedure Print-Details

      print $GPJPSC06_Estab              (+2,15,30)
      print  $Estab                      (0,50,15)

      print $GPJPSC06_Tot_bns_pye        (0,100,35)
      print  #Tot_bns_pye                (0,153,7)  edit 999,990

      print $GPJPSC06_Estab_name         (+1,15,30)
      print  $EstabName                  (0,50,30)

      print $GPJPSC06_Tot_bns_amt        (0,100,30)
      print  #Tot_bns_amt                (0,143,17) edit 9,999,999,999,990

      print $GPJPSC06_Pymt_dt            (+1,15,30)
      do Convert-To-DTU-Date ($Gpjp_actl_pmnt_dt,$Gpjp_actl_pmnt_dt_out)
      print  $Gpjp_actl_pmnt_dt_out          (0,50,10)

      print $GPJPSC06_Tot_insured        (0,100,30)
      print  #Tot_insured                (0,153,7)  edit 999,990

end-procedure  !Print-Details


!***********************************************************************
!                                                                      *
! page_break                                                           *
!                                                                      *
!                                                                      *
! Called By:    TaxEstab-Change                                        *
!                                                                      *
! Description:  Performs Page break and ensures end of report line     *
!               is printed.                                            *
!                                                                      *
!***********************************************************************
begin-procedure page_break

    if $End = 'Y'
      do Reset
    end-if

    new-page
end-procedure  !page_break


!***********************************************************************
!                                                                      *
!                                                                      *
! SQCS included in the report                                          *
!                                                                      *
!***********************************************************************
#include 'getestab.sqc'     ! Get Establishment Data from the GPJP_TAX_ESTAB
#include 'getcodta.sqc'     ! Get selected fields from the Company Table
#include 'datemath.sqc'     ! Date conversion procedures
#include 'sqrtrans.sqc'     ! Translate SQR strings to given language
#include 'prcsapi.sqc'      ! Replaces stdapi.sqc
#include 'prcslng.sqc'      ! Replaces getrplng.sqc
#include 'readxlat.sqc'     ! Procedure to read values from xlattable
#include 'reset.sqc'        ! Reset printer procedure
#include 'number.sqc'   !Routines to format numbers
#include 'datetime.sqc' !Routines for date and time formatting

