!************************************************************************
!  STSU021:  Vesting Schedule
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:57:23                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
  #include 'ptpsp125.sqc'  !Printer and page-size init
end-setup

#define  col2    15
#define  col3    34
#define  col4    53

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init

  let     $ReportID     =   'STSU021'

  do Get-Report-Language

  show    'FLOW: Init-Report'
  show    'Report Title: ' $REPORTTITLE

end-procedure

!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ($ReportID,$language_cd)
  do Append_Report_Translation ($ReportID)

  do Get_Field_Information ('STSU021','STSU021',          $REPORTTITLE,        #DW)
  do Get_Field_Information ('STSU021','DESCR',            $DESCR,              #DW)
  do Get_Field_Information ('STSU021','UNEVEN_SHARE_RULE',$UNEVEN_SHARE_RULE,  #DW)
  do Get_Field_Information ('STSU021','VEST_BEGINS_CD',   $VEST_BEGINS_CD,     #DW)
  do Get_Field_Information ('STSU021','VEST_SCHED_ID',    $VEST_SCHED_ID,      #DW)
  do Get_Field_Information ('STSU021','SEQ_NUM',          $SEQ_NUM,            #DW)
  do Get_Field_Information ('STSU021','VEST_FREQUENCY',   $VEST_FREQUENCY,     #DW)
  do Get_Field_Information ('STSU021','VEST_MONTHS',      $VEST_MONTHS,        #DW)
  do Get_Field_Information ('STSU021','VEST_PERCENT',     $VEST_PERCENT,       #DW)

end-procedure

!************************************************************************
begin-heading 5
!************************************************************************

#include 'stdhdgtr.sqc'

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

begin-Select

A.VEST_SCHED_ID
A.DESCR
A.DESCRSHORT
A.UNEVEN_SHARE_RULE
A.VEST_BEGINS_CD

  do Print-Data
  do Select-Dtl
  print          '  '          (+2,1,1)

from PS_ST_VEST_SCHED A

end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

B.VEST_SCHED_ID
B.SEQ_NUM
B.VEST_MONTHS
B.VEST_PERCENT
B.VEST_FREQUENCY

 do Print-Dtl

from     PS_ST_VEST_DEFN  B
where    B.VEST_SCHED_ID   = &A.VEST_SCHED_ID
order by B.SEQ_NUM

end-Select

end-procedure

!************************************************************************
begin-procedure Print-Data
!************************************************************************

  print $VEST_SCHED_ID         (+1,1,20)
  print ':'                    (,,1)
  print &A.VEST_SCHED_ID       (0,+1,10)

  print $DESCR                 (+2,1,05)
  print ':'                    (,,1)
  print &A.DESCR               (0,+1,30)

  do Get-Data

  print $UNEVEN_SHARE_RULE     (+2,1,18)
  print ':'                    (,,1)
  print $UnevenShareRule       (0,+1,30)

  print $VEST_BEGINS_CD        (+2,1,14)
  print ':'                    (,,1)
  print $StVestBeginsCd        (0,+1,10)

  print $SEQ_NUM               (+3,3,07)
  print $VEST_FREQUENCY        (0,+5,14)
  print $VEST_MONTHS           (0,+5,14)
  print $VEST_PERCENT          (0,+5,15)
  print ' '                    (+1,1,1)

end-procedure

!************************************************************************
begin-procedure Print-Dtl
!************************************************************************

  print &B.SEQ_NUM             (+1,3,03)

  do Get-Data

  print $StVestFrequency       (0,{col2},30)
  print &B.VEST_MONTHS         (0,{col3},03)

  do Format-Number(&B.VEST_PERCENT, $STVestPct, '990.099')

  print $STVestPct             (0,{col4},07)

end-procedure

!************************************************************************
begin-procedure Get-Data
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Data                                              !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'VEST_FREQUENCY'
  let $FieldValue = &B.VEST_FREQUENCY

  do Read-Translate-Table

  let $StVestFrequency = RTRIM($XlatLongName,' ')


  let $FieldName = 'UNEVEN_SHARE_RULE'
  let $FieldValue = &A.UNEVEN_SHARE_RULE

  do Read-Translate-Table

  let $UnevenShareRule = RTRIM($XlatLongName,' ')

  let $FieldName = 'VEST_BEGINS_CD '
  let $FieldValue = &A.VEST_BEGINS_CD

  do Read-Translate-Table

  let $StVestBeginsCd = RTRIM($XlatShortName,' ')

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************

#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'datemath.sqc'  !Date Math Routines
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
