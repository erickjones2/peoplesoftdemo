!***********************************************************************
!  GPBRTC01:  Transport Coupon Format                                  *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:03:22:14                                 *
!       $Release:  HR92                                                *
!      $Revision:  101                                                 *
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   ! set enviroment

Begin-Setup
  #include 'setupdb.sqc'   ! Database specific setup
  Declare-Printer   THIS_REPORT
       Type=HPLASERJET
       Font=22
       Point-Size=12
       Pitch=17
       Symbol-Set=0U     ! ASCII symbol set
  End-Declare
 
  Declare-Layout THIS_REPORT
       Orientation=Portrait
       Top-Margin= .4
       Left-Margin=.60
       Max-Lines=82
       Max-Columns=130
       Char-Width=4.3    ! 7.2
       Line-Height=9     ! 72/printer_point-size
  End-Declare
 
  Declare-Report THIS_REPORT
      Printer-Type=HPLASERJET
      LayOut=THIS_REPORT
  End-Declare
end-Setup

!**************************************
begin-report
!**************************************
  move '1' to $ReportDateType   ! Set for date to format as DMY
  move '1' to $ReportYear4      ! Set for year to be formatted YYYY
  
  do Init-Report
  do Select-Parameters
  do Process-Report
  do Stdapi-Term
  do Reset
  
  date-time () hh:mi:ss &timeEnd
  display 'Report Ended: ' noline
  display &timeEnd
end-report

!***************************** 
Begin-Procedure Init-Report
#debug show '** Init-Report **'
!***************************** 
    do Init-DateTime
    do Init-Number
    do Stdapi-Init
    do Get-Current-DateTime

    move 'GPBRTC01' to $ReportID
    move 'Transport Coupon Format' to $ReportTitle
    move 'Ver. 2008071801' to $ReportVersion
    display $ReportID
    display $ReportTitle
    display $ReportVersion
    date-time () hh:mi:ss &timeBegan
    display 'Report Began: ' noline
    display &timeBegan
End-Procedure Init-Report

!***************************** 
Begin-Procedure Select-Parameters
#debug show '** Select-Parameters **'
#debug show '$AsOfToday ' $AsOfToday
#debug show '$prcs_oprid ' $prcs_oprid
#debug show '$prcs_run_cntl_id ' $prcs_run_cntl_id
!***************************** 
Begin-Select
RC.GPBR_LIST_OPTION
RC.ESTABID
RC.GB_GROUP_ID
  
  Let $strlstOptn = &RC.GPBR_LIST_OPTION
  Let $strEstabId = &RC.ESTABID
  Let $strGroupId = &RC.GB_GROUP_ID
  
  #debug show '$strlstOptn ' $strlstOptn
  #debug show '$strEstabId ' $strEstabId
  #debug show '$strGroupId ' $strGroupId

  Evaluate $strlstOptn
  When = '1' !* All establishments & Payees
     Let $WhereClause = 'WHERE '
     Break
  When = '2' !* Establishment
     Let $WhereClause = 'WHERE JOB.ESTABID = ' || '''' || Rtrim($strEstabId, ' ') || ''''
     Let $WhereClause = $WhereClause || ' AND '
     Break
  When = '3' !* Employees
     Let $WhereClause = ', PS_GPBR_RC_TRCOUPE TRCO WHERE JOB.EMPLID = TRCO.EMPLID AND JOB.EMPL_RCD = TRCO.EMPL_RCD'
     Let $WhereClause = $WhereClause || ' AND TRCO.OPRID = ' || '''' || Rtrim($prcs_oprid, ' ') || ''''
     Let $WhereClause = $WhereClause || ' AND TRCO.RUN_CNTL_ID = ' || '''' || Rtrim($prcs_run_cntl_id, ' ') || ''''
     Let $WhereClause = $WhereClause || ' AND '
     Break
  When = '4' !* Group ID
     Let $WhereClause = ', PS_GB_GRP_RES_TBL GRP WHERE JOB.EMPLID = GRP.EMPLID AND JOB.EMPL_RCD = GRP.EMPL_RCD' 
     Let $WhereClause = $WhereClause || ' AND GRP.GB_GROUP_ID = ' || '''' || Rtrim($strGroupId, ' ') || ''''
     Let $WhereClause = $WhereClause || ' AND GRP.VERSIONGBQDM = (SELECT MAX(VERSIONGBQDM) FROM PS_GB_GRP_RES_TBL ' 
     Let $WhereClause = $WhereClause || ' WHERE GB_GROUP_ID = GRP.GB_GROUP_ID)'
     Let $WhereClause = $WhereClause || ' AND JOB.EFFDT = GRP.JOB_EFFDT'
     Let $WhereClause = $WhereClause || ' AND JOB.EFFSEQ = GRP.JOB_EFFSEQ'
     Break
  End-Evaluate
  
  Evaluate $strlstOptn
  When = '1' !* All establishments & Payees
  When = '2' !* Establishment
  When = '3' !* Employees
     Let $WhereClause = $WhereClause || ' JOB.EFFDT = (SELECT MAX(J.EFFDT) FROM PS_JOB J WHERE J.EMPLID = JOB.EMPLID AND J.EMPL_RCD = JOB.EMPL_RCD AND J.EFFDT <= ' || '''' || $AsOfToday || '''' || ')'
     Let $WhereClause = $WhereClause || ' AND JOB.EFFSEQ = (SELECT MAX(J1.EFFSEQ) FROM PS_JOB J1 WHERE J1.EMPLID = JOB.EMPLID AND J1.EMPL_RCD = JOB.EMPL_RCD AND J1.EFFDT = JOB.EFFDT)'
  Break
  End-Evaluate

  Do Convert-To-Dtu-Date($AsOfToday, $AsOfDateDTU)  
  Do Dtu-Parse-Date($AsOfDateDTU, #Year, #Month, #Day)
    
  Evaluate #Month
  When = 1
     Let $Month = 'Janeiro'
  Break
  When = 2
     Let $Month = 'Fevereiro'
  Break
  When = 3
     Let $Month = 'Março'
  Break
  When = 4
     Let $Month = 'Abril'
  Break
  When = 5
     Let $Month = 'Maio'
  Break
  When = 6
     Let $Month = 'Junho'
  Break
  When = 7
     Let $Month = 'Julho'
  Break
  When = 8
     Let $Month = 'Agosto'
  Break
  When = 9
     Let $Month = 'Setembro'
  Break
  When = 10
     Let $Month = 'Outubro'
  Break
  When = 11
     Let $Month = 'Novembro'
  Break
  When = 12
     Let $Month = 'Dezembro'
  Break
  End-Evaluate
  
  Let $ReportDateFmtBra = Edit(#Day, '09') || '/' || Edit(#Month,'09') || '/' || Edit(#Year,'9999') 
  Let $ReportDateBra = ' , ' || Edit(#Day, '09') || ' de ' || $Month || ' de ' || Edit(#Year,'9999')
 
FROM  PS_GPBR_RC_TRCOUP RC
WHERE RC.OPRID       = $prcs_oprid
  AND RC.RUN_CNTL_ID = $prcs_run_cntl_id
End-Select
End-Procedure Select-Parameters

!*****************************   
Begin-Procedure Process-Report
#debug show '** Process-Report **'
#debug show '$WhereClause ' $WhereClause
!*****************************  
Begin-Select
JOB.EMPLID
JOB.EMPL_RCD
JOB.EFFDT
JOB.SETID_DEPT
JOB.DEPTID

   Let $EmplId          = Rtrim(&JOB.EMPLID, ' ')
   Let #EmplRcd         = &JOB.EMPL_RCD
   Let $JobEffdt        = &JOB.EFFDT
   Let $DepartmentSetId = Rtrim(&JOB.SETID_DEPT, ' ')
   Let $DepartmentId    = Rtrim(&JOB.DEPTID, ' ')
   
   #debug show '$Emplid ' $Emplid
   #debug show '#EmplRcd ' #EmplRcd
   
   Do Get-Department
   Do Get-Name
   Do Get-Address
   Do Print-Layout
   Do Print-Data
   
FROM PS_JOB JOB
[$WhereClause]
AND JOB.EMPL_STATUS = 'A'
ORDER BY JOB.EMPLID, JOB.EMPL_RCD
End-Select
End-Procedure Process-Report

!*****************************   
Begin-Procedure Get-Department
#debug show '** Get-Department **'
#debug show '$DepartmentSetId ' $DepartmentSetId
#debug show '$DepartmentId ' $DepartmentId
#debug show '$JobEffdt ' $JobEffdt
!*****************************  
Let $DepartmentDescr = ''
Begin-Select
DEPMNT.DESCR

   Let $DepartmentDescr = &DEPMNT.DESCR

FROM PS_DEPT_TBL DEPMNT
WHERE DEPMNT.SETID = $DepartmentSetId
AND DEPMNT.DEPTID = $DepartmentId
AND DEPMNT.EFFDT = (SELECT MAX(EFFDT) 
                 FROM PS_DEPT_TBL 
                 WHERE SETID = DEPMNT.SETID 
                 AND DEPTID = DEPMNT.DEPTID 
                 AND EFFDT <= $JobEffdt)
End-Select
Begin-Select
DEPLANG.DESCR

   Let $DepartmentDescr = &DEPLANG.DESCR
   
FROM PS_DEPT_TBL_LANG DEPLANG
WHERE DEPLANG.SETID = $DepartmentSetId
AND DEPLANG.DEPTID = $DepartmentId
AND DEPLANG.LANGUAGE_CD = 'POR'
AND DEPLANG.EFFDT = (SELECT MAX(EFFDT) 
                    FROM PS_DEPT_TBL_LANG 
                    WHERE SETID = DEPLANG.SETID 
                    AND DEPTID = DEPLANG.DEPTID 
                    AND EFFDT <= $JobEffdt)
End-Select
End-Procedure Get-Department

!*****************************   
Begin-Procedure Get-Name
#debug show '** Get-Name **'
#debug show '$EmplId ' $EmplId
#debug show '$JobEffdt ' $JobEffdt
!*****************************  
Let $EmployeeName = ''
Let #EmplNamePos = 60
Begin-Select
EMPLNAME.NAME_DISPLAY 

   Let $EmployeeName = Rtrim(&EMPLNAME.NAME_DISPLAY, ' ')
   Let #EmplNamePos = 61 - Length($EmployeeName)
   If #EmplNamePos <> 0
     Let #EmplNamePos = Abs(#EmplNamePos / 2) + 50
   End-If   
   
FROM PS_NAMES EMPLNAME
WHERE EMPLNAME.EMPLID = $EmplId
AND EMPLNAME.NAME_TYPE = 'PRI'
AND EMPLNAME.EFFDT = (SELECT MAX(EFFDT) 
                  FROM PS_NAMES 
                  WHERE EMPLID = EMPLNAME.EMPLID 
                  AND EFFDT <= $JobEffdt)
End-Select
End-Procedure Get-Name

!*****************************   
Begin-Procedure Get-Address
#debug show '** Get-Address **'
#debug show '$EmplId ' $EmplId
#debug show '$JobEffdt ' $JobEffdt
!*****************************  
Let $Address1 = ''
Let $Address2 = ''
Let $Address4 = ''
Let $City     = ''
Let $State    = ''
Let $Postal   = ''
Begin-Select
ADDRESS.ADDRESS1
ADDRESS.ADDRESS2
ADDRESS.ADDRESS4
ADDRESS.NUM1
ADDRESS.CITY
ADDRESS.STATE
ADDRESS.POSTAL

   Let $Address1 = &ADDRESS.ADDRESS1
   Let $Address2 = &ADDRESS.ADDRESS2
   Let $Address4 = &ADDRESS.ADDRESS4
   Let $Num1     = &ADDRESS.NUM1
   Let $City     = &ADDRESS.CITY
   Let $State    = &ADDRESS.STATE
   Let $Postal   = &ADDRESS.POSTAL
   Let $City     = Rtrim($City , ' ')

FROM PS_ADDRESSES ADDRESS
WHERE ADDRESS.EMPLID = $EmplId
AND ADDRESS.ADDRESS_TYPE = 'HOME'
AND ADDRESS.EFFDT = (SELECT MAX(EFFDT) 
                     FROM PS_ADDRESSES 
                     WHERE EMPLID = ADDRESS.EMPLID 
                     AND EFFDT <= $JobEffdt)
End-Select
End-Procedure Get-Address

!*****************************   
Begin-Procedure Print-Layout
#debug show '** Print-Layout **'
!*****************************  
   graphic ( 2,1 ,123) box  3 5
   graphic ( 6,1 ,123) box 12 5
   graphic (18,1 ,123) box  5 5
   graphic (23,1 ,123) box 56 5
   
   graphic (29,5 ,114) box  2 5
   graphic (31,5 ,114) box  2 5
   graphic (33,5 ,114) box  2 5
   graphic (35,5 ,114) box  2 5    

   graphic (42,5 ,114) box  2 5
   graphic (44,5 ,114) box  2 5
   graphic (46,5 ,114) box  2 5
   graphic (48,5 ,114) box  2 5    
   
   graphic (75,65,45) horz-line
         
   Alter-Printer Point-size = 10 Font-Type = Fixed Font = 22
   print 'DECLARAÇÃO E TERMO DE COMPROMISSO VALE TRANSPORTE' (3,30) Bold  
   
   Alter-Printer Point-size = 5 Font-Type = Proportional Font = 22   
   Print $ReportDateFmtBra ( 4,  1) Bold 
   Print $ReportTime       (  , +2) Bold 
   Print 'PeopleSoft '     (78,  1)
   Print 'GPBRTC01'        (  ,117)

   Alter-Printer Point-size = 8 Font-Type = Proportional Font = 22   
   Print 'Funcionário: '  ( 7, 5) Bold
   Print 'Departamento: ' ( 9, 5) Bold
   Print 'Endereço: '     (11, 5) Bold
   Print 'Complemento: '  (13, 5) Bold
   Print 'Cidade: '       (15, 5) Bold
   
   Print 'Número: '       (11,80) Bold
   Print 'Bairro: '       (13,80) Bold
   Print 'UF: '           (15,65) Bold
   Print 'CEP: '          (15,80) Bold
   
   Print 'Deseja receber vale transporte?  (   ) Sim  (   ) Não' (19, 5)
   Print '(Caso afirmativo preencha os dados abaixo)'          (20, 5)
   
   Print '1.- Informe o meio de transporte coletivo mais adequado para deslocar da residência para o trabalho.' (25, 5)
   Print 'Tipo:  (   )Ônibus  (   )Metrô  (   )Integração' (27, 5)
   Print 'Nome da linha'  (30,  7)
   Print 'Número'         (30, 80)
   Print 'Tarifa'         (30,105)
   
   Print '2.- Informe o meio de transporte coletivo mais adequado para deslocar do trabalho para a residência.' (38, 5)
   Print 'Tipo:  (   )Ônibus  (   )Metrô  (   )Integração' (40, 5)
   Print 'Nome da linha'  (43,  7)
   Print 'Número'         (43, 80)
   Print 'Tarifa'         (43,105)

   Alter-Printer Point-size = 7 Font-Type = Proportional Font = 22
   
   Print 'Assumo o compromisso de utilizar o vale transporte exclusivamente para o meu deslocamento residência - trabalho e' (53,10) Bold
   Print 'vice-versa; e afirmo ter conhecimento do artigo 7°, paragráfo 3° do Decreto n° 95247/87, de que constitui falta grave' (+1, 5) Bold
   Print 'o seu uso indevido ou que essa declaração seja falsa.' (+1, 5) Bold
   
   Print 'Comprometo-me a atualizar anualmente as informaçòes ou a qualquer tempo ocorrer mudança residencial ou' (+2,10) Bold
   Print 'no(s) meio(s) de transporte.' (+1, 5) Bold
   
   Print 'Autorizo a empresa a descontar mensalmente, até 6% (seis por cento) do meu salário base, o valor destinado a cobrir' (+2, 10) Bold
   Print 'o fornecimento de vales transporte por mim utilizados.' (+1, 5) Bold
   
   Print 'Por ser expressão de verdade, firmo a presente declaração e termo de compromisso' (+2, 10) Bold
   
End-Procedure Print-Layout

!*****************************   
Begin-Procedure Print-Data
#debug show '** Print-Data **'
#debug show '#EmplNamePos ' #EmplNamePos
!*****************************   
   Alter-Printer Point-size = 5 Font-Type = Proportional Font = 22
   Print $EmplId          ( 4,110) Bold
   Print #EmplRcd         (  ,+2 ) Edit '99' Bold
   
   Alter-Printer Point-size = 10 Font-Type = Proportional Font = 22
   Print $EmployeeName    ( 7,19) Bold
   Print $DepartmentDescr ( 9,20) Bold
   Print $Address1        (11,16) Bold
   Print $Address2        (13,19) Bold
   Print $City            (15,14) Bold
   
   Print $Num1            (11,89) Bold
   Print $Address4        (13,89) Bold
   Print $State           (15,69) Bold
   Print $Postal          (15,85) Bold
   
   Alter-Printer Point-size = 9 Font-Type = Proportional Font = 22   
   Let $CityAndDate = $City || $ReportDateBra
   Print $CityAndDate     (75,05) Bold
   Print $EmployeeName    (+1, #EmplNamePos) Bold
   new-page
End-Procedure Print-Data

#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'
#Include 'sqrtrans.sqc'
#include 'hrsecty.sqc'   !Get SQR Security parameters   