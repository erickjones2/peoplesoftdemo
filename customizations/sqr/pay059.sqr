!***********************************************************************
!  PAY059.SQR: Encumbrance Accounting Line Report                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:37:45                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'  !Set environment
#include 'setup32.sqc' !Printer and page-size initialization
#include 'paycfprt.sqc'!Print ChartField functions

!************
begin-report
!************

  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report


!***************************
begin-procedure Init-Report
!***************************

  move 'PAY059' to $ReportID
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init
  do Get-Run-Control


  move 'Encumbrance Accounting Line Report' to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '
  do REPORT-TRANSLATION

  !------------------------------------------------------------------------------------------------------!
  ! Dynamic ChartFields variables used by the Common CF Print SQC                                        !
  !------------------------------------------------------------------------------------------------------!
  let $DynCF_ChartFields_Option = 'A'        !CF Configuration - 'P' = Pending Changes, 'A' - Current
  let $DynCF_LabelPosn_Option = 'T'          !ChartField Label Position - 'T' = above, 'S' = side
  let $DynCF_LabelUnderLine = 'N'            !ChartField Label Underline - 'Y' = Print, 'N' = Suppress
  let $DynCF_Alignment_Option = 'L'          !ChartField Label Alignment - 'L' = Left, 'R' = Right, 'C' = Center
  let $DynCF_CF_Headings = 'N'               !ChartField Label Heading - 'Y' = Print, 'N' = Suppress
  let $DynCF_LabelName_Option = 'S'          !ChartField Label - 'L' = LongName, 'S' = ShortName
  let $DynCF_Include_Affiliate = 'Y'         !Include Affiliate - 'Y' = Yes, 'N' = No
  let $DynCF_LanguageCd = $language_cd       !ChartField Language Code
  let #DynCF_StrCol = 28                     !ChartField Starting Column
  let #DynCF_EndCol = 170                    !ChartField Ending Column

  do Create-CF-Array
  do Add-to-Exclude-CF-List ('ACCOUNT')
  do Add-to-Exclude-CF-List ('DEPTID')
  do Build-CF-Array

  let #StrLine    = 1
  !------------------------------------------------------------------------------------------------------!
  ! End of Dynamic ChartFields Variables setup                                                           !
  !------------------------------------------------------------------------------------------------------!

end-procedure


!****************
begin-heading 11
!****************

#Include 'stdhdgtr.sqc'

  let $buhr_line = $PAY059_BU_HR||'  '||$business_unit_hr
  let $bugl_line = $PAY059_BU_GL||'  '||$business_unit_gl
  let $jt_line   = $PAY059_JRNL_TMPLT||'    '||$journal_template

  let $runid_line    = $PAY059_RUN_ID||'   '||$run_id
  let $company_line  = $PAY059_COMPANY||'  '||$company
  let $paygroup_line = $PAY059_PAYGROUP||' '||$paygroup
  let $end_dt_line   = $PAY059_END_DT||' '||$pay_end_dt

  if rtrim($run_id,' ') <> ''
     let $right_hdr_line_1 = $runid_line
     let $right_hdr_line_2 = ' '
     let $right_hdr_line_3 = ' '
  else
     let $right_hdr_line_1 = $company_line
     let $right_hdr_line_2 = $paygroup_line
     let $right_hdr_line_3 = $end_dt_line
  end-if


  let #right_margin = {ColR} - 2



  print $buhr_line              (+2,1)
  print $right_hdr_line_1       (,#right_margin)
  print $bugl_line              (+1,1)
  print $right_hdr_line_2       (,#right_margin)
  print $jt_line                (+1,1)
  print $right_hdr_line_3       (,#right_margin)


  print $PAY059_GL_HDG1       (+1,33,22)
  print $PAY059_PY_HDG1       (,81)
  print $PAY059_GL_HDG2       (,102)

  print $PAY059_CURR_CD       (,122)
  print $PAY059_CONVERSION    (,142)

  print $PAY059_LINE_DESCR    (+1,1,30)
  print $PAY059_ACCOUNT       (,+2,10)
  print $PAY059_DEPT          (,+2,10)
  print $PAY059_CHECK         (,+2,10)
  print $PAY059_CHECK_DT      (,+2,10)
  print $PAY059_FOREIGN_AMT   (,+2,15)
  print $PAY059_MONETARY_AMT  (,+6,16)
  print $PAY059_PY_HDG2       (,+5,7)
  print $PAY059_GL_HDG3       (,+2,3)
  print $PAY059_MULTIPLIER    (,+2,15)
  print $PAY059_DIVISOR       (,+2,15)
  print $PAY059_DOC_ID        (+1,1,31)
  print $PAY059_DOC_LINE_NBR  (,33)
  print $PAY059_FISCAL_YEAR   (,45)
  print $PAY059_BUDGET_YEAR   (,57)


end-HEADING

!*********************************
begin-procedure REPORT-TRANSLATION
!*********************************

  let $language_cd = $PRCS_LANGUAGE_CD
  do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PAY059', 'ACCOUNT',          $PAY059_ACCOUNT,     #DW)
  do Get_Field_Information ('PAY059', 'AFFILIATE',        $PAY059_AFFILIATE,   #DW)
  do Get_Field_Information ('PAY059', 'ALT_ACCT',         $PAY059_ALT_ACCT,    #DW)
  do Get_Field_Information ('PAY059', 'AMOUNT',           $PAY059_AMOUNT,      #DW)
  do Get_Field_Information ('PAY059', 'BUDGET_REF',       $PAY059_BUDGET_REF,  #DW)
  do Get_Field_Information ('PAY059', 'BU_GL',            $PAY059_BU_GL,       #DW)
  do Get_Field_Information ('PAY059', 'BU_HR',            $PAY059_BU_HR,       #DW)
  do Get_Field_Information ('PAY059', 'BUDGET_YEAR',      $PAY059_BUDGET_YEAR, #DW)
  do Get_Field_Information ('PAY059', 'CHARTFIELD1',      $PAY059_CHARTFIELD1, #DW)
  do Get_Field_Information ('PAY059', 'CHARTFIELD2',      $PAY059_CHARTFIELD2, #DW)
  do Get_Field_Information ('PAY059', 'CHARTFIELD3',      $PAY059_CHARTFIELD3, #DW)
  do Get_Field_Information ('PAY059', 'CHARTFLD_DTL',     $PAY059_CHARTFLD_DTL,#DW)
  do Get_Field_Information ('PAY059', 'CHECK',            $PAY059_CHECK,       #DW)
  do Get_Field_Information ('PAY059', 'CHECK_DT',         $PAY059_CHECK_DT,    #DW)
  do Get_Field_Information ('PAY059', 'CLASS',            $PAY059_CLASS,       #DW)
  do Get_Field_Information ('PAY059', 'COMPANY',          $PAY059_COMPANY,     #DW)
  do Get_Field_Information ('PAY059', 'CONVERSION',       $PAY059_CONVERSION,  #DW)
  do Get_Field_Information ('PAY059', 'CURR_CD',          $PAY059_CURR_CD,     #DW)
  do Get_Field_Information ('PAY059', 'DEPT',             $PAY059_DEPT,        #DW)
  do Get_Field_Information ('PAY059', 'DIVISOR',          $PAY059_DIVISOR,     #DW)
  do Get_Field_Information ('PAY059', 'DOC_ID',           $PAY059_DOC_ID,      #DW)
  do Get_Field_Information ('PAY059', 'DOC_LINE_NBR',     $PAY059_DOC_LINE_NBR,#DW)
  do Get_Field_Information ('PAY059', 'END_DT',           $PAY059_END_DT,      #DW)
  do Get_Field_Information ('PAY059', 'FISCAL_YEAR',      $PAY059_FISCAL_YEAR, #DW)
  do Get_Field_Information ('PAY059', 'FOREIGN_AMT',      $PAY059_FOREIGN_AMT, #DW)
  do Get_Field_Information ('PAY059', 'FUND',             $PAY059_FUND,        #DW)
  do Get_Field_Information ('PAY059', 'GL_HDG1',          $PAY059_GL_HDG1,     #DW)
  do Get_Field_Information ('PAY059', 'GL_HDG2',          $PAY059_GL_HDG2,     #DW)
  do Get_Field_Information ('PAY059', 'GL_HDG3',          $PAY059_GL_HDG3,     #DW)
  do Get_Field_Information ('PAY059', 'JRNL_TMPLT',       $PAY059_JRNL_TMPLT,  #DW)
  do Get_Field_Information ('PAY059', 'LINE_DESCR',       $PAY059_LINE_DESCR,  #DW)
  do Get_Field_Information ('PAY059', 'MONETARY_AMT',     $PAY059_MONETARY_AMT,#DW)
  do Get_Field_Information ('PAY059', 'MULTIPLIER',       $PAY059_MULTIPLIER,  #DW)
  do Get_Field_Information ('PAY059', 'OPER_UNIT',        $PAY059_OPER_UNIT,   #DW)
  do Get_Field_Information ('PAY059', 'PAYGROUP',         $PAY059_PAYGROUP,    #DW)
  do Get_Field_Information ('PAY059', 'PRODUCT',          $PAY059_PRODUCT,     #DW)
  do Get_Field_Information ('PAY059', 'PROGRAM',          $PAY059_PROGRAM,     #DW)
  do Get_Field_Information ('PAY059', 'PROJECT',          $PAY059_PROJECT,     #DW)
  do Get_Field_Information ('PAY059', 'PY_HDG1',          $PAY059_PY_HDG1,     #DW)
  do Get_Field_Information ('PAY059', 'PY_HDG2',          $PAY059_PY_HDG2,     #DW)
  do Get_Field_Information ('PAY059', 'REPORT_TITLE',     $REPORTTITLE,        #DW)
  do Get_Field_Information ('PAY059', 'RUN_ID',           $PAY059_RUN_ID,      #DW)
  do Get_Field_Information ('PAY059', 'TOTAL',            $PAY059_TOTAL,       #DW)

end-procedure

!****************************
begin-procedure Process-Main
!****************************

    let #total_foreign_amount  = 0
    let #total_monetary_amount = 0
!   let #grd_total_foreign_amt = 0
!   let #grd_total_mnetary_amt = 0

begin-SELECT

A.RUN_DT                () on-break  print=never before=get-run-id level = 1
A.SEQNUM                () on-break  print=never before=get-run-id level = 1
A.BUSINESS_UNIT         () on-break  print=never after=change-page level = 1
A.BUSINESS_UNIT_GL
A.APPL_JRNL_ID
A.LINE_DESCR            (+1,1,30) on-break print=always skiplines = 2 after=print-totals level = 2
A.ACCOUNT               (,+2,10)
A.DEPTID                (,+2,10)
A.PROJECT_ID
A.PRODUCT
A.FUND_CODE
A.PROGRAM_CODE
A.CLASS_FLD
A.AFFILIATE
A.OPERATING_UNIT
A.ALTACCT
A.BUDGET_REF
A.CHARTFIELD1
A.CHARTFIELD2
A.CHARTFIELD3
A.AFFILIATE_INTRA1
A.AFFILIATE_INTRA2
A.JRNL_LN_REF           (,+2,10)
A.ACCOUNTING_DT
    do Format-Datetime (&A.accounting_dt, $AcctDate, {DEFMDY},'','')
    print $AcctDate     (,+2,10)
A.FOREIGN_AMOUNT        (,+2,19)  edit 999999999999999.99
A.MONETARY_AMOUNT       (,+2,19) edit 999999999999999.99
A.FOREIGN_CURRENCY      (,+4,3)
A.CURRENCY_CD           (,+4,3)
A.RATE_MULT             (,+2,15)
A.RATE_DIV              (,+1,15)
A.BUSINESS_UNIT_PC
A.ACTIVITY_ID
A.RESOURCE_TYPE
A.RESOURCE_CATEGORY
A.RESOURCE_SUB_CAT
A.HP_KK_DOC_ID          (+1,1)
A.HP_KK_DOC_LINE_NBR    (,33)
A.HP_FISCAL_YEAR        (,45)
A.HP_BUDGET_YEAR        (,57)



    let $PROJECT_ID         = RTRIM(&A.PROJECT_ID, ' ')
    let $PRODUCT            = RTRIM(&A.PRODUCT, ' ')
    let $FUND_CODE          = RTRIM(&A.FUND_CODE, ' ')
    let $PROGRAM_CODE       = RTRIM(&A.PROGRAM_CODE, ' ')
    let $CLASS_FLD          = RTRIM(&A.CLASS_FLD, ' ')
    let $AFFILIATE          = RTRIM(&A.AFFILIATE, ' ')
    let $OPERATING_UNIT     = RTRIM(&A.OPERATING_UNIT, ' ')
    let $ALTACCT            = RTRIM(&A.ALTACCT, ' ')
    let $BUDGET_REF         = RTRIM(&A.BUDGET_REF, ' ')
    let $CHARTFIELD1        = RTRIM(&A.CHARTFIELD1, ' ')
    let $CHARTFIELD2        = RTRIM(&A.CHARTFIELD2, ' ')
    let $CHARTFIELD3        = RTRIM(&A.CHARTFIELD3, ' ')
    let $AFFILIATE_INTRA1   = RTRIM(&A.AFFILIATE_INTRA1, ' ')
    let $AFFILIATE_INTRA2   = RTRIM(&A.AFFILIATE_INTRA2, ' ')
    let $BUSINESS_UNIT_PC   = RTRIM(&A.BUSINESS_UNIT_PC, ' ')
    let $ACTIVITY_ID        = RTRIM(&A.ACTIVITY_ID, ' ')
    let $RESOURCE_TYPE      = RTRIM(&A.RESOURCE_TYPE, ' ')
    let $RESOURCE_CATEGORY  = RTRIM(&A.RESOURCE_CATEGORY, ' ')
    let $RESOURCE_SUB_CAT   = RTRIM(&A.RESOURCE_SUB_CAT, ' ')


    do Format-CF-Print-Line ($DynCF_Filler, $DynCF_Filler, $PROJECT_ID, $PRODUCT, $FUND_CODE, $PROGRAM_CODE,
                             $CLASS_FLD, $AFFILIATE, $OPERATING_UNIT, $ALTACCT, $BUDGET_REF, $CHARTFIELD1,
                             $CHARTFIELD2, $CHARTFIELD3, $AFFILIATE_INTRA1, $AFFILIATE_INTRA2,
                             $BUSINESS_UNIT_PC, $ACTIVITY_ID, $RESOURCE_TYPE, $RESOURCE_CATEGORY,
                             $RESOURCE_SUB_CAT)

    if   $PROJECT_ID       <> ''
      or $PRODUCT          <> ''
      or $FUND_CODE        <> ''
      or $PROGRAM_CODE     <> ''
      or $CLASS_FLD        <> ''
      or $AFFILIATE        <> ''
      or $OPERATING_UNIT   <> ''
      or $ALTACCT          <> ''
      or $BUDGET_REF       <> ''
      or $CHARTFIELD1      <> ''
      or $CHARTFIELD2      <> ''
      or $CHARTFIELD3      <> ''
      or $AFFILIATE_INTRA1 <> ''
      or $AFFILIATE_INTRA2 <> ''
      or $BUSINESS_UNIT_PC <> ''
      or $ACTIVITY_ID      <> ''
      or $RESOURCE_TYPE    <> ''
      or $RESOURCE_CATEGORY <> ''
      or $RESOURCE_SUB_CAT <> ''

        print $PAY059_CHARTFLD_DTL     (+1,6,20)
        let #AdvLineNo = 0
        do Print-CF-Labels-and-Values (#AdvLineNo, #StrLine)
    end-if

    let $business_unit_hr = &A.BUSINESS_UNIT
    let $business_unit_gl = &A.BUSINESS_UNIT_GL
    let $journal_template = &A.APPL_JRNL_ID
    do Format-Datetime (&A.RUN_DT, $run_date, {DEFMDY},'','')
    move &A.SEQNUM to $run_seq
    let $run_id = &B.Run_id

    let #total_foreign_amount   = #total_foreign_amount   + &A.FOREIGN_AMOUNT
    let #total_monetary_amount  = #total_monetary_amount  + &A.MONETARY_AMOUNT

!   let #grd_total_foreign_amt  = #grd_total_foreign_amt  + &A.FOREIGN_AMOUNT
!   let #grd_total_mnetary_amt  = #grd_total_mnetary_amt  + &A.MONETARY_AMOUNT

FROM PS_HP_KK_ENC_LN A
[$WhereParam]
ORDER BY A.RUN_DT, A.SEQNUM, A.BUSINESS_UNIT, A.LINE_DESCR, A.JRNL_LN_REF


end-SELECT

end-procedure


!****************************
begin-procedure print-totals
!****************************

  print '-' (+1,81,19) fill
  print '-' (,102,19) fill

  print $PAY059_TOTAL (+1,1)
  print #total_foreign_amount  (,80)  edit 9999999999999999.99
  print #total_monetary_amount (,101)  edit 9999999999999999.99

  let #total_foreign_amount  = 0
  let #total_monetary_amount = 0

end-procedure print-totals


!*********************************
begin-procedure print-grand-total
!*********************************

!  print '-' (+1,81,19) fill
!  print '-' (,102,19)   fill

!  print 'Grand Totals '        (+1,1)
!  print #grd_total_foreign_amt (,80)   edit 99999999999999999.99
!  print #grd_total_mnetary_amt (,101)   edit 99999999999999999.99

end-procedure print-grand-total



!***************************
begin-procedure change-page
!***************************

 new-page

end-procedure change-page


!**************************
begin-procedure get-run-id
!**************************

begin-SELECT

B.RUN_ID
B.COMPANY
B.PAYGROUP
B.PAY_END_DT

  let $run_id      = &B.RUN_ID
  let $company     = &B.COMPANY
  let $paygroup    = &B.PAYGROUP
  do Format-Datetime (&B.PAY_END_DT, $pay_end_dt, {DEFMDY},'','')


FROM PS_GL_GEN_HISTORY B
WHERE B.RUN_DT = &A.RUN_DT AND B.SEQNUM = &A.SEQNUM

end-SELECT

end-procedure get-run-id

!******************************
begin-procedure Get-Run-Control
!******************************

  let $WhereParam = ''

begin-select
RC.HP_RUN_DT
RC.HP_THRU_DT_RUN
RC.BUSINESS_UNIT_GL

  let $RC_RunDt     =  ltrim(rtrim(&RC.HP_RUN_DT,' '),' ')
  let $RC_ThruDt    =  ltrim(rtrim(&RC.HP_THRU_DT_RUN,' '),' ')
  let $RC.BusUnitGL =  ltrim(rtrim(&RC.BUSINESS_UNIT_GL,' '),' ')

  if $RC_RunDt = ''
      let $WhereParam = ''
  else
      let $WhereParam = 'WHERE A.RUN_DT BETWEEN ' || '''' || $RC_RunDt || '''' ||
                        ' AND ' || '''' || $RC_ThruDt || ''''
  end-if

  if $RC.BusUnitGL <> ''
      if $WhereParam = ''
          let $WhereParam = 'WHERE A.BUSINESS_UNIT_GL = ' || '''' || $RC.BusUnitGL || ''''
      else
          let $WhereParam = $WhereParam || ' AND A.BUSINESS_UNIT_GL = ' || '''' || $RC.BusUnitGL || ''''
      end-if
  end-if

from PS_RUN_CNTL_PAY059 RC
where RC.OPRID = $prcs_oprid
  and RC.RUN_CNTL_ID = $prcs_run_cntl_id
end-select

end-procedure Get-Run-Control


#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language


