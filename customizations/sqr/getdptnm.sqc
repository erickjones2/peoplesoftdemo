!*************************************************************
!  GetDptNm:  Get Department Name from the Department Table
!*************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:30:49                                 
!       $Release:  HR92                                                
!      $Revision:  102                                                 
!                                                                      *
!***********************************************************************
!
!***********************************************************************
! Modified for Education & Government                                  *
! HP99999       Release 8 Technical Merge                              *
!***********************************************************************

!**********************************
begin-procedure Get-Department-Name
!**********************************

  !Keys passed in:  $DeptID, $BU, $AsOfDate
  !Values returned: $DeptName, $DeptNameAbbrv, $DeptMgrID, $DeptLocation

  move '' to $DeptName
  move '' to $DeptNameAbbrv
  move '' to $DeptMgrID
  move '' to $DeptLocation

  if Rtrim($DeptID,' ') = ''
    goto GetDptNmExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
X.DESCR
X.DESCRSHORT
X.MANAGER_ID
X.LOCATION

  let $DeptName      = Rtrim(&X.DESCR,' ')
  let $DeptNameAbbrv = Rtrim(&X.DESCRSHORT,' ')
  let $DeptMgrID     = Rtrim(&X.MANAGER_ID,' ')
  let $DeptLocation  = Rtrim(&X.LOCATION,' ')

!Related Language keys...
X.SETID

FROM  PS_DEPT_TBL X
WHERE X.DEPTID = $DeptID
  AND X.SETID  =
       (SELECT SETID
        FROM   PS_SET_CNTRL_REC
        WHERE  SETCNTRLVALUE = $BU
          AND  RECNAME       = 'DEPT_TBL')
  AND X.EFFDT <= $AsOfDate
  AND X.EFFDT  =
       (SELECT MAX(EFFDT)
        FROM   PS_DEPT_TBL
        WHERE  DEPTID = X.DEPTID
          AND  SETID  = X.SETID
          AND  EFFDT <= $AsOfDate)
end-SELECT

  let $SetIDLang  = Rtrim(&X.SETID,' ')
  do Get-Department-Name-Lang

GetDptNmExit:

end-procedure

!***************************************
begin-procedure Get-Department-Name-Lang
!***************************************

  !Keys passed in:  $DeptID, $SetIDLang, $AsOfDate
  !Values returned: $DeptName, $DeptNameAbbrv

  if $Curr_Language_Cd = $PSOptions_Language_Cd
    goto GetDptNmLngExit
  end-if

  if Rtrim($DeptID,' ') = '' or Rtrim($SetIDLang,' ') = ''
    goto GetDptNmLngExit
  end-if

  if $AsOfDate = ''
    move $AsOfToday to $AsOfDate
  end-if

begin-SELECT
XL.DESCR
XL.DESCRSHORT

  if Rtrim(&XL.DESCR,' ') <> ''
    let $DeptName = Rtrim(&XL.DESCR,' ')
  end-if
  if Rtrim(&XL.DESCRSHORT,' ') <> ''
    let $DeptNameAbbrv = Rtrim(&XL.DESCRSHORT,' ')
  end-if

FROM  PS_DEPT_TBL_LANG XL
WHERE XL.DEPTID      = $DeptID
  AND XL.SETID       = $SetIDLang
  AND XL.LANGUAGE_CD = $Curr_Language_Cd
  AND XL.EFFDT      <= $AsOfDate
  AND XL.EFFDT =
        (SELECT MAX(EFFDT)
         FROM  PS_DEPT_TBL_LANG
         WHERE DEPTID      = XL.DEPTID
           AND SETID       = XL.SETID
           AND LANGUAGE_CD = XL.LANGUAGE_CD
           AND EFFDT      <= $AsOfDate)
end-SELECT

GetDptNmLngExit:

end-procedure
                                                                        !HP99999
!***********************************************************************!HP99999
!  Get-Department-Name-with-SetID             PUBSEC                   *!HP99999
!***********************************************************************!HP99999
begin-procedure Get-Department-Name-with-SetID                          !HP99999
  if rtrim($DeptID,' ') = ''                                            !HP99999
    move '' to $DeptName                                                !HP99999
    move '' to $DeptNameAbbrv                                           !HP99999
    goto GetDptNmSetIDExit                                              !HP99999
  end-if                                                                !HP99999
  if $AsOfDate = ''  !if no As-Of-Date is specified, default to Today   !HP99999
    move $AsOfToday to $AsOfDate                                        !HP99999
  end-if                                                                !HP99999
begin-SELECT                                                            !HP99999
X1.DESCR,X1.DESCRSHORT,X1.MANAGER_ID,X1.LOCATION                        !HP99999
X1.HP_STATS_DEPT_CD,X1.HP_STATS_FACULTY,X1.HP_STATS_DPCD_NW             !F_CCONLEY_3
FROM  PS_DEPT_TBL X1                                                    !HP99999
WHERE X1.DEPTID = $DeptID                                               !HP99999
  AND X1.SETID  = $SetID                                                !HP99999
  AND X1.EFFDT =                                                        !HP99999
      (SELECT MAX(Y1.EFFDT)                                             !HP99999
       FROM   PS_DEPT_TBL Y1                                            !HP99999
       WHERE  Y1.DEPTID = X1.DEPTID                                     !HP99999
         AND  Y1.SETID =  X1.SETID                                      !HP99999
         AND  Y1.EFFDT <= $AsOfDate)                                    !HP99999
end-SELECT                                                              !HP99999
  let $DeptName = rtrim(&X1.Descr,' ')                                  !HP99999
  let $DeptNameAbbrv = rtrim(&X1.DescrShort,' ')                        !HP99999
  let $DeptMgrID = rtrim(&X1.Manager_ID,' ')                            !HP99999
  let $DeptLocation = rtrim(&X1.Location,' ')                           !HP99999
GetDptNmSetIDExit:                                                      !HP99999
end-procedure                                                           !HP99999

!***************************** End of Include File **************************
