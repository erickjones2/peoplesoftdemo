!***********************************************************************
! GPJPYE03.sqr:  Wage Payment Summary Report                           *
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2017, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!       $Release:  HR92                                                !
!           $Bug:  26097469                                            !
!                                                                      *
!***********************************************************************

#include 'setenv.sqc'   !Set environment
#include 'setup32.sqc'  !page layout and printer definition


begin-report


        do Init-Processing

        do Process-Main

        do Reset

        do Update-Prcs-Run-Status

end-report


!***********************************************************************
!                                                                      *
! Init-Processing                                                      *
!                                                                      *
!                                                                      *
! Called By:    begin-report                                           *
!                                                                      *
! Calls:        Init-Datetime           - datetime.sqc                 *
!               Get-Current-Datetime    - prcsapi.sqc (curdttim.sqc)   *
!               Get-Run-Control-Parms   - prcsapi.sqc                  *
!               Get-Language-Codes      - prcslng.sqc                  *
!               Get-GPJPYE03-runctl                                    *
!               Init-Report                                            *
!               Get-Estab-Name          - Getestab.sqc                 *
!                                                                      *
! Description:  Standard PeopleSoft initialization processing.         *
!                                                                      *
!***********************************************************************
begin-procedure Init-Processing


        do Init-Datetime

        do Get-Current-Datetime

        do Define-Prcs-Vars

        do Get-Run-Control-Parms

        do Get-Language-Codes

        do Get-GPJPYE03-runctl

        do Init-Report

        let $Estab  = $Rc_Gpjp_tax_estab
        let $EstabType = 'IT'
        do Get-Estab-Name


end-procedure ! Init-Processing


!***********************************************************************
!                                                                      *
! Get-GPJPYE03-runctl                                                  *
!                                                                      *
!                                                                      *
! Called By:    Init-Processing                                        *
!                                                                      *
! Description:  Get data from GPJP_RC_YEA runcontrol record.          *
!                                                                      *
!***********************************************************************
begin-procedure Get-GPJPYE03-runctl


begin-select

RC.GPJP_TAX_ESTAB
RC.DATE_YEAR_VAL



FROM  PS_GPJP_RC_YEA RC
WHERE RC.OPRID          = $Prcs_OprID
AND   RC.RUN_CNTL_ID    = $Prcs_Run_Cntl_ID

end-select
     let #Rc_date_year_val  = &RC.Date_year_val
     move #Rc_date_year_val to $Rc_date_year_val 9999
     let $Rc_Gpjp_tax_estab = RTRIM(&RC.Gpjp_tax_estab, ' ')
BEGIN-SELECT
ESTAB.FIRM_NUM_JPN
  
   let #Firm_Number =  &ESTAB.FIRM_NUM_JPN

FROM PS_GPJP_TAX_ESTAB ESTAB
WHERE ESTAB.GPJP_TAX_ESTAB = $Rc_Gpjp_tax_estab
AND ESTAB.GPJP_TAX_ESTAB_TYP = 'IT'
AND ESTAB.EFFDT = (SELECT MAX(EFFDT)
FROM PS_GPJP_TAX_ESTAB H
WHERE H.GPJP_TAX_ESTAB = ESTAB.GPJP_TAX_ESTAB
AND H.GPJP_TAX_ESTAB_TYP = 'IT'
AND H.EFFDT <= $AsOfToday)
END-SELECT




end-procedure !Get-GPJPYE03-runctl


!***********************************************************************
!                                                                      *
! Init-Report                                                          *
!                                                                      *
!                                                                      *
! Called By:    Init-Processing                                        *
!                                                                      *
! Calls:        Init_Report_Translation    - sqrtrans.sqc              *
!               Append_Report_Translation  - sqrtrans.sqc              *
!               Get_Field_Information                                  *
!                                                                      *
! Description:  Initializes variables, defines values for variables    *
!               used in report header & throughout report.  Performs   *
!               Strings Table translation.  #Defines variables for     *
!               use throughout report in printing columns.             *
!                                                                      *
!***********************************************************************
begin-procedure Init-Report


!***********************************************************************
!                                                                      *
! Translations procedures & Strings Table retrievals.                  *
!                                                                      *
!***********************************************************************

    do Init_Report_Translation ($ReportID, $curr_language_cd)

    do Append_Report_Translation ('GPJPYE03')

    do Get_Field_Information ('GPJPYE03','TITLE',              $GPJPYE03_Title,                  #dummy_width)
    do Get_Field_Information ('GPJPYE03','SALARY_PAYER',       $GPJPYE03_Salary_payer,           #dummy_width)
    do Get_Field_Information ('GPJPYE03','NUMBER',             $GPJPYE03_Number,                 #dummy_width)
    do Get_Field_Information ('GPJPYE03','MUNIC_CODE',         $GPJPYE03_Munic_code,             #dummy_width)
    do Get_Field_Information ('GPJPYE03','MUNIC_NAME',         $GPJPYE03_Munic_name,             #dummy_width)
    do Get_Field_Information ('GPJPYE03','SPECIFIED_NUM',      $GPJPYE03_Specified_num,          #dummy_width)
    do Get_Field_Information ('GPJPYE03','NUM_SAL_PAYER',      $GPJPYE03_Num_sal_payer,          #dummy_width)
    do Get_Field_Information ('GPJPYE03','NUM_TERM_PAYER',     $GPJPYE03_Num_term_payer,         #dummy_width)
    do Get_Field_Information ('GPJPYE03','TOTAL',              $GPJPYE03_Total,                  #dummy_width)
    do Get_Field_Information ('GPJPYE03','SPES_NOT_FOUND',     $GPJPYE03_Spes_not_found,         #dummy_width)
    do Get_Field_Information ('GPJPYE03','FIRM_NBR',           $GPJPYE03_Firm_Nbr,               #dummy_width)

!***********************************************************************
!                                                                      *
! Report variables.                                                    *
!                                                                      *
!***********************************************************************

    let $ReportID              = 'GPJPYE03'          ! Printed in header
    let $ReportTitle           = $GPJPYE03_Title     ! Printed in header
    let $ReportDate            = $AsOfToday          ! Printed in header
    let $ReportTime            = $AsOfNow            ! Printed in header
    let $Report_Language       = $curr_language_cd   ! curr_language_cd resolved in prcslng.sqc
    let $_TableAlias = 'A'
    do Security-Param
end-procedure ! Init-Report


!***************************
begin-heading 7
!***************************

#include 'stdhdgtr.sqc'
  print $Rc_date_year_val          (3,,) center
  print $GPJPYE03_Salary_payer     (+1,1,35)
  print $Estab                     (0,37,15)
  print $EstabName                 (0,52,30)
  print $GPJPYE03_Firm_Nbr         (+1,1,35)
  print #Firm_Number               (0,37,15) edit 9999999999999
  print $GPJPYE03_Number           (+3,5,5)
  print $GPJPYE03_Munic_code       (0,20,22)
  print $GPJPYE03_Munic_name       (0,45,25)
  print $GPJPYE03_Specified_num    (0,70,25)
  print $GPJPYE03_Num_sal_payer    (0,100,28)
  print $GPJPYE03_Num_term_payer   (0,130,28)


end-heading

!***********************************************************************
!                                                                      *
! Process-Main                                                         *
!                                                                      *
!                                                                      *
! Called By:    Begin-Report                                           *
!                                                                      *
! Description:  Main processing                                        *
!                                                                      *
!***********************************************************************
begin-procedure Process-Main

let $Municipality_save  = ' '
let #Munic_number       = 0
let #Munic_count        = 0
let #Munic_term_count   = 0
let $No_records         = 'Y'

begin-select
A.GPJP_TAX_ESTAB
C.GPJP_MUNC_RPT_CD
A.GPJP_YEA_HIRMID

  let $No_records    = 'N'

  if $Municipality_save = ' '
     let $Municipality_save = Rtrim(&C.GPJP_MUNC_RPT_CD,' ')
  end-if

  if &C.GPJP_MUNC_RPT_CD <>  $Municipality_save

     move $Municipality_save to $Munic_code
     do Munic-Change
     let $Municipality_save = Rtrim(&C.GPJP_MUNC_RPT_CD,' ')
  end-if

      if &A.GPJP_YEA_HIRMID = 2
    add 1 to #Munic_term_count
    add 1 to #Munic_term_total
    end-if


  add 1 to #Munic_count
  add 1 to #Munic_total


FROM PS_GPJP_YEA_TABLE A, PS_GPJP_IH_MUNIC2 C
WHERE A.DATE_YEAR_VAL = #Rc_date_year_val
AND A.GPJP_TAX_ESTAB = $Rc_Gpjp_tax_estab
AND A.GPJP_TAX_ESTAB = C.GPJP_TAX_ESTAB
AND C.EFF_STATUS = 'A'
AND C.GPJP_MUNC_IH_CD = A.MUNICIPAL_CD_JPN
AND C.EFFDT = (SELECT Max(C_ED.EFFDT) FROM PS_GPJP_IH_MUNIC2 C_ED
              WHERE C_ED.MUNICIPAL_CD_JPN = C.MUNICIPAL_CD_JPN
              AND C_ED.GPJP_TAX_ESTAB = C.GPJP_TAX_ESTAB
              AND C_ED.EFF_STATUS = 'A'
              AND C_ED.EFFDT <= $AsOfToday)
            AND EXISTS ( 
 SELECT MC1.MUNICIPAL_CD_JPN 
  FROM PS_GPJP_IH_MUNIC2 MC1 
 WHERE MC1.MUNICIPAL_CD_JPN = C.MUNICIPAL_CD_JPN 
 AND MC1.GPJP_TAX_ESTAB = C.GPJP_TAX_ESTAB
   AND MC1.MUNICIPAL_CD_JPN = ( 
 SELECT  MUNICIPAL_CD_JPN 
  FROM PS_POSTAL_TBL_JPN 
 WHERE POSTAL = ( 
 SELECT ADR.POSTAL 
  FROM PS_ADDRESSES ADR 
 WHERE ADR.EMPLID = A.EMPLID
   AND ADR.ADDRESS_TYPE IN ('HOME', 'LEGL') 
   AND ADR.EFF_STATUS = 'A' 
   AND ADR.EFFDT = ( 
 SELECT MAX(ADR_ED.EFFDT) 
  FROM PS_ADDRESSES ADR_ED 
 WHERE ADR_ED.EMPLID = ADR.EMPLID 
   AND ADR_ED.ADDRESS_TYPE = ADR.ADDRESS_TYPE 
   AND ADR_ED.EFFDT <= $AsOfToday)) )) 
[$SecurityClausewithERN]
ORDER BY C.GPJP_MUNC_RPT_CD

end-select

if $No_records <> 'Y'
   let $Munic_code = Rtrim(&C.GPJP_MUNC_RPT_CD,' ')
 ! SHOW $Munic_code
    do Munic-Change
   do Print-total-line
end-if


end-procedure ! Process-Main


!***********************************************************************
!                                                                      *
! Print-Details                                                        *
!                                                                      *
!                                                                      *
! Called By:    Main Proceduer                                         *
!                                                                      *
! Description:  Prints the detail lines                                *
!                                                                      *
!***********************************************************************
begin-procedure Print-Details



    add 1 to #Munic_number
    print  #Munic_number                 (+1,4,4)  edit 9990
    print  $Munic_code                   (0,20,6)
    print  $Munic_descr                  (0,45,30)
    print  $Specified_number             (0,70,36)
    print  #Munic_count                  (0,115,7) edit 999,990
    print  #Munic_term_count             (0,149,7) edit 999,990



end-procedure  !Print-Details

!***********************************************************************
!                                                                      *
! Print-total-line                                                     *
!                                                                      *
!                                                                      *
! Called By:    Main Procedure                                         *
!                                                                      *
! Description:  Prints the detail lines                                *
!                                                                      *
!***********************************************************************
begin-procedure Print-total-line



    print  $GPJPYE03_Total               (+2,70,35)
    print  #Munic_total                  (0,111,11) edit 999,999,990
    print  #Munic_term_total             (0,145,11) edit 999,999,990



end-procedure  !Print-total-line


!***********************************************************************
!                                                                      *
! Munic-Change                                                         *
!                                                                      *
!                                                                      *
! Called By:    Main Procedure                                         *
!                                                                      *
! Description:  This procedure is executed whenever a municipality     *
!               change is detected.                                    *
!                                                                      *
!***********************************************************************
begin-procedure Munic-Change

    do Get-Munic-Descr
    do Get-Munic-spcl
    do Print-Details
    let #Munic_count = 0
    let #Munic_term_count = 0

end-procedure  !Munic-Change

!***********************************************************************
!                                                                      *
! Get-Munic-spcl                                                       *
!                                                                      *
!                                                                      *
! Called By:    Munic-Change                                           *
!                                                                      *
! Description:  This procedure will retrieve the specified number      *
!               for a Municipality code and Salary Payer from the      *
!               GPJP_IH_MUNIC2 table.                                  *
!                                                                      *
!***********************************************************************
begin-procedure Get-Munic-spcl

let $Specified_number = ' '

begin-select
B.GPJP_TAX_ESTAB
B.GPJP_IH_SPEC_CLC

    let $Specified_number       = Rtrim(&B.Gpjp_ih_spec_clc,' ')


FROM PS_GPJP_IH_MUNIC2 B
WHERE B.GPJP_TAX_ESTAB = $Rc_Gpjp_tax_estab
  AND B.GPJP_MUNC_RPT_CD = $Munic_code
  AND B.EFFDT = (SELECT MAX(B2.EFFDT)
                FROM   PS_GPJP_IH_MUNIC2 B2
                WHERE  B2.GPJP_TAX_ESTAB   = B.GPJP_TAX_ESTAB
                AND    B2.MUNICIPAL_CD_JPN = B.MUNICIPAL_CD_JPN
                AND    B2.EFFDT <= $AsOfToday
                AND    B2.EFF_STATUS = 'A')
end-select

if $Specified_number = ' '
   let $Specified_number = $GPJPYE03_Spes_not_found
end-if


end-procedure  !Get-Munic-spcl




!***********************************************************************
!                                                                      *
!                                                                      *
! SQCS included in the report                                          *
!                                                                      *
!***********************************************************************
#include 'getestab.sqc'     ! Get Establishment Data from the GPJP_TAX_ESTAB
#include 'getmunic.sqc'     ! Get Municipality Description from GPJP_IH_MUNIC
#include 'datemath.sqc'     ! Date conversion procedures
#include 'hrsecty.sqc'      !Get SQR Security parameters
#include 'number.sqc'       ! Routines to format numbers
#include 'datetime.sqc'     ! Routines for date and time formatting
#include 'sqrtrans.sqc'     ! Translate SQR strings to given language
#include 'prcsapi.sqc'      ! Replaces stdapi.sqc
#include 'prcslng.sqc'      ! Replaces getrplng.sqc
#include 'readxlat.sqc'     ! Procedure to read values from xlattable
#include 'reset.sqc'        ! Reset printer procedure
