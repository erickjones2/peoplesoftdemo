!***********************************************************************
! GPNZRCL1.SQC:  Retrieve Run Control for the parameters of the        *
!                Global Payroll Result Register Report                 *
!----------------------------------------------------------------------*
!                                                                      *
!   This SQC is used for GPNZ reports.  It is used to select the       *
!   data entered by the user off the run control panel.  This data is  *
!   then used by the calling SQR to determine for what population the  *
!   report should be run.  It uses several sub-procedures to string    *
!   paygroups or employees selected by the user on the run control     *
!   panel.                                                             *
!                                                                      *
!   Called by : GPNZPY11, GPNZPY12, GPNZPY13, GPNZPY14                 *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!----------------------------------------------------------------------*
!                                                                      *
!          $Date:  2013/01/18:03:59:00                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

!***********************************************************************
! Select-Parameters                                                    *
!                                                                      *
! Calls:        Select-Paygroups                                       *
!               Select-Employees                                       *
!               Select-Elements                                        *
! Description:  Selects level zero information off of run control.     *
!               Calls the appropriate sub-procedure, if necessary,     *
!                    to string the selected paygroup or employee data. *
!                                                                      *
! 707230000 : To print Accumulator values on Payroll Register and      *
!              Component reports.                                      *
!***********************************************************************


begin-procedure Select-Parameters

    let $Quote                     = ''''
    let $Multiple_Calendars        = 'N'
    let $CalendarID                = ' '
    let $EE_Select_Clause          = ' '
    let $Employee_List             = ' '
    let $Paygroup_Select_Clause    = ' '
    let $ErnDed_Exclude_Clause     = ' '
    let $ErnDed_Exclude_Sum_Clause = ' '
    let $Accum_Include_Clause      = ' '

begin-SELECT
LVL0.OPRID                    &LVL0.OPRID
LVL0.RUN_CNTL_ID              &LVL0.RUN_CNTL_ID
LVL0.DATE_FROM                &LVL0.DATE_FROM
LVL0.DATE_TO                  &LVL0.DATE_TO
LVL0.PAY_ENTITY               &LVL0.PAY_ENTITY
LVL0.CAL_RUN_ID               &LVL0.CAL_RUN_ID
LVL0.GPNZ_RPT_SECTION         &LVL0.GPNZ_RPT_SECTION
LVL0.GPNZ_RPT_OPTION          &LVL0.GPNZ_RPT_OPTION
LVL0.GPNZ_RPT_YTD             &LVL0.GPNZ_RPT_YTD
LVL0.GPNZ_RPT_SUMMARY         &LVL0.GPNZ_RPT_SUMMARY
LVL0.GPNZ_SORT_DEPT           &LVL0.GPNZ_SORT_DEPT
LVL0.GPNZ_SORT_LOC            &LVL0.GPNZ_SORT_LOC
LVL0.GPNZ_SORT_PYNAME         &LVL0.GPNZ_SORT_PYNAME
LVL0.GPNZ_PAY_RPT_ID          &LVL0.GPNZ_PAY_RPT_ID

    let $Oprid                = &LVL0.OPRID
    let $Run_Cntl_ID          = &LVL0.RUN_CNTL_ID
    let $RC_Cal_Run_ID        = rtrim(&LVL0.CAL_RUN_ID,' ')
    let $RC_Pay_Entity        = rtrim(&LVL0.PAY_ENTITY,' ')
    let $Date_From            = &LVL0.DATE_FROM
    let $Date_To              = &LVL0.DATE_TO
    let #GPNZ_RPT_SECTION     = &LVL0.GPNZ_RPT_SECTION
    let #GPNZ_RPT_OPTION      = &LVL0.GPNZ_RPT_OPTION
    let $Rpt_Sections         = edit(#GPNZ_RPT_SECTION, '88')
    let $Rpt_Option           = edit(#GPNZ_RPT_OPTION, '88')
    let $Sort_Dept            = &LVL0.GPNZ_SORT_DEPT
    let $Sort_Loc             = &LVL0.GPNZ_SORT_LOC
    let $Sort_Pyname          = &LVL0.GPNZ_SORT_PYNAME
    let $Rpt_Summary          = &LVL0.GPNZ_RPT_SUMMARY
    let $Rpt_YTD              = &LVL0.GPNZ_RPT_YTD
    let $Rpt_ID               = rtrim(&LVL0.GPNZ_PAY_RPT_ID,' ')

    do Format-DateTime($Date_From, $out8, {DEFDATE}, '', '')
    do Format-DateTime($Date_To, $out9, {DEFDATE}, '', '')

    if $RC_Pay_Entity =  ''
        let $Cal_or_Pyent_Select_Clause  = 'SEGSTAT.CAL_RUN_ID = '''|| $RC_Cal_Run_ID ||''''
        let $Rpt_Summary          = 'N'
    else
          let $Select_Clause_2  = ' AND PRCSTAT.PRD_BGN_DT >= ''' || &LVL0.DATE_FROM ||''''
          let $Select_Clause_2  = $Select_Clause_2 || ' AND PRCSTAT.PRD_END_DT <= ''' || &LVL0.DATE_TO ||''''
          let $Cal_or_Pyent_Select_Clause  = 'PRCSTAT.PAY_ENTITY = ''' || $RC_Pay_Entity ||''''
          let $Cal_or_Pyent_Select_Clause  = $Cal_or_Pyent_Select_Clause || $Select_Clause_2
    end-if


    if $Rpt_Option = '30'            ! Population = Paygroups
        do Select-Paygroups
    end-if

    if $Rpt_Option = '40'            ! Population = Payees
        do Select-Employees
    end-if

    if $Rpt_Option = '20'            ! Population = Earns & Dedns
       Evaluate  $Rpt_Sections
          when = '10'                ! Section = Earnings
          when = '20'                ! Section = Deductions
            do Select-Earnings
            break
          when = '40'                ! Section = Accumulators
            do Select-Accum
            break
       end-evaluate
    end-if

    if $Rpt_ID <> ''

       if $Rpt_Sections    = '10'                   ! Section = Earnings
          or $Rpt_Sections = '20'                   ! Section = Deductions
          or $Rpt_Sections = '50'                   ! Section = Earns & Dedns
          or $Rpt_Sections = '60'                   ! Section = All

             if $ReportID = 'GPNZPY11' or $ReportID = 'GPNZPY12'     ! Payroll Register,
                                                                     ! Payroll Reconciliation
                do Exclude-Earn-Dedn
             end-if

             if $ReportID = 'GPNZPY13' and $Rpt_Option <> '20'        ! Pay Component
                do Exclude-Earn-Dedn
             end-if
       end-if

       if $Rpt_Sections    = '40'                   ! Section = Accumulators
          or $Rpt_Sections = '60'                   ! Section = All
             do Include-Accum
       end-if

   end-if

FROM PS_GPNZ_RC_REPORTS LVL0

WHERE LVL0.OPRID        = $PRCS_OPRID
  AND LVL0.RUN_CNTL_ID  = $PRCS_RUN_CNTL_ID

END-SELECT

end-procedure ! Select-Parameters


!***********************************************************************
! Select-Paygroups                                                     *
!                                                                      *
! Calls:        none                                                   *
! Description:  Selects paygroups off run control.  Strings them into  *
!                   a variable or use in the main select clause in     *
!                   the calling report.                                *
!***********************************************************************

begin-procedure Select-Paygroups

    let $Multiple = 'N'

begin-SELECT

LVL1A.OPRID           &LVL1A.OPRID
LVL1A.RUN_CNTL_ID     &LVL1A.RUN_CNTL_ID
LVL1A.GP_PAYGROUP     &LVL1A.GP_PAYGROUP

    let $PaygroupID = &LVL1A.GP_PAYGROUP

!***  Note the difference between the condition of which report is being
!***  called.  The difference is in the WHERE clause that is created.
!***  Different reports calling this SQC to do selects from different
!***  tables.


    if $ReportID = 'GPNZPY11' or $ReportID = 'GPNZPY12'
        if $Multiple = 'N'
            let $Paygroup_List      = $Quote || $PaygroupID || $Quote
        else
            let $Paygroup_List      = $Paygroup_List || ', ' || $Quote || $PaygroupID || $Quote
        end-if
    end-if

    let $Multiple = 'Y'

FROM PS_GPNZ_RC_PYGRP LVL1A

WHERE OPRID        = $Oprid
  AND RUN_CNTL_ID  = $Run_Cntl_ID

end-SELECT

    let $Paygroup_Select_Clause  = 'AND SEGSTAT.GP_PAYGROUP IN (' || $Paygroup_List || ')'

end-procedure ! Select-Paygroups


!***********************************************************************
! Select-Employees                                                     *
! Calls:        none                                                   *
! Description:  Selects employees off run control.  Strings them into  *
!                   a variable for use in the main select clause in    *
!                   the calling report.                                *
!***********************************************************************

begin-procedure Select-Employees

    let $Multiple = 'N'     ! Controls the stringing format for the Select_Clause.

begin-SELECT

LVL1B.OPRID                 &LVL1B.OPRID
LVL1B.RUN_CNTL_ID           &LVL1B.RUN_CNTL_ID
LVL1B.EMPLID                &LVL1B.EMPLID


    let $Runctl_Emplid = &LVL1B.EMPLID

!***  Note the difference between the condition of which report is being  *
!***  called.  The difference is in the WHERE clause that is created.     *
!***  Different reports calling this SQC to do selects from different     *
!***  tables.                                                             *

    if $ReportID = 'GPNZPY11'
        if $Multiple = 'N'
            let $Employee_List = $Quote||$Runctl_Emplid||$Quote
        else
            let $Employee_List = $Employee_List||', '|| $Quote || $Runctl_Emplid || $Quote
        end-if
    end-if

    let $Multiple = 'Y'

FROM PS_GPNZ_RC_EMPLID     LVL1B

WHERE LVL1B.OPRID        = $Oprid
  AND LVL1B.RUN_CNTL_ID  = $Run_Cntl_ID

end-SELECT

    let $EE_Select_Clause  = 'AND SEGSTAT.EMPLID IN ('||$Employee_List||')'

end-procedure ! Select-Employees


!***********************************************************************
! Select-Earnings                                                      *
!                                                                      *
! Calls:        none                                                   *
! Description:  Selects element off run control.  Strings them into    *
!                   a variable or use in the main select clause in     *
!                   the calling report.                                *
!***********************************************************************

begin-procedure Select-Earnings

    let $Element_List_Clause = ' '
    let $Multiple = 'N'

begin-SELECT

LVL13.OPRID           &LVL13.OPRID
LVL13.RUN_CNTL_ID     &LVL13.RUN_CNTL_ID
LVL13.PIN_NUM         &LVL13.PIN_NUM

    let $Pin_Num_rcl = edit(&LVL13.PIN_NUM,'99999999')

!***  Note the difference between the condition of which report is being
!***  called.  The difference is in the WHERE clause that is created.
!***  Different reports calling this SQC do selects from different
!***  tables.

    if $ReportID = 'GPNZPY13'
       if  $Multiple = 'N'
            let $Element_List_Clause  =  $Pin_Num_rcl
       else
            let $Element_List_Clause  = $Element_List_Clause || ',' || $Pin_Num_rcl
       end-if
     end-if

    let $Multiple = 'Y'

FROM PS_GPNZ_RC_ELM LVL13

WHERE OPRID        = $Oprid
  AND RUN_CNTL_ID  = $Run_Cntl_ID

end-SELECT

  let $Element_List_Clause = 'AND ERNDED.PIN_NUM IN (' ||  $Element_List_Clause || ')'

end-procedure ! Select-Earnings


!***********************************************************************
! Select-Accum                                                         *
!                                                                      *
! Calls:        none                                                   *
! Description:  Selects element off run control.  Strings them into    *
!                   a variable or use in the main select clause in     *
!                   the calling report.                                *
!***********************************************************************

begin-procedure Select-Accum

    let $Multiple = 'N'

begin-SELECT

LVL14.OPRID           &LVL14.OPRID
LVL14.RUN_CNTL_ID     &LVL14.RUN_CNTL_ID
LVL14.PIN_NUM         &LVL14.PIN_NUM

    let $Pin_Num_rcl =    edit(&LVL14.PIN_NUM,'99999999')

!***  Note the difference between the condition of which report is being
!***  called.  The difference is in the WHERE clause that is created.
!***  Different reports calling this SQC to do selects from different
!***  tables.

    if $ReportID = 'GPNZPY13'
        if  $Multiple = 'N'
            let $Element_List  = $Pin_Num_rcl
        else
            let $Element_List  = $Element_List || ',' || $Pin_Num_rcl
        end-if
     end-if

    let $Multiple = 'Y'

FROM PS_GPNZ_RC_ELM LVL14

WHERE OPRID        = $Oprid
  AND RUN_CNTL_ID  = $Run_Cntl_ID

end-SELECT

    let $Element_List_Clause  = 'AND ACCUM.PIN_NUM IN (' || $Element_List || ')'

end-procedure ! Select-Accum


!***********************************************************************
! Exclude-Earn-Dedn                                                    *
!                                                                      *
! Description:  Selects earn/dedcn elements to be EXCLUDED in the      *
!               report. Strings them into a variable for use in the    *
!               main select clause in the calling report.              *
!***********************************************************************

begin-procedure Exclude-Earn-Dedn

    let $Multiple = 'N'

begin-SELECT

ED.PIN_NUM

    let $Pin_Num_rcl = edit(&ED.PIN_NUM, '99999999')

    if $Multiple = 'N'
       let $ErnDed_Exclude_Clause = $Pin_Num_rcl
    else
       let $ErnDed_Exclude_Clause = $ErnDed_Exclude_Clause || ',' || $Pin_Num_rcl
    end-if

    let $Multiple = 'Y'


FROM PS_GPNZ_RPT_ED_EXC ED
WHERE GPNZ_PAY_RPT_ID = $Rpt_ID

end-SELECT

   If $ErnDed_Exclude_Clause <> ' ' 
    let $ErnDed_Exclude_Sum_Clause = 'AND ERNDED2.PIN_NUM NOT IN (' || $ErnDed_Exclude_Clause || ')'
    let $ErnDed_Exclude_Clause = 'AND ERNDED.PIN_NUM NOT IN (' || $ErnDed_Exclude_Clause || ')'
   end-if

end-procedure


!***********************************************************************
! Include-Accum                                                        *
!                                                                      *
! Description:  Selects accumulator elements to be INCLUDED in the     *
!               report. Strings them into a variable for use in the    *
!               main select clause in the calling report.              *
!***********************************************************************

begin-procedure Include-Accum

    let $Multiple = 'N'

begin-SELECT

AC.PIN_NUM

    let $Pin_Num_rcl = edit(&AC.PIN_NUM, '99999999')

    if $Multiple = 'N'
       let $Accum_Include_Clause = $Pin_Num_rcl
    else
       let $Accum_Include_Clause = $Accum_Include_Clause || ',' || $Pin_Num_rcl
    end-if

    let $Multiple = 'Y'


FROM PS_GPNZ_RPT_AC_INC AC
WHERE GPNZ_PAY_RPT_ID = $Rpt_ID

end-SELECT

   If $Accum_Include_Clause <> ' ' 
    let $Accum_Include_Sum_Clause = 'AND ACCUM2.PIN_NUM IN (' || $Accum_Include_Clause || ')'
    let $Accum_Include_Clause = 'AND ACCUM.PIN_NUM IN (' || $Accum_Include_Clause || ')'
   end-if
   
end-procedure
