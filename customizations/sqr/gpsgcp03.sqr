!      PAY809SG : Adjustment report for CPF Contributions              *
!                                                                      *
!       The Logic of this report is based on a document titled         *
!   'GUIDELINES ON LIMIT TO CPF CONTRIBUTIONS ON ADDITIONAL WAGES'     *
!                                                                      *
!  CPF on additional Wages is subject to an upper limit on the Wages.  *
!  Pay Calculation calculates the Limit based on the Previous  Wages.  *
!                                                                      *
!  At the end of the Year , the Wages of the Current year are Known.   *
!  Due to differences in the Wages in the Previous year and the        *
!  Current year, the Limit on Additional wages may change. This report *
!  Prints the new and old limits.                                      *
!                                                                      *
!  The Last pay calculation of the Year will calculate the deductions  *
!  for December after taking into effect the adjustment required.      *
!  This adjustment is done by paycalc only if CPF calculations show a  *
!  shortfall (ie; Limit as per previous year is less than the limit    *
!  as per current year)                                                *
!                                                                      *
!  However if the adjustment is such that a refund is to collected     *
!  then this report should be printed.                                 *
!                                                                      *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/06/26:00:24:40                                 !
!       $Release:  HR92                                                !
!      $Revision:  103                                                 !
!***********************************************************************
! Modification History                                                 *
! Dated      Author   Changes                                          *
! 18-Apr-2012         Change to accomodate date option As of Date      *
!                     in addition to the current option As of Year End *
!                     /Termination date BUG 12975705*
!***********************************************************************
!
#include 'setenv.sqc'  !Set environment
#Include 'setup02.sqc' !Printer and page-size initialization

begin-setup
 declare-variable
        date $Wage_Date
        date $Birthdate
        date $Term_Dt

 end-declare
end-setup


begin-report
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime

  do Stdapi-Init

  do Select-CPF-Parameters

  !do Get-Date-Codes
  if $Prcs_Process_Instance = ''
     do Accept-Parameters
  end-if

  do Get-Employer-Descr
  do Init-Report-Title
  do Report
  do Stdapi-Term
end-report


begin-heading 8
  #Include 'stdhdg01.sqc'

  PRINT 'CPF Emplr Ref.No./CPF Submission No.:'       (+2,1)
  PRINT $CPF_Emplr_Ref          (0,+2)
  PRINT &Descr                  (0,+5)

  PRINT 'Employee ID'                   (+2, 1,11)
  PRINT 'Account No'                    (0,+2,10)
  PRINT 'Name'                          (0,+1,20)
  PRINT 'Age'                             (0,+4,3)
  PRINT 'Month'                         (0,+4, 5)
  PRINT '    Ord Wages'                 (0,+2,12)
  PRINT '    Add Wages'                 (0,+2,12)

  PRINT '         Ordinary CPF Paid'    (0,+2,28)
  PRINT '        Additional CPF Paid'   (0,+2,28)
  PRINT '  Old Limit'                   (0,+5,12)
  PRINT '  New Limit'                   (0,+2,11)
  PRINT 'Employer'                      (+1,99,14)
  PRINT 'Employee'                      (0,+2,12)
  PRINT 'Employer'                      (0,+2,12)
  PRINT 'Employee'                      (0,+2,12)

end-heading


begin-footing 4

  If $End_of_Report = 'Y'
     Print   'END OF REPORT'     (4,1)  CENTER
  end-if
end-footing

!***********************************************************************
!   Procedure : Select-CPF-Parameters
!              Select the Pay Entity and Year from the run control
!***********************************************************************
begin-procedure Select-CPF-Parameters
begin-SELECT
RC.GPSG_CPF_EMPLR_REF
RC.GPSG_YEAR
RC.GPSG_DETAIL_REPORT
RC.GPSG_AGE_OPTION

  MOVE &RC.GPSG_CPF_EMPLR_REF   TO $CPF_EMPLR_REF
  MOVE &RC.GPSG_YEAR                 TO $YEARCD
  MOVE &RC.GPSG_DETAIL_REPORT   TO $DETAIL_REPORT
  MOVE &RC.GPSG_AGE_OPTION          TO $RC.GPSG_AGE_OPTION


FROM PS_GPSG_CPF_RUNCTL RC
WHERE RC.OPRID    = $Prcs_OprID
AND   RC.RUN_CNTL_ID    = $Prcs_Run_Cntl_ID

end-SELECT
end-procedure




!***********************************************************************
!   Procedure : Accept-Parameters
!              Accept the Pay Entity and Year from the User
!***********************************************************************
begin-procedure Accept-Parameters

  ChooseRef:
    Input $CPF_Emplr_Ref 'Enter the Employer CPF Reference ' MAXLEN=9 TYPE=CHAR
    if rtrim($CPF_Emplr_Ref,' ') = ''
       goto ChooseRef
    end-if

  ChooseYY:
    Input $YearCd 'Enter the Year  (YYYY) ' MAXLEN=4 TYPE=INTEGER
    Move $YearCd to #tempYY
    if #TempYY < 1965 OR #TempYY > 2100
       goto ChooseYY
    end-if

  Input $Detail_Report 'Print Detailed Report (Y/N) ' MAXLEN=1 TYPE=CHAR

end-procedure

!************************************************************************
!     Procedure   : Get-Employer-Descr
!     Description : Get the CPF employer description
!************************************************************************
begin-procedure Get-Employer-Descr
begin-SELECT
REF.GPSG_EMPLR_DESCR      &Descr

From PS_GPSG_ER_CPF_REF REF
Where REF.GPSG_CPF_EMPLR_REF = $CPF_Emplr_Ref
end-Select
end-procedure



!***********************************************************************
!  Procedure    : Init-Report-Title
!  Description  : Initialises the Title strings for the report
!***********************************************************************
begin-procedure Init-Report-Title

  Move 'GPSGCP03'   to $ReportID

  Move 'Additional CPF Contributions Adjustment Report For ' to $temp1
  Let $ReportTitle = $temp1 || $YearCd
  display $ReportTitle

end-procedure


!************************************************************************
!     Procedure   : Report
!     Description : MAIN PROCEDURE FOR THE PRINTING OF THE REPORTS
!************************************************************************
begin-procedure Report
  Move 'N' to $End_of_Report
  
begin-SELECT DISTINCT
RT.EMPLID      &Emplid

  Let #TotEEOrd     =   0
  Let #TotEEAdd     =   0
  Let #TotEROrd     =   0
  Let #TotERAdd     =   0
  Let #TotOrdWages  =   0
  Let #TotAddWages  =   0

  Print ' '    (+2,1)
  do PrintAdjustments

  Let #Old_Limit = &CRT.GPSG_OLD_CPF_LIMIT
  Let #New_Limit = &CRT.GPSG_NEW_CPF_LIMIT

  PRINT 'TOTAL'               (+1,56, 5)   BOLD
  PRINT #TotOrdWages          (0,+1,14)   edit 999,999,999.99 BOLD
  PRINT #TotAddWages          (0,+1,14)   edit 999,999,999.99 BOLD
  PRINT #TotEROrd             (0,+1,14)   edit 999,999,999.99 BOLD
  PRINT #TotEEOrd             (0,+1,14)   edit 999,999,999.99 BOLD
  PRINT #TotERAdd             (0,+1,14)   edit 999,999,999.99 BOLD
  PRINT #TotEEAdd             (0,+1,14)   edit 999,999,999.99 BOLD
  PRINT #Old_Limit            (0,+1,14)   edit 999,999,999.99 BOLD
  PRINT #New_Limit            (0,+1,14)   edit 999,999,999.99 BOLD
 

FROM PS_GPSG_CPF_RPTG RT
WHERE RT.GPSG_CPF_EMPLR_REF = $CPF_Emplr_Ref
  AND RT.GPSG_YEAR = $YearCd
 ! AND RT.GPSG_CPF_REFUND = 'Y'
  ORDER By RT.EMPLID
end-Select

  Move 'Y' to $End_of_Report

end-procedure



!**************************************************************************
!  Procedure :  PrintAdjustments
!
!***************************************************************************
begin-procedure PrintAdjustments

begin-Select
CRT.MONTHCD          &MonthCd
CRT.EMPLID
CRT.GPSG_EE_CPF_ACCT
CRT.GPSG_ER_ORD_CPF
CRT.GPSG_EE_ORD_CPF
CRT.GPSG_ER_ADD_CPF
CRT.GPSG_EE_ADD_CPF
CRT.GPSG_ORD_WAGES
CRT.GPSG_ADD_WAGES
CRT.GPSG_OLD_CPF_LIMIT
CRT.GPSG_NEW_CPF_LIMIT

  Let $Emplid           = &CRT.Emplid
  Let $Translated_CPFNO = &CRT.GPSG_EE_CPF_ACCT
  Let #CPF_ER_Ord       = &CRT.GPSG_ER_ORD_CPF
  Let #CPF_EE_Ord       = &CRT.GPSG_EE_ORD_CPF
  Let #CPF_ER_Add       = &CRT.GPSG_ER_ADD_CPF
  Let #CPF_EE_Add       = &CRT.GPSG_EE_ADD_CPF
  
  Move &MonthCd to $Wage_Month
  Move $YearCd  to #Wage_Year
  Move 0 to #Ord_Wages
  Move 0 to #Add_Wages
  Move 0 to #Old_Limit
  Move 0 to #New_Limit

  Let #Add_Wages = &CRT.GPSG_ADD_WAGES
  Let #Ord_Wages = &CRT.GPSG_ORD_WAGES

  Do Get-Employee-Name
  Do Cal-Employee-Age
  Do Get-Month-Name($Wage_Month,$MonthName)
  Let $MonthName = Substr($MonthName, 1, 3)
  
  Print $EmplId               (+2, 1,11) On-Break  
  PRINT $Translated_CPFNO     (0,+2,10) On-Break
  PRINT &Name                 (0,+1,20) On-Break

  if $PrevEmplid<>$Emplid
      PRINT #Age_Yrs   (0,+1,2)  edit 99
      PRINT 'Y'              (0,,1)
      PRINT #Age_Months   (0,+1,2)  edit 99
      PRINT 'M'              (0,,1)

      let $PrevEmplid=$Emplid


     If $Detail_Report = 'Y'
        PRINT $MonthName            (0,+3,  5)
        PRINT #Ord_Wages            (0,+1,14)    edit 999,999,999.99
        PRINT #Add_Wages            (0,+1,14)    edit 999,999,999.99
        PRINT #CPF_ER_Ord           (0,+1,14)   edit 999,999,999.99
        PRINT #CPF_EE_Ord           (0,+1,14)   edit 999,999,999.99
        PRINT #CPF_ER_Add           (0,+1,14)   edit 999,999,999.99
        PRINT #CPF_EE_Add           (0,+1,14)   edit 999,999,999.99
        PRINT ' '                   (+1,1)
    End-if
  else
     If $Detail_Report = 'Y'
        PRINT $MonthName            (0,+11,  5)
        PRINT #Ord_Wages            (0,+1,22)    edit 999,999,999.99
        PRINT #Add_Wages            (0,+1,22)    edit 999,999,999.99
        PRINT #CPF_ER_Ord           (0,+1,22)   edit 999,999,999.99
        PRINT #CPF_EE_Ord           (0,+1,22)   edit 999,999,999.99
        PRINT #CPF_ER_Add           (0,+1,22)   edit 999,999,999.99
        PRINT #CPF_EE_Add           (0,+1,22)   edit 999,999,999.99
        PRINT ' '                   (+1,1)
     End-if
  end-if


  Add #CPF_ER_Ord to #TotEROrd
  Add #CPF_EE_Ord to #TotEEOrd
  Add #CPF_ER_Add to #TotERAdd
  Add #CPF_EE_Add to #TotEEAdd
  Add #Add_Wages  to #TotAddWages
  Add #Ord_Wages  to #TotOrdWages


FROM PS_GPSG_CPF_RPTG CRT
WHERE CRT.GPSG_CPF_EMPLR_REF = $CPF_Emplr_Ref
  AND CRT.GPSG_YEAR = $YearCd
  AND CRT.EMPLID = &Emplid
  ORDER By CRT.MONTHCD
end-SELECT

end-procedure

!******************************************************************
!   Procedure : Cal-Employee-Age
!   Description : Calculate the age of the employee
!******************************************************************
begin-procedure Cal-Employee-Age

    Let #Thirty_First = 31
    Let #December     = 12
    let #Age_Yrs      =  0
    let  #Age_Months  =  0

    Move #Thirty_First to $Thirty_First
    Move #December to $December
    Let $Wage_Year     =  edit(#Wage_Year,'9999')

    Let $Last_Day_Wage_Yr = $Wage_Year ||'12' || '31'
    Move $Last_Day_Wage_Yr to $Wage_date

Begin-Select
P.BIRTHDATE   &BIRTHDATE
R.TERMINATION_DT  &Termdt

        Move &BIRTHDATE to $Birthdate
        Move &Termdt to $Term_Dt

 IF $RC.GPSG_AGE_OPTION = 'Y'   !Age option Year End selected
  
        if isnull($Term_Dt)
                let #Age_Yrs    = datediff($Wage_Date,$Birthdate, 'year')
                let #Age_Months = datediff($Wage_Date,$Birthdate, 'month')
        else
                let #Age_Yrs    = datediff($Term_Dt,$Birthdate, 'year')
                let #Age_Months = datediff($Term_Dt,$Birthdate, 'month')
             
        end-if
 END-IF

 IF $RC.GPSG_AGE_OPTION = 'C'    ! Age option As of date selected
         let #Age_Yrs    = datediff($Current-date,$Birthdate, 'year')
         let #Age_Months = datediff($Current-date,$Birthdate, 'month')
 END-IF

        let #Age_Yrs = Trunc (#Age_Yrs, 0)
        let #Age_Months= mod(#Age_Months,12)
        let #Age_Months=trunc(#Age_Months,0)

FROM  PS_PERSON P,PS_GPSG_CPF_RPTG R
WHERE P.EMPLID=$Emplid
    AND R.GPSG_CPF_EMPLR_REF = $CPF_Emplr_Ref
    AND R.GPSG_YEAR = $YearCd
    AND R.EMPLID=P.EMPLID
    AND R.MONTHCD=(SELECT MAX(MONTHCD) FROM  PS_GPSG_CPF_RPTG
                        WHERE GPSG_CPF_EMPLR_REF = $CPF_Emplr_Ref
                        AND GPSG_YEAR = $YearCd
                        AND EMPLID = $Emplid  )

end-SELECT

end-procedure

!******************************************************************
!   Procedure : Get-Employee-Name
!   Description : Get the name of the employee
!******************************************************************
begin-procedure Get-Employee-Name
begin-Select
A.NAME           &NAME

FROM  PS_PERSON_NAME A
WHERE A.EMPLID = $Emplid

end-SELECT
end-procedure


#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#include 'getmonnm.sqc'  !Routine to Get Name of the month
#include 'getdatcd.sqc'  !Routine to Get Year,Month,Quarter of Current Date
#Include 'stdapi.sqc'    !Routines to Update Run Status
#Include 'sqrtrans.sqc'  !Routine for translations
#include 'datemath.sqc'  ! Date conversion procedures
#include 'readxlat.sqc'  ! Procedure to read values from xlattable

