!***********************************************************************
!  GPMXAA06:  Employee Subsidy                                         *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/18:03:50:02                                 *
!       $Release:  HR92                                                *
!      $Revision:  101                                                 *
!                                                                      *
!***********************************************************************
#include 'setenv.sqc'    !Set environment

Begin-Setup
#include 'setupdb.sqc'
#include 'ptset02.sqc'
End-Setup
 

begin-report
   move '1' to $ReportDateType
   move '1' to $ReportYear4
   do Init-DateTime
   do Init-Number
   do Stdapi-Init
   do Get-Current-DateTime
   do Select-Parameters
   do Init-Report
   do Stdapi-Term
   do Reset
end-report


begin-procedure Init-Report
   move 'GPMXAA06.' to $ReportID
   move 'SUBSIDIO AL EMPLEO' to $ReportTitle
   display $ReportID
   display $ReportTitle
   date-time () hh:mi:ss &timeBegan
   display 'Report Began: ' noline
   display &timeBegan

   do Process-Main

   date-time () hh:mi:ss &timeEnded
   display 'Report Ended: ' noline
   display &timeEnded
end-procedure Init-Report


begin-heading 10
    uppercase $ReportTitle
    print $ReportTitle      (2,) center
    print 'PeopleSoft'      (1,) center
    print 'Reporte     : '  (3,1)
    print $ReportID         ()
    print 'Compañía    : '  (4,1)
    print $Company          ()
    print ', '              ()
    print $CompanyName      ()
    print 'Año Proceso : '  (5,1)
    print $AsOfYear         ()

    page-number             (3,157) 'Página No. '
    print 'Hora : '         (4,157)
    date-time () HH:MM:SS  &ReportTime
    print &ReportTime       ()

    print 'Fecha: '         (5,157)
    print $ReportDate       ()

    print '='                      (+2,1,175)fill
    
    !New header
    print 'Código '                (+1,  1)
    print 'Fecha'                  (  , 18)
    print 'Fecha'                  (  , 99)
    Print 'Total'                  (  ,121)
    Print '   Subsidio'            (  ,150)
    
    
    Print 'Empleado '              (+1,  1)
    print 'Ingreso'                (  , 18)
    print 'R.F.C.'                 (  , 34)
    Print 'Nombre del Empleado'    (  , 53)
    Print 'Baja'                   (  , 99)
    Print 'Percepciones'           (  ,121)
    Print '   Empleo'              (  ,150)

    print '='                      (+1,1,175)fill
end-heading


begin-procedure Process-Main
begin-select
A.EMPLID
A.GPMX_CALC_TYPE
A.GPMX_TOT_EARN
A.GPMX_TOT_TXBLE
A.GPMX_ISR_RET
A.GPMX_TOT_EXE
A.GPMX_SUB_RET
A.GPMX_EMPL_SUBSIDY
A.GPMX_SUB_PERCENT
A.GPMX_TAX_AMT
A.GPMX_WORST_TAX
A.GPMX_ADJ_TYPE

    move &A.EMPLID         to $Emplid
    move &A.GPMX_TOT_EARN  to #GPMX_TOT_EARN     !Ingreso Anual
    move &A.GPMX_EMPL_SUBSIDY  to #GPMX_EMPL_SUBSIDY     !Credito Efectivo

    do Get-Names
    do Format-DateTime($Hire_Dt, $Hire_Dt1,{DEFDATE},'','')
    do Format-DateTime($Termination_DT, $Termination_DT1,{DEFDATE},'','')

    if #GPMX_EMPL_SUBSIDY > 0 

      print $Emplid                    (+1,  1)
      print $Hire_Dt1                  (  , 18)
      print $RFC_Id                    (  , 34)        
      print $Name                      (  , 53,40)    
      print $Termination_DT1           (  , 99)        
      print #GPMX_TOT_EARN             (  ,121) edit 999,999,999.99    
      print #GPMX_EMPL_SUBSIDY             (  ,150) edit 999,999,999.99    


      add #GPMX_TOT_EARN to #Totales_Ing_Anual
      add #GPMX_EMPL_SUBSIDY to #Totales_Cred_Efect

      add 1 to #Contador_Empleados
    end-if
    
    ! Variables Cleanup
    move 0 TO #GPMX_TOT_EARN
    move 0 TO #GPMX_EMPL_SUBSIDY



FROM PS_GPMX_ANL_ADJ_WA A
WHERE A.COMPANY = $Company
  AND A.GPMX_BALANCE_YEAR = #AsofYear
  AND (A.GPMX_CALC_TYPE <> ' ' or A.GPMX_ADJ_TYPE <> ' ')
  AND A.RSLT_SEG_NUM IN (SELECT MAX(B.RSLT_SEG_NUM)
                         FROM PS_GPMX_ANL_ADJ_WA B
                         WHERE B.EMPLID = A.EMPLID AND B.CAL_RUN_ID = A.CAL_RUN_ID
                         AND B.EMPL_RCD = A.EMPL_RCD 
                         AND B.GP_PAYGROUP = A.GP_PAYGROUP
                         AND B.CAL_ID = A.CAL_ID
                         AND B.ORIG_CAL_RUN_ID = A.ORIG_CAL_RUN_ID
                         AND B.RSLT_SEG_NUM = A.RSLT_SEG_NUM
                         AND B.COMPANY = A.COMPANY)
ORDER BY A.EMPLID, A.GPMX_CALC_TYPE
end-select
  do Print-Totals
end-procedure Process-Main


begin-procedure Get-Names
begin-select
P.NAME
E.HIRE_DT
E.TERMINATION_DT

  let $name           = &P.NAME
  let $Hire_Dt        = &E.HIRE_DT
  let $Termination_DT = &E.TERMINATION_DT

  do Get-Max-TermDt
  do Get-RFC

FROM PS_JOB J,
     PS_PERSON_NAME P,
     PS_PER_ORG_ASGN_VW E

WHERE J.EMPLID = P.EMPLID
AND   E.EMPLID = J.EMPLID
AND   J.EMPLID = $Emplid
AND   J.COMPANY = $Company
AND   J.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOB
                 WHERE COMPANY = J.COMPANY
                 AND   EMPLID = J.EMPLID
                 AND   EMPL_RCD = J.EMPL_RCD
                 AND   EFFDT <= $EndDt)
AND   J.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB
                  WHERE COMPANY = J.COMPANY
                  AND   EMPLID = J.EMPLID
                  AND   EMPL_RCD = J.EMPL_RCD
                  AND   EFFDT = J.EFFDT)
AND   J.EMPL_RCD = (SELECT MAX(EMPL_RCD) FROM PS_JOB
                     WHERE COMPANY   = J.COMPANY
                     AND   EMPLID    = J.EMPLID
                     AND   EMPL_RCD = J.EMPL_RCD
                     AND   EFFDT     = J.EFFDT
                     AND    EFFSEQ   = J.EFFSEQ)
end-select
end-procedure Get-Names


begin-procedure Get-RFC
begin-select
PC.NATIONAL_ID
  let $RFC_Id = rtrim(&PC.NATIONAL_ID,' ')
FROM PS_PERS_NID PC
WHERE PC.NATIONAL_ID_TYPE = 'RFC'
AND PC.COUNTRY = 'MEX'
AND PC.EMPLID = $Emplid
end-select
end-procedure Get-RFC


begin-procedure Get-Max-TermDt
begin-select
J2.EFFDT
   let $Termination_dt = &J2.EFFDT
FROM PS_JOB J2
WHERE J2.EMPLID = $Emplid AND
      J2.COMPANY = $Company AND
      J2.EFFDT = (SELECT MAX(EFFDT) FROM PS_JOB
                  WHERE COMPANY = J2.COMPANY AND
                        EMPLID = J2.EMPLID   AND
                        EFFDT >= $BeginDt    AND
                        EFFDT <= $EndDt)     AND
      J2.EFFSEQ = (SELECT MAX(EFFSEQ) FROM PS_JOB
                   WHERE COMPANY = J2.COMPANY AND
                         EMPLID = J2.EMPLID   AND
                         EFFDT = J2.EFFDT)    AND
      J2.EMPL_RCD = (SELECT MAX(EMPL_RCD) FROM PS_JOB
                      WHERE COMPANY   = J2.COMPANY AND
                            EMPLID    = J2.EMPLID  AND
                            EFFDT     = J2.EFFDT   AND
                            EFFSEQ    = J2.EFFSEQ) AND
      J2.ACTION = 'TER'
end-select
end-procedure Get-Max-TermDt


begin-procedure Print-Totals
  print 'Total de Empleados: '      (+2,14)
  print #Contador_Empleados         ( ,+6) edit 888,888,888
  print '_'                         (+1,121,54)fill
  print 'T O T A L E S  C O M P A Ñ I A:'         (+1,14)
  print #Totales_Ing_Anual          ( ,121) edit 999,999,999.99
  print #Totales_Cred_Efect         ( ,150) edit 99,999,999.99

end-procedure Print-Totals


begin-procedure Select-Parameters
begin-SELECT
MEXRNCTL.OPRID
MEXRNCTL.RUN_CNTL_ID
MEXRNCTL.COMPANY
MEXRNCTL.GPMX_BALANCE_YEAR
MEXRNCTL.LANGUAGE_CD

    let $Company  = &MEXRNCTL.COMPANY
    let #AsofYear = &MEXRNCTL.GPMX_BALANCE_YEAR
    let $AsofYear = To_char (#AsofYear)
    let $lang_opr = &MEXRNCTL.LANGUAGE_CD

    move $AsofYear to $BgnDt
    move $AsofYear to $EndDt

    concat '-01-01' with $BgnDt
    concat '-12-31' with $EndDt

    do Convert-From-DTU-Date($BgnDt,$BgnDt)
    do Convert-From-DTU-Date($EndDt,$EndDt)
    do Get-Company-Data-Mex

FROM  PS_GPMX_RC_ANNLADJ MEXRNCTL
WHERE MEXRNCTL.OPRID       = $Prcs_OprID
  AND MEXRNCTL.RUN_CNTL_ID = $Prcs_Run_Cntl_ID
end-select
end-procedure Select-Parameters


#Include 'reset.sqc'     !Reset printer procedure
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'stdapi.sqc'    !Update Process API
#Include 'datemath.sqc'  !Datemath Routines
#Include 'getcomex.sqc'  !Datemath Routines
