!***********************************************************************
!  PAY038 : Load Chart Keys data into GL Interface from Payroll        *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!                                                                      *
!          $Date:  2013/01/17:15:36:45                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'    !Set Database Environment
#Include 'setup31.sqc'   !printer and page-size initialization


begin-report

  do Initialization

  do Get-Run-ID                 !  RUNCTL_GEN_CKEY


  display 'Report began at: ' noline
  do Display-Time

  do Populate-ChartKeys

  display 'Report Ended: ' noline
  do Display-Time

  do Stdapi-Term

end-report


Begin-Procedure Initialization

  move 'PAY038' to $ReportID
  move 'Load Chart Key Data Into GL Interface' to $ReportTitle

  do Init-DateTime
  do Stdapi-Init

  display $ReportTitle
  display ''

End-Procedure



begin-procedure Get-Run-ID
  if $Prcs_Process_Instance = ''
     Display 'Report Cannot be executed outside of PeopleSoft, please use process scheduler.'
     Stop
  else
    do Select-Parameters
  end-if
end-procedure


begin-procedure Populate-ChartKeys

  display 'Populating Chart Key Data Into GL Interface Table . . . ' noline

  let $Business_unit_PY = $Ckey.Business_Unit
  do Get-BU-Setid
  do Get-GL-BU
  do Process-ChartFields

  display 'Done'

end-procedure


Begin-Procedure Get-BU-Setid
Display $Business_unit_PY
Begin-Select
CNTLREC.SETID

  Let $Setid = &CNTLREC.SETID

FROM PS_SET_CNTRL_REC CNTLREC
WHERE SETCNTRLVALUE  = $Business_unit_PY
  AND REC_GROUP_ID = 'PY_03'
  AND RECNAME = 'CHART_KEYS_HR'
End-Select
End-Procedure


Begin-Procedure Get-GL-BU
Begin-Select
BUSINESS_UNIT_GL

  Move &BUSINESS_UNIT_GL to $Business_unit_GL


FROM PS_BUS_UNIT_TBL_HR
WHERE BUSINESS_UNIT  = $Business_unit_PY
End-Select

End-Procedure


Begin-Procedure process-ChartFields

begin-SELECT DISTINCT
PAY.DEPTID
PAY.ACCT_CD

  move &PAY.DEPTID         to $PAY.DEPTID
  move &PAY.ACCT_CD        to $PAY.ACCT_CD

  let $chart_key = rtrim($PAY.DEPTID,' ')||rtrim($PAY.ACCT_CD,' ')

  do Count-CKey

  If #CKey_count = 0
    do Insert-Ckey
  End-If

FROM PS_PAY_EARNINGS PAY
WHERE PAY.BUSINESS_UNIT = $Business_unit_PY


end-SELECT
End-Procedure


Begin-Procedure Count-CKey


Begin-Select
COUNT(*) &COUNT_CK

  Move &COUNT_CK to #CKey_count

FROM PS_CHART_KEYS_HR
WHERE SETID = $Setid
  AND ACTIVITY = $Ckey.Activity
  AND ACCOUNT_TYPE = $Ckey.Account_Type
  AND BUSINESS_UNIT_GL = $Business_unit_GL
  AND CHART_KEY = $Chart_key
End-Select

End-Procedure


Begin-Procedure Insert-Ckey

  Let $Ins.Deptid  = ' '           ! Takes care of No Mapping - Earns_option ='N'
  Let $Ins.Acct_cd = ' '

  If $CKey.Earns_options = 'P'    ! Map as in payroll
     Let $Ins.Deptid  = $Pay.Deptid
     Let $Ins.Acct_cd = {ps-substr}($Pay.Acct_Cd, #CKey.Begin_Position, #CKey.Length)
  Else
    If $CKey.Earns_options = 'S'  ! select defaults
     Let $Ins.Deptid  = $CKey.Deptid
     Let $Ins.Acct_cd = {ps-substr}($CKey.Acct_Cd, #CKey.Begin_Position, #CKey.Length)
    End-If
  End-If

   let $sql-statement = 'PAY038.SQR, Insert-CKey, Insert, PS_CHART_KEYS_HR'

Begin-Sql On-Error=SQL-Error
INSERT INTO PS_CHART_KEYS_HR
  ( SETID, ACTIVITY, ACCOUNT_TYPE, BUSINESS_UNIT_GL,
    CHART_KEY, ACCOUNT, DEPTID, LINE_DESCR, FUND_CODE)
VALUES
  ($Setid, $CKey.Activity, $CKey.Account_Type, $Business_unit_GL,
    $Chart_Key, $Ins.Acct_Cd, $Ins.Deptid, ' ', ' ')
End-Sql

End-Procedure


BEGIN-PROCEDURE Display-Time

  do Get-Current-DateTime
  display $AsOfToday NoLine
  display ', '       NoLine
  display $AsOfNow
  display ' '

END-PROCEDURE


#include 'curdttim.sqc'  !Get-Current-DateTime procedure
#include 'datetime.sqc'  !Routines for date and time formatting
#include 'stdapi.sqc'    !Update Process API
#include 'ckeyrctl.sqc'  !Select Run Control Parameters
