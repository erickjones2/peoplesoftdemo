!************************************************************************
!  STSU004:  Broker Information
!************************************************************************                                                                      !
!
!                                                                 
!   This software and related documentation are provided under a  
!   license agreement containing restrictions on use and          
!   disclosure and are protected by intellectual property         
!   laws. Except as expressly permitted in your license agreement 
!   or allowed by law, you may not use, copy, reproduce,          
!   translate, broadcast, modify, license, transmit, distribute,  
!   exhibit, perform, publish or display any part, in any form or 
!   by any means. Reverse engineering, disassembly, or            
!   decompilation of this software, unless required by law for    
!   interoperability, is prohibited.                              
!   The information contained herein is subject to change without 
!   notice and is not warranted to be error-free. If you find any 
!   errors, please report them to us in writing.                  
!                                                                 
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.       
!   All Rights Reserved.                                          
!----------------------------------------------------------------------
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      
!          $Date:  2013/01/18:02:56:00                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!
!************************************************************************

#include 'setenv.sqc'    !Set environment

begin-setup
  #include 'ptpsp125.sqc'  !Printer and page-size init
end-setup

#define  col2    20
#define  col3    37
#define  col4    60
#define  col5    80
#define  col6    105

!************************************************************************
begin-program
!************************************************************************

  do Init-DateTime         !procedure in datetime.sqc
  do Init-Number           !procedure in number.sqc
  do Get-Current-DateTime  !procedure in currdttm.sqc
  do Init-Report           !procedure in SQR

  let $timebegan =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Began: '  $timebegan  ' '

  do Process-Main          !does all report processing in SQR
  do Reset                 !procedure in reset.sqc
  do Stdapi-term           !procedure in stdapi.sqc

  let $timeEnded =DateToStr(DateNow(),'hh:mi:ss')

  show 'Report Ended: '  $timeEnded  ' '

end-program

!************************************************************************
begin-procedure Init-Report
!************************************************************************

  do Stdapi-Init
  let     $ReportID     =   'STSU004'

  do Get-Report-Language

  show    'FLOW: Init-Report'
  show    'Report Title: ' $ReportTitle

end-procedure

!************************************************************************
begin-procedure Report-Translation
!************************************************************************

!----------------------------------------------------------------------!
! Procedure: Report-Translation                                        !
! Desc:      This procedure gets the report language and headers       !
!----------------------------------------------------------------------!

  do Init_Report_Translation ('STOCK',$language_cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('STOCK','ST_BROKERAGE_ID',  $StBrokerageID,  #DW)
  do Get_Field_Information ('STOCK','DTC_ACCOUNT_NBR',  $DtcAccountNbr,  #DW)

  do Get_Field_Information ('STSU004','STSU004',        $ReportTitle,    #DW)
  do Get_Field_Information ('STSU004','ST_BRANCH_ID',   $Branch,         #DW)
  do Get_Field_Information ('STSU004','DESCRSHORT',     $DescrShort,     #DW)
  do Get_Field_Information ('STSU004','STATUS_LBL',     $Status_lbl,     #DW)
  do Get_Field_Information ('STSU004','EFFDT_LBL',      $Effdt_lbl,      #DW)
end-procedure

!************************************************************************
begin-heading 5
!************************************************************************

#include 'stdhdgtr.sqc'

  print $StBrokerageID      (+1,01,12)
  print $DtcAccountNbr      (0,+7,04)
  print $DescrShort         (0,+13,10)

end-heading

!************************************************************************
begin-procedure Process-Main
!************************************************************************

!----------------------------------------------------------------------!
! Procedure:  Process-Main                                             !
! Descr:      This is where all required data is selected              !
!----------------------------------------------------------------------!

  display 'FLOW: Process-Main'

begin-Select

A.ST_BROKERAGE_ID
A.DTC_ACCOUNT_NBR

  do Get-Descr

  print   &A.ST_BROKERAGE_ID   (+1,1,10)
  print   &A.DTC_ACCOUNT_NBR   (0,{col2},10)
  print   &B.DESCRSHORT        (0,{col3},10)
  print   '  '                 (+1,1,1)

  do Select-Dtl

from PS_ST_BROKER_TBL A

end-Select

end-procedure

!**************************************************************************
begin-procedure Get-Descr
!**************************************************************************

begin-Select

B.DESCRSHORT
from     PS_ST_BROKER_DTL   B
where    B.ST_BROKERAGE_ID  = &A.ST_BROKERAGE_ID
  and    EFF_STATUS         = 'A'
  and    EFFDT              =(Select MAX(D.EFFDT)
                             from   PS_ST_BROKER_DTL  D
                            where   D.ST_BROKERAGE_ID  =  B.ST_BROKERAGE_ID)



end-Select

end-procedure

!**************************************************************************
begin-procedure Select-Dtl
!**************************************************************************

begin-Select

C.ST_BROKERAGE_ID
C.ST_BRANCH_ID
C.EFFDT
C.EFF_STATUS
C.DESCR
C.ADDRESS1
C.HOUSE_TYPE
C.COUNTY
C.GEO_CODE
C.IN_CITY_LIMIT
C.CITY
C.STATE
C.POSTAL
C.COUNTRY
C.PHONE

 do Print-Brkr-Data

from     PS_ST_BRKR_BRANCH  C
where    C.ST_BROKERAGE_ID   = &A.ST_BROKERAGE_ID
order by C.EFFDT            desc

end-Select

end-procedure

!************************************************************************
begin-procedure Print-Brkr-Data
!************************************************************************
  if &C.ST_BROKERAGE_ID <> $old_st_brokerage_id

     let $old_st_brokerage_id = &C.ST_BROKERAGE_ID

  print   $Branch              (+1,{col2})           
  print   $effdt_lbl           (,{col3})  
  print   $status_lbl          (,{col4})  
  print   $DescrShort          (,{col5})  
  end-if

  print   &C.ST_BRANCH_ID      (+1,{col2},10)       ON-BREAK

  do Format-DateTime(&C.EFFDT, $EFFDT1, {DEFYMD}, '', '')

  print   $EFFDT1              (0,{col3},10)

  do Get-Value

  print   $STATUS              (0,{col4},30)
  print   &C.DESCR             (0,{col5},30)
  print   &C.ADDRESS1          (+1,{col2},35)

  let $TempCity = rtrim(&C.CITY,' ') || ', ' || rtrim(&C.STATE,' ') || ' ' || &C.POSTAL

  print   $TempCity            (+1,{col2},51)

  let $TempCounty = rtrim(&C.COUNTY,' ') || ' ' || &C.COUNTRY

  print   $TempCounty          (+1,{col2},34)

  print   &C.PHONE             (+1,{col2},24)
  print   ' '                  (+1,1,1)

end-procedure

!************************************************************************
begin-procedure Get-Value
!************************************************************************

!-------------------------------------------------------------------!
! Procedure:  Get-Value                                             !
! Descr:      This procedure gets the value of the specific field   !
!             from the Translate Table                              !
!-------------------------------------------------------------------!

  let $FieldName = 'EFF_STATUS'
  let $FieldValue = &C.EFF_STATUS

   do Read-Translate-Table

  let $STATUS = RTRIM($XlatLongName,' ')

end-procedure

!************************************************************************
!  SQC Include Files
!************************************************************************
#include 'stdapi.sqc'
#include 'curdttim.sqc'  !Get-Current-Date-Time
#include 'datetime.sqc'  !Get-Date-Time
#include 'reset.sqc'     !Reset printer procedure
#include 'number.sqc'    !Routines to format numbers
#include 'sqrtrans.sqc'  !Translate SQR strings to given language
#include 'getrplng.sqc'  !Get the report language
#include 'readxlat.sqc'  !Read the Translate Table for the desired values.
