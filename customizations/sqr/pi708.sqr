!***********************************************************************
!  PI708.SQR:INTERFACE PROCESS VALUE TABLE                             *
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!   This software and related documentation are provided under a       *
!   license agreement containing restrictions on use and               *
!   disclosure and are protected by intellectual property              *
!   laws. Except as expressly permitted in your license agreement      *
!   or allowed by law, you may not use, copy, reproduce,               *
!   translate, broadcast, modify, license, transmit, distribute,       *
!   exhibit, perform, publish or display any part, in any form or      *
!   by any means. Reverse engineering, disassembly, or                 *
!   decompilation of this software, unless required by law for         *
!   interoperability, is prohibited.                                   *
!   The information contained herein is subject to change without      *
!   notice and is not warranted to be error-free. If you find any      *
!   errors, please report them to us in writing.                       *
!                                                                      *
!   Copyright (C) 1988, 2013, Oracle and/or its affiliates.            *
!   All Rights Reserved.                                               *
!***********************************************************************
!                                                                      *
!                                                                      *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:15:13:34                                 
!       $Release:  HR92                                                
!      $Revision:  101                                                 
!                                                                      *
!***********************************************************************


#include 'setenv.sqc'  !Set environment
#include 'setup31.sqc' !Printer and page-size initialization


begin-report
  do Init-Report
  do Process-Main
  do Reset
  do Stdapi-Term
end-report

!**************************
begin-procedure Init-Report
!**************************

  move 'PI708' to $ReportID
  do Init-DateTime
  do Init-Number
  do Get-Current-DateTime
  do Stdapi-Init

  do Get-Report-Language

  move $Pi708_ReportTitle to $ReportTitle
  display ' '
  display $ReportTitle
  display ' '

end-procedure


! This procedure is used to read language-dependent text from the database.
!*********************************
begin-procedure Report-Translation
!*********************************

! Read from PI708 program ids
do Init_Report_Translation($ReportID,$Language_Cd)

  do Get_Field_Information ('PI708', 'REPORT_TITLE', $Pi708_ReportTitle, #dummy_width)
  do Get_Field_Information ('PI708', 'PROCID', $Pi708_ProcId, #dummy_width)
  do Get_Field_Information ('PI708', 'DESCRSHORT', $Pi708_DescShort, #dummy_width)
  do Get_Field_Information ('PI708', 'PRCSTYPE', $Pi708_PrcsType, #dummy_width)
  do Get_Field_Information ('PI708', 'RETURNVALUE', $Pi708_ReturnValue, #dummy_width)
  do Get_Field_Information ('PI708', 'DESCRIPTION', $Pi708_Description, #dummy_width)
  do Get_Field_Information ('PI708', 'PI_PROCESS_PHASE', $Pi708_PiProcessPhase, #dummy_width)

end-procedure


begin-heading 7
#Include 'stdhdgtr.sqc'
  print $Pi708_ProcId         (+2,1,11)
  print $Pi708_PiProcessPhase (,13,15)
  print $Pi708_DescShort      (,30,15)
  print $Pi708_PrcsType       (,47,16)
  print $Pi708_ReturnValue    (,65,14)
  print $Pi708_Description    (,81,15)
end-HEADING

!***************************
begin-procedure Process-Main
!***************************

begin-SELECT

A.PI_PROCESS_ID             ! (+1,1) on-break level=1  skiplines = 0
A.PI_PROCESS_PHASE          ! (,13)  on-break level=1
A.DESCRSHORT                ! (,30)  on-break level=1
A.PRCS_TYPE                 ! (,47)  on-break level=1

   let $prnt_pi_process_id    = &A.PI_PROCESS_ID
   let $prnt_pi_process_phase = &A.PI_PROCESS_PHASE
   let $prnt_descrshort       = &A.DESCRSHORT
   let $prnt_prcs_type        = &A.PRCS_TYPE

!---------------------------------------------------------------!
! Only retrieve data from the related language table if         !
! the operator language is not the same to the installation     !
! language or $curr_language_cd  is different from $language_cd !
!---------------------------------------------------------------!

  if $Curr_language_cd <> $Psoptions_language_cd
     do Get-Rel-Lang-Dscshort
  end-if

  PRINT $PRNT_PI_PROCESS_ID      (+1,1,11) on-break level=1  skiplines = 0
  PRINT $PRNT_PI_PROCESS_PHASE   (,13,15)  on-break level=1
  PRINT $PRNT_DESCRSHORT         (,30,15)  on-break level=1
  PRINT $PRNT_PRCS_TYPE          (,47,16)  on-break level=1

  do Get-Process-Values

FROM PS_PI_PROCESS_TBL A
ORDER BY A.PI_PROCESS_ID

end-SELECT

end-procedure


!************************************
begin-procedure Get-Rel-Lang-Dscshort
!************************************

begin-SELECT

AL.DESCRSHORT

  let $prnt_descrshort = Cond(RTRIM(&AL.DESCRSHORT,' ')='', &A.DESCRSHORT, &AL.DESCRSHORT)

FROM PS_PI_PROC_TB_LANG AL
WHERE AL.PI_PROCESS_ID    = &A.PI_PROCESS_ID
  AND AL.PI_PROCESS_PHASE = &A.PI_PROCESS_PHASE
  AND AL.LANGUAGE_CD      = $Curr_language_cd

end-SELECT

end-procedure


!*********************************
begin-procedure Get-Process-Values
!*********************************

let #row = 1

begin-SELECT

B.RETURN_VALUE

  if #row = 1
      print &B.RETURN_VALUE  (,69,10)
  else
      print &B.RETURN_VALUE  (+1,69,10)
  end-if

B.DESCR

   let $prnt_descr = &B.DESCR

!---------------------------------------------------------------!
! Only retrieve data from the related language table if         !
! the operator language is not the same to the installation     !
! language or $curr_language_cd  is different from $language_cd !
!---------------------------------------------------------------!

   if $Curr_language_cd <> $Psoptions_language_cd
      do Get-Rel-Lang-Descr
   end-if

   PRINT $PRNT_DESCR           (,81,30)

  let #row = #row + 1

FROM  PS_PI_PROCESS_VAL B
WHERE B.PI_PROCESS_ID    = &A.PI_PROCESS_ID
  AND B.PI_PROCESS_PHASE = &A.PI_PROCESS_PHASE
  ORDER BY B.RETURN_VALUE
end-SELECT

end-procedure Get-Process-Values


!*********************************
begin-procedure Get-Rel-Lang-Descr
!*********************************

begin-SELECT

BL.DESCR

  let $prnt_descr = Cond(RTRIM(&BL.DESCR,' ')='', &B.DESCR, &BL.DESCR)

FROM  PS_PI_PROC_VA_LANG BL
WHERE BL.PI_PROCESS_ID    = &A.PI_PROCESS_ID
  AND BL.PI_PROCESS_PHASE = &A.PI_PROCESS_PHASE
  AND BL.RETURN_VALUE     = &B.RETURN_VALUE
  AND BL.LANGUAGE_CD      = $Curr_language_cd

end-SELECT

end-procedure


#Include 'reset.sqc'     !Reset printer procedure
#Include 'stdapi.sqc'    !Update Process API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'sqrtrans.sqc'  !Translate SQR strings to a given language
#Include 'getrplng.sqc'  !Gets the report language

