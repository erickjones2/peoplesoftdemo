!***********************************************************************
!  GETINDCD:  Determine Employee's Industry and Sector Code
!***********************************************************************
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:13:40:04                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************


!***********************************************************************
begin-procedure Is-Federal-Employee($Emplid,#EmplRcd,
                                    $Company,$PayGroup,$Effdt,$OprID,
                                    :$Member)
!***********************************************************************

   let $Industry = 'GOVT'
   let $Sector   = 'USFG'

   do Confirm-Industry-Membership($Emplid,#EmplRcd,
                                  $Company,$PayGroup,$Effdt,$OprID,
                                  $Industry, $Sector,
                                  $Member)

end-procedure  !Is-Federal-Employee

!***********************************************************************
begin-procedure Confirm-Industry-Membership($Emplid,#EmplRcd,
                                            $Company,$PayGroup,$Effdt,
                                            $OprID,$Industry,$Sector,
                                            :$Member)
!***********************************************************************

   let $Member = 'N'
   let $Industry_Test = ''
   let $Sector_Test = ''

   if Not IsBlank($Industry) and Not IsBlank($Sector)

      if Not IsNull($Effdt)

         if Not IsBlank($PayGroup) and Not IsBlank($Company)

            do Get-Industry-From-PayGroup($Company,$PayGroup,$Effdt,
                                       $Industry_Test,$Sector_Test)

         else

            if Not IsBlank($Company)

               do Get-Industry-From-Company($Company,$Effdt,
                                         $Industry_Test,$Sector_Test)

            else

               if Not IsBlank($Emplid)

                  do Get-Industry-From-Employee($Emplid,#EmplRcd,$Effdt,
                                             $Industry_Test,$Sector_Test)

               end-if
            end-if
         end-if
      else
         if Not IsBlank($OprID)

            do Get-Industry-From-Security($OprID,$Industry_Test,$Sector_Test)

         end-if
      end-if
   end-if

   if ($Industry_Test = $Industry) and ($Sector_Test = $Sector)

      let $Member = 'Y'

   end-if

end-procedure  !Get-Industry-flag

!***********************************************************************
begin-procedure Get-Industry-From-Employee($Emplid,#EmplRcd,$Effdt,
                                           :$Industry,:$Sector)
!***********************************************************************

begin-select

C.INDUSTRY
C.INDUSTRY_SECTOR

   let $Industry = &C.INDUSTRY
   let $Sector   = &C.INDUSTRY_SECTOR

FROM  PS_COMPANY_TBL C,
      PS_JOB         J
WHERE J.EMPLID   = $Emplid
AND   J.EMPL_RCD = #EmplRcd
AND   J.EFFDT =
          (SELECT MAX(EFFDT)
           FROM   PS_JOB
           WHERE  EMPLID   = J.EMPLID
           AND    EMPL_RCD = J.EMPL_RCD
           AND    EFFDT   <= $Effdt)
AND   J.EFFSEQ =
          (SELECT MAX(EFFSEQ)
           FROM   PS_JOB
           WHERE  EMPLID   = J.EMPLID
           AND    EMPL_RCD = J.EMPL_RCD
           AND    EFFDT    = J.EFFDT)
AND   C.COMPANY    = J.COMPANY
AND   C.EFF_STATUS = 'A'
AND   C.EFFDT =
          (SELECT MAX(EFFDT)
           FROM   PS_COMPANY_TBL
           WHERE  COMPANY    = J.COMPANY
           AND    EFF_STATUS = 'A'
           AND    EFFDT     <= $Effdt)

end-select

end-procedure !Get-Industry-From-employee

!***********************************************************************
begin-procedure Get-Industry-From-Company($Company,$Effdt,
                                          :$Industry,:$Sector)
!***********************************************************************

begin-select

C.INDUSTRY
C.INDUSTRY_SECTOR

   let $Industry = &C.INDUSTRY
   let $Sector   = &C.INDUSTRY_SECTOR

FROM  PS_COMPANY_TBL C
WHERE C.COMPANY    = $Company
AND   C.EFF_STATUS = 'A'
AND   C.EFFDT =
          (SELECT MAX(EFFDT)
           FROM   PS_COMPANY_TBL
           WHERE  COMPANY    = C.COMPANY
           AND    EFF_STATUS = 'A'
           AND    EFFDT     <= $Effdt)

end-select

end-procedure  !Get-Industry-From-company

!***********************************************************************
begin-procedure Get-Industry-From-PayGroup($Company,$PayGroup,$Effdt,
                                           :$Industry,:$Sector)
!***********************************************************************

begin-select

P.INDUSTRY
P.INDUSTRY_SECTOR

   let $Industry = &P.INDUSTRY
   let $Sector   = &P.INDUSTRY_SECTOR

FROM  PS_PAYGROUP_TBL P
WHERE P.COMPANY    = $Company
AND   P.PAYGROUP   = $Paygroup
AND   P.EFF_STATUS = 'A'
AND   P.EFFDT =
          (SELECT MAX(EFFDT)
           FROM   PS_PAYGROUP_TBL
           WHERE  COMPANY    = P.COMPANY
           AND    PAYGROUP   = P.PAYGROUP
           AND    EFF_STATUS = 'A'
           AND    EFFDT     <= $Effdt)

end-select

end-procedure  !Get-Industry-From-PayGroup

!***********************************************************************
begin-procedure Get-Industry-From-Security($OprID,
                                           :$Industry,:$Sector)
!***********************************************************************

begin-select

S1.INDUSTRY
S1.INDUSTRY_SECTOR

   let $Industry = &S1.INDUSTRY
   let $Sector   = &S1.INDUSTRY_SECTOR

FROM PS_OPR_DEF_TBL_HR S1, PSOPRDEFN S2
WHERE S2.OPRID = $OprID
AND   S2.OPRCLASS = S1.OPRCLASS

end-select

end-procedure  !Get-Industry-From-Security

!************************ End of GETINDCD.SQC **************************

