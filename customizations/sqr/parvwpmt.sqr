!***********************************************************************
!  PARVWPMT: Review Payment Results Report
!***********************************************************************
!                                                                      *
!                                                                      *
!                                                                      *
!                                                                      *
! This software and related documentation are provided under a         *
! license agreement containing restrictions on use and                 *
! disclosure and are protected by intellectual property                *
! laws. Except as expressly permitted in your license agreement        *
! or allowed by law, you may not use, copy, reproduce,                 *
! translate, broadcast, modify, license, transmit, distribute,         *
! exhibit, perform, publish or display any part, in any form or        *
! by any means. Reverse engineering, disassembly, or                   *
! decompilation of this software, unless required by law for           *
! interoperability, is prohibited.                                     *
! The information contained herein is subject to change without        *
! notice and is not warranted to be error-free. If you find any        *
! errors, please report them to us in writing.                         *
!                                                                      *
!                                                                      *
! Copyright (C) 1988, 2013, Oracle and/or its affiliates.              *
! All Rights Reserved.                                                 *
!***********************************************************************
! This report is run from the Pension->Payments->Review Payment Results
! report component.  
! This report was created for resolution: 637610
!***********************************************************************
!                                                                      *
!          $Date:  2013/01/17:14:59:29                                 !
!       $Release:  HR92                                                !
!      $Revision:  101                                                 !
!                                                                      *
!***********************************************************************

  #include 'setenv.sqc'

!-----------------------------------------------------------------------
Begin-Setup
!-----------------------------------------------------------------------

  #include 'ptpsl177.sqc'

End-Setup


begin-report
  DO Init-DateTime
  DO Init-Number
  DO Get-Current-DateTime
  DO Init-Report
  DO Process-Main
  DO StdApi-Term
END-report


!-----------------------------------------------------------------------
! Section: Heading Procedures
! Descr:   This sections contains all procedures for
!          printing the optional heading
!-----------------------------------------------------------------------
Begin-Heading 09

  #Include 'stdhdg01.sqc'
  
  evaluate $radio_btn
     when = 'D'
         let $Report_Detail = '- Total Payment Details'
         print $Report_Detail                                   (-2,023,022)
         break
     when = 'N'
         let $Report_Detail = '- NonTaxable Payment Details'
         print $Report_Detail                                   (-2,023,028)
         break   
     when = 'I'
         let $Report_Detail = '- Payment Information'
         print $Report_Detail                                   (-2,023,021)
         break   
  end-evaluate
 
  print $Run_IDLbl                                              (+1,001,006)
  print ':'                                                     (  ,   ,001)
  print $pa_run_id                                              (  , +3,010)
  
  if #bp_count > 1 
       let $Benefit_Plan_Hdg = 'All Plans Selected'
  else
       let $Benefit_Plan_Hdg = $Benefit_Plan
  end-if
  
  let #length = lengthp($Benefit_Plan_Hdg) + 15
  
  let $Benefit_Plan_Print = $Benefit_PlanLbl || ':  ' || $Benefit_Plan_Hdg
  print $Benefit_Plan_Print                                      (-1,   ,#length) center
  
  let $Process_Instance = rtrim($Process_Instance, '0')
  let $Process_Instance = rtrim($Process_Instance, '.')
  let #length = lengthp($Process_Instance) + 19
 
  let $Process_InstanceLbl = $Process_InstanceLbl || ':  ' || $Process_Instance
  print $Process_InstanceLbl                                    (+1,   ,#length) center
  
  print $Payment_FrequencyLbl                                   (+1,001,017)
  print ':'                                                     (  ,   ,001)
  print $Pymt_Freq_Descr                                        (  , +3,020)
  
  print $Pay_Period_End_DtLbl                                   (  ,   ,032) center
  
  print $Process_SelectionLbl                                   (+1,001,020)
  print ':'                                                     (  ,   ,001)
  print $Confirm_Flg_Descr                                      (  , +3,023)
  
  print $Check_DateLbl                                          (  ,   ,023) center
  
  print '-'                                                     (+1,001,177) fill bold
    
  print $Benefit_PlanLbl                                        (+1,001,012) bold
  print ':'                                                     (  ,   ,001) bold
  print $Benefit_Plan_Prev                                      (  , +2,006) 
    
  print $PaymentsLbl                                            (+1,001,008) bold
  print ':'                                                     (  ,   ,001) bold
  print $tot_pmt_cnt_prev                                       (  , +2,008)   
    
  print $Total_PaymentsLbl                                      (-1,078,014) bold
  print ':'                                                     (  ,   ,001) bold
  print $tot_pmt_amt_prev                                       (  , +2,015) edit $999,999,999.99 
 
  print $Adjust_AmountLbl                                       (+1,075,017) bold
  print ':'                                                     (  ,   ,001) bold
  print $tot_adj_amt_prev                                       (  , +2,015) edit $999,999,999.99  
   
  print $Scheduled_AmountLbl                                    (-1,144,016) bold
  print ':'                                                     (  ,   ,001) bold
  print $tot_schd_amt_prev                                      (  , +2,015) edit $999,999,999.99   
    
  print $Manual_AmountLbl                                       (+1,147,013) bold
  print ':'                                                     (  ,   ,001) bold
  print $tot_manl_amt_prev                                      (  , +2,015) edit $999,999,999.99 
  
  print '-'                                                     (+1,001,177) fill bold


End-Heading


!-----------------------------------------------------------------------
! Procedure: Get-Heading-Details
! Descr:     This procedure queries the PS_PA_RT_SUM_HDR record for the
!            data associated with the payments in each benefit plan.
!            The data is printed on each page below the standard SQR
!            heading.
!-----------------------------------------------------------------------
begin-procedure Get-Heading-Details

BEGIN-SELECT
PA_RT_SUM_HDR.PYMT_FREQ
PA_RT_SUM_HDR.CONFIRM_FLG
PA_RT_SUM_HDR.PA_PMT_END_DT
PA_RT_SUM_HDR.PA_CHECK_DATE
  
       let $Pymt_Freq     = &pa_rt_sum_hdr.pymt_freq
       let $Confirm_Flg   = &pa_rt_sum_hdr.confirm_flg
       let $PA_Pmt_End_Dt = rtrim(&pa_rt_sum_hdr.pa_pmt_end_dt, ' ')
       let $PA_Check_Date = rtrim(&pa_rt_sum_hdr.pa_check_date, ' ')

FROM PS_PA_RT_SUM_HDR PA_RT_SUM_HDR
WHERE PA_RT_SUM_HDR.PA_RUN_ID = $pa_run_id AND PA_RT_SUM_HDR.PROCESS_INSTANCE = #process_instance
END-SELECT

  move $Pymt_Freq to $FieldValue
  move 'PYMT_FREQ' to $Fieldname
  do Read-Translate-Table
  let $Pymt_Freq_Descr    = rtrim($Xlatlongname,' ')

  move $Confirm_Flg to $FieldValue
  move 'CONFIRM_FLG' to $Fieldname
  do Read-Translate-Table
  let $Confirm_Flg_Descr    = rtrim($Xlatlongname,' ')
  
  do Format-DateTime ($PA_Pmt_End_Dt,$PA_Pmt_End_Dt, {DEFDATE}, '', '')
  let $Pay_Period_End_DtLbl = $Pay_Period_End_DtLbl || ':  ' || $PA_Pmt_End_Dt
  
  do Format-DateTime ($PA_Check_Date,$PA_Check_Date, {DEFDATE}, '', '')
  let $Check_SubLabel = $Check_DateLbl
  let $Check_DateLbl  = $Check_DateLbl || ':  ' || $PA_Check_Date

end-procedure

!-----------------------------------------------------------------------
! Procedure: Print-New-Page
! Descr:     This report prints the details of pension payments.  Each
!            payment can contain one or many benefit plans.  If the 
!            report has more than one benefit plan, this procedure will
!            start a new page for each benefit plan.  
!-----------------------------------------------------------------------
begin-procedure Print-New-Page

  if #page_count > 1 
       new-page
  end-if
  
  let $Benefit_Plan_Prev = $Benefit_Plan
  let $tot_pmt_cnt_prev  = $tot_pmt_cnt
  let $tot_pmt_amt_prev  = $tot_pmt_amt
  let $tot_adj_amt_prev  = $tot_adj_amt
  let $tot_schd_amt_prev = $tot_schd_amt
  let $tot_manl_amt_prev = $tot_manl_amt

end-procedure

!-----------------------------------------------------------------------
! Procedure: Print-Payment-Details-Heading
! Descr:     This procedure prints the appropriate labels for the report
!            when the 'Payment Details - Total' radio button is 
!            selected.
!-----------------------------------------------------------------------
begin-procedure Print-Payment-Details-Heading

  print $EmplidLbl                                              (+1,001,006)
  print $Empl_RcdLbl                                            (  ,014,009)
  print $Payment_AmountLbl                                      (  ,030,014)
  print $Adjusted_AmountLbl                                     (  ,050,017)
  print $Payment_TotalLbl                                       (  ,074,013)
  print $Check_SubLabel                                         (  ,094,010)
  print $SetIDLbl                                               (  ,110,006)
  print $VendorLbl                                              (  ,120,006)
  print $NameLbl                                                (  ,137,040)
  
  print '-'                                                     (+1,001,177) fill bold

end-procedure

!-----------------------------------------------------------------------
! Procedure: Print-Payment-NoTax-Heading
! Descr:     This procedure prints the appropriate labels for the report
!            when the 'Payment Details - NonTaxable' radio button is 
!            selected.
!-----------------------------------------------------------------------
begin-procedure Print-Payment-NoTax-Heading

  print $EmplidLbl                                              (+1,001,006)
  print $Empl_RcdLbl                                            (  ,014,009)
  print $Payment_Amount_NTLbl                                   (  ,030,017)
  print $Adjust_Amount_NTLbl                                    (  ,050,020)
  print $Payment_NT_TotalLbl                                    (  ,074,016)
  print $Check_SubLabel                                         (  ,094,010)
  print $SetIDLbl                                               (  ,110,006)
  print $VendorLbl                                              (  ,120,006)
  print $NameLbl                                                (  ,137,040)
  
  print '-'                                                     (+1,001,177) fill bold

end-procedure

!-----------------------------------------------------------------------
! Procedure: Print-Payment-Information-Heading
! Descr:     This procedure prints the appropriate labels for the report
!            when the 'Payment Information' radio button is selected.
!-----------------------------------------------------------------------
begin-procedure Print-Payment-Information-Heading

  print $EmplidLbl                                              (+1,001,006)
  print $Empl_RcdLbl                                            (  ,014,009)
  print $Form_CodeLbl                                           (  ,030,009)
  print $Payment_NumberLbl                                      (  ,050,014)
  print $Guaranteed_PayemntLbl                                  (  ,074,019)
  print $%_To_SurvivorLbl                                       (  ,100,019)
  print $Designated_PayeeLbl                                    (  ,125,021)
  
  print '-'                                                     (+1,001,177) fill bold

end-procedure

!-----------------------------------------------------------------------
! Procedure: Print-Payment-Details
! Descr:     This procedure prints employee payment information
!            when the 'Payment Details - Total' radio button is 
!            selected.
!-----------------------------------------------------------------------
begin-procedure Print-Payment-Details

print $Emplid                                                    (+1,001,011)

print $Empl_Rcd                                                 (  ,014,003)

print $PA_Pmt_Amt                                               (  ,030,015) edit $999,999,999.99
print $PA_Adj_Amt                                               (  ,050,015) edit $999,999,999.99
print $PA_Pmt_Total                                             (  ,074,015) edit $999,999,999.99

print $Check_Date                                               (  ,094,010)

print $SetID                                                    (  ,110,005)
print $Vendor_ID                                                (  ,120,010)
print $Vendor_Name                                              (  ,137,040)

end-procedure

!-----------------------------------------------------------------------
! Procedure: Print-Payment-NoTax-Details
! Descr:     This procedure prints employee payment information
!            when the 'Payment Details - NonTaxable' radio button is 
!            selected.
!-----------------------------------------------------------------------
begin-procedure Print-Payment-NoTax-Details

print $Emplid                                                    (+1,001,011)

print $Empl_Rcd                                                 (  ,014,003)

print $PA_Pmt_Amt_NoTax                                         (  ,030,015) edit $999,999,999.99
print $PA_Adj_Amt_NoTax                                         (  ,050,015) edit $999,999,999.99
print $PA_Pmt_Total_NoTax                                       (  ,074,015) edit $999,999,999.99

print $Check_Date                                               (  ,094,010)

print $SetID                                                    (  ,110,005)
print $Vendor_ID                                                (  ,120,010)
print $Vendor_Name                                              (  ,137,040)

end-procedure

!-----------------------------------------------------------------------
! Procedure: Print-Payment-Information-Details
! Descr:     This procedure prints employee payment information
!            when the 'Payment Details - Information' radio button is 
!            selected.
!-----------------------------------------------------------------------
begin-procedure Print-Payment-Information-Details

print $Emplid                                                    (+1,001,011)

print $Empl_Rcd                                                 (  ,014,003)

print $Form_Code                                                (  ,030,004) 
print $PA_Pmt_Nbr                                               (  ,050,002)
print $Guaranteed_Payment                                       (  ,074,003)

let #length = lengthp($PCT_Continued)
print $PCT_Continued                                            (  ,100,#length)
print '%'                                                       (  ,   ,001)

print $PA_Alt_Payee                                             (  ,125,030)

end-procedure

!-----------------------------------------------------------------------
begin-procedure Init-Report
!-----------------------------------------------------------------------
DO StdApi-Init

let $ReportId = 'PARVWPMT'

do Get-Report-Language    ! Procedure that decides the report language
                          ! and then calls 'Report-Translation' procedure
                          ! that reads the language dep. text.

do Get-Parameter-Values
do Get-Heading-Details
  
end-procedure

!-----------------------------------------------------------------------
! Procedure: Report-Translation
! Descr:     This procedure is used to read language-dependent text from
!            the database.
!-----------------------------------------------------------------------
Begin-Procedure Report-Translation

  do Init_Report_Translation('PENSION', $Language_Cd)
  do Append_Report_Translation ($ReportId)

  do Get_Field_Information ('PARVWPMT',  'BENEFIT_PLAN',        $Benefit_PlanLbl,       #DW)
  do Get_Field_Information ('PARVWPMT',  'PAYMENTS',            $PaymentsLbl,           #DW)
  do Get_Field_Information ('PARVWPMT',  'RUN_ID',              $Run_IDLbl,             #DW)
  do Get_Field_Information ('PARVWPMT',  'PROCESS_INSTANCE',    $Process_InstanceLbl,   #DW)
  do Get_Field_Information ('PARVWPMT',  'PAYMENT_FREQUENCY',   $Payment_FrequencyLbl,  #DW)
  do Get_Field_Information ('PARVWPMT',  'PAY_PERIOD_END_DT',   $Pay_Period_End_DtLbl,  #DW)
  do Get_Field_Information ('PARVWPMT',  'PROCESS_SELECTION',   $Process_SelectionLbl,  #DW)
  do Get_Field_Information ('PARVWPMT',  'CHECK_DATE',          $Check_DateLbl,         #DW)
  
  do Get_Field_Information ('PARVWPMT',  'TOTAL_PAYMENTS',      $Total_PaymentsLbl,     #DW)
  do Get_Field_Information ('PARVWPMT',  'SCHEDULED_AMOUNT',    $Scheduled_AmountLbl,   #DW)
  do Get_Field_Information ('PARVWPMT',  'MANUAL_AMOUNT',       $Manual_AmountLbl,      #DW)
  do Get_Field_Information ('PARVWPMT',  'ADJUST_AMOUNT',       $Adjust_AmountLbl,      #DW)
  
  do Get_Field_Information ('PARVWPMT',  'EMPLID',              $EmplidLbl,             #DW)
  do Get_Field_Information ('PARVWPMT',  'EMPL_RCD',            $Empl_RcdLbl,           #DW)
  do Get_Field_Information ('PARVWPMT',  'PAYMENT_NUMBER',      $Payment_NumberLbl,     #DW)
  do Get_Field_Information ('PARVWPMT',  'FORM_CODE',           $Form_CodeLbl,          #DW)
  do Get_Field_Information ('PARVWPMT',  'GAURANTEED_PAYMENT',  $Guaranteed_PayemntLbl, #DW)
  do Get_Field_Information ('PARVWPMT',  '%_TO_SURVIVOR',       $%_To_SurvivorLbl,      #DW)
  do Get_Field_Information ('PARVWPMT',  'DESIGNATED_PAYEE',    $Designated_PayeeLbl,   #DW)
  do Get_Field_Information ('PARVWPMT',  'PAYMENT_AMOUNT',      $Payment_AmountLbl,     #DW)
  do Get_Field_Information ('PARVWPMT',  'ADJUSTED_AMOUNT',     $Adjusted_AmountLbl,    #DW)
  do Get_Field_Information ('PARVWPMT',  'PAYMENT_TOTAL',       $Payment_TotalLbl,      #DW)
  do Get_Field_Information ('PARVWPMT',  'PAYMENT_AMOUNT_NT',   $Payment_Amount_NTLbl,  #DW)
  do Get_Field_Information ('PARVWPMT',  'ADJUSTED_AMOUNT_NT',  $Adjust_Amount_NTLbl,   #DW)
  do Get_Field_Information ('PARVWPMT',  'PAYMENT_NT_TOTAL',    $Payment_NT_TotalLbl,   #DW)
  do Get_Field_Information ('PARVWPMT',  'NO_TAX',              $No_TaxLbl,             #DW)
  do Get_Field_Information ('PARVWPMT',  'SETID',               $SetIDLbl,              #DW)
  do Get_Field_Information ('PARVWPMT',  'VENDOR',              $VendorLbl,             #DW)
  do Get_Field_Information ('PARVWPMT',  'NAME',                $NameLbl,               #DW)


End-Procedure Report-Translation

!-----------------------------------------------------------------------
! Procedure: Get-Parameter-Values
! Descr:     This procedure queries the PS_PA_RVWPMNT record for the 
!            parameters specified by the user in the run control page.
!-----------------------------------------------------------------------
begin-procedure Get-Parameter-Values

BEGIN-SELECT
PA_RVWPMNT.PA_RUN_ID
PA_RVWPMNT.PROCESS_INSTANCE
PA_RVWPMNT.ST_RADIO_BTN

     let $pa_run_id = &pa_rvwpmnt.pa_run_id
     
     let $process_instance = &pa_rvwpmnt.process_instance
     let #process_instance = to_number($process_instance)
     let #process_instance = round(#process_instance,0)
     
     let $radio_btn = &pa_rvwpmnt.st_radio_btn

 FROM PS_PA_RVWPMNT PA_RVWPMNT
 WHERE PA_RVWPMNT.OPRID = $prcs_oprid
   AND PA_RVWPMNT.RUN_CNTL_ID = $prcs_run_cntl_id
 END-SELECT

end-procedure

!-----------------------------------------------------------------------
begin-procedure Process-Main
!-----------------------------------------------------------------------

do Select-Benefit-Plan

end-procedure

!-----------------------------------------------------------------------
! Procedure: Select-Benefit-Plan
! Descr:     This procedure queries the PS_PA_RT_DETL_VW record for the 
!            data to be printed below the standard header on each page.
!-----------------------------------------------------------------------
begin-procedure Select-Benefit-Plan

let #bp_count   = 0
let #page_count = 0

BEGIN-SELECT

PA_RT_DETL_VW.BENEFIT_PLAN
PA_RT_DETL_VW.PA_TOT_PMT_CNT
PA_RT_DETL_VW.PA_TOT_PMT_AMT
PA_RT_DETL_VW.PA_TOT_SCHD_AMT
PA_RT_DETL_VW.PA_TOT_MANL_AMT
PA_RT_DETL_VW.PA_TOT_ADJ_AMT

     let #bp_count = #bp_count + 1
     let $Benefit_Plan = &pa_rt_detl_vw.benefit_plan
     
     let $tot_pmt_cnt  = &pa_rt_detl_vw.pa_tot_pmt_cnt
     let $tot_pmt_cnt  = rtrim($tot_pmt_cnt,'0')
     let $tot_pmt_cnt  = rtrim($tot_pmt_cnt, '.')
     
     let $tot_pmt_amt  = &pa_rt_detl_vw.pa_tot_pmt_amt
     let $tot_schd_amt = &pa_rt_detl_vw.pa_tot_schd_amt
     let $tot_manl_amt = &pa_rt_detl_vw.pa_tot_manl_amt
     let $tot_adj_amt  = &pa_rt_detl_vw.pa_tot_adj_amt
     
     let #page_count = #page_count + 1
     do Select_Payment_Details

FROM PS_PA_RT_DETL_VW PA_RT_DETL_VW
WHERE PA_RT_DETL_VW.PA_RUN_ID = $pa_run_id
AND PA_RT_DETL_VW.PROCESS_INSTANCE = #process_instance
ORDER BY PA_RT_DETL_VW.BENEFIT_PLAN ASC
END-SELECT

end-procedure

!-----------------------------------------------------------------------
! Procedure: Select_Payment_Details
! Descr:     This procedure queries the PS_PA_RT_PAY_DETL record for the 
!            data to be printed for each employee on the report.
!-----------------------------------------------------------------------
begin-procedure Select_Payment_Details

let #ee_count = 0

do Print-New-Page

evaluate $radio_btn
   when = 'D'
       do Print-Payment-Details-Heading
       break
   when = 'N'
       do Print-Payment-NoTax-Heading
       break   
   when = 'I'
       do Print-Payment-Information-Heading
       break   
end-evaluate

BEGIN-SELECT
PA_RT_PAY_DETL.EMPLID
PA_RT_PAY_DETL.EMPL_RCD
PA_RT_PAY_DETL.PA_PMT_NBR
PA_RT_PAY_DETL.FORM_CODE
PA_RT_PAY_DETL.GUARANTEED_PAYMENT
PA_RT_PAY_DETL.PCT_CONTINUED
PA_RT_PAY_DETL.PA_ALT_PAYEE
PA_RT_PAY_DETL.PA_PMT_AMT
PA_RT_PAY_DETL.PA_ADJ_AMT
PA_RT_PAY_DETL.PA_PMT_TOTAL
PA_RT_PAY_DETL.PA_PMT_AMT_NOTAX
PA_RT_PAY_DETL.PA_ADJ_AMT_NOTAX
PA_RT_PAY_DETL.PA_PMT_TOTAL_NOTAX
PA_RT_PAY_DETL.SETID
PA_RT_PAY_DETL.VENDOR_ID
PA_RT_PAY_DETL.PA_CHECK_DATE
     let #ee_count = #ee_count + 1

     let $Emplid = &pa_rt_pay_detl.emplid
     
     let $Empl_Rcd = &pa_rt_pay_detl.empl_rcd
     let $Empl_Rcd = rtrim($Empl_Rcd, '0')
     let $Empl_Rcd = rtrim($Empl_Rcd, '.')
     
     let $PA_Pmt_Nbr = &pa_rt_pay_detl.pa_pmt_nbr
     let $PA_Pmt_Nbr = rtrim($PA_Pmt_Nbr, '0')
     let $PA_Pmt_Nbr = rtrim($PA_Pmt_Nbr, '.')
     
     let $Form_Code = &pa_rt_pay_detl.form_code
     
     let $Guaranteed_Payment = &pa_rt_pay_detl.guaranteed_payment
     let $Guaranteed_Payment = rtrim($Guaranteed_Payment, '0')
     let $Guaranteed_Payment = rtrim($Guaranteed_Payment, '.')
  
     let $PCT_Continued = &pa_rt_pay_detl.pct_continued
     let $PCT_Continued = rtrim($PCT_Continued, '0')
     let $PCT_Continued = rtrim($PCT_Continued, '.')
     
     let $PA_Alt_Payee = &pa_rt_pay_detl.pa_alt_payee
     let $PA_Pmt_Amt = &pa_rt_pay_detl.pa_pmt_amt
     let $PA_Adj_Amt = &pa_rt_pay_detl.pa_adj_amt
     let $PA_Pmt_Total = &pa_rt_pay_detl.pa_pmt_total
     
     let $PA_Pmt_Amt_NoTax = &pa_rt_pay_detl.pa_pmt_amt_notax
     let $PA_Adj_Amt_NoTax = &pa_rt_pay_detl.pa_adj_amt_notax
     let $PA_Pmt_Total_NoTax = &pa_rt_pay_detl.pa_pmt_total_notax
     
     let $SetID = &pa_rt_pay_detl.setid
     let $Vendor_ID = &pa_rt_pay_detl.vendor_id
     
     let $Check_Date = rtrim(&pa_rt_pay_detl.pa_check_date, ' ')
     do Format-DateTime ($Check_Date,$Check_Date, {DEFDATE}, '', '')
       
     if $Vendor_ID = ' '
          let $Vendor_Name = ' '
     else
          do Get-Vendor-Name
     end-if
     
     evaluate $radio_btn
         when = 'D'
            do Print-Payment-Details
            break
         when = 'N'
            do Print-Payment-NoTax-Details
            break   
         when = 'I'
            do Print-Payment-Information-Details
            break   
     end-evaluate
    
     
FROM PS_PA_RT_PAY_DETL PA_RT_PAY_DETL
WHERE PA_RT_PAY_DETL.PA_RUN_ID = $pa_run_id
AND PA_RT_PAY_DETL.BENEFIT_PLAN = $Benefit_Plan
ORDER BY PA_RT_PAY_DETL.EMPLID ASC,
PA_RT_PAY_DETL.EMPL_RCD ASC
END-SELECT

end-procedure

!-----------------------------------------------------------------------
! Procedure: Get-Vendor-Name
! Descr:     This procedure queries the PS_VENDOR record for the 
!            name of the Vendor_ID selected from the PS_PA_RT_PAY_DETL
!            record.
!-----------------------------------------------------------------------
begin-procedure Get-Vendor-Name

BEGIN-SELECT
VENDOR.NAME1
     
     let $Vendor_Name = &vendor.name1
     
FROM PS_VENDOR VENDOR
WHERE VENDOR.SETID = $SetID
AND VENDOR.VENDOR_ID = $Vendor_ID
END-SELECT

end-procedure


#Include 'paextrct.sqc'  !Report ID INPUT
#Include 'stdapi.sqc'    !Update Process Request API
#Include 'curdttim.sqc'  !Get-Current-DateTime procedure
#Include 'datetime.sqc'  !Routines for date and time formatting
#Include 'datemath.sqc'  !Routines for date and time formatting
#Include 'number.sqc'    !Routines to format numbers
#Include 'getrplng.sqc'  !Gets the report language
#Include 'readxlat.sqc'  !read translate table
#Include 'sqrtrans.sqc'  !Access Strings Table